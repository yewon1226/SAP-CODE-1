## <  핵심 개념 >
</br>

- **1) ABAP 이벤트**
- **2) Selection Screen - Selection Screen Block / Selection Screen Lines**
- **3) AT SELECTION-SCREEN ON <field>**
- **4) Selection Screen 서브스크린 & 탭스트립**
- **5) Message Class**
- **6) Variant**

</br>
</br>

---

</br>

## < Selection Screen >
- **프로그램 실행 시 표준 선택화면(Screen 1000) 자동 생성**
```abap
PARAMETERS: ...
SELECT-OPTIONS: ...
```
- **직접 번호를 지정한 사용자 정의 Selection Screen 생성**
```abap
SELECTION-SCREEN BEGIN OF SCREEN <no>.
  PARAMETERS ...
  SELECT-OPTIONS ...
SELECTION-SCREEN END OF SCREEN <no>.
```
```abap
SELECTION-SCREEN BEGIN OF SCREEN 1100.
  PARAMETERS: p_name TYPE string.
  SELECT-OPTIONS: s_date FOR sy-datum.
SELECTION-SCREEN END OF SCREEN 1100.
```
</br>
</br>

### 1) ABAP 이벤트 
- `LOAD-OF-PROGRAM` : 프로그램이 메모리에 로드될 때 한 번 실행됨
- `INITIALIZATION` : Selection Screen이 처음 뜨기 전에 초기값을 설정함
- `AT SELECTION-SCREEN OUTPUT (PBO)` : Selection Screen이 그려질 때마다 실행되어 화면을 제어함
- `AT SELECTION-SCREEN (PAI)` : 사용자가 Enter나 실행을 누를 때 입력값을 검사함
- `START-OF-SELECTION` : Selection Screen을 통과한 후 본 처리 로직이 시작됨
```abap
LOAD-OF-PROGRAM
→ INITIALIZATION
→ AT SELECTION-SCREEN OUTPUT   (PBO)
→ [Selection Screen 표시]
→ AT SELECTION-SCREEN          (PAI)
→ START-OF-SELECTION
```
```abap
LOAD-OF-PROGRAM.
  "프로그램 로딩 시 1회 실행

INITIALIZATION.
  p_carr = 'AA'.   "초기값 세팅

AT SELECTION-SCREEN OUTPUT.   "PBO
  LOOP AT SCREEN.
    IF screen-group1 = 'A1'.
      screen-active = 0.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN.          "PAI
  IF p_carr IS INITIAL.
    MESSAGE '필수 입력값 누락' TYPE 'E'.
  ENDIF.

START-OF-SELECTION.
  PERFORM get_data.
```
</br>
</br>

### 2) SELECT-OPTIONS
- `SELECT-OPTIONS` → 범위 입력용 선택 조건
- `SELECT-OPTIONS` 는 헤더라인이 있는 내부 테이블이므로 `INTO` 생략 가능
- 내부적으로 `SIGN / OPTION / LOW / HIGH` 구조
```abap
SELECT-OPTIONS so_carr FOR sflight-carrid.

INITIALIZATION.
  MOVE: 'I'  TO so_carr-sign,
        'BT' TO so_carr-option,
        'AA' TO so_carr-low,
        'LH' TO so_carr-high.
  APPEND so_carr.
  CLEAR so_carr.

  MOVE: 'E'  TO so_carr-sign,
        'EQ' TO so_carr-option,
        'DL' TO so_carr-low.
  APPEND so_carr.
```
- `SELECT-OPTIONS`는 데이터베이스 테이블 필드를 직접 참조할 수 없고, 해당 필드가 포함된 이미 선언된 변수(Work Area 또는 Structure)의 필드를 통해 참조해야 함

```abap
SELECT-OPTIONS <seltab> FOR <field>
  [DEFAULT <value>]
  [DEFAULT <value1> TO <value2>]
  [SIGN <I|E> OPTION <op>]
  [MEMORY ID <pid>]
  [LOWER CASE]
  [OBLIGATORY]
  [NO-EXTENSION]
  [NO INTERVALS]
  [MODIF ID <mod>].
```
```abap
SELECT-OPTIONS so_car FOR sflight-carrid
  DEFAULT 'AA'                 "초기값 AA
  SIGN I OPTION EQ             "포함(I), 같음(EQ)
  MEMORY ID CAR                "이전 입력값 자동 불러오기
  OBLIGATORY                   "필수 입력
  NO-EXTENSION                 "다중 선택 버튼 비활성화
  NO INTERVALS                 "from~to 범위 입력 금지
  MODIF ID A1.                 "화면 제어용 그룹 ID
```

- SIGN : `I` - 포함, `E` - 제외
  
| ABAP OPTION 값 | 의미 (Meaning) | LOW/HIGH 사용 | SQL 연산자 개념 |
| :---: | :--- | :---: | :---: |
| **`EQ`** | Equal (같음) | `LOW` 값만 사용 | `fieldName = LOW` |
| **`BT`** | Between (사이) | `LOW`와 `HIGH` 값 모두 사용 | `fieldName BETWEEN LOW AND HIGH` |
| **`GE`** | Greater or Equal (크거나 같음) | `LOW` 값만 사용 | `fieldName >= LOW` |
| **`LE`** | Less or Equal (작거나 같음) | `LOW` 값만 사용 | `fieldName <= LOW` |
| **`GT`** | Greater Than (초과) | `LOW` 값만 사용 | `fieldName > LOW` |
| **`LT`** | Less Than (미만) | `LOW` 값만 사용 | `fieldName < LOW` |
| **`CP`** | Contains Pattern (패턴 포함) | `LOW` 값만 사용 (`*`, `?` 등 사용) | `fieldName LIKE LOW` |
| **`NE`** | Not Equal (같지 않음) | `LOW` 값만 사용 | `fieldName != LOW` (또는 `NOT =`) |
| **`NP`** | Not Contains Pattern (패턴 불일치) | `LOW` 값만 사용 | `NOT (fieldName LIKE LOW)` |

</br>
</br>

### 3) Selection Screen Block
- `Selection Screen` 을 **논리적인 그룹(박스)** 으로 묶어 화면을 보기 좋게 구성
- `WITH FRAME` 를 쓰면 테두리 박스가 생김
- TITLE로 블록 제목 표시 가능
- 블록 안에는 PARAMETERS, SELECT-OPTIONS 모두 배치 가능
```abap
SELECTION-SCREEN BEGIN OF BLOCK <block>
  [WITH FRAME [TITLE <text>]].

  " PARAMETERS / SELECT-OPTIONS 선언

SELECTION-SCREEN END OF BLOCK <block>.
```
```abap
REPORT bc405_screen_d5a.

SELECTION-SCREEN BEGIN OF BLOCK limit
  WITH FRAME TITLE text-001.

PARAMETERS:
  pa_lim_1 RADIOBUTTON GROUP lim DEFAULT 'X',
  pa_lim_2 RADIOBUTTON GROUP lim,
  pa_lim_3 RADIOBUTTON GROUP lim.

SELECTION-SCREEN END OF BLOCK limit.
```
</br>
</br>

### 4) Selection Screen Lines
- Selection Screen에서 여러 필드를 한 줄(row)에 나란히 배치하기 위한 문법
- 라벨(COMMENT), 위치(POSITION), 입력필드(PARAMETERS)를 한 줄로 묶음
```abap
SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT <pos>(<len>) <text> [FOR FIELD <field>].
  SELECTION-SCREEN POSITION <pos>.
  PARAMETERS <p> ...
SELECTION-SCREEN END OF LINE.
```
- `BEGIN OF LINE / END OF LINE` → 같은 줄에 화면 요소 배치
- `COMMENT pos(len) text` → 화면에 설명 텍스트 출력
- `FOR FIELD <field>` → 해당 입력 필드와 텍스트 연결
- `POSITION pos` → 출력 시작 위치 직접 지정
```abap
REPORT bc405_screen_d4b.

SELECTION-SCREEN BEGIN OF LINE.

  SELECTION-SCREEN COMMENT 1(20) text-s03.
  SELECTION-SCREEN COMMENT pos_low(8) text-s04 FOR FIELD pa_col.
  PARAMETERS pa_col AS CHECKBOX.
  SELECTION-SCREEN COMMENT pos_high(8) text-s05.
  PARAMETERS pa_ico AS CHECKBOX.

SELECTION-SCREEN END OF LINE.
```
</br>
</br>

### 5) AT SELECTION-SCREEN ON <field>

- Selection Screen 전체 Enter / 실행 시 호출
- 전체 입력값 공통 체크
```abap
`AT SELECTION-SCREEN`
```
- 특정 입력 필드 하나만 검사
- 그 필드 값이 바뀌거나 Enter 시 실행
```abap
AT SELECTION-SCREEN ON <field>
```
```abap
SELECTION-SCREEN BEGIN OF SCREEN 1100.
PARAMETERS:
  pa_cus AS CHECKBOX,
  pa_agy AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 1100.

AT SELECTION-SCREEN ON pa_add.
  IF pa_add = 'X'.
    CALL SELECTION-SCREEN 1100
      STARTING AT 5 5
      ENDING   AT 50 10.

    IF sy-subrc <> 0.
      LEAVE TO SCREEN 1000.
    ENDIF.
  ENDIF.
```
- SELECT-OPTIONS 전체 라인(low/high) 검사
```abap
AT SELECTION-SCREEN ON <select-options>
```
- 라디오버튼 그룹 중 선택 상태 검사
```abap
AT SELECTION-SCREEN ON RADIOBUTTON GROUP <grp>
```
- 블록 안의 입력값 전체를 한 번에 검사
```abap
AT SELECTION-SCREEN ON BLOCK <block>
```
- Selection Screen에서 특정 필드에서 F1(Help) 를 눌렀을 때 실행되는 이벤트
```abap
AT SELECTION-SCREEN ON HELP-REQUEST FOR <field>
```
```abap
PARAMETERS pa_carr TYPE sflight-carrid.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100
    STARTING AT 5 5.
```
</br>
</br>

### 6) Selection Screen 서브스크린 & 탭스트립
- 서브스크린 코드 형식
```abap
SELECTION-SCREEN BEGIN OF SCREEN <subscreen_number> AS SUBSCREEN.
  PARAMETERS / SELECT-OPTIONS <input_field>.
SELECTION-SCREEN END OF SCREEN <subscreen_number>.
```
- 탭스트립 코드 형식
```abap
SELECTION-SCREEN BEGIN OF TABBED BLOCK <tab_block_name> FOR <number_of_lines> LINES.

  SELECTION-SCREEN TAB (<max_tab_text_length>) <tab_text_variable>
    USER-COMMAND <ok_code> DEFAULT SCREEN <subscreen_number>.

SELECTION-SCREEN END OF BLOCK <tab_block_name>.
```
- 탭스트립 초기 활성 탭 / 서브스크린 지정
```abap
<tab_block_name>-activetab = '<ok_code>'.
<tab_block_name>-dynnr     = '<subscreen_number>'.
```
```abap
"----------------------------------------
" 서브스크린 정의
"----------------------------------------
SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS so_carr FOR spfli-carrid.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS pa_date TYPE sy-datum.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS pa_flag AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.


"----------------------------------------
" 탭스트립 정의
"----------------------------------------
SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.

  SELECTION-SCREEN TAB (20) tab1
    USER-COMMAND COMM1 DEFAULT SCREEN 101.

  SELECTION-SCREEN TAB (20) tab2
    USER-COMMAND COMM2 DEFAULT SCREEN 102.

  SELECTION-SCREEN TAB (20) tab3
    USER-COMMAND COMM3 DEFAULT SCREEN 103.

SELECTION-SCREEN END OF BLOCK tab_strip.


"----------------------------------------
" 초기 활성 탭 / 서브스크린 지정
"----------------------------------------
INITIALIZATION.
  tab1 = 'Carrier'.
  tab2 = 'Date'.
  tab3 = 'Option'.

  tab_strip-activetab = 'COMM1'.
  tab_strip-dynnr     = '0101'.
```
</br>
</br>

### 7) Variant
- Selection Screen 입력값을 저장해두고 재사용하는 실행 조건
<img width="472" height="390" alt="image" src="https://github.com/user-attachments/assets/e64f958c-25d6-4824-b7b5-8d6f30db8eda" />
</br>

<img width="485" height="214" alt="image" src="https://github.com/user-attachments/assets/f9b18ff3-9c06-44f2-816b-00880e01fc90" />
</br>
</br>

- DB Table 에서도 Variant 생성 가능
<img width="485" height="339" alt="image" src="https://github.com/user-attachments/assets/d0f76d8f-7a8f-4fb1-8e10-d3b6f24545eb" />
</br>

<img width="485" height="360" alt="image" src="https://github.com/user-attachments/assets/c722b5e3-a61d-496a-b57f-c0f17f39d7fa" />
</br>
</br>

- 삭제 가능
<img width="481" height="215" alt="image" src="https://github.com/user-attachments/assets/242e7596-1590-466d-8ea3-fe423286c99c" />

</br>
</br>
</br>

---

</br>

## < ABAP 기본 문법 >
</br>

### 1) WRITE 추가 옵션
- `WRITE AT` : 위치 지정 출력 모드 선언
- `<pos>(<len>)` : 출력 시작 열과 길이 지정
- `COLOR <color>` : 출력 색상 지정
- `CURRENCY <f>` : 금액을 통화 단위에 맞게 포맷 출력
```abap
WRITE AT: / <pos>(<len>) <field>
          COLOR <color>
          CURRENCY <currency_field>.
```
```abap
REPORT bc405_fold_write.

CONSTANTS: gc_pos     TYPE i VALUE 12,
           gc_len     TYPE i VALUE 10,
           gc_len_pri TYPE i VALUE 15,
           gc_len_cur TYPE i VALUE 5.

WRITE AT: / sy-line,
          gc_pos(gc_len) gs_sflight-fldate COLOR COL_KEY,
          (gc_len_pri)   gs_sflight-price  CURRENCY gs_sflight-currency,
          (gc_len_cur)   gs_sflight-currency.
```
</br>

- `NO-GAP` → 필드 뒤에 출력되는 공백 제거
- `NO-ZERO` → 값이 0이면 공백 출력, 선행 0을 공백으로 치환
- `DD/MM/YY` → 날짜 출력 형식(일/월/년) 지정
- `CURRENCY <currency_key>` → 통화 키 기준으로 금액 포맷 적용 (TCURX 기준)
- `UNIT <unit_key>` → 단위 키 기준으로 수량 포맷 적용 (T006 기준)
- `USING EDIT MASK <mask>` → 지정한 편집 마스크 형식으로 출력
- `UNDER <g>` → <g> 필드가 출력된 열 아래에서 출력 시작
- `LEFT-JUSTIFIED` → 왼쪽 정렬 (C, N, D, T, X 기본)
- `CENTERED` → 출력 길이 기준 가운데 정렬
- `RIGHT-JUSTIFIED` → 오른쪽 정렬 (숫자 타입 기본)
```abap
WRITE [AT /<pos>(<len>)] <field>
      [NO-GAP]
      [NO-ZERO]
      [DD/MM/YY]
      [CURRENCY <currency_key>]
      [UNIT <unit_key>]
      [USING EDIT MASK <mask>]
      [UNDER <g>]
      [LEFT-JUSTIFIED | CENTERED | RIGHT-JUSTIFIED].
```
```abap
DATA: gv_char TYPE c LENGTH 5 VALUE 'ABC',
      gv_num  TYPE n LENGTH 4 VALUE '0004',
      gv_date TYPE d VALUE sy-datum,
      gv_amt  TYPE p DECIMALS 2 VALUE '1234.50',
      gv_qty  TYPE p DECIMALS 1 VALUE '10.0'.

WRITE AT: / 1(10)  gv_char NO-GAP,
          12(10)   gv_num  NO-ZERO,
          23(10)   gv_date DD/MM/YY,
          35(15)   gv_amt  CURRENCY 'KRW',
          52(10)   gv_qty  UNIT 'EA',
          / 1(10)  gv_amt  USING EDIT MASK '__,___',
          / 1(10)  gv_char UNDER gv_char,
          / 1(10)  gv_char LEFT-JUSTIFIED,
          / 1(10)  gv_char CENTERED,
          / 1(10)  gv_char RIGHT-JUSTIFIED.
```
</br>
</br>

### 2) PARAMETERS
- `OBLIGATORY` : 필수 입력값
- `LOWER CASE` : 소문자 입력 허용
- `VALUE CHECK` : 도메인/서치헬프 기준으로 입력값 유효성 자동 검사
- `MEMORY ID <pid>` : 이전에 입력했던 값을 SAP 메모리에서 자동 불러옴
- `MODIF ID <mod>` : 같은 MODIF ID끼리 묶어 화면 활성/비활성 등 동적 제어에 사용 (그룹핑)
```abap
PARAMETERS <f>
  [TYPE <type> | LIKE <f1>]
  [DECIMALS <n>]
  [OBLIGATORY]
  [DEFAULT <value>]
  [LOWER CASE]
  [VALUE CHECK]
  [AS CHECKBOX]
  [RADIOBUTTON GROUP <grp>]
  [MEMORY ID <pid>]
  [MODIF ID <mod>].
```
```abap
REPORT bc405_screen_d2.

PARAMETERS: pa_carr TYPE sflight-carrid MEMORY ID CAR OBLIGATORY,
            pa_name AS CHECKBOX DEFAULT 'X',
            pa_curr AS CHECKBOX DEFAULT 'X',
            pa_lim1 RADIOBUTTON GROUP lim DEFAULT 'X',
            pa_lim2 RADIOBUTTON GROUP lim,
            pa_lim3 RADIOBUTTON GROUP lim,
            pa_price TYPE p DECIMALS 2 DEFAULT '500',
            pa_text TYPE string LOWER CASE.

CONSTANTS gc_mark VALUE 'X'.

IF pa_name EQ gc_mark.
  " Output name
ENDIF.

CASE gc_mark.
  WHEN pa_lim1.
  WHEN pa_lim2.
  WHEN pa_lim3.
ENDCASE.
```
</br>
</br>

### 3) SELECTION-SCREEN SKIP
- Selection Screen에서 빈 줄(여백)을 추가해서 화면을 보기 좋게 만듦
- <n> 생략 시 → 1줄, <n> 지정 시 → n줄
```abap
SELECTION-SCREEN SKIP [<n>].
```
```abap
SELECTION-SCREEN SKIP 2.
```
</br>
</br>

### 4) CHECK 조건식
- 조건이 참이면 계속 진행, 거짓이면 즉시 현재 처리 블록 종료
```abap
CHECK <조건식>.
```
```abap
LOOP AT gt_data INTO gs_data.
  CHECK gs_data-status = 'A'.        " status ≠ 'A' 인 데이터는 자동으로 건너뜀
  WRITE: / gs_data-id.
ENDLOOP.
```
</br>
</br>

---

</br>

## < 모듈풀 프로그램 카피 방법 >
- 이름 다르게 설정해야 함
  
<img width="279" height="112" alt="image" src="https://github.com/user-attachments/assets/4349df47-c2dc-4617-a1e9-66967b391097" />

</br>

<img width="299" height="204" alt="image" src="https://github.com/user-attachments/assets/5ca4ac8e-7abc-404d-a8ea-3b99dfce6222" />
</br>

<img width="450" height="250" alt="image" src="https://github.com/user-attachments/assets/07819a3b-7694-4b4f-a43f-c7c919b9abee" />
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- Selection screen
- AT SELECTION-SCREEN ON <field>
- Selection Screen 서브스크린 & 탭스트립
- Message Class & Variant
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZPROGB03_0027.

INCLUDE ZPROGB03_0027_TOP.
INCLUDE ZPROGB03_0027_E01.
```
- `TOP` 모듈 생성
```abap
DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

PARAMETERS: pa_carr LIKE gs_flight-carrid DEFAULT 'AA',
            pa_conn TYPE s_conn_id MEMORY ID con.

SELECTION-SCREEN SKIP 2. "두줄 띄우기

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.

  PARAMETERS: pa_rad1 RADIOBUTTON GROUP rad,
              pa_rad2 RADIOBUTTON GROUP rad DEFAULT 'X',
              pa_rad3 RADIOBUTTON GROUP rad.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid DEFAULT 'AA' TO 'LH' OPTION BT SIGN E,
                  so_con FOR gs_flight-connid DEFAULT '0017' NO-EXTENSION. " NO INTERVALS

SELECTION-SCREEN END OF BLOCK blk1.

SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(8) TEXT-002.
    PARAMETERS: pa_name AS CHECKBOX DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 20(8) TEXT-003.
    PARAMETERS: pa_curr AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK blk2.

SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS: so_fdt FOR gs_flight-fldate.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS: pa_chk1 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS: pa_chk2 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.

"TAB STRIP + Sub screen 추가
SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) TEXT-SB1
    USER-COMMAND comm1 DEFAULT SCREEN 101.
  SELECTION-SCREEN TAB (20) TEXT-SB2
    USER-COMMAND comm2 DEFAULT SCREEN 102.
  SELECTION-SCREEN TAB (20) TEXT-SB3
    USER-COMMAND comm3 DEFAULT SCREEN 103.
SELECTION-SCREEN END OF BLOCK tab_strip.
```
- `E01` 모듈 생성
```abap
INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'AZ'.
  APPEND so_car.

  CLEAR so_car. " Header (Structure)만 클리어됨
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.

  tab_strip-activetab ='COMM3'.
  tab_strip-dynnr = '0103'. " 초기 스크린 설정

*AT SELECTION-SCREEN ON pa_carr.
*  IF pa_carr IS INITIAL.
*    MESSAGE e003(zmcb03).
*  ENDIF.

AT SELECTION-SCREEN ON so_car.
  IF so_car IS INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP rad.
  IF pa_rad1 = 'X'.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON BLOCK blk1.
  IF pa_rad3 = 'X' AND so_con IS NOT INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100 STARTING AT 5 5.

START-OF-SELECTION.

  SELECT *
    FROM dv_flights
    INTO TABLE gt_flight
    WHERE carrid = 'AA'
      AND connid = '0017'.

  IF sy-subrc = 0.
    LOOP AT gt_flight INTO gs_flight.
      WRITE:/ gs_flight-carrid,
              gs_flight-connid,
              gs_flight-fldate,
              gs_flight-cityfrom,
              gs_flight-cityto.
    ENDLOOP.
  ENDIF.
```
- 결과 확인
<img width="454" height="248" alt="image" src="https://github.com/user-attachments/assets/30389e0a-7f6a-482b-a2c7-946649622acc" />
</br>

<img width="287" height="208" alt="image" src="https://github.com/user-attachments/assets/94fd9122-9436-43d6-8084-4d3dd75ac8fa" />

</br>
</br>
</br>
</br>

### 실습 2
- Selection screen 예제
- 라디오버튼에 따라 조건에 맞는 all / 국제선 / 국내선 조회 프로그램
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZBC400_B03_PRACTICE4.

INCLUDE ZBC400_B03_PRACTICE4_TOP.
INCLUDE ZBC400_B03_PRACTICE4_E01.
```
- `TOP` 모듈 생성
```abap
DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                so_con FOR gs_flight-connid,
                so_dat FOR gs_flight-fldate NO-EXTENSION.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  PARAMETERS: pa_all RADIOBUTTON GROUP rad,
              pa_nat RADIOBUTTON GROUP rad,
              pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK blk1.
```
- `E01` 모듈 생성
```abap
INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'AZ'.
  APPEND so_car.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.

  LOOP AT gt_flight INTO gs_flight.
    WRITE:/ gs_flight-carrid,
            gs_flight-connid,
            gs_flight-fldate,
            gs_flight-countryfr,
            gs_flight-cityfrom,
            gs_flight-airpfrom,
            gs_flight-countryto,
            gs_flight-cityto,
            gs_flight-airpto,
            gs_flight-price,
            gs_flight-currency,
            gs_flight-seatsmax,
            gs_flight-seatsocc.
  ENDLOOP.
```
- 결과 확인

<img width="436" height="162" alt="image" src="https://github.com/user-attachments/assets/02267c6c-8889-4516-9d8a-1c7fdb2c8bb9" />

</br>

<img width="565" height="204" alt="image" src="https://github.com/user-attachments/assets/b694d73b-55ca-4b64-8327-79d87c8aa313" />
</br>
</br>
</br>
</br>

### 실습 3
- 서브스크린과 탭스크린 실습
- 실습 2 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

SELECTION-SCREEN BEGIN OF SCREEN 1100 AS SUBSCREEN.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                  so_con FOR gs_flight-connid.
SELECTION-SCREEN END OF SCREEN 1100.

SELECTION-SCREEN BEGIN OF SCREEN 1200 AS SUBSCREEN.
  SELECT-OPTIONS: so_dat FOR gs_flight-fldate NO-EXTENSION.
SELECTION-SCREEN END OF SCREEN 1200.

SELECTION-SCREEN BEGIN OF SCREEN 1300 AS SUBSCREEN.
  PARAMETERS: pa_all RADIOBUTTON GROUP rad,
              pa_nat RADIOBUTTON GROUP rad,
              pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
SELECTION-SCREEN END OF SCREEN 1300.

SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) TEXT-TA1 USER-COMMAND comm1 DEFAULT SCREEN 1100.
  SELECTION-SCREEN TAB (20) TEXT-TA2 USER-COMMAND comm2 DEFAULT SCREEN 1200.
  SELECTION-SCREEN TAB (20) TEXT-TA3 USER-COMMAND comm3 DEFAULT SCREEN 1300.
SELECTION-SCREEN END OF BLOCK tab_strip.
```
- `E01` 모듈 수정
```abap
tab_strip-activetab = 'comm2'.
tab_strip-dynnr = '1200'.
```
- 결과 확인
  
<img width="550" height="148" alt="image" src="https://github.com/user-attachments/assets/9f69be33-2db6-4d5f-b09a-e3fec3bb8ca4" />
</br>

<img width="540" height="206" alt="image" src="https://github.com/user-attachments/assets/c56cb335-aa82-413e-a355-33e8dd5c5a07" />
</br>
</br>
</br>
</br>

### 실습 4
- Message Class, Variant 예제
- 실습 3 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
SELECTION-SCREEN BEGIN OF SCREEN 1300 AS SUBSCREEN.
  SELECTION-SCREEN BEGIN OF BLOCK blk.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
    PARAMETERS: pa_con LIKE gs_flight-countryfr.      " countryfr 파라미터 추가
  SELECTION-SCREEN END OF BLOCK blk.
SELECTION-SCREEN END OF SCREEN 1300.
```
- `E01` 모듈 수정
```abap
AT SELECTION-SCREEN ON BLOCK blk.
  IF pa_nat = 'X' AND pa_con IS INITIAL.
    MESSAGE e003(bc405).
  ENDIF.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr
          AND countryfr = pa_con.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.
```
- 결과 확인
  
<img width="596" height="194" alt="image" src="https://github.com/user-attachments/assets/9dfeae5f-6b89-4237-8d67-b663454e459c" />
</br>

<img width="307" height="189" alt="image" src="https://github.com/user-attachments/assets/c348cd17-17a0-46a5-b774-77e8924e9f9b" />
</br>

<img width="547" height="317" alt="image" src="https://github.com/user-attachments/assets/15e61821-6653-48c6-94f1-2a98c4387acd" />
</br>

- countyfr 입력 안하면 오류 ( message class )
  
<img width="140" height="35" alt="image" src="https://github.com/user-attachments/assets/3f16b039-3eef-4334-b13a-26857c62a9bc" />

