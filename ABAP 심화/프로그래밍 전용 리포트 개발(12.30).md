## <  핵심 개념 >
</br>

- **1) ALV 데이터 갱신 (refresh_table_display, stable)**
- **2) ALV 레이아웃 설정 (IS_LAYOUT)**
- **3) ALV 임계값(Threshold) 아이콘 표시 – Exception Display (특정 조건에 따라 빨강/노랑/초록 아이콘 자동 표시)**
- **4) ALV 초기 정렬 기준 설정 (lvc_s_sort)**
- **5) ALV 초기 필터 조건 설정 (LVC_S_FILT)**
- **6) ALV Line Color (행 색상 제어)**
- **7) ALV Cell Color (셀 단위 색상 제어)**
</br>

- **SELECT-OPTIONS는 `Selection Screen` 에서만 가능**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>


### 1) ALV 표준 툴바 버튼 제어
- ALV Grid에서 기본 제공되는 표준 버튼(필터, 합계 등)을 화면에서 숨김 → 기능은 유지되고 화면에서만 제거
</br>

- `CL_GUI_ALV_GRID` → ALV Grid 화면을 생성·제어하는 표준 클래스
- `UI_FUNCTIONS` → 숨길 Function Code들을 담는 내부테이블 타입
- `IT_TOOLBAR_EXCLUDING` → ALV에서 제외할 툴바 기능 전달 파라미터
- `GS_LAYOUT-NO_TOOLBAR` → ALV 툴바 영역 자체를 통째로 숨김 (툴바 자체 제거)
</br>

- `MC_FC_*` → ALV 개별 버튼 1개를 의미하는 Function Code
- `MC_MB_*` → 여러 기능이 묶인 메뉴 버튼 Function Code
- `MC_FC_EXCL_ALL` → ALV 툴바 전체 버튼을 숨기는 Function Code (버튼만 숨김)
```abap
DATA: <gt_excluded> TYPE ui_functions.

APPEND cl_gui_alv_grid=><function_code> TO <gt_excluded>.

go_alv_grid->set_table_for_first_display(
  EXPORTING
    i_structure_name     = '<DDIC 구조>'
    it_toolbar_excluding = <gt_excluded>
  CHANGING
    it_outtab            = <itab>
).
```
```abap
DATA: gt_uifunc TYPE ui_functions.      " TOP

APPEND cl_gui_alv_grid=>mc_fc_filter TO gt_uifunc.    " F01
" → ALV 툴바에서 '필터(Filter)' 버튼 1개만 숨김

APPEND cl_gui_alv_grid=>mc_mb_sum TO gt_uifunc.
" → 합계/소계 등 숫자 관련 기능이 들어있는 '합계 메뉴 버튼' 전체 숨김

APPEND cl_gui_alv_grid=>mc_fc_excl_all TO gt_uifunc.
" → ALV 툴바에 있는 모든 표준 버튼을 한 번에 전부 숨김

go_alv_grid->set_table_for_first_display(    " PBO
  EXPORTING
    i_structure_name     = 'SBOOK'
    it_toolbar_excluding = gt_uifunc
  CHANGING
    it_outtab            = gt_data
).
```
</br>
</br>

### 2) ALV Field Catalog
- ALV 컬럼의 제목, 순서, 숨김, 아이콘·단위 등 화면 표시 방식을 제어하는 설정
- 구조에 없는 컬럼을 추가하거나, 기존 컬럼의 출력 형태를 변경하기 위해 사용
- `LVC_T_FCAT` → Field Catalog 전체를 담는 내부테이블
- `LVC_S_FCAT` → 컬럼 1개에 대한 속성 구조
</br>
</br>

**1) 기본 컬럼 정의 / DDIC 참조**
- `FIELDNAME` → 내부테이블의 컬럼명
- `REF_FIELD` → DDIC에서 참조할 필드명
- `REF_TABLE` → DDIC에서 참조할 테이블/구조명

**2) 컬럼 표시 방식 제어**
- `ICON` → `X` 면 아이콘으로 표시, 공백이면 일반 값
- `CHECKBOX` → `X` 면 값을 읽기 전용 체크박스로 표시하고, 공백이면 일반 값으로 표시
- `NO_OUT` → `X` 면 컬럼을 ALV 화면에서 숨김, 공백이면 정상 표시
- `TECH` → `X` 면 기술용 컬럼으로 처리하고, 공백이면 일반 사용자 컬럼으로 표시

**3) 컬럼 제목 / 위치 / 크기**
- `COLTEXT` → 컬럼 헤더에 표시될 제목 문자열, 미지정 시 DDIC 텍스트 사용
- `COL_POS` → 컬럼의 출력 위치 번호, 숫자가 작을수록 왼쪽에 표시
- `OUTPUTLEN` → 컬럼 출력 폭(너비)

**4) 금액(Currency) / 수량(Unit) 포맷 설정**
- `CFIELDNAME` → 금액 컬럼에 대해 행별 통화 필드 지정
- `CURRENCY` → 금액 컬럼 전체에 고정 통화 코드 적용
- `QFIELDNAME` → 수량 컬럼에 대해 행별 단위 필드 지정
- `QUANTITY` → 수량 컬럼 전체에 고정 단위 적용

**5) 집계 / 키 / 인터랙션**
- `DO_SUM` → `X` 면 합계 라인을 생성하고, 공백이면 합계 계산 안 함
- `NO_SUM` → `X` 면 합계 기능을 비활성화하고, 공백이면 합계 가능
- `KEY` → `X` 면 키 컬럼으로 고정 표시되고, 공백이면 일반 컬럼
- `HOTSPOT` → `X` 면 클릭 이벤트가 발생하고, 공백이면 일반 텍스트 컬럼

**6) 강조 / 값 표시 포맷**
- `EMPHASIZE` → 컬럼 값을 색상·강조·반전 효과로 표시
- `DECIMALS_O` → 표시할 소수점 자릿수 지정
- `DECMLFIELD` → 소수점 자릿수를 행별 필드로 제어
- `ROUND` → `X` 면 반올림 처리하고, 공백이면 반올림 안 함
- `ROUNDFIELD` → 반올림 자릿수를 행별 필드로 제어
- `EDIT_MASK` → 출력 시 편집 마스크 적용
- `JUST` → 값 정렬 방식(L/R/C) 지정
- `LZERO` → `X` 면 앞의 0을 표시하고, 공백이면 0 제거
- `NO_ZERO` → `X` 면 값이 0일 때 표시하지 않고, 공백이면 0 표시
- `NO_SIGN` → `X` 면 부호(+/–)를 숨기고, 공백이면 부호 표시
  
</br>
</br>
</br>

**1) 전역 구조(DDIC) 필드 그대로 사용하는 경우**
- `FIELDNAME` 만 지정
- `REF_FIELD` / `REF_TABLE` 자동
  
**2) 전역 구조 필드 + 속성 조정하는 경우**
- `FIELDNAME` + `REF_FIELD` + `REF_TABLE` 지정
- 길이·텍스트 등만 추가 설정
  
**3) 로컬 타입(내부 정의 타입) 사용하는 경우**
- `FIELDNAME` 만 사용
- `REF_FIELD` / `REF_TABLE` 사용 ❌
- 컬럼 속성 전부 직접 정의
</br>
</br>

```abap
" 출력 구조 (기존 DDIC + 추가 컬럼)
TYPES: BEGIN OF <gs_type>.
  INCLUDE TYPE <DDIC_STRUCTURE>.
  <add_field_1> TYPE <ddic_type_1>.
  <add_field_2> TYPE <ddic_type_2>.
END OF <gs_type>.

DATA: <gt_outtab> TYPE TABLE OF <gs_type>.

" Field Catalog 선언
DATA: <gt_fcat> TYPE lvc_t_fcat,
      <gs_fcat> TYPE lvc_s_fcat.

" 추가 컬럼 1
<gs_fcat>-fieldname = '<ADD_FIELD_1>'.
<gs_fcat>-ref_field = '<REF_FIELD>'.
<gs_fcat>-ref_table = '<REF_TABLE>'.
<gs_fcat>-col_pos   = <N>.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

" 추가 컬럼 2 (아이콘 등 표현 변경)
<gs_fcat>-fieldname = '<ADD_FIELD_2>'.
<gs_fcat>-icon      = 'X'.
<gs_fcat>-coltext   = '<TEXT>'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

" 기존 컬럼 숨김
<gs_fcat>-fieldname = '<ORIGINAL_FIELD>'.
<gs_fcat>-no_out    = 'X'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

" ALV 출력
<go_alv>->set_table_for_first_display(
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <gt_outtab>
    it_fieldcatalog  = <gt_fcat>
).
```
```abap
*---------------------------------------------------------------------*
* TOP 영역 - 데이터 구조 / 타입 선언
*---------------------------------------------------------------------*
TYPE-POOLS: icon.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA:   cancelled_icon TYPE icon-id,
        phone          TYPE scustom-telephone,
      END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.

DATA: BEGIN OF ls_custom,
        id        TYPE s_customer,
        telephone TYPE s_phoneno,
      END OF ls_custom.
DATA lt_custom LIKE TABLE OF ls_custom.

DATA: gt_fcat TYPE lvc_t_fcat,
      gs_fcat TYPE lvc_s_fcat.

*---------------------------------------------------------------------*
* F01 영역 - 데이터 조회
*---------------------------------------------------------------------*
SELECT *
  FROM sbook
  INTO CORRESPONDING FIELDS OF TABLE gt_data.

SELECT id telephone
  FROM scustom
  INTO TABLE lt_custom
  FOR ALL ENTRIES IN gt_data
  WHERE id = gt_data-customid
    AND telephone <> space.

*---------------------------------------------------------------------*
* F01 영역 - 데이터 가공 (아이콘 / 전화번호 세팅)
*---------------------------------------------------------------------*
LOOP AT gt_data INTO gs_data.

  " 취소 아이콘
  IF gs_data-cancelled = 'X'.
    gs_data-cancelled_icon = icon_cancel.
  ENDIF.

  " 전화번호 매핑
  READ TABLE lt_custom INTO ls_custom
       WITH KEY id = gs_data-customid.
  IF sy-subrc = 0.
    gs_data-phone = ls_custom-telephone.
  ENDIF.

  MODIFY gt_data FROM gs_data
    TRANSPORTING cancelled_icon phone.
  CLEAR gs_data.
ENDLOOP.

*---------------------------------------------------------------------*
* F01 영역 - Field Catalog 설정
*---------------------------------------------------------------------*
CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED_ICON'.   " → 취소 여부를 아이콘으로 표시
gs_fcat-icon      = 'X'.               
gs_fcat-coltext   = 'Cancelled'. 
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED'.        " → 기존 취소 컬럼 숨김
gs_fcat-no_out    = 'X'.               
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSFORM'.         " → 여권 형식 컬럼 숨김
gs_fcat-no_out    = 'X'.             
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSBIRTH'.        " → 여권 생년월일 컬럼 숨김   
gs_fcat-no_out    = 'X'.             
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PHONE'.            
gs_fcat-ref_field = 'TELEPHONE'.        " → 전화번호 컬럼을 SCUSTOM 기준으로 참조하여 4번째 위치에 표시
gs_fcat-ref_table = 'SCUSTOM'.          
gs_fcat-col_pos   = 4.                 
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'SMOKER'.           " → 흡연 여부 체크박스로 표시
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'INVOICE'.          " → 송장 여부 체크박스로 표시
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CLASS'.            " → 컬럼을 색상(C5) + 강조 효과로 표시
gs_fcat-emphasize = 'C510'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'LOCCURAM'.         " → LOCCURAM 컬럼에 대해 합계 계산을 적용
gs_fcat-do_sum = 'X'. 
APPEND gs_fcat TO gt_fcat.

*---------------------------------------------------------------------*
* PBO 영역 - ALV 출력
*---------------------------------------------------------------------*
go_alv_grid->set_table_for_first_display(
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab        = gt_data
    it_fieldcatalog  = gt_fcat
).
```

- 결과출력
  
<img width="626" height="343" alt="image" src="https://github.com/user-attachments/assets/3918147c-8030-496b-8685-9a8cb5eca11b" />

</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- layout 실습
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.
DATA: gs_variant TYPE disvariant,
      gs_stable TYPE LVC_S_STBL,
      gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
DATA: ls_field_colors TYPE LVC_S_SCOL.

*DATA: gs_flight2 TYPE BC400_S_FLIGHT,
*      gt_flight2 TYPE TABLE OF BC400_S_FLIGHT.

PARAMETERS: pa_va TYPE disvariant-variant.

SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.

START-OF-SELECTION.
  PERFORM get_data.
*  PERFORM get_data2.
  CALL SCREEN 100.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
TYPE-POOLS COL.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc = 0.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc < 50.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp color lt_field_colors.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-grid_title = 'Flights'.
  gs_layout-zebra = 'X'.
  gs_layout-sel_mode = 'C'.
  gs_layout-excp_fname = 'EXCP'.
  gs_layout-info_fname = 'COLOR'.
  gs_layout-ctab_fname = 'LT_FIELD_COLORS'.
  gs_layout-no_hgridln = 'X'.
  gs_layout-no_vgridln = 'X'.
ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv .
  IF go_container IS INITIAL.
    go_container->free( ).
  ENDIF.
  IF go_alv_grid IS INITIAL.
    go_alv_grid->free( ).
  ENDIF.
  FREE: go_container, go_alv_grid.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈 수정**
```abap
MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE create_and_transfer OUTPUT.
  IF go_container IS INITIAL.
    CREATE OBJECT go_container
      EXPORTING
        container_name              = 'MY_CONTROL_AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    CREATE OBJECT go_alv_grid
      EXPORTING
        i_parent          = go_container
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    gs_variant-report = sy-cprog.
    gs_variant-variant = pa_va.

    PERFORM set_layout.
    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'sflight'
        is_variant                    = gs_variant
        i_save                        = 'A'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_flight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE A012(BC405_408).
    ENDIF.


  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE clear_ok_code OUTPUT.
  CLEAR OK_CODE.
ENDMODULE.
```
</br>
</br>

**4) `PAI` 모듈**
```abap
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
      PERFORM free_alv.
    WHEN 'CANCEL'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```

</br>
</br>

**5) 결과 출력**

<img width="433" height="332" alt="image" src="https://github.com/user-attachments/assets/738970ec-5c0c-419b-955d-116a19b81e09" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
- Field Catalog 실습
- 위 코드 이어서 작성
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      changes_possible TYPE icon-id,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
TYPE-POOLS: COL, ICON.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc = 0.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc < 50.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-fldate < sy-datum.
        gs_flight-changes_possible = ICON_SPACE.
      ELSE.
        gs_flight-changes_possible = ICON_OKAY.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight
        TRANSPORTING excp color lt_field_colors changes_possible.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSOCC'.
  gs_fcat-do_sum = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'PAYMENTSUM'.
  gs_fcat-no_out = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CHANGES_POSSIBLE'.
  gs_fcat-icon = 'X'.
  gs_fcat-coltext = 'Changes Possible'.
  gs_fcat-col_pos = 5.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'EXCP'.
  gs_fcat-coltext = 'traffic light'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈 수정**
```abap
PERFORM set_layout.
PERFORM set_fcat.
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
    is_variant                    = gs_variant
    i_save                        = 'A'
    is_layout                     = gs_layout
  CHANGING
    it_outtab                     = gt_flight
    IT_FIELDCATALOG               = gt_fcat
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
IF sy-subrc <> 0.
  MESSAGE A012(BC405_408).
ENDIF.
```
</br>
</br>

**4) 결과 출력**

<img width="426" height="328" alt="image" src="https://github.com/user-attachments/assets/25767891-bfc2-4ea2-973f-c1382470486a" />

</br>
</br>
</br>
</br>
</br>
</br>
