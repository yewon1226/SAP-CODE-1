# 12/23
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ABAP 이벤트**
- **2) Selection Screen - Selection Screen Block / Selection Screen Lines**
- **3) AT SELECTION-SCREEN ON <field>**
- **4) Selection Screen 서브스크린(Subscreen) & 탭스트립(Tabstrip)**
- **5) Message Class**
- **6) Variant**

</br>
</br>

---

</br>

## < Selection Screen >
- **프로그램 실행 시 표준 선택화면(Screen 1000) 자동 생성**
```abap
PARAMETERS: ...
SELECT-OPTIONS: ...
```
- **직접 번호를 지정한 사용자 정의 Selection Screen 생성**
```abap
SELECTION-SCREEN BEGIN OF SCREEN <no>.
  PARAMETERS ...
  SELECT-OPTIONS ...
SELECTION-SCREEN END OF SCREEN <no>.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 1100.
  PARAMETERS: p_name TYPE string.
  SELECT-OPTIONS: s_date FOR sy-datum.
SELECTION-SCREEN END OF SCREEN 1100.
```
</br>
</br>

### 1) ABAP 이벤트 
- `LOAD-OF-PROGRAM` : 프로그램이 메모리에 로드될 때 한 번 실행됨
- `INITIALIZATION` : Selection Screen이 처음 뜨기 전에 초기값을 설정함
- `AT SELECTION-SCREEN OUTPUT (PBO)` : Selection Screen이 그려질 때마다 실행되어 화면을 제어함
- `AT SELECTION-SCREEN (PAI)` : 사용자가 Enter나 실행을 누를 때 입력값을 검사함
- `START-OF-SELECTION` : Selection Screen을 통과한 후 본 처리 로직이 시작됨
```abap
LOAD-OF-PROGRAM
→ INITIALIZATION
→ AT SELECTION-SCREEN OUTPUT   (PBO)
→ [Selection Screen 표시]
→ AT SELECTION-SCREEN          (PAI)
→ START-OF-SELECTION
```
```abap
*************** E01 ****************
LOAD-OF-PROGRAM.
  "프로그램 로딩 시 1회 실행

INITIALIZATION.
  p_carr = 'AA'.   "초기값 세팅

AT SELECTION-SCREEN OUTPUT.   "PBO
  LOOP AT SCREEN.
    IF screen-group1 = 'A1'.
      screen-active = 0.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN.          "PAI
  IF p_carr IS INITIAL.
    MESSAGE '필수 입력값 누락' TYPE 'E'.
  ENDIF.

START-OF-SELECTION.
  PERFORM get_data.
```
</br>
</br>

### 2) SELECT-OPTIONS
- `SELECT-OPTIONS` → 범위 입력용 선택 조건
- `SELECT-OPTIONS` 는 헤더라인이 있는 내부 테이블이므로 `INTO` 생략 가능
- 내부적으로 `SIGN / OPTION / LOW / HIGH` 구조
```abap
*************** TOP ****************
SELECT-OPTIONS so_carr FOR sflight-carrid.

*************** E01 ****************
INITIALIZATION.
  MOVE: 'I'  TO so_carr-sign,
        'BT' TO so_carr-option,
        'AA' TO so_carr-low,
        'LH' TO so_carr-high.
  APPEND so_carr.
  CLEAR so_carr.

  MOVE: 'E'  TO so_carr-sign,
        'EQ' TO so_carr-option,
        'DL' TO so_carr-low.
  APPEND so_carr.
```
- `SELECT-OPTIONS`는 데이터베이스 테이블 필드를 직접 참조할 수 없고, 해당 필드가 포함된 이미 선언된 변수(Work Area 또는 Structure)의 필드를 통해 참조해야 함

```abap
SELECT-OPTIONS <seltab> FOR <field>
  [DEFAULT <value>]
  [DEFAULT <value1> TO <value2>]
  [SIGN <I|E> OPTION <op>]
  [MEMORY ID <pid>]
  [LOWER CASE]
  [OBLIGATORY]
  [NO-EXTENSION]
  [NO INTERVALS]
  [MODIF ID <mod>].
```
```abap
*************** TOP ****************
SELECT-OPTIONS so_car FOR sflight-carrid
  DEFAULT 'AA'                 "초기값 AA
  SIGN I OPTION EQ             "포함(I), 같음(EQ)
  MEMORY ID CAR                "이전 입력값 자동 불러오기
  OBLIGATORY                   "필수 입력
  NO-EXTENSION                 "다중 선택 버튼 비활성화
  NO INTERVALS                 "from~to 범위 입력 금지
  MODIF ID A1.                 "화면 제어용 그룹 ID
```

- SIGN : `I` - 포함, `E` - 제외
  
| ABAP OPTION 값 | 의미 (Meaning) | LOW/HIGH 사용 | SQL 연산자 개념 |
| :---: | :--- | :---: | :---: |
| **`EQ`** | Equal (같음) | `LOW` 값만 사용 | `fieldName = LOW` |
| **`BT`** | Between (사이) | `LOW`와 `HIGH` 값 모두 사용 | `fieldName BETWEEN LOW AND HIGH` |
| **`GE`** | Greater or Equal (크거나 같음) | `LOW` 값만 사용 | `fieldName >= LOW` |
| **`LE`** | Less or Equal (작거나 같음) | `LOW` 값만 사용 | `fieldName <= LOW` |
| **`GT`** | Greater Than (초과) | `LOW` 값만 사용 | `fieldName > LOW` |
| **`LT`** | Less Than (미만) | `LOW` 값만 사용 | `fieldName < LOW` |
| **`CP`** | Contains Pattern (패턴 포함) | `LOW` 값만 사용 (`*`, `?` 등 사용) | `fieldName LIKE LOW` |
| **`NE`** | Not Equal (같지 않음) | `LOW` 값만 사용 | `fieldName != LOW` (또는 `NOT =`) |
| **`NP`** | Not Contains Pattern (패턴 불일치) | `LOW` 값만 사용 | `NOT (fieldName LIKE LOW)` |

</br>
</br>

### 3) Selection Screen Block
- `Selection Screen` 을 **논리적인 그룹(박스)** 으로 묶어 화면을 보기 좋게 구성
- `WITH FRAME` 를 쓰면 테두리 박스가 생김
- TITLE로 블록 제목 표시 가능
- 블록 안에는 PARAMETERS, SELECT-OPTIONS 모두 배치 가능
```abap
SELECTION-SCREEN BEGIN OF BLOCK <block>
  [WITH FRAME [TITLE <text>]].

  " PARAMETERS / SELECT-OPTIONS 선언

SELECTION-SCREEN END OF BLOCK <block>.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF BLOCK limit
  WITH FRAME TITLE text-001.

PARAMETERS:
  pa_lim_1 RADIOBUTTON GROUP lim DEFAULT 'X',
  pa_lim_2 RADIOBUTTON GROUP lim,
  pa_lim_3 RADIOBUTTON GROUP lim.

SELECTION-SCREEN END OF BLOCK limit.
```
</br>
</br>

### 4) Selection Screen Lines
- Selection Screen에서 여러 필드를 한 줄(row)에 나란히 배치하기 위한 문법
- 라벨(COMMENT), 위치(POSITION), 입력필드(PARAMETERS)를 한 줄로 묶음
```abap
SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT <pos>(<len>) <text> [FOR FIELD <field>].
  SELECTION-SCREEN POSITION <pos>.
  PARAMETERS <p> ...
SELECTION-SCREEN END OF LINE.
```
- `BEGIN OF LINE / END OF LINE` → 같은 줄에 화면 요소 배치
- `COMMENT pos(len) text` → 화면에 설명 텍스트 출력
- `FOR FIELD <field>` → 해당 입력 필드와 텍스트 연결
- `POSITION pos` → 출력 시작 위치 직접 지정
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF LINE.

  SELECTION-SCREEN COMMENT 1(20) text-s03.
  SELECTION-SCREEN COMMENT pos_low(8) text-s04 FOR FIELD pa_col.
  PARAMETERS pa_col AS CHECKBOX.
  SELECTION-SCREEN COMMENT pos_high(8) text-s05.
  PARAMETERS pa_ico AS CHECKBOX.

SELECTION-SCREEN END OF LINE.
```
</br>
</br>

### 5) AT SELECTION-SCREEN ON <field>

- Selection Screen 전체 Enter / 실행 시 호출
- 전체 입력값 공통 체크
```abap
`AT SELECTION-SCREEN`
```
- 특정 입력 필드 하나만 검사
- 그 필드 값이 바뀌거나 Enter 시 실행
```abap
AT SELECTION-SCREEN ON <field>
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 1100.
PARAMETERS:
  pa_cus AS CHECKBOX,
  pa_agy AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 1100.

*************** E01 ****************
AT SELECTION-SCREEN ON pa_add.
  IF pa_add = 'X'.
    CALL SELECTION-SCREEN 1100
      STARTING AT 5 5
      ENDING   AT 50 10.

    IF sy-subrc <> 0.
      LEAVE TO SCREEN 1000.
    ENDIF.
  ENDIF.
```
- SELECT-OPTIONS 전체 라인(low/high) 검사
```abap
AT SELECTION-SCREEN ON <select-options>
```
- 라디오버튼 그룹 중 선택 상태 검사
```abap
AT SELECTION-SCREEN ON RADIOBUTTON GROUP <grp>
```
- 블록 안의 입력값 전체를 한 번에 검사
```abap
AT SELECTION-SCREEN ON BLOCK <block>
```
- Selection Screen에서 특정 필드에서 F1(Help) 를 눌렀을 때 실행되는 이벤트
```abap
AT SELECTION-SCREEN ON HELP-REQUEST FOR <field>
```
```abap
PARAMETERS pa_carr TYPE sflight-carrid.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100
    STARTING AT 5 5.
```
</br>
</br>

### 6) Selection Screen 서브스크린 & 탭스트립
- 서브스크린 코드 형식
```abap
SELECTION-SCREEN BEGIN OF SCREEN <subscreen_number> AS SUBSCREEN.
  PARAMETERS / SELECT-OPTIONS <input_field>.
SELECTION-SCREEN END OF SCREEN <subscreen_number>.
```
- 탭스트립 코드 형식
```abap
SELECTION-SCREEN BEGIN OF TABBED BLOCK <tab_block_name> FOR <number_of_lines> LINES.

  SELECTION-SCREEN TAB (<max_tab_text_length>) <tab_text_variable>
    USER-COMMAND <ok_code> DEFAULT SCREEN <subscreen_number>.

SELECTION-SCREEN END OF BLOCK <tab_block_name>.
```
- 탭스트립 초기 활성 탭 / 서브스크린 지정
```abap
<tab_block_name>-activetab = '<ok_code>'.
<tab_block_name>-dynnr     = '<subscreen_number>'.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS so_carr FOR spfli-carrid.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS pa_date TYPE sy-datum.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS pa_flag AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.


SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) tab1
    USER-COMMAND COMM1 DEFAULT SCREEN 101.

  SELECTION-SCREEN TAB (20) tab2
    USER-COMMAND COMM2 DEFAULT SCREEN 102.

  SELECTION-SCREEN TAB (20) tab3
    USER-COMMAND COMM3 DEFAULT SCREEN 103.
SELECTION-SCREEN END OF BLOCK tab_strip.


*************** E01 ****************
INITIALIZATION.
  tab1 = 'Carrier'.
  tab2 = 'Date'.
  tab3 = 'Option'.

  tab_strip-activetab = 'COMM1'.
  tab_strip-dynnr     = '0101'.
```
</br>
</br>

### 7) Variant
- Selection Screen 입력값을 저장해두고 재사용하는 실행 조건
<img width="472" height="390" alt="image" src="https://github.com/user-attachments/assets/e64f958c-25d6-4824-b7b5-8d6f30db8eda" />
</br>

<img width="485" height="214" alt="image" src="https://github.com/user-attachments/assets/f9b18ff3-9c06-44f2-816b-00880e01fc90" />
</br>
</br>

- DB Table 에서도 Variant 생성 가능
<img width="485" height="339" alt="image" src="https://github.com/user-attachments/assets/d0f76d8f-7a8f-4fb1-8e10-d3b6f24545eb" />
</br>

<img width="485" height="360" alt="image" src="https://github.com/user-attachments/assets/c722b5e3-a61d-496a-b57f-c0f17f39d7fa" />
</br>
</br>

- 삭제 가능
<img width="481" height="215" alt="image" src="https://github.com/user-attachments/assets/242e7596-1590-466d-8ea3-fe423286c99c" />

</br>
</br>
</br>

---

</br>

## < ABAP 기본 문법 >
</br>

### 1) WRITE 추가 옵션
- `WRITE AT` : 위치 지정 출력 모드 선언
- `<pos>(<len>)` : 출력 시작 열과 길이 지정
- `COLOR <color>` : 출력 색상 지정
- `CURRENCY <f>` : 금액을 통화 단위에 맞게 포맷 출력
```abap
WRITE AT: / <pos>(<len>) <field>
          COLOR <color>
          CURRENCY <currency_field>.
```
```abap
*************** TOP ****************
CONSTANTS: gc_pos     TYPE i VALUE 12,
           gc_len     TYPE i VALUE 10,
           gc_len_pri TYPE i VALUE 15,
           gc_len_cur TYPE i VALUE 5.

WRITE AT: / sy-line,
          gc_pos(gc_len) gs_sflight-fldate COLOR COL_KEY,
          (gc_len_pri)   gs_sflight-price  CURRENCY gs_sflight-currency,
          (gc_len_cur)   gs_sflight-currency.
```
</br>

- `NO-GAP` → 필드 뒤에 출력되는 공백 제거
- `NO-ZERO` → 값이 0이면 공백 출력, 선행 0을 공백으로 치환
- `DD/MM/YY` → 날짜 출력 형식(일/월/년) 지정
- `CURRENCY <currency_key>` → 통화 키 기준으로 금액 포맷 적용 (TCURX 기준)
- `UNIT <unit_key>` → 단위 키 기준으로 수량 포맷 적용 (T006 기준)
- `USING EDIT MASK <mask>` → 지정한 편집 마스크 형식으로 출력
- `UNDER <g>` → <g> 필드가 출력된 열 아래에서 출력 시작
- `LEFT-JUSTIFIED` → 왼쪽 정렬 (C, N, D, T, X 기본)
- `CENTERED` → 출력 길이 기준 가운데 정렬
- `RIGHT-JUSTIFIED` → 오른쪽 정렬 (숫자 타입 기본)
```abap
WRITE [AT /<pos>(<len>)] <field>
      [NO-GAP]
      [NO-ZERO]
      [DD/MM/YY]
      [CURRENCY <currency_key>]
      [UNIT <unit_key>]
      [USING EDIT MASK <mask>]
      [UNDER <g>]
      [LEFT-JUSTIFIED | CENTERED | RIGHT-JUSTIFIED].
```
```abap
DATA: gv_char TYPE c LENGTH 5 VALUE 'ABC',
      gv_num  TYPE n LENGTH 4 VALUE '0004',
      gv_date TYPE d VALUE sy-datum,
      gv_amt  TYPE p DECIMALS 2 VALUE '1234.50',
      gv_qty  TYPE p DECIMALS 1 VALUE '10.0'.

WRITE AT: / 1(10)  gv_char NO-GAP,
          12(10)   gv_num  NO-ZERO,
          23(10)   gv_date DD/MM/YY,
          35(15)   gv_amt  CURRENCY 'KRW',
          52(10)   gv_qty  UNIT 'EA',
          / 1(10)  gv_amt  USING EDIT MASK '__,___',
          / 1(10)  gv_char UNDER gv_char,
          / 1(10)  gv_char LEFT-JUSTIFIED,
          / 1(10)  gv_char CENTERED,
          / 1(10)  gv_char RIGHT-JUSTIFIED.
```
</br>
</br>

### 2) PARAMETERS
- `OBLIGATORY` : 필수 입력값
- `LOWER CASE` : 소문자 입력 허용
- `VALUE CHECK` : 도메인/서치헬프 기준으로 입력값 유효성 자동 검사
- `MEMORY ID <pid>` : 이전에 입력했던 값을 SAP 메모리에서 자동 불러옴
- `MODIF ID <mod>` : 같은 MODIF ID끼리 묶어 화면 활성/비활성 등 동적 제어에 사용 (그룹핑)
```abap
PARAMETERS <f>
  [TYPE <type> | LIKE <f1>]
  [DECIMALS <n>]
  [OBLIGATORY]
  [DEFAULT <value>]
  [LOWER CASE]
  [VALUE CHECK]
  [AS CHECKBOX]
  [RADIOBUTTON GROUP <grp>]
  [MEMORY ID <pid>]
  [MODIF ID <mod>].
```
```abap
*************** TOP ****************
PARAMETERS: pa_carr TYPE sflight-carrid MEMORY ID CAR OBLIGATORY,
            pa_name AS CHECKBOX DEFAULT 'X',
            pa_curr AS CHECKBOX DEFAULT 'X',
            pa_lim1 RADIOBUTTON GROUP lim DEFAULT 'X',
            pa_lim2 RADIOBUTTON GROUP lim,
            pa_lim3 RADIOBUTTON GROUP lim,
            pa_price TYPE p DECIMALS 2 DEFAULT '500',
            pa_text TYPE string LOWER CASE.

CONSTANTS gc_mark VALUE 'X'.

IF pa_name EQ gc_mark.
  " Output name
ENDIF.

CASE gc_mark.
  WHEN pa_lim1.
  WHEN pa_lim2.
  WHEN pa_lim3.
ENDCASE.
```
</br>
</br>

### 3) SELECTION-SCREEN SKIP
- Selection Screen에서 빈 줄(여백)을 추가해서 화면을 보기 좋게 만듦
- <n> 생략 시 → 1줄, <n> 지정 시 → n줄
```abap
SELECTION-SCREEN SKIP [<n>].
```
```abap
*************** TOP ****************
SELECTION-SCREEN SKIP 2.
```
</br>
</br>

### 4) CHECK 조건식
- 조건이 참이면 계속 진행, 거짓이면 즉시 현재 처리 블록 종료
```abap
CHECK <조건식>.
```
```abap
LOOP AT gt_data INTO gs_data.
  CHECK gs_data-status = 'A'.        " status ≠ 'A' 인 데이터는 자동으로 건너뜀
  WRITE: / gs_data-id.
ENDLOOP.
```
</br>
</br>

---

</br>

## < 모듈풀 프로그램 카피 방법 >
- 이름 다르게 설정해야 함
  
<img width="279" height="112" alt="image" src="https://github.com/user-attachments/assets/4349df47-c2dc-4617-a1e9-66967b391097" />

</br>

<img width="299" height="204" alt="image" src="https://github.com/user-attachments/assets/5ca4ac8e-7abc-404d-a8ea-3b99dfce6222" />
</br>

<img width="450" height="250" alt="image" src="https://github.com/user-attachments/assets/07819a3b-7694-4b4f-a43f-c7c919b9abee" />
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- program 27에서 진행
- Selection screen
- AT SELECTION-SCREEN ON <field>
- Selection Screen 서브스크린 & 탭스트립
- Message Class & Variant
- HELP-REQUEST 사용 (특정 필드에서 F1(Help) 를 눌렀을 때 실행되는 이벤트)
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZPROGB03_0027.

INCLUDE ZPROGB03_0027_TOP.
INCLUDE ZPROGB03_0027_E01.
```
- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

PARAMETERS: pa_carr LIKE gs_flight-carrid DEFAULT 'AA',
            pa_conn TYPE s_conn_id MEMORY ID con.

SELECTION-SCREEN SKIP 2. "두줄 띄우기

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.

  PARAMETERS: pa_rad1 RADIOBUTTON GROUP rad,
              pa_rad2 RADIOBUTTON GROUP rad DEFAULT 'X',
              pa_rad3 RADIOBUTTON GROUP rad.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid DEFAULT 'AA' TO 'LH' OPTION BT SIGN E,
                  so_con FOR gs_flight-connid DEFAULT '0017' NO-EXTENSION. " NO INTERVALS

SELECTION-SCREEN END OF BLOCK blk1.

SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(8) TEXT-002.
    PARAMETERS: pa_name AS CHECKBOX DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 20(8) TEXT-003.
    PARAMETERS: pa_curr AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK blk2.

SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS: so_fdt FOR gs_flight-fldate.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS: pa_chk1 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS: pa_chk2 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.

"TAB STRIP + Sub screen 추가
SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) TEXT-SB1
    USER-COMMAND comm1 DEFAULT SCREEN 101.
  SELECTION-SCREEN TAB (20) TEXT-SB2
    USER-COMMAND comm2 DEFAULT SCREEN 102.
  SELECTION-SCREEN TAB (20) TEXT-SB3
    USER-COMMAND comm3 DEFAULT SCREEN 103.
SELECTION-SCREEN END OF BLOCK tab_strip.
```
- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'AZ'.
  APPEND so_car.

  CLEAR so_car. " Header (Structure)만 클리어됨
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.

  tab_strip-activetab ='COMM3'.
  tab_strip-dynnr = '0103'. " 초기 스크린 설정

*AT SELECTION-SCREEN ON pa_carr.
*  IF pa_carr IS INITIAL.
*    MESSAGE e003(zmcb03).
*  ENDIF.

AT SELECTION-SCREEN ON so_car.
  IF so_car IS INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP rad.
  IF pa_rad1 = 'X'.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON BLOCK blk1.
  IF pa_rad3 = 'X' AND so_con IS NOT INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100 STARTING AT 5 5.

START-OF-SELECTION.

  SELECT *
    FROM dv_flights
    INTO TABLE gt_flight
    WHERE carrid = 'AA'
      AND connid = '0017'.

  IF sy-subrc = 0.
    LOOP AT gt_flight INTO gs_flight.
      WRITE:/ gs_flight-carrid,
              gs_flight-connid,
              gs_flight-fldate,
              gs_flight-cityfrom,
              gs_flight-cityto.
    ENDLOOP.
  ENDIF.
```
- 결과 확인
<img width="454" height="248" alt="image" src="https://github.com/user-attachments/assets/30389e0a-7f6a-482b-a2c7-946649622acc" />
</br>

<img width="287" height="208" alt="image" src="https://github.com/user-attachments/assets/94fd9122-9436-43d6-8084-4d3dd75ac8fa" />
</br>

<img width="463" height="333" alt="image" src="https://github.com/user-attachments/assets/daef5024-c8bb-478d-8fe2-00ed21c31262" />

</br>
</br>
</br>
</br>

### 실습 2
- Selection screen 예제
- 라디오버튼에 따라 조건에 맞는 all / 국제선 / 국내선 조회 프로그램
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZBC400_B03_PRACTICE4.

INCLUDE ZBC400_B03_PRACTICE4_TOP.
INCLUDE ZBC400_B03_PRACTICE4_E01.
```
- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                so_con FOR gs_flight-connid,
                so_dat FOR gs_flight-fldate NO-EXTENSION.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  PARAMETERS: pa_all RADIOBUTTON GROUP rad,
              pa_nat RADIOBUTTON GROUP rad,
              pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK blk1.
```
- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'AZ'.
  APPEND so_car.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.

  LOOP AT gt_flight INTO gs_flight.
    WRITE:/ gs_flight-carrid,
            gs_flight-connid,
            gs_flight-fldate,
            gs_flight-countryfr,
            gs_flight-cityfrom,
            gs_flight-airpfrom,
            gs_flight-countryto,
            gs_flight-cityto,
            gs_flight-airpto,
            gs_flight-price,
            gs_flight-currency,
            gs_flight-seatsmax,
            gs_flight-seatsocc.
  ENDLOOP.
```
- 결과 확인

<img width="436" height="162" alt="image" src="https://github.com/user-attachments/assets/02267c6c-8889-4516-9d8a-1c7fdb2c8bb9" />

</br>

<img width="565" height="204" alt="image" src="https://github.com/user-attachments/assets/b694d73b-55ca-4b64-8327-79d87c8aa313" />
</br>
</br>
</br>
</br>

### 실습 3
- 서브스크린과 탭스크린 실습
- 실습 2 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights.

PARAMETERS: pa_name TYPE c LENGTH 3.
SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.

SELECTION-SCREEN SKIP 2.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(15) TEXT-003.
    SELECTION-SCREEN COMMENT 35(10) TEXT-004 FOR FIELD pa_abap.
    PARAMETERS: pa_abap AS CHECKBOX.
    SELECTION-SCREEN COMMENT 60(10) TEXT-005 FOR FIELD pa_fiori.
    PARAMETERS: pa_fiori AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME TITLE TEXT-002.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
  SELECTION-SCREEN END OF BLOCK blk2.
SELECTION-SCREEN END OF BLOCK blk1.
```
- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

tab_strip-activetab = 'comm2'.
tab_strip-dynnr = '1200'.
```
- 결과 확인
  
<img width="550" height="148" alt="image" src="https://github.com/user-attachments/assets/9f69be33-2db6-4d5f-b09a-e3fec3bb8ca4" />
</br>

<img width="540" height="206" alt="image" src="https://github.com/user-attachments/assets/c56cb335-aa82-413e-a355-33e8dd5c5a07" />
</br>
</br>
</br>
</br>

### 실습 4
- Message Class, Variant 예제
- 실습 3 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF SCREEN 1300 AS SUBSCREEN.
  SELECTION-SCREEN BEGIN OF BLOCK blk.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
    PARAMETERS: pa_con LIKE gs_flight-countryfr.      " countryfr 파라미터 추가
  SELECTION-SCREEN END OF BLOCK blk.
SELECTION-SCREEN END OF SCREEN 1300.
```
- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

AT SELECTION-SCREEN ON BLOCK blk.
  IF pa_nat = 'X' AND pa_con IS INITIAL.
    MESSAGE e003(bc405).
  ENDIF.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr
          AND countryfr = pa_con.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.
```
- 결과 확인
  
<img width="596" height="194" alt="image" src="https://github.com/user-attachments/assets/9dfeae5f-6b89-4237-8d67-b663454e459c" />
</br>

<img width="307" height="189" alt="image" src="https://github.com/user-attachments/assets/c348cd17-17a0-46a5-b774-77e8924e9f9b" />
</br>

<img width="547" height="317" alt="image" src="https://github.com/user-attachments/assets/15e61821-6653-48c6-94f1-2a98c4387acd" />
</br>

- countyfr 입력 안하면 오류 ( message class )
  
<img width="140" height="35" alt="image" src="https://github.com/user-attachments/assets/3f16b039-3eef-4334-b13a-26857c62a9bc" />


</br>
</br>

# 12/14
</br>
</br>

## <  핵심 개념 >
</br>

- **1) Pushbutton**
- **2) Selection Screen 필드 제어**
</br>

- 그룹명 같은 **이름** 지정 시 무조건 **대문자**
</br>
</br>

---

</br>

## < Selection Screen >
</br>

### 1) Pushbutton
- Selection Screen 에 버튼을 하나 추가
- 버튼 클릭 시 USER-COMMAND(OKCODE) 발생
- 버튼 생성 안되면 `/` 또는 `SKIP` 으로 띄우고 생성하기
```abap
TABLES <screen_fields>.

SELECTION-SCREEN
  PUSHBUTTON <position>(<length>) <text_var>
  USER-COMMAND <okcode>.
```
```abap
INITIALIZATION.
  <text_var> = '<버튼 텍스트>'.

AT SELECTION-SCREEN.
  CASE <screen_fields>-ucomm.
    WHEN '<okcode>'.
      " 버튼 클릭 시 처리 로직
  ENDCASE.
```
- 예시 코드
```abap
*************** TOP ****************
TABLES SSCRFIELDS.

DATA gv_btn_text TYPE string.

PARAMETERS p_carr TYPE scarr-carrid.

SELECTION-SCREEN
  PUSHBUTTON /10(20) gv_btn_text USER-COMMAND btn.    " / 은 줄 바꿈

*************** E01 ****************
INITIALIZATION.
  gv_btn_text = 'Display details'.

AT SELECTION-SCREEN.
  CASE SSCRFIELDS-ucomm.
    WHEN 'BTN'.
      MESSAGE 'Pushbutton 클릭됨' TYPE 'I'.
  ENDCASE.
```

</br>
</br>

### 2) Selection Screen 필드 제어
- Selection Screen에서 필드의 표시/입력 여부를 화면 출력 직전에 제어
- **CS (Contain String)** : 왼쪽 문자열 안에 오른쪽 문자열이 포함되면 TRUE
```abap
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF <조건>.
      screen-<속성> = <값>.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
```
- `screen-active` → 0이면 필드 완전 숨김, 1이면 표시
- `screen-invisible` → 1이면 값이 *****로 마스킹되어 보임
- `screen-input` → 0이면 입력 불가(읽기 전용), 1이면 입력 가능
- `screen-output` → 0이면 출력 안 함, 1이면 출력
- `screen-required` → 1이면 필수 입력 표시
- `screen-intensified` → 1이면 필드 강조 표시
- `screen-display_3d` → 1이면 3D 스타일, 0이면 Flat 스타일
```abap
*************** TOP ****************
DATA: gs_booking TYPE sbook.

SELECT-OPTIONS:
  so_car FOR gs_booking-carrid,
  so_con FOR gs_booking-connid MODIF ID abc.

*************** E01 ****************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-name CS 'SO_CON'
       OR screen-group1 = 'ABC'.
      screen-active = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 문제
  
<img width="554" height="250" alt="image" src="https://github.com/user-attachments/assets/ca025a6b-eab5-4677-bdfb-d307ad114460" />
</br>
</br>
</br>
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE5_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights.

PARAMETERS: pa_name TYPE c LENGTH 3.
SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.

SELECTION-SCREEN SKIP 2.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(15) TEXT-003.
    SELECTION-SCREEN COMMENT 35(10) TEXT-004.
    PARAMETERS: pa_abap AS CHECKBOX.
    SELECTION-SCREEN COMMENT 60(10) TEXT-005.
    PARAMETERS: pa_fiori AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME TITLE TEXT-002.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
  SELECTION-SCREEN END OF BLOCK blk2.
SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE5_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QF'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP rad.
  IF pa_all = 'X'.
    MESSAGE 'ERROR !' TYPE 'E'.
  ENDIF.
```
</br>

- 결과 확인
  
<img width="554" height="262" alt="image" src="https://github.com/user-attachments/assets/250511f8-7d2d-4ce1-82a2-7ab4724209c0" />
</br>

- PA_ALL 선택하고 실행하면 에러 발생하며 라디오 버튼 그룹만 활성화
<img width="546" height="424" alt="image" src="https://github.com/user-attachments/assets/041c32eb-eb29-4ba4-8681-fb6c6d8ed2b5" />
</br>
</br>
</br>
</br>

### 실습 2
- 전 실습에서 program 27에 이어서 진행
- Selection Screen 필드 제어
- ALV 실습 활용
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_TOP
*&---------------------------------------------------------------------*

TABLES sscrfields.

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights,
      gv_mode TYPE C VALUE '1'.

" 2025-12-24: SALV 관련 변수 선언
DATA: go_salv TYPE REF TO cl_salv_table.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.
  "버튼 추가. 버튼명:gv_text, 버튼코드: ON
  SELECTION-SCREEN PUSHBUTTON pos_low(20) gv_text USER-COMMAND ON.

  PARAMETERS: pa_rad1 RADIOBUTTON GROUP rad,
              pa_rad2 RADIOBUTTON GROUP rad DEFAULT 'X',
              pa_rad3 RADIOBUTTON GROUP rad.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid DEFAULT 'AA' TO 'LH' OPTION BT SIGN E,
                  so_con FOR gs_flight-connid DEFAULT '0017' NO-EXTENSION. " NO INTERVALS

SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  tab_strip-activetab ='COMM3'.
  tab_strip-dynnr = '0103'. " 초기 스크린 설정

  gv_text = '잠금 적용'.

AT SELECTION-SCREEN OUTPUT. " PBO
  LOOP AT SCREEN.
    IF screen-name CS 'RAD3'.
      screen-input = gv_mode.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN. " PAI
  CHECK sy-dynnr = 1000.
  CASE sscrfields-ucomm.
    WHEN 'ON'.
      IF gv_mode = '1'.
        gv_mode = '0'.
        gv_text = '잠금해제'.
      ELSE.
        gv_mode = '1'.
        gv_text = '잠금 적용'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
```
</br>

- ALV 적용 방식
- `Pattern` 클릭 후'ABAP Objects Patterns' 더블클릭
<img width="579" height="307" alt="image" src="https://github.com/user-attachments/assets/1f21c1ac-9374-4ca0-9ae2-f645d5ea9a68" />
</br>
</br>

<img width="351" height="333" alt="image" src="https://github.com/user-attachments/assets/101a0700-8bac-448b-ae9b-632edf5a6447" />
</br>
</br>
</br>

- 결과 확인

<img width="543" height="292" alt="image" src="https://github.com/user-attachments/assets/a1325d18-9403-4923-9f58-8691794b55e1" />
</br>
</br>

- 버튼 누르면 잠금
<img width="548" height="298" alt="image" src="https://github.com/user-attachments/assets/95dc1dfe-6b7e-4887-9985-226981e7421e" />
</br>
</br>

- ALV 이용하여 출력
<img width="543" height="176" alt="image" src="https://github.com/user-attachments/assets/571dc55b-d20f-4b93-9d2c-378d8d026ebd" />

</br>
</br>
</br>
</br>

### 실습 3
- `Pushbutton` 을 이용한 `Selection Screen` 필드 제어 실습
- 전 실습(exercise)에서 이어서 진행
</br>
</br>

**1) 프로그램 최초 실행**
- `INITIALIZATION` 실행
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1000)`
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1200)`
  → 화면 표시
  
**2) 탭을 1100번으로 이동**
- `AT SELECTION-SCREEN OUTPUT(PAI, 1200)`
  → `AT SELECTION-SCREEN OUTPUT(PAI, 1000)`
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1000)`
  → `AT SELECTION-SCREEN(PBO, 1100)`
  → 1100 화면 출력
  
**3) 1100 화면에서 HIDE 버튼 클릭**
- 버튼 클릭
  → `AT SELECTION-SCREEN(PAI, 1100)`
  → `AT SELECTION-SCREEN OUTPUT(PAI, 1000)`
</br>
</br>
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

TABLES SSCRFIELDS.

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights,
      gv_mode TYPE I VALUE 1.

SELECTION-SCREEN BEGIN OF SCREEN 1100 AS SUBSCREEN.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                  so_con FOR gs_flight-connid.
  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN PUSHBUTTON 1(10) gv_text USER-COMMAND hide.
  SELECTION-SCREEN SKIP 1.
  SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
    SELECT-OPTIONS: so_cif FOR gs_flight-cityfrom MODIF ID grp,
                    so_cit FOR gs_flight-cityto MODIF ID grp.
  SELECTION-SCREEN END OF BLOCK blk1.
SELECTION-SCREEN END OF SCREEN 1100.
```
</br>

- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  tab_strip-activetab = 'COMM2'.
  tab_strip-dynnr = '1200'.

  gv_text = '숨김모드 적용'.

AT SELECTION-SCREEN OUTPUT.
*  CHECK sy-dynnr = 1100.  " IF sy-dynnr = 1100. 도 가능
  LOOP AT SCREEN.
    IF screen-group1 = 'GRP'.
      screen-active = gv_mode.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

AT SELECTION-SCREEN.
  CHECK sy-dynnr = 1100.  " 1100번일때만 로직 실행
  CASE SSCRFIELDS-ucomm.
    WHEN 'HIDE'.
      IF gv_mode = 1.
        gv_mode = 0.
        gv_text = '숨김모드 해제'.
      ELSE.
        gv_mode = 1.
        gv_text = '숨김모드 적용'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
```
</br>

- 결과 확인
<img width="578" height="221" alt="image" src="https://github.com/user-attachments/assets/72062d90-598c-41b5-b648-f9e50eb3c5c1" />
</br>
</br>

- 버튼 클릭하면 입력창이 숨겨짐
<img width="579" height="222" alt="image" src="https://github.com/user-attachments/assets/0045c78e-b46c-439c-8095-df18e8af3f99" />
</br>
</br>
</br>
</br>

### 실습 4 (과제)
- 문제
- `FOR ALL ENTRIES IN <itab>` 사용
- `LOOP AT <itab> … ENDLOOP` 사용
- `READ TABLE <itab> INTO .. WITH KEY …` 사용
- `SORT <itab>` 사용
<img width="501" height="553" alt="image" src="https://github.com/user-attachments/assets/35361289-6707-4c88-a1fd-94288881aefd" />
</br>
</br>
</br>
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE6_TOP
*&---------------------------------------------------------------------*

DATA: gs_scarr TYPE scarr,
      gt_scarr TYPE TABLE OF scarr.

DATA: BEGIN OF gs_data,
        carrid    TYPE s_carr_id,     "DV_FLIGHTS
        carrname  TYPE s_carrname,    "SCARR
        connid    TYPE s_conn_id,     "DV_FLIGHTS
        fldate    TYPE s_date,        "DV_FLIGHTS
        cityfrom  TYPE s_from_cit,    "DV_FLIGHTS
        cityto    TYPE s_to_city,     "DV_FLIGHTS
        countryfr TYPE land1,         "DV_FLIGHTS
        countryto TYPE land1,         "DV_FLIGHTS
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data.

SELECT-OPTIONS: SO_CAR FOR gs_data-carrid,
                SO_CON FOR gs_data-connid,
                SO_FDT FOR gs_data-fldate NO INTERVALS.

DATA: go_salv TYPE REF TO cl_salv_table.
```
</br>

- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE6_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  SO_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QF'.
  APPEND so_car.

START-OF-SELECTION.
  SELECT * FROM dv_flights
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid IN SO_car
      AND connid IN SO_con
      AND fldate IN SO_fdt.

  MOVE-CORRESPONDING gt_data TO gt_copy_data.

  SORT gt_copy_data BY carrid.
  DELETE ADJACENT DUPLICATES FROM gt_copy_data COMPARING carrid.

  " scarr에서 dv_flights의 carrid 에 맞는 데이터 뽑기
  SELECT carrid carrname FROM scarr
    INTO TABLE gt_scarr
    FOR ALL ENTRIES IN gt_copy_data
    WHERE carrid = gt_copy_data-carrid.

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid.
    gs_data-carrname = gs_scarr-carrname.
    MODIFY gt_data FROM gs_data.
  ENDLOOP.

  SORT gt_data BY carrid connid fldate ASCENDING.

  TRY.
      CALL METHOD cl_salv_table=>factory
        IMPORTING
          r_salv_table = go_salv
        CHANGING
          t_table      = gt_data.
    CATCH cx_salv_msg.
      MESSAGE 'Error !' TYPE 'E'.
  ENDTRY.

  go_salv->get_functions( )->set_all( abap_true ).
  go_salv->display( ).
```
</br>

- 결과 확인
<img width="460" height="407" alt="image" src="https://github.com/user-attachments/assets/d34e516b-a7ed-4f27-9a7f-78410d3f9406" />

</br>
</br>

# 12/26
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ABAP 객체 생성 기본 문법**
- **2) ALV 레이아웃 Variant 저장 옵션**

</br>
</br>

---

</br>

## < ALV 화면 구성 >
</br>

- **ALV 기본 툴바 버튼 기능 설명**
<img width="444" height="337" alt="image" src="https://github.com/user-attachments/assets/4e7d929d-9c27-4a8b-aede-4acf4a0965b2" />

</br>
</br>
</br>

- **상단 영역**
- `List Header` (선택) : ALV 제목/설명
- `Application Toolbar` : 정렬·필터·엑셀 버튼 (클릭 시 이벤트 발생)
</br>

- **중앙(Data Area)**
- `Column Header` : 컬럼 제목 + 정렬 기준
- `Key Column` : 왼쪽 고정 핵심 컬럼
- `Selection Button` (선택) : 행 선택용 체크/아이콘
- `Data` : 실제 조회 데이터
</br>

- **하단 영역**
- `Total Column` : 합계 대상 컬럼
- `Aggregation Row` : 합계 / 평균 / 최소 / 최대

<img width="490" height="346" alt="image" src="https://github.com/user-attachments/assets/5a287e4b-e177-4673-99a7-acdc4164259f" />
</br>
</br>


<img width="495" height="393" alt="image" src="https://github.com/user-attachments/assets/c069f6ba-2f59-4113-a372-eaa7a211bf23" />
</br>
</br>

### < ALV 마우스 사용 >
- **행 선택** : 클릭 (Ctrl = 다중)
- **컬럼 선택** : 헤더 클릭 (Ctrl = 다중)
- **컬럼 이동** : 드래그
- **폭 조절** : 컬럼 경계 드래그
- **헤더 우클릭** : 복사 / 숨김 / 자동폭 / 고정
</br>
</br>

### < ALV Subtotals >
- `Total` : 전체 데이터 합계 → 맨 아래 1줄
- `Subtotal` : 기준 컬럼별 그룹 합계 → 그룹마다 표시

<img width="465" height="361" alt="image" src="https://github.com/user-attachments/assets/5be7e416-eda5-42ea-a17d-f8bf3a0f0a0d" />
</br>
</br>
</br>
</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
- **Area는 Container로, Container는 ALV Grid로 연결됨**
- **클래스** : 객체의 설계도
- **인스턴스(객체)** : 클래스로 생성된 실제 실체
- **참조 변수** : 객체를 가리키는 포인터 변수
</br>
</br>

**1) 참조 변수 선언**
- 객체를 담는 변수가 아니라 객체를 가리키는 참조 변수
- 실제 객체는 아직 없음
```abap
DATA: <ref_var> TYPE REF TO <class_name>.
```
- `CL_GUI_CUSTOM_CONTAINER` : Screen의 Area와 연결되는 컨테이너
- `CL_GUI_ALV_GRID` : Container 안에서 데이터를 표시하는 ALV Grid 객체
```abap
*************** TOP ****************
DATA: go_cont TYPE REF TO CL_GUI_CUSTOM_CONTAINER,     " Container 참조변수 생성 (아직 객체 생성되기 전)
      go_alv TYPE REF TO CL_GUI_ALV_GRID.              " ALV 참조변수 생성 (아직 객체 생성되기 전)
```
</br>
</br>

**2) 객체 생성**
- `CREATE OBJECT` 로 실제 객체 생성 (생성자 자동 호출)
- 생성 시 생성자(CONSTRUCTOR) 호출
- 필요한 값은 `EXPORTING` 으로 전달
- PBO에서 INITIAL 체크 후 1회 생성, Container → ALV 순서
```abap
IF <ref_var> IS INITIAL.
  CREATE OBJECT <ref_var>
    EXPORTING
      <param> = <value>.
    EXCEPTIONS
      <exception_1> = <rc_1>
ENDIF.
```
- `container_name` → Screen의 Area와 연결
- `i_parent` → ALV ↔ Container 연결
```abap
*************** PBO ****************
MODULE init_alv OUTPUT.
  " 1. SCREEN에 Custom Area 추가
  " 2. Container Control 생성 (AREA와 연결)
  IF go_cont IS INITIAL. " 최초 1번만 실행
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.

    " 3. ALV Grid 생성
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont " Container와 연결
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
  ENDIF.
ENDMODULE.
```
</br>
</br>

- `Custom Container` 객체 생성
  
<img width="377" height="363" alt="image" src="https://github.com/user-attachments/assets/6d99dc1d-c5bd-4140-b21a-e41130485067" />
</br>
</br>

- `ALV Grid` 객체 생성

<img width="375" height="359" alt="image" src="https://github.com/user-attachments/assets/90735023-c617-449e-98ad-6b3eed0e0f61" />
</br>
</br>
</br>
</br>

**3) ALV 컨트롤 인스턴스 해제**
- 화면 종료 시 PAI에서 ALV/Container → free 호출로 자원 해제
- PAI(EXIT, CANCEL) 에서 수행
```abap
FORM free_control_resources.
  CALL METHOD <alv_ref>->free.
  CALL METHOD <container_ref>->free.
  FREE: <alv_ref>, <container_ref>.
ENDFORM.
```
- `->FREE()` : 객체가 사용하는 GUI 컨트롤 자원 해제
- `FREE ref` : 참조 변수만 해제(값 INITIAL로 초기화)
```abap
*************** PAI ****************
MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      PERFORM FREE_ALV.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      PERFORM FREE_ALV.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*************** F01 ****************
FORM free_alv.
  IF GO_ALV IS NOT INITIAL.
    GO_ALV->FREE( ).
  ENDIF.
  IF GO_CONT IS NOT INITIAL.
    GO_CONT->FREE( ).
  ENDIF.
  FREE: GO_ALV, GO_CONT.
ENDFORM.
```
</br>
</br>

**4) ALV Grid 데이터 표시**
- ALV Grid에 처음으로 데이터를 출력하는 메서드
- 내부 테이블( `it_outtab` )의 데이터를 기준으로 화면 구성
- `i_structure_name` 에 지정한 DDIC 구조 기준으로 ALV 컬럼 구조가 자동 출력됨
```abap
CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <internal_table>
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
- `SET_TABLE_FOR_FIRST_DISPLAY` : 내부 테이블 데이터를 ALV Grid에 최초로 표시하는 메서드
- SET_TABLE* 하고 f4 누르면 자동완성
```abap
*************** PBO ****************
" 4. ALV Grid 안에 Data Display
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
</br>
</br>

- ALV Grid 메서드 호출
<img width="384" height="362" alt="image" src="https://github.com/user-attachments/assets/e2594dd2-511b-4721-b9c6-b9e5ec7d13ab" />
</br>
</br>
</br>
</br>

**5) ALV 데이터 갱신**
- `REFRESH_TABLE_DISPLAY`
- 이미 생성된 ALV Grid가 있을 때 사용
- ALV를 다시 만들지 않고 데이터만 화면에 반영
- 보통 PBO에서 호출
```abap
CALL METHOD <alv_ref>->refresh_table_display
  EXPORTING
    is_stable = <ls_stable>.
```
- `LVC_S_STBL` 은 ALV 새로고침 시 스크롤·선택 위치가 초기화되지 않도록 화면을 고정하는 옵션
```abap
DATA ls_stable TYPE lvc_s_stbl.

ls_stable-row = 'X'. " 행 위치 유지
ls_stable-col = 'X'. " 열 위치 유지
```
- `REFRESH_TABLE_DISPLAY` : 내부 테이블 변경 내용을 ALV Grid에 다시 표시(갱신)하는 메서드
```abap
*************** TOP ****************
DATA gs_stable TYPE LVC_S_STBL.

*************** PBO ****************
IF go_cont IS INITIAL.
    " 생략
ELSE.
  gs_stable-row = 'X'.
  gs_stable-col = 'X'.

  CALL METHOD go_alv->refresh_table_display
    EXPORTING
      is_stable      = gs_stable
      i_soft_refresh = ''  " X : 정렬, 필터, 그외 기능들 유지
      " _ : 전체 리프레쉬 (기본)
    EXCEPTIONS
      finished       = 1
      OTHERS         = 2.
ENDIF.
```
</br>
</br>

**6) 결과 확인**

<img width="475" height="127" alt="image" src="https://github.com/user-attachments/assets/a7190486-1fe1-40c7-be7f-222564219cbc" />
</br>

<img width="574" height="174" alt="image" src="https://github.com/user-attachments/assets/9b72474e-7ab8-47fd-a1f1-5c8b5a0c0e48" />
</br>
</br>
</br>
</br>
</br>

---

</br>

## < ALV 레이아웃 Variant 저장 옵션 >
</br>

**1) `I_SAVE` (저장 범위)**
- 프로그램 실행 시 자동 적용 여부 지정
- `A` : Global / Local 레이아웃 모두 저장 가능
- `U` : Local(사용자별) 레이아웃만 저장 가능
- `X` : Global(공용) 레이아웃만 저장 가능
- `space` : 레이아웃 저장 불가
```abap
i_save = '<A|U|X|space>'.      " ' ' (공백 문자) 쓰지 않기. '' 는 가능.
```
</br>

**2) `I_DEFAULT` (기본 레이아웃)**
- 프로그램 실행 시 자동 적용 여부 지정
- `space` : 레이아웃은 저장되지만 자동 적용 안 됨
- `X` : 해당 레이아웃을 기본(Default) 으로 저장 → 실행 시 자동 적용
```abap
i_default = '<X|space>'.
```
</br>

**3) `<ls_variant>-report`**
- 레이아웃 Variant가 속한 프로그램명
- Variant 저장/조회에 필수
- 보통 항상 sy-cprog 사용
```abap
<ls_variant>-report = sy-cprog.
```
</br>

**4) `<ls_variant>-variant`**
- 적용할 레이아웃(Variant) 이름
- Selection Screen에서 입력받은 Variant를 ALV에 적용할 때 사용
```abap
<ls_variant>-variant = <p_var>.
```
</br>

**5) `<ls_variant>-handle`**
- 같은 프로그램에서 여러 ALV Grid 구분용 식별자
- ALV 1개면 생략 가능
```abap
<ls_variant>-handle = '1'.
```
</br>

**6) `<ls_variant>-username`**
- 특정 사용자 전용 Variant 지정
- 대부분 시스템이 자동 처리 → 직접 설정 거의 안 함
```abap
<ls_variant>-username = sy-uname.
```
</br>

**7) <ls_variant>-text**
- 레이아웃 설명 텍스트
- Variant 저장 화면에서 입력·표시되는 값
```abap
<ls_variant>-text = 'Flight ALV Layout'.
```
</br>
</br>
</br>

### 1) ALV 레이아웃 Variant 저장
- `<ls_variant>-report = sy-cprog.` 는 필수
```abap
DATA: <ls_variant> TYPE disvariant,
      <lv_save>    TYPE c LENGTH 1.

<ls_variant>-report = sy-cprog.
<lv_save> = '<A|U|X|space>'.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_variant = <ls_variant>
    i_save     = <lv_save>
    i_default  = '<X|space>'
  CHANGING
    it_outtab  = <itab>.
```
```abap
*************** TOP ****************
DATA: gs_variant TYPE disvariant,
      gv_save    TYPE c LENGTH 1.

gs_variant-report = sy-cprog.
gv_save = 'A'.   " Global / Local 모두 저장

*************** PBO ****************
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_variant = gs_variant
    i_save     = gv_save
    i_default  = 'X'
  CHANGING
    it_outtab  = gt_bookings.
```
</br>

### 2) Selection Screen에서 Variant 전달
```abap
DATA <ls_variant> TYPE disvariant.
PARAMETERS <p_var> TYPE disvariant-variant.

<ls_variant>-report  = sy-cprog.
<ls_variant>-variant = <p_var>.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_variant = <ls_variant>
  CHANGING
    it_outtab  = <itab>.
```
```abap
*************** TOP ****************
DATA gv_variant TYPE disvariant.
PARAMETERS pa_lv TYPE disvariant-variant.

gv_variant-report  = sy-cprog.
gv_variant-variant = pa_lv.

*************** PBO ****************
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_variant = gv_variant
  CHANGING
    it_outtab  = gt_bookings.
```
</br>
</br>
</br>
</br>

## < ALV 레이아웃 Variant 저장 옵션 (I_SAVE) 예제 >
</br>

**1) `TOP` 모듈 수정**
```abap
DATA: gs_variant TYPE disvariant.

INITIALIZATION.
  PERFORM set_options.
  gs_variant-report = sy-cprog.
  gs_variant-username = sy-uname.
  gs_variant-handle = 'MAIN'.
  " handle : 하나의 화면에 여러개의 ALV가 있는 경우
  "          각 ALV마다 구분자가 필요하므로 구분자 지정해줌
```
</br>

**2) `PBO` 의 `init_alv` 모듈 수정**
```abap
" 4. ALV Grid 안에 Data display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SBOOK'
        is_variant                    = gs_variant
        i_save                        = 'A' " A, X, U, ''
        i_default                     = 'X'
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
```
</br>

**3) `Variant` 지정**
- 레이아웃 수정 후 레이아웃 저장
  
<img width="511" height="174" alt="image" src="https://github.com/user-attachments/assets/2066b032-4300-445e-b15d-6943b89a5324" />
</br>
</br>

- `/` 로 시작 → Global(공용) 레이아웃
- `/` 없음 → Local(개인) 레이아웃

<img width="435" height="351" alt="image" src="https://github.com/user-attachments/assets/fbab5ce1-d915-4938-a262-f83736fe20f4" />
</br>
</br>

- **`User-Specific`** ✔ → Local(개인) / ✖ → Global(공용)
- **`Default`** ✔ → 실행 시 자동 적용 / ✖ → 수동 선택
<img width="435" height="361" alt="image" src="https://github.com/user-attachments/assets/fd7a76e1-0a81-4205-b3d7-70edded26b4d" />
</br>
</br>
</br>

**4) Default 선택 안했을 때 레이아웃 선택하면 바뀜**

<img width="546" height="178" alt="image" src="https://github.com/user-attachments/assets/f3b2e2a4-e952-4c70-8c8b-c3efbe07d126" />

</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- ALV 실습
</br>
</br>

**1) Program 에 모듈 생성**
```abap
REPORT ZPROGB03_0029.

INCLUDE ZPROGB03_0029_TOP.
INCLUDE ZPROGB03_0029_O01.
INCLUDE ZPROGB03_0029_I01.
INCLUDE ZPROGB03_0029_F01.
```
</br>
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_TOP
*&---------------------------------------------------------------------*

DATA: gt_data TYPE TABLE OF sbook,
      gs_data LIKE LINE OF gt_data.

DATA: ok_code TYPE sy-ucomm,
      go_cont TYPE REF TO cl_gui_custom_container,
      go_alv  TYPE REF TO cl_gui_alv_grid.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.

INITIALIZATION.
  PERFORM set_options.

START-OF-SELECTION.
  PERFORM GET_DATA.
  CALL SCREEN 100.
```
</br>
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_F01
*&---------------------------------------------------------------------*

FORM set_options.
  so_fdt-sign = 'I'.
  so_fdt-option = 'BT'.
  so_fdt-low = sy-datum(4) && '0101'. " 매년 첫째날
  so_fdt-high = sy-datum(4) && '1231'. " 매년 마지막날
  APPEND so_fdt.

  so_car-sign = 'I'.
  so_car-option = 'GT'.
  so_car-low = 'AZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.
ENDFORM.

*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM SBOOK
    INTO TABLE gt_data
    WHERE carrid IN so_car
      AND connid IN so_con
      AND fldate IN so_fdt.
ENDFORM.
```
</br>
</br>

**4) `PBO` 모듈 생성**
- `SY-UNAME` : 현재 로그인한 사용자 ID
- `SY-DATUM` : 현재 시스템 날짜 (`YYYYMMDD`)
- `SY-UZEIT` : 현재 시스템 시간 (`HHMMSS`)
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100' WITH sy-uname sy-datum sy-uzeit.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  " 1. 100번 스크린 Layout에 Area 추가
  " 2. 만약 Container 가 만들어지기 전이라면, 객체 생성함
  "    Custom Container 생성
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    " 3. ALV Grid 객체 생성해서 Container와 연결
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    " 4. ALV Grid 안에 Data display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SBOOK'
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4
            .
    IF sy-subrc <> 0.
*     Implement suitable error handling here
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_I01
*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

**6) 결과 확인**

<img width="488" height="141" alt="image" src="https://github.com/user-attachments/assets/0593593a-ab76-4ee7-a844-32473cfcbab4" />
</br>

<img width="515" height="238" alt="image" src="https://github.com/user-attachments/assets/3e58a92f-1c2d-4be3-af54-f6a5e9ea0fb8" />
</br>
</br>
</br>
</br>
</br>

### 실습 2
- 문제
<img width="356" height="208" alt="image" src="https://github.com/user-attachments/assets/f0ef060a-6af1-4039-baf9-1cb3d57ad293" />
</br>
</br>
</br>
</br>
</br>

**1) Program 에 모듈 생성**
```abap
REPORT ZBC405_03_ALV.

INCLUDE ZBC405_03_ALV_TOP.
INCLUDE ZBC405_03_ALV_O01.
INCLUDE ZBC405_03_ALV_I01.
INCLUDE ZBC405_03_ALV_F01.

START-OF-SELECTION.

*PERFORM get_data.
PERFORM get_data2.
CALL SCREEN 100.
```
</br>
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.

DATA: gs_flight TYPE sflight,
      gt_flight TYPE TABLE OF sflight.

DATA: gs_flight2 TYPE BC400_S_FLIGHT,
      gt_flight2 TYPE TABLE OF BC400_S_FLIGHT.

SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.
```
</br>
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
ENDFORM.

*&---------------------------------------------------------------------*

FORM get_data2.
  SELECT carrid connid fldate seatsmax seatsocc FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight2
    WHERE carrid IN so_car
      AND connid IN so_con.

  LOOP AT gt_flight2 INTO gs_flight2.
    gs_flight2-PERCENTAGE = ( gs_flight2-seatsocc / gs_flight2-seatsmax ) * 100.
    MODIFY gt_flight2 FROM gs_flight2.
  ENDLOOP.
ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv.
  IF go_container IS INITIAL.
    go_container->free( ).
  ENDIF.
  IF go_alv_grid IS INITIAL.
    go_alv_grid->free( ).
  ENDIF.
  FREE: go_container, go_alv_grid.
ENDFORM.
```
</br>
</br>

**4) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE create_and_transfer OUTPUT.
  IF go_container IS INITIAL.
    CREATE OBJECT go_container
      EXPORTING
        container_name              = 'MY_CONTROL_AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    CREATE OBJECT go_alv_grid
      EXPORTING
        i_parent          = go_container
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'BC400_S_FLIGHT'
      CHANGING
        it_outtab                     = gt_flight2
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE A012(BC405_408).
    ENDIF.


  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE clear_ok_code OUTPUT.
  CLEAR OK_CODE.
ENDMODULE.
```
</br>
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
      PERFORM free_alv.
    WHEN 'CANCEL'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

**6) 결과 확인**

<img width="500" height="103" alt="image" src="https://github.com/user-attachments/assets/91faf0f7-6bcc-4e33-91bf-32f8557a5f2a" />
</br>

<img width="506" height="227" alt="image" src="https://github.com/user-attachments/assets/dc02a463-7156-409c-ae94-416c3b6eb75b" />

</br>
</br>
</br>
</br>
</br>

### 실습 3
- Variat 실습
- 실습 2에서 이어서 진행
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: gs_variant TYPE disvariant.
PARAMETERS: pa_va TYPE disvariant-variant.
```
</br>
</br>

**2) `PBO` 의 `create_and_transfer` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

gs_variant-report = sy-cprog.
gs_variant-variant = pa_va.

CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'sflight'
    is_variant = gs_variant
    i_save = 'A'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
IF sy-subrc <> 0.
  MESSAGE A012(BC405_408).
ENDIF.
```
</br>
</br>

**3) 결과 확인**

<img width="492" height="282" alt="image" src="https://github.com/user-attachments/assets/5ae999f6-b27a-4a39-9a9c-f1a4094975ea" />

</br>
</br>

# 12/29
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV 데이터 갱신 (refresh_table_display, stable)**
- **2) ALV 레이아웃 설정 (IS_LAYOUT)**
- **3) ALV 임계값(Threshold) 아이콘 표시 – Exception Display (특정 조건에 따라 빨강/노랑/초록 아이콘 자동 표시)**
- **4) ALV 초기 정렬 기준 설정 (lvc_s_sort)**
- **5) ALV 초기 필터 조건 설정 (LVC_S_FILT)**
- **6) ALV Line Color (행 색상 제어)**
- **7) ALV Cell Color (셀 단위 색상 제어)**
</br>

- **SELECT-OPTIONS는 `Selection Screen` 에서만 가능**
- **`TYPE-POOLS COL.` 로 색상 코드 확인 가능 (필수)**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

### 1) ALV 데이터 갱신
- 이미 생성된 ALV Grid가 있을 때 ALV를 다시 생성하지 않고 데이터만 화면에 반영
- `LVC_S_STBL` : ALV 새로고침 시 화면 위치 고정용 구조
- `row = 'X'` → 행(스크롤) 위치 유지
- `col = 'X'` → 열(커서) 위치 유지
</br>

- `REFRESH_TABLE_DISPLAY` → ALV Grid 데이터를 다시 그리되 스크롤·선택 상태를 유지한 채 화면만 갱신하는 메서드
- `i_soft_refresh` = 'X' : 정렬·필터·레이아웃을 유지한 채 데이터만 갱신
- `i_soft_refresh` = space : ALV를 전체 재계산하여 정렬·필터까지 다시 적용
```abap
********* TOP *********
DATA ls_stable TYPE lvc_s_stbl.    

********* PBO *********
ls_stable-row = 'X'.   " 행(스크롤) 위치 유지
ls_stable-col = 'X'.   " 열(커서) 위치 유지

CALL METHOD go_alv->refresh_table_display
  EXPORTING
    is_stable      = ls_stable
    i_soft_refresh = 'X'   " 정렬·필터·레이아웃 유지
  EXCEPTIONS
    finished       = 1
    others         = 2.
```
</br>
</br>
</br>

### 2) ALV 레이아웃 설정 (IS_LAYOUT)
- ALV Grid의 화면 모양(제목, 줄무늬, 컬럼 폭 등)을 설정
- 데이터 내용은 안 바뀌고 표시 방식만 제어
- `SET_TABLE_FOR_FIRST_DISPLAY` 에서 최초 출력 시만 적용
- **`Ctrl + Space`** - `gs_layout-` 뒤에서 누르면 구조체 필드 목록 자동 표시
</br>

- `LVC_S_LAYO` : ALV 화면의 모양(레이아웃) 을 제어하는 구조체
```abap
DATA <ls_layout> TYPE lvc_s_layo.

<ls_layout>-grid_title = 'Booking List'.
<ls_layout>-zebra      = 'X'.
<ls_layout>-cwidth_opt = 'X'.
<ls_layout>-totals_bef = 'X'.
<ls_layout>-sel_mode   = 'A'.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
    is_layout        = <ls_layout>
  CHANGING
    it_outtab        = <itab>.
```
```abap
********* TOP *********
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM set_layout.
  gs_layout-grid_title = 'Booking List'.   " ALV 제목
  gs_layout-zebra      = 'X'.               " 행 줄무늬
  gs_layout-cwidth_opt = 'X'.               " 컬럼 너비 자동
  gs_layout-totals_bef = 'X'.               " 합계 상단
  gs_layout-sel_mode   = 'A'.               " 선택 모드 (_,A,B,C,D)
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
    is_layout        = gs_layout
  CHANGING
    it_outtab        = gt_data.
```
</br>
</br>

**< LVC_S_LAYO (ALV Layout Structure 주요 필드 정리) >**
  
| Field Name     | Description |
|---------------|-------------|
| `grid_title`  | ALV 상단에 표시되는 제목 텍스트 (최대 70자) |
| `zebra`       | `'X'` 설정 시 행을 교차 색상(Zebra 패턴)으로 표시 |
| `no_headers`  | `'X'` 설정 시 컬럼 헤더 숨김 |
| `sel_mode`    | 행 / 셀 / 컬럼 선택 모드 설정 |
| `cwidth_opt`  | `'X'` 설정 시 컬럼 폭 자동 최적화 |
| `no_toolbar`  | `'X'` 설정 시 ALV 툴바 숨김 |
| `totals_bef`  | `'X'` 설정 시 합계 행을 데이터 맨 위에 표시 |
| `no_hgridln`  | `'X'` 설정 시 가로(Grid) 라인 숨김 |
| `no_vgridln`  | `'X'` 설정 시 세로(Grid) 라인 숨김 |
| `info_fname`  | **행(Row) 색상 정보**를 담고 있는 내부 테이블 필드명 |
| `ctab_fname`  | **셀(Cell) 색상 정보**를 담고 있는 내부 테이블 필드명 |

</br>

**< ALV Selection Mode (lvc_s_layo-sel_mode) >**

| 값(Value) | 선택 가능 범위 (Possible Selections) | 선택 컬럼 표시 (Selection Column) |
|-----------|--------------------------------------|-----------------------------------|
| space     | B와 동일                              | No                                |
| A         | 다중 행 선택<br>다중 컬럼 선택        | Yes                               |
| B         | 단일 행 선택<br>다중 컬럼 선택        | No                                |
| C         | 다중 행 선택<br>다중 컬럼 선택        | No                                |
| D         | 다중 행 선택<br>다중 컬럼 선택<br>셀 단위 선택 가능 | Yes |

</br>
</br>
</br>

### 3) ALV 임계값(Threshold) 아이콘 표시 – Exception Display
- 특정 조건에 따라 빨강/노랑/초록 아이콘 자동 표시
- `excp_fname` : 예외(아이콘) 값을 담고 있는 컬럼 이름을 ALV에 알려줌
- `excp_led` : 해당 컬럼 값을 신호등(LED) 아이콘으로 표시하도록 설정
- ALV Exception 컬럼은 `1 = 빨강` , `2 = 노랑` , `3 = 초록`
```abap
LOOP AT <itab> INTO <wa>.
  IF <조건1>.
    <wa>-<excp_field> = '1'.
  ELSEIF <조건2>.
    <wa>-<excp_field> = '2'.
  ELSE.
    <wa>-<excp_field> = '3'.
  ENDIF.

  MODIFY <itab> FROM <wa> TRANSPORTING <excp_field>.
ENDLOOP.

*&---------------------------------------------------------------------*

gs_layout-excp_fname = '<예외컬럼명>'.
gs_layout-excp_led   = 'X'.

*&---------------------------------------------------------------------*

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = <itab>.
```
```abap
********* TOP *********
DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: excp_fld        TYPE char1,          " 예외(신호등) 값
      col_fld         TYPE char4,
      lt_scol         TYPE lvc_t_scol,
      cancelled_icon  TYPE icon-id,
      phone           TYPE scustom-telephone,
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.

********* F01 *********
LOOP AT gt_data INTO gs_data.

  IF gs_data-loccuram < 500.
    gs_data-excp_fld = '1'.   " 🔴 빨강
  ELSEIF gs_data-loccuram <= 800.
    gs_data-excp_fld = '2'.   " 🟡 노랑
  ELSE.
    gs_data-excp_fld = '3'.   " 🟢 초록
  ENDIF.

  MODIFY gt_data FROM gs_data
    TRANSPORTING excp_fld.    " 예외 컬럼만 반영

  CLEAR gs_data.
ENDLOOP.

********* F01 (LAYOUT) *********
gs_layout-excp_fname = 'EXCP_FLD'.   " 예외 아이콘 컬럼명
gs_layout-excp_led   = 'X'.          " LED(신호등) 아이콘

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = gt_data.
```
</br>
</br>
</br>

### 4) ALV 초기 정렬 기준 설정 (lvc_s_sort)
- ALV 최초 출력 시 정렬 기준을 지정
- **`set_sorter_criteria` 는 이미 생성된 ALV Grid에 정렬 기준만 새로 적용하는 메서드**
</br>

- `lvc_s_sort` : 정렬 기준 1개, `lvc_t_sort` : 정렬 기준 여러 개를 담는 테이블
- `spos` : 정렬 우선순위 (1, 2, 3 …)
- `up = 'X'` : 오름차순, `down = 'X'` : 내림차순
- `SUBTOT = 'X'` : 해당 정렬 필드 기준으로 소계 라인 출력하며, DO_SUM = 'X' 로 지정된 합계 필드가 있어야 소계 값이 계산·표시됨
```abap
DATA: gt_sort TYPE lvc_t_sort,
      gs_sort TYPE lvc_s_sort.

gs_sort-fieldname = '<FIELD_NAME>'.
gs_sort-spos      = <순번>.
gs_sort-up        = 'X'.   " 또는 gs_sort-down = 'X'
APPEND gs_sort TO gt_sort.
CLEAR gs_sort.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab = <itab>
    it_sort   = gt_sort.
```
```abap
********* TOP *********
DATA: gt_sorter TYPE lvc_t_sort,    " 정렬 기준 테이블
      gs_sorter TYPE lvc_s_sort.    " 정렬 기준 구조

********* F01 *********
FORM set_sorter.
  IF gt_sorter IS INITIAL.
    gs_sorter-fieldname = 'ORDER_DATE'. " 1순위 정렬 필드
    gs_sorter-spos      = 1.            " 정렬 우선순위
    gs_sorter-up        = 'X'.          " 오름차순
    gs_sorter-subtot    = 'X'.          " 소계 표시
    APPEND gs_sorter TO gt_sorter.
    CLEAR gs_sorter.

    gs_sorter-fieldname = 'LOCCURAM'.   " 2순위 정렬 필드
    gs_sorter-spos      = 2.            " 정렬 우선순위
    gs_sorter-down      = 'X'.          " 내림차순
    APPEND gs_sorter TO gt_sorter.
  ENDIF.
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab        = gt_data
    it_sort          = gt_sorter.
```
</br>
</br>
</br>

### 5) ALV 초기 필터 조건 설정 (LVC_S_FILT)
- ALV 최초 출력 시 특정 조건으로 데이터 필터링
- **`set_filter_criteria` 는 이미 생성된 ALV Grid에 필터 조건만 새로 적용하는 메서드**
- `SY-REPID` 시스템 변수 → 현재 실행 중인 프로그램의 리포트(프로그램) ID를 담고 있는 시스템 필드
</br>

- `lvc_s_filt` : 필터 조건 1개
- `lvc_t_filt` : 필터 조건 여러 개를 담는 테이블
- `fieldname` 는 ALV에서 필터를 적용할 컬럼명을 의미
- `sign` 은 조건을 포함(I)할지 제외(E)할지를 결정
- `option` 은 비교 방식으로 EQ(같음), BT(범위), GE/LE(이상·이하) 등을 지정
- `low` 는 비교에 사용되는 기준 값이며, BT 사용 시 시작값. `high` 는 BT 옵션 사용 시 범위의 종료값.
```abap
DATA: gt_filter TYPE lvc_t_filt,
      gs_filter TYPE lvc_s_filt.

gs_filter-fieldname = '<FIELD_NAME>'.
gs_filter-sign      = 'I'.
gs_filter-option    = 'EQ'.
gs_filter-low       = <VALUE>.
APPEND gs_filter TO gt_filter.
CLEAR gs_filter.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab = <itab>
    it_filter = gt_filter.
```
```abap
********* TOP *********
DATA: gt_filter TYPE lvc_t_filt,    " ALV 필터 조건 테이블
      gs_filter TYPE lvc_s_filt.    " ALV 필터 조건 구조

SELECTION-SCREEN BEGIN OF SCREEN 1100 AS SUBSCREEN.
  SELECT-OPTIONS: so_car2 FOR SDYN_CONN-CARRID DEFAULT 'DL', " 항공사 필터
                  so_con2 FOR SDYN_CONN-CONNID.              " 노선 필터
SELECTION-SCREEN END OF SCREEN 1100.

********* START-OF-SELECTION *********
START-OF-SELECTION.
  PERFORM get_data.                 " 최초 데이터 조회
  CALL SCREEN 100.                  " 메인 화면 호출

********* (SCREEN 100) PROCESS BEFORE OUTPUT *********
PROCESS BEFORE OUTPUT.
  MODULE status_0100.                            " 상태바
  CALL SUBSCREEN SUB01 INCLUDING sy-repid '1100'. " 필터 입력
  MODULE init_alv.                               " ALV 출력

********* (SCREEN 100) PROCESS AFTER INPUT *********
PROCESS AFTER INPUT.
  MODULE exit AT EXIT-COMMAND.                   " 종료 처리
  CALL SUBSCREEN SUB01.                          " 필터 입력 반영
  MODULE user_command_0100.                      " SEARCH 처리

********* PAI *********
MODULE user_command_0100 INPUT.
  CASE ok_code.
    WHEN 'SEARCH'.
      PERFORM set_filter.           " 필터 재적용
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
  ENDCASE.
ENDMODULE.

********* F01 *********
FORM set_filter.
  CLEAR: gt_filter, gs_filter.

  IF so_car2 IS NOT INITIAL.
    LOOP AT so_car2.
      gs_filter-fieldname = 'CARRID'.        " 필터 컬럼
      gs_filter-sign      = so_car2-sign.    " 포함/제외
      gs_filter-option    = so_car2-option.  " EQ / BT
      gs_filter-low       = so_car2-low.     " 기준값
      gs_filter-high      = so_car2-high.    " 종료값
      APPEND gs_filter TO gt_filter.
    ENDLOOP.
  ENDIF.

  IF so_con2 IS NOT INITIAL.
    CLEAR gs_filter.
    LOOP AT so_con2.
      gs_filter-fieldname = 'CONNID'.        " 필터 컬럼
      gs_filter-sign      = so_con2-sign.
      gs_filter-option    = so_con2-option.
      gs_filter-low       = so_con2-low.
      gs_filter-high      = so_con2-high.
      APPEND gs_filter TO gt_filter.
    ENDLOOP.
  ENDIF.

  CALL METHOD go_alv->set_filter_criteria
    EXPORTING
      it_filter = gt_filter.

ENDFORM.

********* PBO (INIT_ALV 내부) *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab = gt_data
    it_filter = gt_filter.
```
</br>
</br>
</br>

### 6) ALV Line Color (행 색상 제어)
- ALV 행(Line)에 색상을 적용하는 기능
- `INFO_FNAME` : ALV에게 “행 색상(Line Color) 정보가 들어있는 필드명”을 알려주는 설정
- `TYPE-POOLS COL.` 로 색상 코드 확인 가능 (필수)
```abap
TYPE-POOLS COL.
'C' && <color_constant> && <intensified> && <inverse>
```
- `C` : 색상 사용 표시
- `<color_constant>` : 색상 번호 (0~7)
- `<intensified>` : 진하게 표시 (1) / 기본 (0)
- `<inverse>` : 글자색 ↔ 배경색 반전 (1) / 기본 (0)
```abap
********* TOP *********
TYPE-POOLS col.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: col_fld TYPE char4, "예시)'C310'
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM get_data.
    LOOP AT gt_data INTO gs_data.
      IF gs_data-smoker = 'X'.
        gs_data-col_fld = 'C' && col_negative && '10'.
      ENDIF.
      MODIFY gt_data FROM gs_data.
      CLEAR: gs_data.
    ENDLOOP.
  ENDIF.
ENDFORM.

********* F01 *********
FORM set_layout.
  gs_layout-info_fname = 'COL_FLD'. 
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout                     = gs_layout
  CHANGING
    it_outtab                     = gt_data
```
</br>
</br>
</br>

### 7) ALV Cell Color (셀 단위 색상 제어)
- ALV에서 특정 셀만 색상 변경하는 기능
- 행 색상과 달리 셀 색상용 내부테이블 사용
- 각 행마다 `lvc_t_scol` 타입 내부테이블을 가짐
- 색상 적용 후 `CTAB_FNAME` 으로 ALV에 전달해야 표시됨
- `CTAB_FNAME` : ALV에게 “셀 색상(Cell Color) 정보가 들어있는 필드명”을 알려주는 설정
</br>

- `lt_scol` : 이 행에서 색칠할 셀 목록 (`lvc_t_scol`)
- `ls_scol` : 셀 하나 색상 정보 (`lvc_s_scol`)
- `<ls_scol>-fname` : 색상을 적용할 컬럼(필드) 이름을 지정함.
- `<ls_scol>-color-col` : 해당 셀에 적용할 색상을 지정함.
- `<ls_scol>-color-int` : 색상을 진하게 표시할지 여부를 지정함.
- `<ls_scol>-color-inv` : 글자색과 배경색을 서로 바꿀지 여부를 지정함.
```abap
LVC_S_SCOL
 ├─ FNAME        "색칠할 컬럼명
 └─ COLOR        "색상 정보 구조체
     ├─ COL
     ├─ INT
     └─ INV
```
```abap
" 1. 출력 구조
DATA: BEGIN OF <gs_data>.
        INCLUDE TYPE <structure>.
DATA: <excp_fld> TYPE char1,
      <col_fld>  TYPE char4,        " Line Color
      <lt_scol>  TYPE lvc_t_scol,   " Cell Color
     END OF <gs_data>.
DATA <gt_data> LIKE TABLE OF <gs_data>.
DATA <ls_scol> TYPE lvc_s_scol.

" 2. 셀 색상 처리
LOOP AT <gt_data> INTO <gs_data>.
  IF <조건>.
    <ls_scol>-fname      = '<FIELDNAME>'.
    <ls_scol>-color-col  = <COLOR>.
    <ls_scol>-color-int  = '1'.
    <ls_scol>-color-inv  = '0'.
    APPEND <ls_scol> TO <gs_data>-<lt_scol>.
  ENDIF.
  MODIFY <gt_data> FROM <gs_data>
    TRANSPORTING <lt_scol>.
ENDLOOP.

" 3. Layout 연결
gs_layout-ctab_fname = '<lt_scol 필드명>'.

" 4. ALV 출력
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = <gt_data>.
```
```abap
********* TOP *********
TYPE-POOLS col.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: lt_scol TYPE lvc_t_scol,   " 셀 색상 정보 (행 단위)
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.
DATA ls_scol TYPE lvc_s_scol.    " 셀 1개 색상 구조
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM get_data.
  LOOP AT gt_data INTO gs_data.
    IF gs_data-smoker = 'X'.
      CLEAR ls_scol.
      ls_scol-fname = 'SMOKER'.      " 색칠할 컬럼
      ls_scol-color-col = '3'.       " 색상 번호
      ls_scol-color-int = '1'.       " 진하게
      ls_scol-color-inv = '0'.       " 반전 없음
      APPEND ls_scol TO gs_data-lt_scol.
    ENDIF.
    MODIFY gt_data FROM gs_data.
    CLEAR gs_data.
  ENDLOOP.
ENDFORM.

FORM set_layout.
  gs_layout-ctab_fname = 'LT_SCOL'.  
ENDFORM.

********* PBO *********
PERFORM set_layout.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = gt_data.
```
</br>
</br>
</br>

### 8) 결과 확인

<img width="536" height="370" alt="image" src="https://github.com/user-attachments/assets/e036f5f4-8676-4972-9727-f6f3954d72af" />

</br>
</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
- layout 실습
</br>

### 실습 1
</br>

- 문제

<img width="497" height="276" alt="image" src="https://github.com/user-attachments/assets/3d4d776c-d9ab-4b3a-ae11-d5e9062edb65" />
</br>
</br>
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.
DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.

START-OF-SELECTION.
  PERFORM get_data.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc < 50.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc >= 50 AND gs_flight-seatsocc <= 200.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout.
  gs_layout-grid_title = 'Flight List'.
  gs_layout-zebra = 'X'.
  gs_layout-sel_mode = 'C'.
  gs_layout-excp_fname = 'EXCP'.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
  CALL METHOD go_alv_grid->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'sflight'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = gt_flight
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      others                        = 4.
ENDMODULE.
```
</br>
</br>

**4) 결과 확인**

<img width="499" height="384" alt="image" src="https://github.com/user-attachments/assets/ac3e549d-a76c-48cd-b8b3-da963ca38b9a" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
</br>

- 문제

<img width="474" height="283" alt="image" src="https://github.com/user-attachments/assets/c6b45c6d-2851-41f2-97a9-1a145c147234" />

</br>
</br>
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_col TYPE LVC_T_SCOL,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
DATA: ls_col TYPE LVC_S_SCOL.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc < 50.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc >= 50 AND gs_flight-seatsocc <= 200.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+0(4) = sy-datum+0(4).
        gs_flight-color = 'C' && col_positive && 10.
      ENDIF.

      IF gs_flight-seatsmax - gs_flight-seatsocc  < 10.
        ls_col-fname = 'SEATSMAX'.
        ls_col-color-col = '6'.
        ls_col-color-int = '0'.
        ls_col-color-inv = '1'.
        APPEND ls_col TO gs_flight-lt_col.
        CLEAR ls_col.

        ls_col-fname = 'SEATSOCC'.
        ls_col-color-col = '6'.
        ls_col-color-int = '0'.
        ls_col-color-inv = '1'.

        APPEND ls_col TO gs_flight-lt_col.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp color lt_col.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-grid_title = 'Flight List'.
  gs_layout-zebra = 'X'.
  gs_layout-sel_mode = 'C'.
  gs_layout-excp_fname = 'EXCP'.
  gs_layout-info_fname = 'COLOR'.
  gs_layout-ctab_fname = 'LT_COL'.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'sflight'
        is_variant                    = gs_variant
        i_save                        = 'A'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_flight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE A012(BC405_408).
    ENDIF.
```
</br>
</br>

**4) 결과 확인**

<img width="399" height="308" alt="image" src="https://github.com/user-attachments/assets/fd86219f-6de5-48d8-8e4b-006378b0b7a9" />

</br>
</br>

# 12/30
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV 표준 툴바 버튼 제어**
- **2) ALV Field Catalog**
</br>

- **SELECT-OPTIONS는 `Selection Screen` 에서만 가능**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>


### 1) ALV 표준 툴바 버튼 제어
- ALV Grid에서 기본 제공되는 표준 버튼(필터, 합계 등)을 화면에서 숨김 → 기능은 유지되고 화면에서만 제거
</br>

- `CL_GUI_ALV_GRID` → ALV Grid 화면을 생성·제어하는 표준 클래스
- `UI_FUNCTIONS` → 숨길 Function Code들을 담는 내부테이블 타입
- `IT_TOOLBAR_EXCLUDING` → ALV에서 제외할 툴바 기능 전달 파라미터
- `GS_LAYOUT-NO_TOOLBAR` → ALV 툴바 영역 자체를 통째로 숨김 (툴바 자체 제거)
</br>

- `MC_FC_*` → ALV 개별 버튼 1개를 의미하는 Function Code
- `MC_MB_*` → 여러 기능이 묶인 메뉴 버튼 Function Code
- `MC_FC_EXCL_ALL` → ALV 툴바 전체 버튼을 숨기는 Function Code (버튼만 숨김)
```abap
********* TOP *********
DATA: <gt_excluded> TYPE ui_functions.

********* F01 *********
APPEND cl_gui_alv_grid=><function_code> TO <gt_excluded>.

********* PBO *********
go_alv_grid->set_table_for_first_display(
  EXPORTING
    i_structure_name     = '<DDIC 구조>'
    it_toolbar_excluding = <gt_excluded>
  CHANGING
    it_outtab            = <itab>
).
```
```abap
********* TOP *********
DATA gt_uifunc TYPE ui_functions.      " ALV 툴바 제외 기능 목록

********* F01 *********
APPEND cl_gui_alv_grid=>mc_fc_filter TO gt_uifunc.    " 필터(Filter) 버튼 숨김
APPEND cl_gui_alv_grid=>mc_mb_sum TO gt_uifunc.       " 합계/소계 메뉴 전체 숨김
APPEND cl_gui_alv_grid=>mc_fc_excl_all TO gt_uifunc.  " 모든 표준 툴바 버튼 숨김

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name     = 'SBOOK'
    it_toolbar_excluding = gt_uifunc
  CHANGING
    it_outtab            = gt_data.
```
</br>
</br>

### 2) ALV Field Catalog
- ALV 컬럼의 제목, 순서, 숨김, 아이콘·단위 등 화면 표시 방식을 제어하는 설정
- 구조에 없는 컬럼을 추가하거나, 기존 컬럼의 출력 형태를 변경하기 위해 사용
- `LVC_T_FCAT` → Field Catalog 전체를 담는 내부테이블
- `LVC_S_FCAT` → 컬럼 1개에 대한 속성 구조
</br>
</br>

**1) 기본 컬럼 정의 / DDIC 참조**
- `FIELDNAME` → 내부테이블의 컬럼명
- `REF_FIELD` → DDIC에서 참조할 필드명
- `REF_TABLE` → DDIC에서 참조할 테이블/구조명

**2) 컬럼 표시 방식 제어**
- `ICON` → `X` 면 아이콘으로 표시, 공백이면 일반 값
- `CHECKBOX` → `X` 면 값을 읽기 전용 체크박스로 표시하고, 공백이면 일반 값으로 표시
- `NO_OUT` → `X` 면 컬럼을 ALV 화면에서 숨김, 공백이면 정상 표시
- `TECH` → `X` 면 기술용 컬럼으로 처리하고, 공백이면 일반 사용자 컬럼으로 표시

**3) 컬럼 제목 / 위치 / 크기**
- `COLTEXT` → 컬럼 헤더에 표시될 제목 문자열, 미지정 시 DDIC 텍스트 사용
- `TOOLTIP` → 컬럼 헤더에 마우스를 올렸을 때 표시되는 설명 문구
- `COL_POS` → 컬럼의 출력 위치 번호, 숫자가 작을수록 왼쪽에 표시
- `OUTPUTLEN` → 컬럼 출력 폭(너비)

**4) 금액(Currency) / 수량(Unit) 포맷 설정**
- `CFIELDNAME` → 금액 컬럼에 대해 행별 통화 필드 지정
- `CURRENCY` → 금액 컬럼 전체에 고정 통화 코드 적용
- `QFIELDNAME` → 수량 컬럼에 대해 행별 단위 필드 지정
- `QUANTITY` → 수량 컬럼 전체에 고정 단위 적용

**5) 집계 / 키 / 인터랙션**
- `DO_SUM` → `X` 면 합계 라인을 생성하고, 공백이면 합계 계산 안 함
- `NO_SUM` → `X` 면 합계 기능을 비활성화하고, 공백이면 합계 가능
- `KEY` → `X` 면 키 컬럼으로 고정 표시되고, 공백이면 일반 컬럼
- `HOTSPOT` → `X` 면 클릭 이벤트가 발생하고, 공백이면 일반 텍스트 컬럼

**6) 강조 / 값 표시 포맷**
- `EMPHASIZE` → 컬럼 값을 색상·강조·반전 효과로 표시
- `DECIMALS_O` → 표시할 소수점 자릿수 지정
- `DECMLFIELD` → 소수점 자릿수를 행별 필드로 제어
- `ROUND` → `X` 면 반올림 처리하고, 공백이면 반올림 안 함
- `ROUNDFIELD` → 반올림 자릿수를 행별 필드로 제어
- `EDIT_MASK` → 출력 시 편집 마스크 적용
- `JUST` → 값 정렬 방식(L/R/C) 지정
- `LZERO` → `X` 면 앞의 0을 표시하고, 공백이면 0 제거
- `NO_ZERO` → `X` 면 값이 0일 때 표시하지 않고, 공백이면 0 표시
- `NO_SIGN` → `X` 면 부호(+/–)를 숨기고, 공백이면 부호 표시
  
</br>
</br>
</br>

**1) 전역 구조(DDIC) 필드 그대로 사용하는 경우**
- `FIELDNAME` 만 지정
- `REF_FIELD` / `REF_TABLE` 자동
  
**2) 전역 구조 필드 + 속성 조정하는 경우**
- `FIELDNAME` + `REF_FIELD` + `REF_TABLE` 지정
- 길이·텍스트 등만 추가 설정
  
**3) 로컬 타입(내부 정의 타입) 사용하는 경우**
- `FIELDNAME` 만 사용
- `REF_FIELD` / `REF_TABLE` 사용 ❌
- 컬럼 속성 전부 직접 정의
</br>
</br>

```abap
********* TOP *********
TYPE-POOLS: icon.

TYPES: BEGIN OF <gs_type>.
  INCLUDE TYPE <DDIC_STRUCTURE>.
  <add_field_1> TYPE <ddic_type_1>.
  <add_field_2> TYPE <ddic_type_2>.
END OF <gs_type>.
DATA: <gt_outtab> TYPE TABLE OF <gs_type>.

" Field Catalog 선언
DATA: <gt_fcat> TYPE lvc_t_fcat,
      <gs_fcat> TYPE lvc_s_fcat.

********* F01 *********
<gs_fcat>-fieldname = '<ADD_FIELD_1>'.      " 추가 컬럼 1
<gs_fcat>-ref_field = '<REF_FIELD>'.
<gs_fcat>-ref_table = '<REF_TABLE>'.
<gs_fcat>-col_pos   = <N>.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

<gs_fcat>-fieldname = '<ADD_FIELD_2>'.      " 추가 컬럼 2 (아이콘 등 표현 변경)
<gs_fcat>-icon      = 'X'.
<gs_fcat>-coltext   = '<TEXT>'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

<gs_fcat>-fieldname = '<ORIGINAL_FIELD>'.   " 기존 컬럼 숨김
<gs_fcat>-no_out    = 'X'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

********* PBO *********
<go_alv>->set_table_for_first_display(
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <gt_outtab>
    it_fieldcatalog  = <gt_fcat>
).
```
```abap
********* TOP *********
TYPE-POOLS: icon.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA:   cancelled_icon TYPE icon-id,           " 취소 아이콘
        phone          TYPE scustom-telephone,  " 전화번호
      END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.

DATA: BEGIN OF ls_custom.
        id        TYPE s_customer,
        telephone TYPE s_phoneno,
      END OF ls_custom.
DATA lt_custom LIKE TABLE OF ls_custom.

DATA: gt_fcat TYPE lvc_t_fcat,                 " Field Catalog itab
      gs_fcat TYPE lvc_s_fcat.                 " Field Catalog str

********* F01 (데이터 조회) *********
SELECT *
  FROM sbook
  INTO CORRESPONDING FIELDS OF TABLE gt_data.

SELECT id telephone
  FROM scustom
  INTO TABLE lt_custom
  FOR ALL ENTRIES IN gt_data
  WHERE id = gt_data-customid
    AND telephone <> space.

********* F01 (데이터 가공) *********
LOOP AT gt_data INTO gs_data.

  IF gs_data-cancelled = 'X'.                  " 취소 여부 아이콘
    gs_data-cancelled_icon = icon_cancel.
  ENDIF.

  READ TABLE lt_custom INTO ls_custom           " 전화번호 매핑
       WITH KEY id = gs_data-customid.
  IF sy-subrc = 0.
    gs_data-phone = ls_custom-telephone.
  ENDIF.

  MODIFY gt_data FROM gs_data
    TRANSPORTING cancelled_icon phone.

  CLEAR gs_data.
ENDLOOP.

********* F01 (Field Catalog 설정) *********
CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED_ICON'.   " 취소 아이콘 표시
gs_fcat-icon      = 'X'.
gs_fcat-coltext   = 'Cancelled'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED'.        " 기존 취소 컬럼 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSFORM'.         " 여권 형식 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSBIRTH'.        " 여권 생년월일 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PHONE'.            " 전화번호 표시
gs_fcat-ref_field = 'TELEPHONE'.
gs_fcat-ref_table = 'SCUSTOM'.
gs_fcat-col_pos   = 4.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'SMOKER'.           " 체크박스
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'INVOICE'.          " 체크박스
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CLASS'.            " 컬럼 강조 표시
gs_fcat-emphasize = 'C510'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'LOCCURAM'.          " 합계 적용
gs_fcat-do_sum    = 'X'.
APPEND gs_fcat TO gt_fcat.

********* PBO *********
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab        = gt_data
    it_fieldcatalog  = gt_fcat.
```

- 결과출력
  
<img width="626" height="343" alt="image" src="https://github.com/user-attachments/assets/3918147c-8030-496b-8685-9a8cb5eca11b" />

</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- layout 실습
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.
DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
DATA: ls_field_colors TYPE LVC_S_SCOL.

START-OF-SELECTION.
  PERFORM get_data.
  CALL SCREEN 100.
```
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_F01
*&---------------------------------------------------------------------*

TYPE-POOLS COL.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp color lt_field_colors.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-info_fname = 'COLOR'.
  gs_layout-ctab_fname = 'LT_FIELD_COLORS'.
  gs_layout-no_hgridln = 'X'.
  gs_layout-no_vgridln = 'X'.
ENDFORM.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
  CALL METHOD go_alv_grid->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'sflight'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = gt_flight
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      others                        = 4.
ENDMODULE.
```
</br>

**4) `PAI` 모듈**
```abap
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
      PERFORM free_alv.
    WHEN 'CANCEL'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```

</br>

**5) 결과 출력**

<img width="433" height="332" alt="image" src="https://github.com/user-attachments/assets/738970ec-5c0c-419b-955d-116a19b81e09" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
- Field Catalog 실습
- 위 코드 이어서 작성
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_TOP
*&---------------------------------------------------------------------*

DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      changes_possible TYPE icon-id,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
```
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_F01
*&---------------------------------------------------------------------*

TYPE-POOLS: COL, ICON.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc = 0.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc < 50.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-fldate < sy-datum.
        gs_flight-changes_possible = ICON_SPACE.
      ELSE.
        gs_flight-changes_possible = ICON_OKAY.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight
        TRANSPORTING excp color lt_field_colors changes_possible.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSOCC'.
  gs_fcat-do_sum = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'PAYMENTSUM'.
  gs_fcat-no_out = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CHANGES_POSSIBLE'.
  gs_fcat-icon = 'X'.
  gs_fcat-coltext = 'Changes Possible'.
  gs_fcat-tooltip = 'Are changes still possible?'.
  gs_fcat-col_pos = 5.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'EXCP'.
  gs_fcat-coltext = 'traffic light'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
PERFORM set_fcat.
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
    is_variant                    = gs_variant
    i_save                        = 'A'
    is_layout                     = gs_layout
  CHANGING
    it_outtab                     = gt_flight
    IT_FIELDCATALOG               = gt_fcat
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
IF sy-subrc <> 0.
  MESSAGE A012(BC405_408).
ENDIF.
```
</br>

**4) 결과 출력**

<img width="530" height="403" alt="image" src="https://github.com/user-attachments/assets/fee92021-0c8d-45a3-8159-d042cbf869df" />

</br>
</br>
</br>
</br>
</br>
</br>

### 실습 3
</br>

- 문제

<img width="485" height="288" alt="image" src="https://github.com/user-attachments/assets/b550caff-ae1b-44b1-b987-1829e83d7599" />
</br>
</br>
</br>

**1) `Program` 코드 생성**
```abap
INCLUDE ZBC400_B03_PRACTICE7_TOP.
INCLUDE ZBC400_B03_PRACTICE7_F01.
INCLUDE ZBC400_B03_PRACTICE7_O01.
INCLUDE ZBC400_B03_PRACTICE7_I01.

AT SELECTION-SCREEN ON BLOCK blk1.
  IF so_car IS INITIAL AND so_con IS INITIAL AND so_fdt IS INITIAL.
    MESSAGE '전부 입력하지 않았습니다.' TYPE 'E'.
  ENDIF.

START-OF-SELECTION.
  PERFORM set_data.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm.

DATA: go_cont TYPE REF TO cl_gui_custom_container,
      go_alv TYPE REF TO cl_gui_alv_grid.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE ZSCARRFLIGHT_B03.
DATA: seatsrem TYPE INT4,
      class TYPE sbook-CLASS,
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data,
      gt_sflight TYPE TABLE OF sflight,
      gt_scarr TYPE TABLE OF scarr,
      gs_scarr LIKE LINE OF gt_scarr,
      gt_sbook TYPE TABLE OF sbook,
      gs_sbook LIKE LINE OF gt_sbook.

DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT,
      gt_sort TYPE LVC_T_SORT,
      gs_sort TYPE LVC_S_SORT.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_F01
*&---------------------------------------------------------------------*

FORM set_data.
  SELECT *
    FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid IN so_car
      AND connid IN so_con
      AND fldate IN so_fdt.

  SELECT *
    FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE gt_scarr
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid.

  SELECT *
    FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE gt_sbook
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid
      AND connid = gt_data-connid
      AND fldate = gt_data-fldate.
  SORT gt_sbook.  " 첫번째 인덱스의 class를 가져오므로

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid
      TRANSPORTING carrname currcode.
    READ TABLE gt_sbook INTO gs_sbook
      WITH KEY carrid = gs_data-carrid
               connid = gs_data-connid
               fldate = gs_data-fldate
      TRANSPORTING class.
    gs_data-class = gs_sbook-class.
    gs_data-carrname = gs_scarr-carrname.
    gs_data-currcode = gs_scarr-currcode.
    gs_data-seatsrem = gs_data-seatsmax - gs_data-seatsocc.
    MODIFY gt_data FROM gs_data TRANSPORTING carrname currcode seatsrem class.
    CLEAR gs_data.
  ENDLOOP.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSREM'.
  gs_fcat-coltext = 'Seats Remain'.
  gs_fcat-col_pos = 3.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CLASS'.
  gs_fcat-ref_field = 'CLASS'.
  gs_fcat-ref_table = 'SBOOK'.
  gs_fcat-col_pos = 2.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'PRICE'.
  gs_fcat-do_sum = 'X'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_sort.
  CLEAR gs_sort.
  gs_sort-fieldname = 'CLASS'.
  gs_sort-spos = 1.
  gs_sort-up = 'X'.
  gs_sort-subtot = 'X'.
  APPEND gs_sort TO gt_sort.
ENDFORM.
```
</br>

**4) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
* SET TITLEBAR 'xxx'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    PERFORM set_fcat.
    PERFORM set_sort.
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'ZSCARRFLIGHT_B03'
      CHANGING
        it_outtab                     = gt_data
        it_fieldcatalog               = gt_fcat
        it_sort                       = gt_sort.
    IF sy-subrc <> 0.
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**6) 결과 확인**

<img width="500" height="146" alt="image" src="https://github.com/user-attachments/assets/326940de-3a7a-4697-9af2-cb232651886c" />
</br>

<img width="515" height="357" alt="image" src="https://github.com/user-attachments/assets/765b20d2-30a5-4a6a-a180-8d5741bd635e" />

</br>
</br>

# 12/31
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV Grid 이벤트 처리 정리 (`CLASS-METHODS`)**
- **2) ALV Hotspot 이벤트 처리**
</br>

- **ALV 핫스팟 클릭은 OKCODE가 없어서 PAI를 안 탐**
- **`i_structure_name` 는 DDIC(Dictionary)에 정의된 구조만 가능**
</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

### 1) ALV Grid 이벤트 처리 정리
- ALV 화면에서 발생하는 사용자 동작을 처리
- `CL_GUI_ALV_GRID` : ALV Grid 이벤트를 제공하는 클래스
</br>

- `Instance Attribute` → 객체마다 메모리 가지는 속성 (DATA)
- `Instance Method` → 객체마다 호출되는 메서드 (METHODS)
- `Static Attribute` → 클래스마다 하나만 존재하는 속성 (CLASS-DATA)
- `Static Method` → 클래스마다 하나만 존재하는 메서드 (CLASS-METHODS)
- `Constants` → 값 변경 불가한 고정값 (CONSTANTS)
</br>
</br>

**< ALV Grid 이벤트 / 파라미터 >**
- 이벤트: 언제 발생했는가
- 파라미터: 발생했을 때 어떤 정보가 넘어오는가

| 이벤트 | 설명 |
|--------|------|
| `DOUBLE_CLICK` | 셀을 더블클릭했을 때 발생 |
| `PRINT_TOP_OF_PAGE` | 출력 시 페이지 상단 처리 |
| `USER_COMMAND` | ALV 버튼 클릭 시 발생 |
| `TOOLBAR` | ALV 툴바 생성 시 발생 |
  
| 파라미터 | 설명 |
|----------|------|
| `ES_ROW_NO` | 화면 기준 행 번호 (정렬/필터 적용 후 기준) |
| `E_ROW_ID` | 내부 테이블 기준 행 인덱스 |
| `E_COLUMN` | 더블클릭한 컬럼 정보 (`fieldname`) |
| `E_COLUMN_ID` | 핫스팟 클릭한 컬럼명 |
| `E_UCOMM` | 눌린 버튼의 Function Code |
| `E_OBJECT` | 툴바 버튼 추가/제어 객체 |
| `E_INTERACTIVE` | 인터랙티브 ALV 여부 |
| `SENDER` | 이벤트를 발생시킨 ALV 객체 |
| `ER_DATA_CHANGED` | 변경된 셀 정보 객체 |
| `E_MODIFIED` | 데이터 변경 발생 여부 |
| `E_FIELDNAME` | F4 도움말이 호출된 필드명 |
| `E_FIELDVALUE` | F4 호출 시 필드의 현재 값 |
| `ER_EVENT_DATA` | 이벤트 제어 객체 (F4 처리 등) |
| `E_ONF4` | F4 도움말 호출 여부 |
| `E_ONF4_BEFORE` | F4 호출 이전 시점 |
| `E_ONF4_AFTER` | F4 호출 이후 시점 |

</br>
</br>

**1) CLASS-METHODS (정적 메서드)**
- 객체 생성 없이 클래스명으로 바로 호출
- 공통 로직, 이벤트 핸들러에 자주 사용
- 인스턴스 속성 사용 불가

**2) METHODS (인스턴스 메서드)**
- 객체 생성(CREATE OBJECT) 후에만 호출
- 객체별 인스턴스 속성 사용 가능
- 객체 상태(속성)를 사용하는 로직에 사용
</br>
</br>

- `CLASS-METHODS` : 정적(static) 메서드,  `METHODS` : 인스턴스 메서드
- `<handler_class>` : 이벤트 핸들러 클래스명
- `<handler_method>` : 이벤트 처리 메소드명
- `<event_name>` : 발생 이벤트명
- `<event_class>` : 이벤트를 가진 클래스
- `<param_1>, <param_2>` : 이벤트 파라미터
```abap
*************** C01 ****************
CLASS <handler_class> DEFINITION.
  [PUBLIC | PROTECTED | PRIVATE] SECTION.
    [CLASS-]METHODS <handler_method>
      FOR EVENT <event_name> OF <event_class>
      IMPORTING
        <param_1>
        <param_2>
        sender.
ENDCLASS.

CLASS <handler_class> IMPLEMENTATION.
  METHOD <handler_method>.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER <handler_class>=><handler_method> FOR <event_object>.

CALL METHOD <event_object>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC 구조>'
  CHANGING
    it_outtab        = <itab>
```
- `SENDER` → 이벤트를 발생시킨 객체(오브젝트) 참조
```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_DOUBLE_CLICK
    FOR EVENT DOUBLE_CLICK OF CL_GUI_ALV_GRID
    IMPORTING ES_ROW_NO E_COLUMN SENDER.
  " CL_GUI_ALV_GRID 클래스의 더블클릭 이벤트가 발생하면
  " ON_DOUBLE_CLICK 이라는 메서드를 실행하겠다
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  "더블클릭 이벤트의 메서드 구현
  METHOD ON_DOUBLE_CLICK.
    IF SENDER = GO_ALV.
      MESSAGE I004(ZMCB03) WITH ES_ROW_NO-row_id        " 더블클릭된 ALV 행 번호
                                E_COLUMN-fieldname.     " 더블클릭된 ALV 컬럼명
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_double_click FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name     = 'SBOOK'
  CHANGING
    it_outtab            = gt_data
```
</br>
</br>

### 2) ALV Hotspot 이벤트 처리
- ALV 컬럼을 클릭 가능한 링크(밑줄)로 표시하는 기능
- 필드카탈로그 생성 시(PBO) `gs_fcat-hotspot = 'X'` 로 적용
```abap
*************** F01 ****************
CLEAR gs_fcat.
gs_fcat-fieldname = '<HOTSPOT_FIELD>'.
gs_fcat-hotspot   = 'X'.      " 클릭 가능한 링크(밑줄) 처리
APPEND gs_fcat TO gt_fcat.

*************** C01 ****************
CLASS <handler_class> DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS <handler_method>
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING e_row_id e_column_id sender.
ENDCLASS.

CLASS <handler_class> IMPLEMENTATION.
  METHOD <handler_method>.
    " e_row_id-row_id        : 클릭된 행 번호
    " e_column_id-fieldname : 클릭된 컬럼명
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER <handler_class>=><handler_method> FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab        = <itab>
    it_fieldcatalog  = gt_fcat.
```
```abap
*************** F01 ****************
CLEAR gs_fcat.
gs_fcat-fieldname = 'PHONE'.   " 핫스팟 적용 컬럼
gs_fcat-hotspot   = 'X'.       " 핫스팟(링크) 활성화
APPEND gs_fcat TO gt_fcat.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_hotspot_click
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING
        es_row_no     " 클릭된 행 정보 (row_id)
        e_column_id  " 클릭된 컬럼 정보 (fieldname)
        sender.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_hotspot_click.
    IF sender = go_alv.
      MESSAGE I004(ZMCB03)
        WITH es_row_no-row_id        " 클릭된 행 번호
             e_column_id-fieldname. " 클릭된 컬럼명
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>
</br>

- 문제
- 전 exercise 코드에서 추가
- CLASS-METHODS, Hotspot 실습
<img width="374" height="208" alt="image" src="https://github.com/user-attachments/assets/90e9c079-c318-4291-9848-07923ecc223d" />
</br>
</br>

**1) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_F01
*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSOCC'.
  gs_fcat-do_sum = 'X'.
  gs_fcat-hotspot = 'X'.    " 핫스팟 추가
  APPEND gs_fcat TO gt_fcat.
ENDFORM.
```
</br>

**2) `C01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_C01_3
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_double_click
      FOR EVENT double_click OF cl_gui_alv_grid
      IMPORTING es_row_no sender.

    CLASS-METHODS: on_hotspot_click
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING es_row_no sender.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_double_click.
    IF sender = go_alv_grid.
      DATA: total_seat TYPE int4.

      READ TABLE gt_flight INTO gs_flight INDEX es_row_no-row_id.
      IF sy-subrc <> 0.
        MESSAGE i075(bc405_408).
        EXIT.
      ENDIF.

      total_seat = gs_flight-seatsocc + gs_flight-seatsocc_b + gs_flight-seatsocc_f.
      MESSAGE 'Total:' && total_seat TYPE 'I'.
    ENDIF.
  ENDMETHOD.

  METHOD on_hotspot_click.
    IF sender = go_alv_grid.
      READ TABLE gt_flight INTO gs_flight INDEX es_row_no-row_id.
      IF gs_flight-excp = '1'.
        MESSAGE '빨간색 입니다.' TYPE 'I'.
      ELSEIF gs_flight-excp = '2'.
        MESSAGE '노란색 입니다.' TYPE 'I'.
      ELSE.
        MESSAGE '초록색 입니다.' TYPE 'I'.
      ENDIF.
    ENDIF.
  ENDMETHOD.
endclass.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_O01
*&---------------------------------------------------------------------*

MODULE create_and_transfer OUTPUT.
    SET HANDLER lcl_event_handler=>on_double_click FOR go_alv_grid.
    SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_flight
        IT_FIELDCATALOG               = gt_fcat
ENDMODULE.
```
</br>

**4) 결과 확인**
- 더블클릭시 이벤트
<img width="513" height="394" alt="image" src="https://github.com/user-attachments/assets/5e605617-f7f0-43bf-b979-bc7aaa143583" />
</br>

- 핫스팟 설정
<img width="515" height="394" alt="image" src="https://github.com/user-attachments/assets/1e321642-c072-4eb0-a2e1-7458d965dd97" />

</br>
</br>

# 01/02
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV Grid 이벤트 처리 정리 (`CLASS-METHODS`)**
- **2) ALV Hotspot 이벤트 처리**
</br>

- **C01(INCLUDE)은 TOP 바로 아래에 두는 게 정석**
</br>
</br>

---

</br>

## < ALV 이벤트 정리 >
</br>

### 1) ALV 표준 툴바 확장 (TOOLBAR 이벤트)
- `TOOLBAR` : 툴바에 버튼을 추가/구성
- `USER_COMMAND` : 버튼 클릭 시 동작 처리
  
| 값 | 의미 |
|----|------|
| `0` | 일반 버튼 |
| `1` | 메뉴 + 표준 버튼 |
| `2` | 메뉴 |
| `3` | 구분선 (Separator) |
| `4` | 선택 버튼 |
| `5` | 체크박스 버튼 |
| `6` | 메뉴 엔트리 |

```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_toolbar
      FOR EVENT toolbar OF cl_gui_alv_grid
      IMPORTING e_object e_interactive sender.

    CLASS-METHODS on_user_command
      FOR EVENT user_command OF cl_gui_alv_grid
      IMPORTING e_ucomm sender.
ENDCLASS.


CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE stb_button.

    CLEAR ls_button.
    ls_button-function  = '<FUNC_CODE>'.
    ls_button-icon      = <ICON>.
    ls_button-text      = '<TEXT>'.
    ls_button-quickinfo = '<QUICKINFO>'.
    ls_button-butn_type = '0'.

    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.


  METHOD on_user_command.
    CASE e_ucomm.
      WHEN '<FUNC_CODE>'.
        " 버튼 클릭 시 처리 로직
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_toolbar      FOR go_alv.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC 구조>'
  CHANGING
    it_outtab        = <itab>.

```
```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_TOOLBAR FOR EVENT TOOLBAR
    OF CL_GUI_ALV_GRID IMPORTING E_OBJECT,
      ON_USER_COMMAND FOR EVENT USER_COMMAND
    OF CL_GUI_ALV_GRID IMPORTING E_UCOMM.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD ON_TOOLBAR.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'CUSTOM_TB'.
    ls_button-icon = ICON_FLIGHT.
    ls_button-quickinfo = 'This is tooltip~~'.
    ls_button-butn_type = 0.
    ls_button-text = 'flight'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD ON_USER_COMMAND.
    DATA: lv_row TYPE I,
          lv_col TYPE I.

    CASE E_UCOMM.
      WHEN 'CUSTOM_TB'.
        MESSAGE 'flight' TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SBOOK'
  CHANGING
    it_outtab                     = gt_data
```
</br>
</br>
</br>

### 2) ALV 셀 버튼(Button) 동적 생성 및 클릭 이벤트 처리
- 조건에 따라 ALV 특정 셀을 버튼으로 표시하고, 버튼 클릭 시 해당 행 데이터를 기준으로 동작을 처리
- `LVC_S_STYL` → ALV 셀 단위 스타일(버튼/입력불가 등) 정의 구조
- `LVC_T_STYL` → 셀 스타일을 여러 개 담는 내부테이블(행·컬럼별 적용용)
</br>

- ALV에서 셀 단위로 버튼/비활성 등 UI 스타일을 다르게 적용하기 위해 사용하는 방식
- `cl_gui_alv_grid=>mc_style_button` → ALV 셀을 “클릭 가능한 버튼 형태” 로 표시하는 스타일 상수
- `gs_cell_style-fieldname` → 버튼/스타일을 적용할 ALV 컬럼(필드)명 지정
- `gs_cell_style-style` → 해당 컬럼 셀에 적용할 UI 스타일(버튼, 비활성 등) 지정
- `gs_layout-stylefname` → ALV 데이터 구조에서 셀 스타일 정보를 담고 있는 필드명 연결
</br>

<img width="464" height="222" alt="image" src="https://github.com/user-attachments/assets/ceb7e53e-2aa1-451a-a292-50d109b49cc8" />
</br>
</br>

```abap
*************** TOP ****************
DATA: BEGIN OF gs_data.
        INCLUDE TYPE <DB_STRUCTURE>.
DATA: <BUTTON_FIELD> TYPE c LENGTH 10,
      <STYLE_FIELD>  TYPE lvc_t_styl,
     END OF gs_data.

DATA: gt_data       TYPE STANDARD TABLE OF gs_data,
      gs_cell_style TYPE lvc_s_styl.


*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_button_click
      FOR EVENT button_click OF cl_gui_alv_grid
      IMPORTING es_row_no es_col_id.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_button_click.
    IF es_col_id-fieldname = '<BUTTON_FIELD>'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.


*************** F01 ****************
FORM set_button.
  LOOP AT gt_data INTO gs_data.
    gs_cell_style-fieldname = '<BUTTON_FIELD>'.
    gs_cell_style-style     = cl_gui_alv_grid=>mc_style_button.
    APPEND gs_cell_style TO gs_data-<STYLE_FIELD>.
    gs_data-<BUTTON_FIELD> = '<BUTTON_TEXT>'.
    MODIFY gt_data FROM gs_data.
  ENDLOOP.
ENDFORM.


*************** PBO ****************
SET HANDLER lcl_event_handler=>on_button_click FOR <ALV_OBJECT>.
gs_layout-stylefname = '<STYLE_FIELD>'.

CALL METHOD <ALV_OBJECT>->set_table_for_first_display
  CHANGING it_outtab = gt_data.
```
```abap
*************** TOP ****************
DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: display_bookings TYPE c LENGTH 10,   " 버튼 텍스트
      it_cell_style    TYPE LVC_T_STYL,     " 셀 스타일
     END OF gs_data.
DATA gs_cell_style TYPE LVC_S_STYL.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: on_button_click
    FOR EVENT button_click OF CL_GUI_ALV_GRID
    IMPORTING ES_ROW_NO ES_COL_ID.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_button_click.
    IF ES_COL_ID-fieldname = 'DISPLAY_BOOKINGS'.
      READ TABLE gt_data INTO gs_data INDEX es_row_no-row_id.
      MESSAGE gs_data-class TYPE 'I'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** F01 ****************
FORM get_data.
  SELECT * FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE gt_data.

  LOOP AT gt_data INTO gs_data.
    IF gs_data-loccuram >= 800.
      " excp_fld 초록일 때, 버튼도 표시하도록 함
      gs_cell_style-fieldname = 'DISPLAY_BOOKINGS'.
      gs_cell_Style-style = cl_gui_alv_grid=>MC_STYLE_BUTTON.
      APPEND gs_cell_style TO gs_data-it_cell_style.
      gs_data-display_bookings = 'Detail'. " 버튼Text
    ENDIF.

    MODIFY gt_data FROM gs_data.
    CLEAR: gs_data, gs_cell_style.
  ENDLOOP.
ENDFORM.


FORM set_layout.
  gs_layout-stylefname = 'IT_CELL_STYLE'.
ENDFORM.


FORM set_fieldcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'DISPLAY_BOOKINGS'.
  gs_fcat-col_pos = 3.
  gs_fcat-coltext = 'Display Detail'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_button_click FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout        = gs_layout
  CHANGING
    it_outtab        = gt_data
    it_fieldcatalog  = gt_fcat.
```
</br>
</br>
</br>

### 3) ALV USER_COMMAND 실행 전 제어 (before_user_command)
- UUSER_COMMAND보다 먼저 동일한 UCOMM을 받아 실행 여부를 제어하는 이벤트
- `before_user_command` → ALV에서 사용자 액션이 발생했을 때, USER_COMMAND가 처리되기 직전에 먼저 호출되는 이벤트
- `e_ucomm` → 클릭된 버튼의 Function Code
```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_before_user_command
      FOR EVENT before_user_command OF cl_gui_alv_grid
      IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_before_user_command.
    CASE e_ucomm.
      WHEN <FUNCTION_CODE>.
        MESSAGE '실행 전 제어' TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_before_user_command FOR go_alv.
```
</br>
</br>

---

</br>

## < ALV Control 주요 메소드 및 추가적인 이벤트 정리 >
</br>

- 메서드 = 객체의 상태를 조회하거나 동작을 수행하는 클래스 내부 함수

| Method | 설명 |
|------|------|
| **get_current_cell** | 현재 커서가 위치한 셀의 좌표와 값 반환 |
| **get_selected_cells** | 사용자가 선택한 셀들의 좌표와 값 반환 |
| **get_selected_columns** | 선택된 컬럼 이름 목록 반환 |
| **get_selected_rows** | 선택된 행 번호(Row Index) 반환 |
| **get_subtotals** | ALV Grid에 설정된 현재 소계(Subtotal) 정보 반환 |
| **set_toolbar_interactive** | ALV 툴바 이벤트를 강제로 발생시킴 |
| **set_user_command** | 현재 USER-COMMAND 값을 강제로 덮어씀 |
</br>
</br>

### 1) get_current_cell
- 현재 커서가 위치한 ALV 셀의 행·컬럼 인덱스를 가져오는 메서드
```abap
*************** C01 ****************
CALL METHOD <alv_grid>->get_current_cell
  IMPORTING
    e_row = <row_index>
    e_col = <col_index>.
```
- 커스텀 툴바 버튼(`CUSTOM_TB`) 클릭 시 현재 커서가 있는 ALV 셀의 행·컬럼을 조회
```abap
*************** C01 ****************
CLASS-METHODS on_user_command
  FOR EVENT user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.

METHOD on_user_command.
  DATA: lv_row TYPE i,   " 현재 행 번호
        lv_col TYPE i.   " 현재 컬럼 번호

  CASE e_ucomm.
    WHEN 'CUSTOM_TB'.   " 툴바 버튼 클릭 시
      CALL METHOD go_alv->get_current_cell
        IMPORTING
          e_row = lv_row
          e_col = lv_col.
      MESSAGE 'Row: ' && lv_row && ', Col: ' && lv_col TYPE 'I'.
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
```
</br>
</br>
</br>

### 2) set_user_command
- USER_COMMAND 값을 강제로 세팅하여 해당 USER_COMMAND 이벤트를 실행시키는 메서드

<img width="34" height="29" alt="image" src="https://github.com/user-attachments/assets/232a14e6-7938-429f-9d88-57338d1bd4d0" />
</br>

```abap
*************** C01 ****************
CALL METHOD <alv_grid>->set_user_command
  EXPORTING
    i_ucomm = <FUNCTION_CODE>.
```
```abap
*************** C01 ****************
CLASS-METHODS on_before_user_command
  FOR EVENT before_user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.
CLASS-METHODS on_user_command
  FOR EVENT user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.

METHOD on_before_user_command.
  CASE e_ucomm.
    WHEN go_alv->mc_fc_detail.   " ALV 표준 Detail 버튼 클릭 시
      CALL METHOD go_alv->set_user_command
        EXPORTING
          i_ucomm = 'SHOW_BOOK'.  " USER_COMMAND 강제 세팅
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
METHOD on_user_command.
  CASE e_ucomm.
    WHEN 'SHOW_BOOK'.            " set_user_command로 유도된 명령
      MESSAGE 'Show-book 클릭되었습니다' TYPE 'I'.
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
```
</br>
</br>
</br>

### 3) get_selected_rows
- ALV에서 사용자가 선택(체크/드래그)한 행들의 인덱스 목록을 가져오는 메서드
```abap
*************** C01 ****************
CALL METHOD <alv_grid>->get_selected_rows
  IMPORTING
    et_index_rows = <selected_rows>.
```
```abap
*************** PAI ****************
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'SELECT'.
      DATA: lt_select_data TYPE LVC_T_ROW,
            ls_select_data TYPE LVC_S_ROW.
      DATA: lt_data LIKE gt_data,
            ls_data LIKE gs_data.

      CALL METHOD go_alv->get_selected_rows
        IMPORTING
          et_index_rows = lt_select_data.

      IF lt_select_data IS NOT INITIAL.
        LOOP AT lt_select_data INTO ls_select_data.
          READ TABLE gt_data INTO ls_data
          INDEX ls_select_data-index.
          IF sy-subrc = 0.
            APPEND ls_data TO lt_data.
          ENDIF.
        ENDLOOP.
        cl_demo_output=>display( lt_data ).
      ELSE.
        MESSAGE 'Please select record' TYPE 'E'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 새로 구성하는 실습
- alv 2개, hotspot 실습
  
<img width="384" height="223" alt="image" src="https://github.com/user-attachments/assets/e891f22d-5304-45f1-b709-97a0ac023e6f" />
</br>
</br>
</br>

**1) `Report program` 에서 `Include` 파일 생성**
```abap
*&---------------------------------------------------------------------*
*& Report ZALV_SPFLI_B03
*&---------------------------------------------------------------------*

INCLUDE ZALV_SPFLI_B03_TOP.
INCLUDE ZALV_SPFLI_B03_C01.
INCLUDE ZALV_SPFLI_B03_O01.
INCLUDE ZALV_SPFLI_B03_I01.
INCLUDE ZALV_SPFLI_B03_F01.

START-OF-SELECTION.
  PERFORM get_data.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm.
DATA: go_cont TYPE REF TO cl_gui_custom_container,
      go_alv TYPE REF TO cl_gui_alv_grid,
      go_cont2 TYPE REF TO cl_gui_custom_container,
      go_alv2 TYPE REF TO cl_gui_alv_grid,
      gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

DATA: gs_sflight TYPE sflight,
      gt_sflight TYPE TABLE OF sflight,
      gv_index TYPE I.

DATA: BEGIN OF gs_data,
        carrid TYPE spfli-carrid,
        carrname TYPE scarr-carrname,
        connid TYPE spfli-connid,
        cityfrom TYPE spfli-cityfrom,
        cityto TYPE spfli-cityto,
        airpfrom TYPE spfli-airpfrom,
        airpto TYPE spfli-airpto,
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data.

PARAMETERS: pa_carr TYPE spfli-carrid DEFAULT 'AZ'.
SELECT-OPTIONS: so_con FOR gs_data-connid.
```
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_F01
*&---------------------------------------------------------------------*

FORM get_data.
  DATA: gt_scarr TYPE TABLE OF scarr,
        gs_scarr TYPE scarr.

  SELECT carrid connid cityfrom cityto airpfrom airpto FROM spfli
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid = pa_carr
      AND connid IN so_con.
  SELECT carrid carrname FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE gt_scarr
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid.

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid
      TRANSPORTING carrname.
    gs_data-carrname = gs_scarr-carrname.
    MODIFY gt_data FROM gs_data TRANSPORTING carrname.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*

FORM get_sflight.
  READ TABLE gt_data INTO gs_data INDEX gv_index.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_sflight
    WHERE carrid = gs_data-carrid
      AND connid = gs_data-connid.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CARRID'.
  gs_fcat-ref_field = 'CARRID'.
  gs_fcat-ref_table = 'SPFLI'.
  gs_fcat-hotspot = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CARRNAME'.
  gs_fcat-ref_field = 'CARRNAME'.
  gs_fcat-ref_table = 'SCARR'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CONNID'.
  gs_fcat-ref_field = 'CONNID'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CITYFROM'.
  gs_fcat-ref_field = 'CITYFROM'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CITYTO'.
  gs_fcat-ref_field = 'CITYTO'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'AIRPFROM'.
  gs_fcat-ref_field = 'AIRPFROM'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

    CLEAR gs_fcat.
  gs_fcat-fieldname = 'AIRPTO'.
  gs_fcat-ref_field = 'AIRPTO'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv.
  IF GO_ALV2 IS NOT INITIAL.
    GO_ALV2->FREE( ).
  ENDIF.
  IF GO_CONT2 IS NOT INITIAL.
    GO_CONT2->FREE( ).
  ENDIF.
  FREE: GO_ALV2, GO_CONT2.
ENDFORM.
```
</br>

**4) `C01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_C01
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_HOTSPOT_CLICK
    FOR EVENT HOTSPOT_CLICK OF CL_GUI_ALV_GRID
    IMPORTING E_ROW_ID SENDER.
ENDCLASS.


CLASS lcl_event_handler IMPLEMENTATION.
  METHOD ON_HOTSPOT_CLICK.
    IF SENDER = GO_ALV.
      gv_index = E_ROW_ID-index.
      PERFORM get_sflight.
      CALL SCREEN 110.
    ENDIF.
  ENDMETHOD.
ENDCLASS.
```
</br>

**5) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
  SET PF-STATUS 'S100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        OTHERS                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    PERFORM set_fcat.
    SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv.
    CALL METHOD go_alv->set_table_for_first_display
      CHANGING
        it_outtab                     = gt_data
        it_fieldcatalog               = gt_fcat
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
    ENDIF.
  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE status_0110 OUTPUT.
 SET PF-STATUS 'S110'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv2 OUTPUT.
  IF go_cont2 IS INITIAL.
    CREATE OBJECT go_cont2
      EXPORTING
        container_name              = 'AREA2'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv2
      EXPORTING
        i_parent          = go_cont2
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    CALL METHOD go_alv2->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_sflight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>

**6) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_I01
*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0110 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**7) 결과 확인**

<img width="423" height="102" alt="image" src="https://github.com/user-attachments/assets/0336d2c5-97f0-46db-9709-17cb3433a6b6" />
</br>

<img width="470" height="164" alt="image" src="https://github.com/user-attachments/assets/4c4c5b8d-8711-428a-b928-30b01aa655cd" />
</br>

<img width="472" height="193" alt="image" src="https://github.com/user-attachments/assets/69b1f9a6-1e38-40b3-b325-dd25f24d2f93" />
</br>
</br>
</br>
</br>

### 실습 2
- 전 exercise 코드에서 이어서 진행
</br>
</br>

**1) `C01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV4_C01_3
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_toolbar FOR EVENT toolbar
      OF cl_gui_alv_grid IMPORTING e_object,
        on_user_command FOR EVENT user_command
      OF cl_gui_alv_grid IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'PERCENTAGE'.
    ls_button-text = '% total'.
    ls_button-quickinfo = 'Total economy utilization'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'PERCENTAGE_MARKED'.
    ls_button-text = '% marked'.
    ls_button-quickinfo = 'Economy utilization (marked flights)'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD on_user_command.
    DATA: gv_seatsocc TYPE I VALUE IS INITIAL,
          gv_seatsmax TYPE I VALUE IS INITIAL,
          gv_percentage TYPE P LENGTH 8 DECIMALS 2 VALUE IS INITIAL.
    DATA: lt_select_data TYPE LVC_T_ROW,
          ls_select_data TYPE LVC_S_ROW,
          gs_select_flight LIKE gs_flight.
    CASE E_UCOMM.
      WHEN 'PERCENTAGE'.
        LOOP AT gt_flight INTO gs_flight.
          gv_seatsocc = gv_seatsocc + gs_flight-seatsocc.
          gv_seatsmax = gv_seatsmax + gs_flight-seatsmax.
        ENDLOOP.
        gv_percentage = ( gv_seatsocc / gv_seatsmax ) * 100.
        MESSAGE '총 좌석: ' && gv_percentage TYPE 'I'.
      WHEN 'PERCENTAGE_MARKED'.
        CALL METHOD go_alv_grid->get_selected_rows
          IMPORTING et_index_rows = lt_select_data.
        IF lt_select_data IS NOT INITIAL.
          LOOP AT lt_select_data INTO ls_select_data.
            READ TABLE gt_flight INTO gs_select_flight
            INDEX ls_select_data-index.
            gv_seatsocc = gv_seatsocc + gs_select_flight-seatsocc.
            gv_seatsmax = gv_seatsmax + gs_select_flight-seatsmax.
          ENDLOOP.
          gv_percentage = ( gv_seatsocc / gv_seatsmax ) * 100.
          MESSAGE '총 좌석: ' && gv_percentage TYPE 'I'.
        ELSE.
          MESSAGE 'Please select record' TYPE 'E'.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.
```
</br>

**2) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV4_O01
*&---------------------------------------------------------------------*

SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv_grid.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv_grid.
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
</br>

**3) 결과 확인**

<img width="510" height="349" alt="image" src="https://github.com/user-attachments/assets/2673cb36-cefa-4576-952e-98928d7e6b08" />
</br>

<img width="510" height="353" alt="image" src="https://github.com/user-attachments/assets/8ce97d02-ede4-4a4a-aa23-2f7286e3deb5" />

</br>
</br>
