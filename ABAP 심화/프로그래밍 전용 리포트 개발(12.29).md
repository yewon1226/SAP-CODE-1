## <  핵심 개념 >
</br>

- **1) ABAP 객체 생성 기본 문법**
- **2) ALV 레이아웃 Variant 저장 옵션**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

### 1) ALV 데이터 갱신
- 이미 생성된 ALV Grid가 있을 때 ALV를 다시 생성하지 않고 데이터만 화면에 반영
- `i_soft_refresh` = 'X' : 정렬·필터·레이아웃을 유지한 채 데이터만 갱신
- `i_soft_refresh` = space : ALV를 전체 재계산하여 정렬·필터까지 다시 적용
```abap
CALL METHOD <alv_ref>->refresh_table_display
  EXPORTING
    is_stable      = <ls_stable>
    i_soft_refresh = 'X'
  EXCEPTIONS
    finished       = 1
    others         = 2.
```
</br>

- `lvc_s_stbl` : ALV 새로고침 시 화면 위치 고정용 구조
- `row = 'X'` → 행(스크롤) 위치 유지
- `col = 'X'` → 열(커서) 위치 유지
```abap
DATA ls_stable TYPE lvc_s_stbl.

ls_stable-row = 'X'.   " 행 위치 유지
ls_stable-col = 'X'.   " 열 위치 유지
```
</br>

- 예시 코드
```abap
DATA gs_stable TYPE lvc_s_stbl.

gs_stable-row = 'X'.
gs_stable-col = 'X'.

CALL METHOD go_alv->refresh_table_display
  EXPORTING
    is_stable      = gs_stable
    i_soft_refresh = 'X'
  EXCEPTIONS
    finished       = 1
    others         = 2.
```
</br>
</br>
</br>

### 2) ALV 레이아웃 설정 (IS_LAYOUT)
- ALV Grid의 화면 모양(제목, 줄무늬, 컬럼 폭 등)을 설정
- 데이터 내용은 안 바뀌고 표시 방식만 제어
- `SET_TABLE_FOR_FIRST_DISPLAY` 에서 최초 출력 시만 적용
- **`Ctrl + Space`** - `gs_layout-` 뒤에서 누르면 구조체 필드 목록 자동 표시
</br>

- `lvc_s_layo` : ALV 화면의 모양(레이아웃) 을 제어하는 구조체
```abap
DATA <ls_layout> TYPE lvc_s_layo.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
    is_layout        = <ls_layout>
  CHANGING
    it_outtab        = <itab>.
```
```abap
DATA gs_layout TYPE lvc_s_layo.

gs_layout-grid_title = 'ALV Grid Title'.
gs_layout-zebra      = 'X'.   " 행 줄무늬
gs_layout-cwidth_opt = 'X'.   " 컬럼 너비 자동

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
    is_layout        = gs_layout
  CHANGING
    it_outtab        = gt_bookings.
```
</br>
</br>

**< LVC_S_LAYO (ALV Layout Structure 주요 필드 정리) >**
  
| Field Name     | Description |
|---------------|-------------|
| `grid_title`  | ALV 상단에 표시되는 제목 텍스트 (최대 70자) |
| `zebra`       | `'X'` 설정 시 행을 교차 색상(Zebra 패턴)으로 표시 |
| `no_headers`  | `'X'` 설정 시 컬럼 헤더 숨김 |
| `sel_mode`    | 행 / 셀 / 컬럼 선택 모드 설정 |
| `cwidth_opt`  | `'X'` 설정 시 컬럼 폭 자동 최적화 |
| `no_toolbar`  | `'X'` 설정 시 ALV 툴바 숨김 |
| `totals_bef`  | `'X'` 설정 시 합계 행을 데이터 맨 위에 표시 |
| `info_fname`  | **행(Row) 색상 정보**를 담고 있는 내부 테이블 필드명 |
| `ctab_fname`  | **셀(Cell) 색상 정보**를 담고 있는 내부 테이블 필드명 |

</br>

**< ALV Selection Mode (lvc_s_layo-sel_mode) >**

| 값(Value) | 선택 가능 범위 (Possible Selections) | 선택 컬럼 표시 (Selection Column) |
|-----------|--------------------------------------|-----------------------------------|
| space     | B와 동일                              | No                                |
| A         | 다중 행 선택<br>다중 컬럼 선택        | Yes                               |
| B         | 단일 행 선택<br>다중 컬럼 선택        | No                                |
| C         | 다중 행 선택<br>다중 컬럼 선택        | No                                |
| D         | 다중 행 선택<br>다중 컬럼 선택<br>셀 단위 선택 가능 | Yes |

</br>

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

**1) `TOP` 모듈 수정**
```abap
DATA: gt_data TYPE TABLE OF sbook,
      gs_data LIKE LINE OF gt_data.

DATA: ok_code TYPE sy-ucomm,
      go_cont TYPE REF TO cl_gui_custom_container,
      go_alv  TYPE REF TO cl_gui_alv_grid.
DATA: gs_variant TYPE disvariant,
      gs_stable TYPE LVC_S_STBL,
      gs_layout TYPE LVC_S_LAYO.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.

INITIALIZATION.
  PERFORM set_options.
  gs_variant-report = sy-cprog.
  gs_variant-username = sy-uname.
  gs_variant-handle = 'MAIN'.
  " handle : 하나의 화면에 여러개의 ALV가 있는 경우
  "          각 ALV마다 구분자가 필요하므로 구분자 지정해줌

START-OF-SELECTION.
  PERFORM GET_DATA.
  CALL SCREEN 100.
```
</br>
</br>

**2) `PBO` 모듈 수정**
```abap
MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100' WITH sy-uname sy-datum sy-uzeit.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  " 1. 100번 스크린 Layout에 Area 추가
  " 2. 만약 Container 가 만들어지기 전이라면, 객체 생성함
  "    Custom Container 생성
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    " 3. ALV Grid 객체 생성해서 Container와 연결
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    PERFORM set_layout.
    " 4. ALV Grid 안에 Data display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
*        i_buffer_active               =
*        i_bypassing_buffer            =
*        i_consistency_check           =
        i_structure_name              = 'SBOOK'
        is_variant                    = gs_variant
        i_save                        = 'A' "A, X, U, ''
        i_default                     = 'X'
        is_layout                     = gs_layout
*        is_print                      =
*        it_special_groups             =
*        it_toolbar_excluding          =
*        it_hyperlink                  =
*        it_alv_graphics               =
*        it_except_qinfo               =
*        ir_salv_adapter               =
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4
            .
    IF sy-subrc <> 0.
*     Implement suitable error handling here
    ENDIF.
  ELSE.
    gs_stable-row = 'X'.
    gs_stable-col = 'X'.

    CALL METHOD go_alv->refresh_table_display
      EXPORTING
        is_stable      = gs_stable
        i_soft_refresh = ''  " X : 정렬, 필터, 그외 기능들 유지
                             " _ : 전체 리프레쉬 (기본)
      EXCEPTIONS
        finished       = 1
        others         = 2.

    IF sy-subrc <> 0.
      MESSAGE 'Data refresh 중 Error 발생' TYPE 'E'.
    ENDIF.

  ENDIF.
ENDMODULE.
```
