## <  핵심 개념 >
</br>

- **1) Pushbutton**
- **2) Selection Screen 필드 제어**

</br>
</br>

---

</br>

## < ALV 화면 구성 >
</br>

- **ALV 기본 툴바 버튼 기능 설명**
<img width="444" height="337" alt="image" src="https://github.com/user-attachments/assets/4e7d929d-9c27-4a8b-aede-4acf4a0965b2" />

</br>
</br>
</br>

- **상단 영역**
- `List Header` (선택) : ALV 제목/설명
- `Application Toolbar` : 정렬·필터·엑셀 버튼 (클릭 시 이벤트 발생)
</br>

- **중앙(Data Area)**
- `Column Header` : 컬럼 제목 + 정렬 기준
- `Key Column` : 왼쪽 고정 핵심 컬럼
- `Selection Button` (선택) : 행 선택용 체크/아이콘
- `Data` : 실제 조회 데이터
</br>

- **하단 영역**
- `Total Column` : 합계 대상 컬럼
- `Aggregation Row` : 합계 / 평균 / 최소 / 최대

<img width="490" height="346" alt="image" src="https://github.com/user-attachments/assets/5a287e4b-e177-4673-99a7-acdc4164259f" />
</br>
</br>


<img width="495" height="393" alt="image" src="https://github.com/user-attachments/assets/c069f6ba-2f59-4113-a372-eaa7a211bf23" />
</br>
</br>

### < ALV 마우스 사용 >
- **행 선택** : 클릭 (Ctrl = 다중)
- **컬럼 선택** : 헤더 클릭 (Ctrl = 다중)
- **컬럼 이동** : 드래그
- **폭 조절** : 컬럼 경계 드래그
- **헤더 우클릭** : 복사 / 숨김 / 자동폭 / 고정
</br>
</br>

### < ALV Subtotals >
- `Total` : 전체 데이터 합계 → 맨 아래 1줄
- `Subtotal` : 기준 컬럼별 그룹 합계 → 그룹마다 표시

<img width="465" height="361" alt="image" src="https://github.com/user-attachments/assets/5be7e416-eda5-42ea-a17d-f8bf3a0f0a0d" />
</br>
</br>
</br>
</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
- **Area는 Container로, Container는 ALV Grid로 연결됨**
- **클래스** : 객체의 설계도
- **인스턴스(객체)** : 클래스로 생성된 실제 실체
- **참조 변수** : 객체를 가리키는 포인터 변수
</br>
</br>

**1) 참조 변수 선언**
- 객체를 담는 변수가 아니라 객체를 가리키는 참조 변수
- 실제 객체는 아직 없음
```abap
DATA: <ref_var> TYPE REF TO <class_name>.
```
```abap
DATA: go_alv TYPE REF TO cl_gui_alv_grid.
```
</br>
</br>

**2) 객체 생성**
- `CREATE OBJECT` 로 실제 객체 생성 (생성자 자동 호출)
- 생성 시 생성자(CONSTRUCTOR) 호출
- 필요한 값은 `EXPORTING` 으로 전달
- PBO에서 INITIAL 체크 후 1회 생성, Container → ALV 순서
```abap
IF <ref_var> IS INITIAL.
  CREATE OBJECT <ref_var>
    EXPORTING
      <param> = <value>.
    EXCEPTIONS
      <exception_1> = <rc_1>
ENDIF.
```
- `container_name` → Screen의 Area와 연결
- `i_parent` → ALV ↔ Container 연결
```abap
MODULE init_alv OUTPUT.
  IF go_container IS INITIAL.
    CREATE OBJECT go_container
      EXPORTING
        container_name = 'MY_CONTROL_AREA'.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent = go_container.
  ENDIF.
ENDMODULE.
```
</br>
</br>

**3) ALV 컨트롤 인스턴스 해제**
- 화면 종료 시 PAI에서 ALV/Container → free 호출로 자원 해제
- PAI(EXIT, CANCEL) 에서 수행
```abap
FORM free_control_resources.
  CALL METHOD <alv_ref>->free.
  CALL METHOD <container_ref>->free.
  FREE: <alv_ref>, <container_ref>.
ENDFORM.
```
- `->FREE()` : 객체가 사용하는 GUI 컨트롤 자원 해제
- `FREE ref` : 참조 변수만 해제(값 INITIAL로 초기화)
```abap
MODULE exit_commands_0100 INPUT.    " PAI
  CASE ok_code.
    WHEN 'EXIT' OR 'CANCEL'.
      PERFORM free_control_resources.
      LEAVE PROGRAM.
  ENDCASE.
ENDMODULE.

FORM free_control_resources.        " F01
  CALL METHOD go_alv->free.
  CALL METHOD go_container->free.
  FREE: go_alv, go_container.
ENDFORM.
```
</br>
</br>

**4) ALV Grid 데이터 표시**
- ALV Grid에 처음으로 데이터를 출력하는 메서드
- 내부 테이블( `it_outtab` )의 데이터를 기준으로 화면 구성
- `i_structure_name` 에 지정한 DDIC 구조 기준으로 ALV 컬럼 구조가 자동 출력됨
```abap
CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <internal_table>
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
```abap
DATA: gt_flight TYPE STANDARD TABLE OF sflight.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SFLIGHT'
  CHANGING
    it_outtab        = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
</br>
</br>

**5) ALV 데이터 갱신**
- ALV Grid를 다시 생성하지 않고 부 테이블 변경 내용을 화면에 반영
```abap
CALL METHOD <alv_ref>->refresh_table_display
  EXPORTING
    is_stable = <lvc_s_stbl>
  EXCEPTIONS
    finished = 1
    others   = 2.
```
```abap
CALL METHOD go_alv->refresh_table_display
  EXPORTING
    is_stable = VALUE lvc_s_stbl( row = 'X' col = 'X' )
  EXCEPTIONS
    finished = 1
    others   = 2.
```
</br>
</br>
</br>

---

</br>

## < ABAP 객체 생성 예제 >
</br>

**1) 참조 변수 선언**
- `CL_GUI_CUSTOM_CONTAINER` : Screen의 Area와 연결되는 컨테이너
- `CL_GUI_ALV_GRID` : Container 안에서 데이터를 표시하는 ALV Grid 객체
```abap
REPORT ZPROGB03_0028.

DATA: OK_CODE TYPE sy-ucomm.
DATA: gs_flight TYPE sflight,
      gt_flight TYPE TABLE OF sflight.

" Container 참조변수 생성 (아직 객체 생성되기 전)
DATA: go_cont TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
      " ALV 참조변수 생성 (아직 객체 생성되기 전)
      go_alv TYPE REF TO CL_GUI_ALV_GRID.

PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_flight-connid,
                so_fdt FOR gs_flight-fldate.

INCLUDE ZPROGB03_0028_O01.  " PBO
INCLUDE ZPROGB03_0028_I01.  " PAI
INCLUDE ZPROGB03_0028_F01.

START-OF-SELECTION.

PERFORM get_data.
CALL SCREEN 100.
```
</br>
</br>
</br>

**2) `Screen` 에 모듈 생성**
```abap
PROCESS BEFORE OUTPUT.
 MODULE STATUS_0100.
 MODULE INIT_ALV.
 MODULE CLEAR_OK_CODE.

PROCESS AFTER INPUT.
 MODULE EXIT AT EXIT-COMMAND.
 MODULE USER_COMMAND_0100.
```
</br>
</br>
</br>

**3) `PAI` 에 ALV 컨트롤 인스턴스 해제**
```abap
FORM get_data.
  SELECT *
    FROM sflight
    INTO TABLE gt_flight
    WHERE carrid = pa_car
      AND connid IN so_con
      AND fldate IN so_fdt.
  IF sy-subrc = 0.

  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv.
  IF GO_ALV IS NOT INITIAL.
    GO_ALV->FREE( ).
  ENDIF.
  IF GO_CONT IS NOT INITIAL.
    GO_CONT->FREE( ).
  ENDIF.
  FREE: GO_ALV, GO_CONT.
ENDFORM.
```
```abap
MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      PERFORM FREE_ALV.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      PERFORM FREE_ALV.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>
</br>

**4) `PBO` 에 객체 생성 및 ALV Grid 데이터 표시**
- `Custom Container` 객체 생성
  
<img width="377" height="363" alt="image" src="https://github.com/user-attachments/assets/6d99dc1d-c5bd-4140-b21a-e41130485067" />
</br>
</br>

- `ALV Grid` 객체 생성

<img width="375" height="359" alt="image" src="https://github.com/user-attachments/assets/90735023-c617-449e-98ad-6b3eed0e0f61" />
</br>
</br>

- ALV Grid 메서드 호출
<img width="384" height="362" alt="image" src="https://github.com/user-attachments/assets/e2594dd2-511b-4721-b9c6-b9e5ec7d13ab" />
</br>
</br>

- `PBO` 에 객체 생성 및 ALV Grid 데이터 표시
- `SET_TABLE_FOR_FIRST_DISPLAY` : 내부 테이블 데이터를 ALV Grid에 최초로 표시하는 메서드
- SET_TABLE* 하고 f4 누르면 자동완성
- `REFRESH_TABLE_DISPLAY` : 내부 테이블 변경 내용을 ALV Grid에 다시 표시(갱신)하는 메서드
```abap
MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE clear_ok_code OUTPUT.
  CLEAR OK_CODE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  " 1. SCREEN에 Custom Area 추가
  " 2. Container Control 생성 (AREA와 연결)
  IF go_cont IS INITIAL. " 최초 1번만 실행
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.

    IF sy-subrc <> 0.
      MESSAGE  'Container 생성 중 오류 발생' TYPE 'E'.
    ENDIF.

    " 3. ALV Grid 생성
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont " Container와 연결
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.

    IF sy-subrc <> 0.
      MESSAGE 'ALV Grid 생성 중 오류 발생' TYPE 'E'.
    ENDIF.

    " 4. ALV Grid 안에 Data Display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_flight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.

    IF sy-subrc <> 0.
*     Implement suitable error handling here
    ENDIF.
  ELSE.
    " 최초에 1번 생성괸 후, 두번째부터는 ELSE구문을 실행한다
    CALL METHOD go_alv->refresh_table_display
      EXCEPTIONS
        finished       = 1
        others         = 2.

    IF sy-subrc <> 0.
*     Implement suitable error handling here
    ENDIF.

  ENDIF.

ENDMODULE.
```
</br>
</br>
</br>

**5) 결과 확인**

<img width="475" height="127" alt="image" src="https://github.com/user-attachments/assets/a7190486-1fe1-40c7-be7f-222564219cbc" />
</br>

<img width="574" height="174" alt="image" src="https://github.com/user-attachments/assets/9b72474e-7ab8-47fd-a1f1-5c8b5a0c0e48" />
</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- ALV 실습
</br>

**1) Program 에 모듈 생성**
```abap
REPORT ZPROGB03_0029.

INCLUDE ZPROGB03_0029_TOP.
INCLUDE ZPROGB03_0029_O01.
INCLUDE ZPROGB03_0029_I01.
INCLUDE ZPROGB03_0029_F01.
```
</br>
</br>

**2) `TOP` 모듈 생성**
```abap
DATA: gt_data TYPE TABLE OF sbook,
      gs_data LIKE LINE OF gt_data.

DATA: ok_code TYPE sy-ucomm,
      go_cont TYPE REF TO cl_gui_custom_container,
      go_alv  TYPE REF TO cl_gui_alv_grid.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.

INITIALIZATION.
  PERFORM set_options.

START-OF-SELECTION.
  PERFORM GET_DATA.
  CALL SCREEN 100.
```
</br>
</br>

**3) `F01` 모듈 생성**
```abap
FORM set_options.
  so_fdt-sign = 'I'.
  so_fdt-option = 'BT'.
  so_fdt-low = sy-datum(4) && '0101'. " 매년 첫째날
  so_fdt-high = sy-datum(4) && '1231'. " 매년 마지막날
  APPEND so_fdt.

  so_car-sign = 'I'.
  so_car-option = 'GT'.
  so_car-low = 'AZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.
ENDFORM.

*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM SBOOK
    INTO TABLE gt_data
    WHERE carrid IN so_car
      AND connid IN so_con
      AND fldate IN so_fdt.
ENDFORM.
```
