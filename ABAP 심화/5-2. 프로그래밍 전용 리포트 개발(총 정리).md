# 12/23
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ABAP 이벤트**
- **2) Selection Screen - Selection Screen Block / Selection Screen Lines**
- **3) AT SELECTION-SCREEN ON <field>**
- **4) Selection Screen 서브스크린(Subscreen) & 탭스트립(Tabstrip)**
- **5) Message Class**
- **6) Variant**

</br>
</br>

---

</br>

## < Selection Screen >
- **프로그램 실행 시 표준 선택화면(Screen 1000) 자동 생성**
```abap
PARAMETERS: ...
SELECT-OPTIONS: ...
```
- **직접 번호를 지정한 사용자 정의 Selection Screen 생성**
```abap
SELECTION-SCREEN BEGIN OF SCREEN <no>.
  PARAMETERS ...
  SELECT-OPTIONS ...
SELECTION-SCREEN END OF SCREEN <no>.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 1100.
  PARAMETERS: p_name TYPE string.
  SELECT-OPTIONS: s_date FOR sy-datum.
SELECTION-SCREEN END OF SCREEN 1100.
```
</br>
</br>

### 1) ABAP 이벤트 
- `LOAD-OF-PROGRAM` : 프로그램이 메모리에 로드될 때 한 번 실행됨
- `INITIALIZATION` : Selection Screen이 처음 뜨기 전에 초기값을 설정함
- `AT SELECTION-SCREEN OUTPUT (PBO)` : Selection Screen이 그려질 때마다 실행되어 화면을 제어함
- `AT SELECTION-SCREEN (PAI)` : 사용자가 Enter나 실행을 누를 때 입력값을 검사함
- `START-OF-SELECTION` : Selection Screen을 통과한 후 본 처리 로직이 시작됨
```abap
LOAD-OF-PROGRAM
→ INITIALIZATION
→ AT SELECTION-SCREEN OUTPUT   (PBO)
→ [Selection Screen 표시]
→ AT SELECTION-SCREEN          (PAI)
→ START-OF-SELECTION
```
```abap
*************** E01 ****************
LOAD-OF-PROGRAM.
  "프로그램 로딩 시 1회 실행

INITIALIZATION.
  p_carr = 'AA'.   "초기값 세팅

AT SELECTION-SCREEN OUTPUT.   "PBO
  LOOP AT SCREEN.
    IF screen-group1 = 'A1'.
      screen-active = 0.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN.          "PAI
  IF p_carr IS INITIAL.
    MESSAGE '필수 입력값 누락' TYPE 'E'.
  ENDIF.

START-OF-SELECTION.
  PERFORM get_data.
```
</br>
</br>

### 2) SELECT-OPTIONS
- `SELECT-OPTIONS` → 범위 입력용 선택 조건
- `SELECT-OPTIONS` 는 헤더라인이 있는 내부 테이블이므로 `INTO` 생략 가능
- 내부적으로 `SIGN / OPTION / LOW / HIGH` 구조
```abap
*************** TOP ****************
SELECT-OPTIONS so_carr FOR sflight-carrid.

*************** E01 ****************
INITIALIZATION.
  MOVE: 'I'  TO so_carr-sign,
        'BT' TO so_carr-option,
        'AA' TO so_carr-low,
        'LH' TO so_carr-high.
  APPEND so_carr.
  CLEAR so_carr.

  MOVE: 'E'  TO so_carr-sign,
        'EQ' TO so_carr-option,
        'DL' TO so_carr-low.
  APPEND so_carr.
```
- `SELECT-OPTIONS`는 데이터베이스 테이블 필드를 직접 참조할 수 없고, 해당 필드가 포함된 이미 선언된 변수(Work Area 또는 Structure)의 필드를 통해 참조해야 함

```abap
SELECT-OPTIONS <seltab> FOR <field>
  [DEFAULT <value>]
  [DEFAULT <value1> TO <value2>]
  [SIGN <I|E> OPTION <op>]
  [MEMORY ID <pid>]
  [LOWER CASE]
  [OBLIGATORY]
  [NO-EXTENSION]
  [NO INTERVALS]
  [MODIF ID <mod>].
```
```abap
*************** TOP ****************
SELECT-OPTIONS so_car FOR sflight-carrid
  DEFAULT 'AA'                 "초기값 AA
  SIGN I OPTION EQ             "포함(I), 같음(EQ)
  MEMORY ID CAR                "이전 입력값 자동 불러오기
  OBLIGATORY                   "필수 입력
  NO-EXTENSION                 "다중 선택 버튼 비활성화
  NO INTERVALS                 "from~to 범위 입력 금지
  MODIF ID A1.                 "화면 제어용 그룹 ID
```

- SIGN : `I` - 포함, `E` - 제외
  
| ABAP OPTION 값 | 의미 (Meaning) | LOW/HIGH 사용 | SQL 연산자 개념 |
| :---: | :--- | :---: | :---: |
| **`EQ`** | Equal (같음) | `LOW` 값만 사용 | `fieldName = LOW` |
| **`BT`** | Between (사이) | `LOW`와 `HIGH` 값 모두 사용 | `fieldName BETWEEN LOW AND HIGH` |
| **`GE`** | Greater or Equal (크거나 같음) | `LOW` 값만 사용 | `fieldName >= LOW` |
| **`LE`** | Less or Equal (작거나 같음) | `LOW` 값만 사용 | `fieldName <= LOW` |
| **`GT`** | Greater Than (초과) | `LOW` 값만 사용 | `fieldName > LOW` |
| **`LT`** | Less Than (미만) | `LOW` 값만 사용 | `fieldName < LOW` |
| **`CP`** | Contains Pattern (패턴 포함) | `LOW` 값만 사용 (`*`, `?` 등 사용) | `fieldName LIKE LOW` |
| **`NE`** | Not Equal (같지 않음) | `LOW` 값만 사용 | `fieldName != LOW` (또는 `NOT =`) |
| **`NP`** | Not Contains Pattern (패턴 불일치) | `LOW` 값만 사용 | `NOT (fieldName LIKE LOW)` |

</br>
</br>

### 3) Selection Screen Block
- `Selection Screen` 을 **논리적인 그룹(박스)** 으로 묶어 화면을 보기 좋게 구성
- `WITH FRAME` 를 쓰면 테두리 박스가 생김
- TITLE로 블록 제목 표시 가능
- 블록 안에는 PARAMETERS, SELECT-OPTIONS 모두 배치 가능
```abap
SELECTION-SCREEN BEGIN OF BLOCK <block>
  [WITH FRAME [TITLE <text>]].

  " PARAMETERS / SELECT-OPTIONS 선언

SELECTION-SCREEN END OF BLOCK <block>.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF BLOCK limit
  WITH FRAME TITLE text-001.

PARAMETERS:
  pa_lim_1 RADIOBUTTON GROUP lim DEFAULT 'X',
  pa_lim_2 RADIOBUTTON GROUP lim,
  pa_lim_3 RADIOBUTTON GROUP lim.

SELECTION-SCREEN END OF BLOCK limit.
```
</br>
</br>

### 4) Selection Screen Lines
- Selection Screen에서 여러 필드를 한 줄(row)에 나란히 배치하기 위한 문법
- 라벨(COMMENT), 위치(POSITION), 입력필드(PARAMETERS)를 한 줄로 묶음
```abap
SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT <pos>(<len>) <text> [FOR FIELD <field>].
  SELECTION-SCREEN POSITION <pos>.
  PARAMETERS <p> ...
SELECTION-SCREEN END OF LINE.
```
- `BEGIN OF LINE / END OF LINE` → 같은 줄에 화면 요소 배치
- `COMMENT pos(len) text` → 화면에 설명 텍스트 출력
- `FOR FIELD <field>` → 해당 입력 필드와 텍스트 연결
- `POSITION pos` → 출력 시작 위치 직접 지정
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF LINE.

  SELECTION-SCREEN COMMENT 1(20) text-s03.
  SELECTION-SCREEN COMMENT pos_low(8) text-s04 FOR FIELD pa_col.
  PARAMETERS pa_col AS CHECKBOX.
  SELECTION-SCREEN COMMENT pos_high(8) text-s05.
  PARAMETERS pa_ico AS CHECKBOX.

SELECTION-SCREEN END OF LINE.
```
</br>
</br>

### 5) AT SELECTION-SCREEN ON <field>

- Selection Screen 전체 Enter / 실행 시 호출
- 전체 입력값 공통 체크
```abap
`AT SELECTION-SCREEN`
```
- 특정 입력 필드 하나만 검사
- 그 필드 값이 바뀌거나 Enter 시 실행
```abap
AT SELECTION-SCREEN ON <field>
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 1100.
PARAMETERS:
  pa_cus AS CHECKBOX,
  pa_agy AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 1100.

*************** E01 ****************
AT SELECTION-SCREEN ON pa_add.
  IF pa_add = 'X'.
    CALL SELECTION-SCREEN 1100
      STARTING AT 5 5
      ENDING   AT 50 10.

    IF sy-subrc <> 0.
      LEAVE TO SCREEN 1000.
    ENDIF.
  ENDIF.
```
- SELECT-OPTIONS 전체 라인(low/high) 검사
```abap
AT SELECTION-SCREEN ON <select-options>
```
- 라디오버튼 그룹 중 선택 상태 검사
```abap
AT SELECTION-SCREEN ON RADIOBUTTON GROUP <grp>
```
- 블록 안의 입력값 전체를 한 번에 검사
```abap
AT SELECTION-SCREEN ON BLOCK <block>
```
- Selection Screen에서 특정 필드에서 F1(Help) 를 눌렀을 때 실행되는 이벤트
```abap
AT SELECTION-SCREEN ON HELP-REQUEST FOR <field>
```
```abap
PARAMETERS pa_carr TYPE sflight-carrid.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100
    STARTING AT 5 5.
```
</br>
</br>

### 6) Selection Screen 서브스크린 & 탭스트립
- 서브스크린 코드 형식
```abap
SELECTION-SCREEN BEGIN OF SCREEN <subscreen_number> AS SUBSCREEN.
  PARAMETERS / SELECT-OPTIONS <input_field>.
SELECTION-SCREEN END OF SCREEN <subscreen_number>.
```
- 탭스트립 코드 형식
```abap
SELECTION-SCREEN BEGIN OF TABBED BLOCK <tab_block_name> FOR <number_of_lines> LINES.

  SELECTION-SCREEN TAB (<max_tab_text_length>) <tab_text_variable>
    USER-COMMAND <ok_code> DEFAULT SCREEN <subscreen_number>.

SELECTION-SCREEN END OF BLOCK <tab_block_name>.
```
- 탭스트립 초기 활성 탭 / 서브스크린 지정
```abap
<tab_block_name>-activetab = '<ok_code>'.
<tab_block_name>-dynnr     = '<subscreen_number>'.
```
```abap
*************** TOP ****************
SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS so_carr FOR spfli-carrid.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS pa_date TYPE sy-datum.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS pa_flag AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.


SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) tab1
    USER-COMMAND COMM1 DEFAULT SCREEN 101.

  SELECTION-SCREEN TAB (20) tab2
    USER-COMMAND COMM2 DEFAULT SCREEN 102.

  SELECTION-SCREEN TAB (20) tab3
    USER-COMMAND COMM3 DEFAULT SCREEN 103.
SELECTION-SCREEN END OF BLOCK tab_strip.


*************** E01 ****************
INITIALIZATION.
  tab1 = 'Carrier'.
  tab2 = 'Date'.
  tab3 = 'Option'.

  tab_strip-activetab = 'COMM1'.
  tab_strip-dynnr     = '0101'.
```
</br>
</br>

### 7) Variant
- Selection Screen 입력값을 저장해두고 재사용하는 실행 조건
<img width="472" height="390" alt="image" src="https://github.com/user-attachments/assets/e64f958c-25d6-4824-b7b5-8d6f30db8eda" />
</br>

<img width="485" height="214" alt="image" src="https://github.com/user-attachments/assets/f9b18ff3-9c06-44f2-816b-00880e01fc90" />
</br>
</br>

- DB Table 에서도 Variant 생성 가능
<img width="485" height="339" alt="image" src="https://github.com/user-attachments/assets/d0f76d8f-7a8f-4fb1-8e10-d3b6f24545eb" />
</br>

<img width="485" height="360" alt="image" src="https://github.com/user-attachments/assets/c722b5e3-a61d-496a-b57f-c0f17f39d7fa" />
</br>
</br>

- 삭제 가능
<img width="481" height="215" alt="image" src="https://github.com/user-attachments/assets/242e7596-1590-466d-8ea3-fe423286c99c" />

</br>
</br>
</br>

---

</br>

## < ABAP 기본 문법 >
</br>

### 1) WRITE 추가 옵션
- `WRITE AT` : 위치 지정 출력 모드 선언
- `<pos>(<len>)` : 출력 시작 열과 길이 지정
- `COLOR <color>` : 출력 색상 지정
- `CURRENCY <f>` : 금액을 통화 단위에 맞게 포맷 출력
```abap
WRITE AT: / <pos>(<len>) <field>
          COLOR <color>
          CURRENCY <currency_field>.
```
```abap
*************** TOP ****************
CONSTANTS: gc_pos     TYPE i VALUE 12,
           gc_len     TYPE i VALUE 10,
           gc_len_pri TYPE i VALUE 15,
           gc_len_cur TYPE i VALUE 5.

WRITE AT: / sy-line,
          gc_pos(gc_len) gs_sflight-fldate COLOR COL_KEY,
          (gc_len_pri)   gs_sflight-price  CURRENCY gs_sflight-currency,
          (gc_len_cur)   gs_sflight-currency.
```
</br>

- `NO-GAP` → 필드 뒤에 출력되는 공백 제거
- `NO-ZERO` → 값이 0이면 공백 출력, 선행 0을 공백으로 치환
- `DD/MM/YY` → 날짜 출력 형식(일/월/년) 지정
- `CURRENCY <currency_key>` → 통화 키 기준으로 금액 포맷 적용 (TCURX 기준)
- `UNIT <unit_key>` → 단위 키 기준으로 수량 포맷 적용 (T006 기준)
- `USING EDIT MASK <mask>` → 지정한 편집 마스크 형식으로 출력
- `UNDER <g>` → <g> 필드가 출력된 열 아래에서 출력 시작
- `LEFT-JUSTIFIED` → 왼쪽 정렬 (C, N, D, T, X 기본)
- `CENTERED` → 출력 길이 기준 가운데 정렬
- `RIGHT-JUSTIFIED` → 오른쪽 정렬 (숫자 타입 기본)
```abap
WRITE [AT /<pos>(<len>)] <field>
      [NO-GAP]
      [NO-ZERO]
      [DD/MM/YY]
      [CURRENCY <currency_key>]
      [UNIT <unit_key>]
      [USING EDIT MASK <mask>]
      [UNDER <g>]
      [LEFT-JUSTIFIED | CENTERED | RIGHT-JUSTIFIED].
```
```abap
DATA: gv_char TYPE c LENGTH 5 VALUE 'ABC',
      gv_num  TYPE n LENGTH 4 VALUE '0004',
      gv_date TYPE d VALUE sy-datum,
      gv_amt  TYPE p DECIMALS 2 VALUE '1234.50',
      gv_qty  TYPE p DECIMALS 1 VALUE '10.0'.

WRITE AT: / 1(10)  gv_char NO-GAP,
          12(10)   gv_num  NO-ZERO,
          23(10)   gv_date DD/MM/YY,
          35(15)   gv_amt  CURRENCY 'KRW',
          52(10)   gv_qty  UNIT 'EA',
          / 1(10)  gv_amt  USING EDIT MASK '__,___',
          / 1(10)  gv_char UNDER gv_char,
          / 1(10)  gv_char LEFT-JUSTIFIED,
          / 1(10)  gv_char CENTERED,
          / 1(10)  gv_char RIGHT-JUSTIFIED.
```
</br>
</br>

### 2) PARAMETERS
- `OBLIGATORY` : 필수 입력값
- `LOWER CASE` : 소문자 입력 허용
- `VALUE CHECK` : 도메인/서치헬프 기준으로 입력값 유효성 자동 검사
- `MEMORY ID <pid>` : 이전에 입력했던 값을 SAP 메모리에서 자동 불러옴
- `MODIF ID <mod>` : 같은 MODIF ID끼리 묶어 화면 활성/비활성 등 동적 제어에 사용 (그룹핑)
```abap
PARAMETERS <f>
  [TYPE <type> | LIKE <f1>]
  [DECIMALS <n>]
  [OBLIGATORY]
  [DEFAULT <value>]
  [LOWER CASE]
  [VALUE CHECK]
  [AS CHECKBOX]
  [RADIOBUTTON GROUP <grp>]
  [MEMORY ID <pid>]
  [MODIF ID <mod>].
```
```abap
*************** TOP ****************
PARAMETERS: pa_carr TYPE sflight-carrid MEMORY ID CAR OBLIGATORY,
            pa_name AS CHECKBOX DEFAULT 'X',
            pa_curr AS CHECKBOX DEFAULT 'X',
            pa_lim1 RADIOBUTTON GROUP lim DEFAULT 'X',
            pa_lim2 RADIOBUTTON GROUP lim,
            pa_lim3 RADIOBUTTON GROUP lim,
            pa_price TYPE p DECIMALS 2 DEFAULT '500',
            pa_text TYPE string LOWER CASE.

CONSTANTS gc_mark VALUE 'X'.

IF pa_name EQ gc_mark.
  " Output name
ENDIF.

CASE gc_mark.
  WHEN pa_lim1.
  WHEN pa_lim2.
  WHEN pa_lim3.
ENDCASE.
```
</br>
</br>

### 3) SELECTION-SCREEN SKIP
- Selection Screen에서 빈 줄(여백)을 추가해서 화면을 보기 좋게 만듦
- <n> 생략 시 → 1줄, <n> 지정 시 → n줄
```abap
SELECTION-SCREEN SKIP [<n>].
```
```abap
*************** TOP ****************
SELECTION-SCREEN SKIP 2.
```
</br>
</br>

### 4) CHECK 조건식
- 조건이 참이면 계속 진행, 거짓이면 즉시 현재 처리 블록 종료
```abap
CHECK <조건식>.
```
```abap
LOOP AT gt_data INTO gs_data.
  CHECK gs_data-status = 'A'.        " status ≠ 'A' 인 데이터는 자동으로 건너뜀
  WRITE: / gs_data-id.
ENDLOOP.
```
</br>
</br>

---

</br>

## < 모듈풀 프로그램 카피 방법 >
- 이름 다르게 설정해야 함
  
<img width="279" height="112" alt="image" src="https://github.com/user-attachments/assets/4349df47-c2dc-4617-a1e9-66967b391097" />

</br>

<img width="299" height="204" alt="image" src="https://github.com/user-attachments/assets/5ca4ac8e-7abc-404d-a8ea-3b99dfce6222" />
</br>

<img width="450" height="250" alt="image" src="https://github.com/user-attachments/assets/07819a3b-7694-4b4f-a43f-c7c919b9abee" />
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- program 27에서 진행
- Selection screen
- AT SELECTION-SCREEN ON <field>
- Selection Screen 서브스크린 & 탭스트립
- Message Class & Variant
- HELP-REQUEST 사용 (특정 필드에서 F1(Help) 를 눌렀을 때 실행되는 이벤트)
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZPROGB03_0027.

INCLUDE ZPROGB03_0027_TOP.
INCLUDE ZPROGB03_0027_E01.
```
- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

PARAMETERS: pa_carr LIKE gs_flight-carrid DEFAULT 'AA',
            pa_conn TYPE s_conn_id MEMORY ID con.

SELECTION-SCREEN SKIP 2. "두줄 띄우기

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.

  PARAMETERS: pa_rad1 RADIOBUTTON GROUP rad,
              pa_rad2 RADIOBUTTON GROUP rad DEFAULT 'X',
              pa_rad3 RADIOBUTTON GROUP rad.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid DEFAULT 'AA' TO 'LH' OPTION BT SIGN E,
                  so_con FOR gs_flight-connid DEFAULT '0017' NO-EXTENSION. " NO INTERVALS

SELECTION-SCREEN END OF BLOCK blk1.

SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(8) TEXT-002.
    PARAMETERS: pa_name AS CHECKBOX DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 20(8) TEXT-003.
    PARAMETERS: pa_curr AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK blk2.

SELECTION-SCREEN BEGIN OF SCREEN 101 AS SUBSCREEN.
  SELECT-OPTIONS: so_fdt FOR gs_flight-fldate.
SELECTION-SCREEN END OF SCREEN 101.

SELECTION-SCREEN BEGIN OF SCREEN 102 AS SUBSCREEN.
  PARAMETERS: pa_chk1 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 102.

SELECTION-SCREEN BEGIN OF SCREEN 103 AS SUBSCREEN.
  PARAMETERS: pa_chk2 AS CHECKBOX.
SELECTION-SCREEN END OF SCREEN 103.

"TAB STRIP + Sub screen 추가
SELECTION-SCREEN BEGIN OF TABBED BLOCK tab_strip FOR 5 LINES.
  SELECTION-SCREEN TAB (20) TEXT-SB1
    USER-COMMAND comm1 DEFAULT SCREEN 101.
  SELECTION-SCREEN TAB (20) TEXT-SB2
    USER-COMMAND comm2 DEFAULT SCREEN 102.
  SELECTION-SCREEN TAB (20) TEXT-SB3
    USER-COMMAND comm3 DEFAULT SCREEN 103.
SELECTION-SCREEN END OF BLOCK tab_strip.
```
- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'AZ'.
  APPEND so_car.

  CLEAR so_car. " Header (Structure)만 클리어됨
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.

  tab_strip-activetab ='COMM3'.
  tab_strip-dynnr = '0103'. " 초기 스크린 설정

*AT SELECTION-SCREEN ON pa_carr.
*  IF pa_carr IS INITIAL.
*    MESSAGE e003(zmcb03).
*  ENDIF.

AT SELECTION-SCREEN ON so_car.
  IF so_car IS INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP rad.
  IF pa_rad1 = 'X'.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON BLOCK blk1.
  IF pa_rad3 = 'X' AND so_con IS NOT INITIAL.
    MESSAGE e003(zmcb03).
  ENDIF.

AT SELECTION-SCREEN ON HELP-REQUEST FOR pa_carr.
  CALL SCREEN 100 STARTING AT 5 5.

START-OF-SELECTION.

  SELECT *
    FROM dv_flights
    INTO TABLE gt_flight
    WHERE carrid = 'AA'
      AND connid = '0017'.

  IF sy-subrc = 0.
    LOOP AT gt_flight INTO gs_flight.
      WRITE:/ gs_flight-carrid,
              gs_flight-connid,
              gs_flight-fldate,
              gs_flight-cityfrom,
              gs_flight-cityto.
    ENDLOOP.
  ENDIF.
```
- 결과 확인
<img width="454" height="248" alt="image" src="https://github.com/user-attachments/assets/30389e0a-7f6a-482b-a2c7-946649622acc" />
</br>

<img width="287" height="208" alt="image" src="https://github.com/user-attachments/assets/94fd9122-9436-43d6-8084-4d3dd75ac8fa" />
</br>

<img width="463" height="333" alt="image" src="https://github.com/user-attachments/assets/daef5024-c8bb-478d-8fe2-00ed21c31262" />

</br>
</br>
</br>
</br>

### 실습 2
- Selection screen 예제
- 라디오버튼에 따라 조건에 맞는 all / 국제선 / 국내선 조회 프로그램
</br>

- Program 코드에 `TOP` , `E01` 모듈 생성
```abap
REPORT ZBC400_B03_PRACTICE4.

INCLUDE ZBC400_B03_PRACTICE4_TOP.
INCLUDE ZBC400_B03_PRACTICE4_E01.
```
- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights.

SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                so_con FOR gs_flight-connid,
                so_dat FOR gs_flight-fldate NO-EXTENSION.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  PARAMETERS: pa_all RADIOBUTTON GROUP rad,
              pa_nat RADIOBUTTON GROUP rad,
              pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK blk1.
```
- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'AZ'.
  APPEND so_car.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.

  LOOP AT gt_flight INTO gs_flight.
    WRITE:/ gs_flight-carrid,
            gs_flight-connid,
            gs_flight-fldate,
            gs_flight-countryfr,
            gs_flight-cityfrom,
            gs_flight-airpfrom,
            gs_flight-countryto,
            gs_flight-cityto,
            gs_flight-airpto,
            gs_flight-price,
            gs_flight-currency,
            gs_flight-seatsmax,
            gs_flight-seatsocc.
  ENDLOOP.
```
- 결과 확인

<img width="436" height="162" alt="image" src="https://github.com/user-attachments/assets/02267c6c-8889-4516-9d8a-1c7fdb2c8bb9" />

</br>

<img width="565" height="204" alt="image" src="https://github.com/user-attachments/assets/b694d73b-55ca-4b64-8327-79d87c8aa313" />
</br>
</br>
</br>
</br>

### 실습 3
- 서브스크린과 탭스크린 실습
- 실습 2 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights.

PARAMETERS: pa_name TYPE c LENGTH 3.
SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.

SELECTION-SCREEN SKIP 2.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(15) TEXT-003.
    SELECTION-SCREEN COMMENT 35(10) TEXT-004 FOR FIELD pa_abap.
    PARAMETERS: pa_abap AS CHECKBOX.
    SELECTION-SCREEN COMMENT 60(10) TEXT-005 FOR FIELD pa_fiori.
    PARAMETERS: pa_fiori AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME TITLE TEXT-002.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
  SELECTION-SCREEN END OF BLOCK blk2.
SELECTION-SCREEN END OF BLOCK blk1.
```
- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

tab_strip-activetab = 'comm2'.
tab_strip-dynnr = '1200'.
```
- 결과 확인
  
<img width="550" height="148" alt="image" src="https://github.com/user-attachments/assets/9f69be33-2db6-4d5f-b09a-e3fec3bb8ca4" />
</br>

<img width="540" height="206" alt="image" src="https://github.com/user-attachments/assets/c56cb335-aa82-413e-a355-33e8dd5c5a07" />
</br>
</br>
</br>
</br>

### 실습 4
- Message Class, Variant 예제
- 실습 3 에서 이어서 진행
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF SCREEN 1300 AS SUBSCREEN.
  SELECTION-SCREEN BEGIN OF BLOCK blk.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
    PARAMETERS: pa_con LIKE gs_flight-countryfr.      " countryfr 파라미터 추가
  SELECTION-SCREEN END OF BLOCK blk.
SELECTION-SCREEN END OF SCREEN 1300.
```
- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

AT SELECTION-SCREEN ON BLOCK blk.
  IF pa_nat = 'X' AND pa_con IS INITIAL.
    MESSAGE e003(bc405).
  ENDIF.

START-OF-SELECTION.
  CASE 'X'.
    WHEN pa_all.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat.
    WHEN pa_nat.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto <> dv_flights~countryfr
          AND countryfr = pa_con.
    WHEN pa_int.
      SELECT * FROM dv_flights
        INTO TABLE gt_flight
        WHERE carrid IN so_car
          AND connid IN so_con
          AND fldate IN so_dat
          AND countryto = dv_flights~countryfr.
    WHEN OTHERS.
  ENDCASE.
```
- 결과 확인
  
<img width="596" height="194" alt="image" src="https://github.com/user-attachments/assets/9dfeae5f-6b89-4237-8d67-b663454e459c" />
</br>

<img width="307" height="189" alt="image" src="https://github.com/user-attachments/assets/c348cd17-17a0-46a5-b774-77e8924e9f9b" />
</br>

<img width="547" height="317" alt="image" src="https://github.com/user-attachments/assets/15e61821-6653-48c6-94f1-2a98c4387acd" />
</br>

- countyfr 입력 안하면 오류 ( message class )
  
<img width="140" height="35" alt="image" src="https://github.com/user-attachments/assets/3f16b039-3eef-4334-b13a-26857c62a9bc" />


</br>
</br>

# 12/14
</br>
</br>

## <  핵심 개념 >
</br>

- **1) Pushbutton**
- **2) Selection Screen 필드 제어**
</br>

- 그룹명 같은 **이름** 지정 시 무조건 **대문자**
</br>
</br>

---

</br>

## < Selection Screen >
</br>

### 1) Pushbutton
- Selection Screen 에 버튼을 하나 추가
- 버튼 클릭 시 USER-COMMAND(OKCODE) 발생
- 버튼 생성 안되면 `/` 또는 `SKIP` 으로 띄우고 생성하기
```abap
TABLES <screen_fields>.

SELECTION-SCREEN
  PUSHBUTTON <position>(<length>) <text_var>
  USER-COMMAND <okcode>.
```
```abap
INITIALIZATION.
  <text_var> = '<버튼 텍스트>'.

AT SELECTION-SCREEN.
  CASE <screen_fields>-ucomm.
    WHEN '<okcode>'.
      " 버튼 클릭 시 처리 로직
  ENDCASE.
```
- 예시 코드
```abap
*************** TOP ****************
TABLES SSCRFIELDS.

DATA gv_btn_text TYPE string.

PARAMETERS p_carr TYPE scarr-carrid.

SELECTION-SCREEN
  PUSHBUTTON /10(20) gv_btn_text USER-COMMAND btn.    " / 은 줄 바꿈

*************** E01 ****************
INITIALIZATION.
  gv_btn_text = 'Display details'.

AT SELECTION-SCREEN.
  CASE SSCRFIELDS-ucomm.
    WHEN 'BTN'.
      MESSAGE 'Pushbutton 클릭됨' TYPE 'I'.
  ENDCASE.
```

</br>
</br>

### 2) Selection Screen 필드 제어
- Selection Screen에서 필드의 표시/입력 여부를 화면 출력 직전에 제어
- **CS (Contain String)** : 왼쪽 문자열 안에 오른쪽 문자열이 포함되면 TRUE
```abap
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF <조건>.
      screen-<속성> = <값>.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
```
- `screen-active` → 0이면 필드 완전 숨김, 1이면 표시
- `screen-invisible` → 1이면 값이 *****로 마스킹되어 보임
- `screen-input` → 0이면 입력 불가(읽기 전용), 1이면 입력 가능
- `screen-output` → 0이면 출력 안 함, 1이면 출력
- `screen-required` → 1이면 필수 입력 표시
- `screen-intensified` → 1이면 필드 강조 표시
- `screen-display_3d` → 1이면 3D 스타일, 0이면 Flat 스타일
```abap
*************** TOP ****************
DATA: gs_booking TYPE sbook.

SELECT-OPTIONS:
  so_car FOR gs_booking-carrid,
  so_con FOR gs_booking-connid MODIF ID abc.

*************** E01 ****************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-name CS 'SO_CON'
       OR screen-group1 = 'ABC'.
      screen-active = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 문제
  
<img width="554" height="250" alt="image" src="https://github.com/user-attachments/assets/ca025a6b-eab5-4677-bdfb-d307ad114460" />
</br>
</br>
</br>
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE5_TOP
*&---------------------------------------------------------------------*

DATA: gs_flight TYPE dv_flights.

PARAMETERS: pa_name TYPE c LENGTH 3.
SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.

SELECTION-SCREEN SKIP 2.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(15) TEXT-003.
    SELECTION-SCREEN COMMENT 35(10) TEXT-004.
    PARAMETERS: pa_abap AS CHECKBOX.
    SELECTION-SCREEN COMMENT 60(10) TEXT-005.
    PARAMETERS: pa_fiori AS CHECKBOX.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME TITLE TEXT-002.
    PARAMETERS: pa_all RADIOBUTTON GROUP rad,
                pa_nat RADIOBUTTON GROUP rad,
                pa_int RADIOBUTTON GROUP rad DEFAULT 'X'.
  SELECTION-SCREEN END OF BLOCK blk2.
SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE5_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  so_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QF'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP rad.
  IF pa_all = 'X'.
    MESSAGE 'ERROR !' TYPE 'E'.
  ENDIF.
```
</br>

- 결과 확인
  
<img width="554" height="262" alt="image" src="https://github.com/user-attachments/assets/250511f8-7d2d-4ce1-82a2-7ab4724209c0" />
</br>

- PA_ALL 선택하고 실행하면 에러 발생하며 라디오 버튼 그룹만 활성화
<img width="546" height="424" alt="image" src="https://github.com/user-attachments/assets/041c32eb-eb29-4ba4-8681-fb6c6d8ed2b5" />
</br>
</br>
</br>
</br>

### 실습 2
- 전 실습에서 program 27에 이어서 진행
- Selection Screen 필드 제어
- ALV 실습 활용
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_TOP
*&---------------------------------------------------------------------*

TABLES sscrfields.

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights,
      gv_mode TYPE C VALUE '1'.

" 2025-12-24: SALV 관련 변수 선언
DATA: go_salv TYPE REF TO cl_salv_table.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN SKIP 2.
  "버튼 추가. 버튼명:gv_text, 버튼코드: ON
  SELECTION-SCREEN PUSHBUTTON pos_low(20) gv_text USER-COMMAND ON.

  PARAMETERS: pa_rad1 RADIOBUTTON GROUP rad,
              pa_rad2 RADIOBUTTON GROUP rad DEFAULT 'X',
              pa_rad3 RADIOBUTTON GROUP rad.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid DEFAULT 'AA' TO 'LH' OPTION BT SIGN E,
                  so_con FOR gs_flight-connid DEFAULT '0017' NO-EXTENSION. " NO INTERVALS

SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

- `E01` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0027_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  tab_strip-activetab ='COMM3'.
  tab_strip-dynnr = '0103'. " 초기 스크린 설정

  gv_text = '잠금 적용'.

AT SELECTION-SCREEN OUTPUT. " PBO
  LOOP AT SCREEN.
    IF screen-name CS 'RAD3'.
      screen-input = gv_mode.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN. " PAI
  CHECK sy-dynnr = 1000.
  CASE sscrfields-ucomm.
    WHEN 'ON'.
      IF gv_mode = '1'.
        gv_mode = '0'.
        gv_text = '잠금해제'.
      ELSE.
        gv_mode = '1'.
        gv_text = '잠금 적용'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
```
</br>

- ALV 적용 방식
- `Pattern` 클릭 후'ABAP Objects Patterns' 더블클릭
<img width="579" height="307" alt="image" src="https://github.com/user-attachments/assets/1f21c1ac-9374-4ca0-9ae2-f645d5ea9a68" />
</br>
</br>

<img width="351" height="333" alt="image" src="https://github.com/user-attachments/assets/101a0700-8bac-448b-ae9b-632edf5a6447" />
</br>
</br>
</br>

- 결과 확인

<img width="543" height="292" alt="image" src="https://github.com/user-attachments/assets/a1325d18-9403-4923-9f58-8691794b55e1" />
</br>
</br>

- 버튼 누르면 잠금
<img width="548" height="298" alt="image" src="https://github.com/user-attachments/assets/95dc1dfe-6b7e-4887-9985-226981e7421e" />
</br>
</br>

- ALV 이용하여 출력
<img width="543" height="176" alt="image" src="https://github.com/user-attachments/assets/571dc55b-d20f-4b93-9d2c-378d8d026ebd" />

</br>
</br>
</br>
</br>

### 실습 3
- `Pushbutton` 을 이용한 `Selection Screen` 필드 제어 실습
- 전 실습(exercise)에서 이어서 진행
</br>
</br>

**1) 프로그램 최초 실행**
- `INITIALIZATION` 실행
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1000)`
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1200)`
  → 화면 표시
  
**2) 탭을 1100번으로 이동**
- `AT SELECTION-SCREEN OUTPUT(PAI, 1200)`
  → `AT SELECTION-SCREEN OUTPUT(PAI, 1000)`
  → `AT SELECTION-SCREEN OUTPUT(PBO, 1000)`
  → `AT SELECTION-SCREEN(PBO, 1100)`
  → 1100 화면 출력
  
**3) 1100 화면에서 HIDE 버튼 클릭**
- 버튼 클릭
  → `AT SELECTION-SCREEN(PAI, 1100)`
  → `AT SELECTION-SCREEN OUTPUT(PAI, 1000)`
</br>
</br>
</br>

- `TOP` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_TOP
*&---------------------------------------------------------------------*

TABLES SSCRFIELDS.

DATA: gs_flight TYPE dv_flights,
      gt_flight TYPE TABLE OF dv_flights,
      gv_mode TYPE I VALUE 1.

SELECTION-SCREEN BEGIN OF SCREEN 1100 AS SUBSCREEN.
  SELECT-OPTIONS: so_car FOR gs_flight-carrid MEMORY ID car,
                  so_con FOR gs_flight-connid.
  SELECTION-SCREEN SKIP 2.
  SELECTION-SCREEN PUSHBUTTON 1(10) gv_text USER-COMMAND hide.
  SELECTION-SCREEN SKIP 1.
  SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE TEXT-001.
    SELECT-OPTIONS: so_cif FOR gs_flight-cityfrom MODIF ID grp,
                    so_cit FOR gs_flight-cityto MODIF ID grp.
  SELECTION-SCREEN END OF BLOCK blk1.
SELECTION-SCREEN END OF SCREEN 1100.
```
</br>

- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE4_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  tab_strip-activetab = 'COMM2'.
  tab_strip-dynnr = '1200'.

  gv_text = '숨김모드 적용'.

AT SELECTION-SCREEN OUTPUT.
*  CHECK sy-dynnr = 1100.  " IF sy-dynnr = 1100. 도 가능
  LOOP AT SCREEN.
    IF screen-group1 = 'GRP'.
      screen-active = gv_mode.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

AT SELECTION-SCREEN.
  CHECK sy-dynnr = 1100.  " 1100번일때만 로직 실행
  CASE SSCRFIELDS-ucomm.
    WHEN 'HIDE'.
      IF gv_mode = 1.
        gv_mode = 0.
        gv_text = '숨김모드 해제'.
      ELSE.
        gv_mode = 1.
        gv_text = '숨김모드 적용'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
```
</br>

- 결과 확인
<img width="578" height="221" alt="image" src="https://github.com/user-attachments/assets/72062d90-598c-41b5-b648-f9e50eb3c5c1" />
</br>
</br>

- 버튼 클릭하면 입력창이 숨겨짐
<img width="579" height="222" alt="image" src="https://github.com/user-attachments/assets/0045c78e-b46c-439c-8095-df18e8af3f99" />
</br>
</br>
</br>
</br>

### 실습 4 (과제)
- 문제
- `FOR ALL ENTRIES IN <itab>` 사용
- `LOOP AT <itab> … ENDLOOP` 사용
- `READ TABLE <itab> INTO .. WITH KEY …` 사용
- `SORT <itab>` 사용
<img width="501" height="553" alt="image" src="https://github.com/user-attachments/assets/35361289-6707-4c88-a1fd-94288881aefd" />
</br>
</br>
</br>
</br>

- `TOP` 모듈 생성
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE6_TOP
*&---------------------------------------------------------------------*

DATA: gs_scarr TYPE scarr,
      gt_scarr TYPE TABLE OF scarr.

DATA: BEGIN OF gs_data,
        carrid    TYPE s_carr_id,     "DV_FLIGHTS
        carrname  TYPE s_carrname,    "SCARR
        connid    TYPE s_conn_id,     "DV_FLIGHTS
        fldate    TYPE s_date,        "DV_FLIGHTS
        cityfrom  TYPE s_from_cit,    "DV_FLIGHTS
        cityto    TYPE s_to_city,     "DV_FLIGHTS
        countryfr TYPE land1,         "DV_FLIGHTS
        countryto TYPE land1,         "DV_FLIGHTS
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data.

SELECT-OPTIONS: SO_CAR FOR gs_data-carrid,
                SO_CON FOR gs_data-connid,
                SO_FDT FOR gs_data-fldate NO INTERVALS.

DATA: go_salv TYPE REF TO cl_salv_table.
```
</br>

- `E01` 모듈 수정
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE6_E01
*&---------------------------------------------------------------------*

INITIALIZATION.
  SO_car-sign = 'I'.
  so_car-option = 'BT'.
  so_car-low = 'AA'.
  so_car-high = 'QF'.
  APPEND so_car.

START-OF-SELECTION.
  SELECT * FROM dv_flights
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid IN SO_car
      AND connid IN SO_con
      AND fldate IN SO_fdt.

  MOVE-CORRESPONDING gt_data TO gt_copy_data.

  SORT gt_copy_data BY carrid.
  DELETE ADJACENT DUPLICATES FROM gt_copy_data COMPARING carrid.

  " scarr에서 dv_flights의 carrid 에 맞는 데이터 뽑기
  SELECT carrid carrname FROM scarr
    INTO TABLE gt_scarr
    FOR ALL ENTRIES IN gt_copy_data
    WHERE carrid = gt_copy_data-carrid.

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid.
    gs_data-carrname = gs_scarr-carrname.
    MODIFY gt_data FROM gs_data.
  ENDLOOP.

  SORT gt_data BY carrid connid fldate ASCENDING.

  TRY.
      CALL METHOD cl_salv_table=>factory
        IMPORTING
          r_salv_table = go_salv
        CHANGING
          t_table      = gt_data.
    CATCH cx_salv_msg.
      MESSAGE 'Error !' TYPE 'E'.
  ENDTRY.

  go_salv->get_functions( )->set_all( abap_true ).
  go_salv->display( ).
```
</br>

- 결과 확인
<img width="460" height="407" alt="image" src="https://github.com/user-attachments/assets/d34e516b-a7ed-4f27-9a7f-78410d3f9406" />

</br>
</br>

# 12/26
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ABAP 객체 생성 기본 문법**
- **2) ALV 레이아웃 Variant 저장 옵션**

</br>
</br>

---

</br>

## < ALV 화면 구성 >
</br>

- **ALV 기본 툴바 버튼 기능 설명**
<img width="444" height="337" alt="image" src="https://github.com/user-attachments/assets/4e7d929d-9c27-4a8b-aede-4acf4a0965b2" />

</br>
</br>
</br>

- **상단 영역**
- `List Header` (선택) : ALV 제목/설명
- `Application Toolbar` : 정렬·필터·엑셀 버튼 (클릭 시 이벤트 발생)
</br>

- **중앙(Data Area)**
- `Column Header` : 컬럼 제목 + 정렬 기준
- `Key Column` : 왼쪽 고정 핵심 컬럼
- `Selection Button` (선택) : 행 선택용 체크/아이콘
- `Data` : 실제 조회 데이터
</br>

- **하단 영역**
- `Total Column` : 합계 대상 컬럼
- `Aggregation Row` : 합계 / 평균 / 최소 / 최대

<img width="490" height="346" alt="image" src="https://github.com/user-attachments/assets/5a287e4b-e177-4673-99a7-acdc4164259f" />
</br>
</br>


<img width="495" height="393" alt="image" src="https://github.com/user-attachments/assets/c069f6ba-2f59-4113-a372-eaa7a211bf23" />
</br>
</br>

### < ALV 마우스 사용 >
- **행 선택** : 클릭 (Ctrl = 다중)
- **컬럼 선택** : 헤더 클릭 (Ctrl = 다중)
- **컬럼 이동** : 드래그
- **폭 조절** : 컬럼 경계 드래그
- **헤더 우클릭** : 복사 / 숨김 / 자동폭 / 고정
</br>
</br>

### < ALV Subtotals >
- `Total` : 전체 데이터 합계 → 맨 아래 1줄
- `Subtotal` : 기준 컬럼별 그룹 합계 → 그룹마다 표시

<img width="465" height="361" alt="image" src="https://github.com/user-attachments/assets/5be7e416-eda5-42ea-a17d-f8bf3a0f0a0d" />
</br>
</br>
</br>
</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
- **Area는 Container로, Container는 ALV Grid로 연결됨**
- **클래스** : 객체의 설계도
- **인스턴스(객체)** : 클래스로 생성된 실제 실체
- **참조 변수** : 객체를 가리키는 포인터 변수
</br>
</br>

**1) 참조 변수 선언**
- 객체를 담는 변수가 아니라 객체를 가리키는 참조 변수
- 실제 객체는 아직 없음
```abap
DATA: <ref_var> TYPE REF TO <class_name>.
```
- `CL_GUI_CUSTOM_CONTAINER` : Screen의 Area와 연결되는 컨테이너
- `CL_GUI_ALV_GRID` : Container 안에서 데이터를 표시하는 ALV Grid 객체
```abap
*************** TOP ****************                   " REPORT ZPROGB03_0028.
DATA: go_cont TYPE REF TO CL_GUI_CUSTOM_CONTAINER,     " Container 참조변수 생성 (아직 객체 생성되기 전)
      go_alv TYPE REF TO CL_GUI_ALV_GRID.              " ALV 참조변수 생성 (아직 객체 생성되기 전)
```
</br>
</br>

**2) 객체 생성**
- `CREATE OBJECT` 로 실제 객체 생성 (생성자 자동 호출)
- 생성 시 생성자(CONSTRUCTOR) 호출
- 필요한 값은 `EXPORTING` 으로 전달
- PBO에서 INITIAL 체크 후 1회 생성, Container → ALV 순서
```abap
IF <ref_var> IS INITIAL.
  CREATE OBJECT <ref_var>
    EXPORTING
      <param> = <value>.
    EXCEPTIONS
      <exception_1> = <rc_1>
ENDIF.
```
- `container_name` → Screen의 Area와 연결
- `i_parent` → ALV ↔ Container 연결
```abap
REPORT ZPROGB03_0028.

*************** PBO ****************
MODULE init_alv OUTPUT.
  " 1. SCREEN에 Custom Area 추가
  " 2. Container Control 생성 (AREA와 연결)
  IF go_cont IS INITIAL. " 최초 1번만 실행
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.

    " 3. ALV Grid 생성
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont " Container와 연결
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
  ENDIF.
ENDMODULE.
```
</br>
</br>

- `Custom Container` 객체 생성
  
<img width="377" height="363" alt="image" src="https://github.com/user-attachments/assets/6d99dc1d-c5bd-4140-b21a-e41130485067" />
</br>
</br>

- `ALV Grid` 객체 생성

<img width="375" height="359" alt="image" src="https://github.com/user-attachments/assets/90735023-c617-449e-98ad-6b3eed0e0f61" />
</br>
</br>
</br>
</br>

**3) ALV 컨트롤 인스턴스 해제**
- 화면 종료 시 PAI에서 ALV/Container → free 호출로 자원 해제
- PAI(EXIT, CANCEL) 에서 수행
```abap
FORM free_control_resources.
  CALL METHOD <alv_ref>->free.
  CALL METHOD <container_ref>->free.
  FREE: <alv_ref>, <container_ref>.
ENDFORM.
```
- `->FREE()` : 객체가 사용하는 GUI 컨트롤 자원 해제
- `FREE ref` : 참조 변수만 해제(값 INITIAL로 초기화)
```abap
REPORT ZPROGB03_0028.

*************** PAI ****************
MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      PERFORM FREE_ALV.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      PERFORM FREE_ALV.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*************** F01 ****************
FORM free_alv.
  IF GO_ALV IS NOT INITIAL.
    GO_ALV->FREE( ).
  ENDIF.
  IF GO_CONT IS NOT INITIAL.
    GO_CONT->FREE( ).
  ENDIF.
  FREE: GO_ALV, GO_CONT.
ENDFORM.
```
</br>
</br>

**4) ALV Grid 데이터 표시**
- ALV Grid에 처음으로 데이터를 출력하는 메서드
- 내부 테이블( `it_outtab` )의 데이터를 기준으로 화면 구성
- `i_structure_name` 에 지정한 DDIC 구조 기준으로 ALV 컬럼 구조가 자동 출력됨
```abap
CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <internal_table>
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
- `SET_TABLE_FOR_FIRST_DISPLAY` : 내부 테이블 데이터를 ALV Grid에 최초로 표시하는 메서드
- SET_TABLE* 하고 f4 누르면 자동완성
```abap
REPORT ZPROGB03_0028.

*************** PBO ****************
" 4. ALV Grid 안에 Data Display
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
</br>
</br>

- ALV Grid 메서드 호출
<img width="384" height="362" alt="image" src="https://github.com/user-attachments/assets/e2594dd2-511b-4721-b9c6-b9e5ec7d13ab" />
</br>
</br>
</br>
</br>

**5) ALV 데이터 갱신**
- `REFRESH_TABLE_DISPLAY`
- 이미 생성된 ALV Grid가 있을 때 ALV를 다시 만들지 않고 데이터만 화면에 반영
```abap
REPORT ZPROGB03_0028.

*************** PBO ****************
IF go_cont IS INITIAL. " 최초 1번만 실행
   " ALV 생성 로직
ELSE.
   " 최초에 1번 생성된 후, 두번째부터는 ELSE구문을 실행한다
   CALL METHOD go_alv->refresh_table_display
     EXCEPTIONS
       finished       = 1
       others         = 2.
ENDIF.
```
</br>
</br>

**6) 결과 확인**

<img width="475" height="127" alt="image" src="https://github.com/user-attachments/assets/a7190486-1fe1-40c7-be7f-222564219cbc" />
</br>

<img width="574" height="174" alt="image" src="https://github.com/user-attachments/assets/9b72474e-7ab8-47fd-a1f1-5c8b5a0c0e48" />
</br>
</br>
</br>
</br>
</br>

---

</br>

## < ALV 레이아웃 Variant 저장 옵션 >
</br>

**1) `I_SAVE` (저장 범위)**
- 프로그램 실행 시 자동 적용 여부 지정
- `A` : Global / Local 레이아웃 모두 저장 가능
- `U` : Local(사용자별) 레이아웃만 저장 가능
- `X` : Global(공용) 레이아웃만 저장 가능
- `space` : 레이아웃 저장 불가
```abap
i_save = '<A|U|X|space>'.      " ' ' (공백 문자) 쓰지 않기. '' 는 가능.
```
</br>

**2) `I_DEFAULT` (기본 레이아웃)**
- 프로그램 실행 시 자동 적용 여부 지정
- `space` : 레이아웃은 저장되지만 자동 적용 안 됨
- `X` : 해당 레이아웃을 기본(Default) 으로 저장 → 실행 시 자동 적용
```abap
i_default = '<X|space>'.
```
</br>

**3) `<ls_variant>-report`**
- 레이아웃 Variant가 속한 프로그램명
- Variant 저장/조회에 필수
- 보통 항상 sy-cprog 사용
```abap
<ls_variant>-report = sy-cprog.
```
</br>

**4) `<ls_variant>-variant`**
- 적용할 레이아웃(Variant) 이름
- Selection Screen에서 입력받은 Variant를 ALV에 적용할 때 사용
```abap
<ls_variant>-variant = <p_var>.
```
</br>

**5) `<ls_variant>-handle`**
- 같은 프로그램에서 여러 ALV Grid 구분용 식별자
- ALV 1개면 생략 가능
```abap
<ls_variant>-handle = '1'.
```
</br>

**6) `<ls_variant>-username`**
- 특정 사용자 전용 Variant 지정
- 대부분 시스템이 자동 처리 → 직접 설정 거의 안 함
```abap
<ls_variant>-username = sy-uname.
```
</br>

**7) <ls_variant>-text**
- 레이아웃 설명 텍스트
- Variant 저장 화면에서 입력·표시되는 값
```abap
<ls_variant>-text = 'Flight ALV Layout'.
```
</br>
</br>
</br>

### 1) ALV 레이아웃 Variant 저장
- `<ls_variant>-report = sy-cprog.` 는 필수
```abap
DATA: <ls_variant> TYPE disvariant,
      <lv_save>    TYPE c LENGTH 1.

<ls_variant>-report = sy-cprog.
<lv_save> = '<A|U|X|space>'.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_variant = <ls_variant>
    i_save     = <lv_save>
    i_default  = '<X|space>'
  CHANGING
    it_outtab  = <itab>.
```
```abap
REPORT ZPROGB03_0029.

*************** TOP ****************
DATA: gs_variant TYPE disvariant,
      gv_save    TYPE c LENGTH 1.

gs_variant-report = sy-cprog.
gv_save = 'A'.   " Global / Local 모두 저장

*************** PBO ****************
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_variant = gs_variant
    i_save     = gv_save
    i_default  = 'X'
  CHANGING
    it_outtab  = gt_bookings.
```
</br>

### 2) Selection Screen에서 Variant 전달
```abap
DATA <ls_variant> TYPE disvariant.
PARAMETERS <p_var> TYPE disvariant-variant.

<ls_variant>-report  = sy-cprog.
<ls_variant>-variant = <p_var>.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_variant = <ls_variant>
  CHANGING
    it_outtab  = <itab>.
```
```abap
REPORT ZPROGB03_0029.

*************** TOP ****************
DATA gv_variant TYPE disvariant.
PARAMETERS pa_lv TYPE disvariant-variant.

gv_variant-report  = sy-cprog.
gv_variant-variant = pa_lv.

*************** PBO ****************
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_variant = gv_variant
  CHANGING
    it_outtab  = gt_bookings.
```
</br>
</br>
</br>
</br>

## < ALV 레이아웃 Variant 저장 옵션 (I_SAVE) 예제 >
</br>

**1) `TOP` 모듈 수정**
```abap
REPORT ZPROGB03_0029.

DATA: gs_variant TYPE disvariant.

INITIALIZATION.
  PERFORM set_options.
  gs_variant-report = sy-cprog.
  gs_variant-username = sy-uname.
  gs_variant-handle = 'MAIN'.
  " handle : 하나의 화면에 여러개의 ALV가 있는 경우
  "          각 ALV마다 구분자가 필요하므로 구분자 지정해줌
```
</br>

**2) `PBO` 의 `init_alv` 모듈 수정**
```abap
REPORT ZPROGB03_0029.

" 4. ALV Grid 안에 Data display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SBOOK'
        is_variant                    = gs_variant
        i_save                        = 'A' " A, X, U, ''
        i_default                     = 'X'
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
```
</br>

**3) `Variant` 지정**
- 레이아웃 수정 후 레이아웃 저장
  
<img width="511" height="174" alt="image" src="https://github.com/user-attachments/assets/2066b032-4300-445e-b15d-6943b89a5324" />
</br>
</br>

- `/` 로 시작 → Global(공용) 레이아웃
- `/` 없음 → Local(개인) 레이아웃

<img width="435" height="351" alt="image" src="https://github.com/user-attachments/assets/fbab5ce1-d915-4938-a262-f83736fe20f4" />
</br>
</br>

- **`User-Specific`** ✔ → Local(개인) / ✖ → Global(공용)
- **`Default`** ✔ → 실행 시 자동 적용 / ✖ → 수동 선택
<img width="435" height="361" alt="image" src="https://github.com/user-attachments/assets/fd7a76e1-0a81-4205-b3d7-70edded26b4d" />
</br>
</br>
</br>

**4) Default 선택 안했을 때 레이아웃 선택하면 바뀜**

<img width="546" height="178" alt="image" src="https://github.com/user-attachments/assets/f3b2e2a4-e952-4c70-8c8b-c3efbe07d126" />

</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- ALV 실습
</br>
</br>

**1) Program 에 모듈 생성**
```abap
REPORT ZPROGB03_0029.

INCLUDE ZPROGB03_0029_TOP.
INCLUDE ZPROGB03_0029_O01.
INCLUDE ZPROGB03_0029_I01.
INCLUDE ZPROGB03_0029_F01.
```
</br>
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_TOP
*&---------------------------------------------------------------------*

DATA: gt_data TYPE TABLE OF sbook,
      gs_data LIKE LINE OF gt_data.

DATA: ok_code TYPE sy-ucomm,
      go_cont TYPE REF TO cl_gui_custom_container,
      go_alv  TYPE REF TO cl_gui_alv_grid.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.

INITIALIZATION.
  PERFORM set_options.

START-OF-SELECTION.
  PERFORM GET_DATA.
  CALL SCREEN 100.
```
</br>
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_F01
*&---------------------------------------------------------------------*

FORM set_options.
  so_fdt-sign = 'I'.
  so_fdt-option = 'BT'.
  so_fdt-low = sy-datum(4) && '0101'. " 매년 첫째날
  so_fdt-high = sy-datum(4) && '1231'. " 매년 마지막날
  APPEND so_fdt.

  so_car-sign = 'I'.
  so_car-option = 'GT'.
  so_car-low = 'AZ'.
  APPEND so_car.

  CLEAR so_car.
  so_car-sign = 'E'.
  so_car-option = 'EQ'.
  so_car-low = 'LH'.
  APPEND so_car.
ENDFORM.

*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM SBOOK
    INTO TABLE gt_data
    WHERE carrid IN so_car
      AND connid IN so_con
      AND fldate IN so_fdt.
ENDFORM.
```
</br>
</br>

**4) `PBO` 모듈 생성**
- `SY-UNAME` : 현재 로그인한 사용자 ID
- `SY-DATUM` : 현재 시스템 날짜 (`YYYYMMDD`)
- `SY-UZEIT` : 현재 시스템 시간 (`HHMMSS`)
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100' WITH sy-uname sy-datum sy-uzeit.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  " 1. 100번 스크린 Layout에 Area 추가
  " 2. 만약 Container 가 만들어지기 전이라면, 객체 생성함
  "    Custom Container 생성
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    " 3. ALV Grid 객체 생성해서 Container와 연결
    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    " 4. ALV Grid 안에 Data display
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SBOOK'
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4
            .
    IF sy-subrc <> 0.
*     Implement suitable error handling here
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0029_I01
*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

**6) 결과 확인**

<img width="488" height="141" alt="image" src="https://github.com/user-attachments/assets/0593593a-ab76-4ee7-a844-32473cfcbab4" />
</br>

<img width="515" height="238" alt="image" src="https://github.com/user-attachments/assets/3e58a92f-1c2d-4be3-af54-f6a5e9ea0fb8" />
</br>
</br>
</br>
</br>
</br>

### 실습 2
- 문제
<img width="356" height="208" alt="image" src="https://github.com/user-attachments/assets/f0ef060a-6af1-4039-baf9-1cb3d57ad293" />
</br>
</br>
</br>
</br>
</br>

**1) Program 에 모듈 생성**
```abap
REPORT ZBC405_03_ALV.

INCLUDE ZBC405_03_ALV_TOP.
INCLUDE ZBC405_03_ALV_O01.
INCLUDE ZBC405_03_ALV_I01.
INCLUDE ZBC405_03_ALV_F01.

START-OF-SELECTION.

*PERFORM get_data.
PERFORM get_data2.
CALL SCREEN 100.
```
</br>
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.

DATA: gs_flight TYPE sflight,
      gt_flight TYPE TABLE OF sflight.

DATA: gs_flight2 TYPE BC400_S_FLIGHT,
      gt_flight2 TYPE TABLE OF BC400_S_FLIGHT.

SELECT-OPTIONS: so_car FOR gs_flight-carrid,
                so_con FOR gs_flight-connid.
```
</br>
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
ENDFORM.

*&---------------------------------------------------------------------*

FORM get_data2.
  SELECT carrid connid fldate seatsmax seatsocc FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight2
    WHERE carrid IN so_car
      AND connid IN so_con.

  LOOP AT gt_flight2 INTO gs_flight2.
    gs_flight2-PERCENTAGE = ( gs_flight2-seatsocc / gs_flight2-seatsmax ) * 100.
    MODIFY gt_flight2 FROM gs_flight2.
  ENDLOOP.
ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv.
  IF go_container IS INITIAL.
    go_container->free( ).
  ENDIF.
  IF go_alv_grid IS INITIAL.
    go_alv_grid->free( ).
  ENDIF.
  FREE: go_container, go_alv_grid.
ENDFORM.
```
</br>
</br>

**4) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE create_and_transfer OUTPUT.
  IF go_container IS INITIAL.
    CREATE OBJECT go_container
      EXPORTING
        container_name              = 'MY_CONTROL_AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    CREATE OBJECT go_alv_grid
      EXPORTING
        i_parent          = go_container
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
      MESSAGE A010(BC405_408).
    ENDIF.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'BC400_S_FLIGHT'
      CHANGING
        it_outtab                     = gt_flight2
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE A012(BC405_408).
    ENDIF.


  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE clear_ok_code OUTPUT.
  CLEAR OK_CODE.
ENDMODULE.
```
</br>
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
      PERFORM free_alv.
    WHEN 'CANCEL'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

**6) 결과 확인**

<img width="500" height="103" alt="image" src="https://github.com/user-attachments/assets/91faf0f7-6bcc-4e33-91bf-32f8557a5f2a" />
</br>

<img width="506" height="227" alt="image" src="https://github.com/user-attachments/assets/dc02a463-7156-409c-ae94-416c3b6eb75b" />

</br>
</br>
</br>
</br>
</br>

### 실습 3
- Variat 실습
- 실습 2에서 이어서 진행
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: gs_variant TYPE disvariant.
PARAMETERS: pa_va TYPE disvariant-variant.
```
</br>
</br>

**2) `PBO` 의 `create_and_transfer` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

gs_variant-report = sy-cprog.
gs_variant-variant = pa_va.

CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'sflight'
    is_variant = gs_variant
    i_save = 'A'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
IF sy-subrc <> 0.
  MESSAGE A012(BC405_408).
ENDIF.
```
</br>
</br>

**3) 결과 확인**

<img width="492" height="282" alt="image" src="https://github.com/user-attachments/assets/5ae999f6-b27a-4a39-9a9c-f1a4094975ea" />

</br>
</br>

# 12/29
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV 데이터 갱신 (refresh_table_display, stable)**
- **2) ALV 레이아웃 설정 (IS_LAYOUT)**
- **3) ALV 임계값(Threshold) 아이콘 표시 – Exception Display (특정 조건에 따라 빨강/노랑/초록 아이콘 자동 표시)**
- **4) ALV 초기 정렬 기준 설정 (lvc_s_sort)**
- **5) ALV 초기 필터 조건 설정 (LVC_S_FILT)**
- **6) ALV Line Color (행 색상 제어)**
- **7) ALV Cell Color (셀 단위 색상 제어)**
</br>

- **SELECT-OPTIONS는 `Selection Screen` 에서만 가능**
- **`TYPE-POOLS COL.` 로 색상 코드 확인 가능 (필수)**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

### 1) ALV 데이터 갱신
- 이미 생성된 ALV Grid가 있을 때 ALV를 다시 생성하지 않고 데이터만 화면에 반영
- `REFRESH_TABLE_DISPLAY` → ALV Grid 데이터를 다시 그리되 스크롤·선택 상태를 유지한 채 화면만 갱신하는 메서드
- 보통 PBO에서 호출
```abap
CALL METHOD <alv_ref>->refresh_table_display
  EXPORTING
    is_stable = <ls_stable>.
```
</br>

- `LVC_S_STBL` 은 ALV 새로고침 시 스크롤·선택 위치가 초기화되지 않도록 화면을 고정하는 옵션
- `row = 'X'` → 행(스크롤) 위치 유지
- `col = 'X'` → 열(커서) 위치 유지
```abap
DATA ls_stable TYPE lvc_s_stbl.

ls_stable-row = 'X'. " 행 위치 유지
ls_stable-col = 'X'. " 열 위치 유지
```
</br>

- `i_soft_refresh` = 'X' : 정렬·필터·레이아웃을 유지한 채 데이터만 갱신
- `i_soft_refresh` = space : ALV를 전체 재계산하여 정렬·필터까지 다시 적용
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA gs_stable TYPE LVC_S_STBL.   

********* PBO *********
gs_stable-row = 'X'.
gs_stable-col = 'X'.

CALL METHOD go_alv->refresh_table_display
  EXPORTING
    is_stable      = gs_stable
    i_soft_refresh = ''  " X : 정렬, 필터, 그외 기능들 유지
                         " _ : 전체 리프레쉬 (기본)
  EXCEPTIONS
    finished       = 1
    OTHERS         = 2.
```
</br>
</br>
</br>

### 2) ALV 레이아웃 설정 (IS_LAYOUT)
- ALV Grid의 화면 모양(제목, 줄무늬, 컬럼 폭 등)을 설정
- 데이터 내용은 안 바뀌고 표시 방식만 제어
- `SET_TABLE_FOR_FIRST_DISPLAY` 에서 최초 출력 시만 적용
- **`Ctrl + Space`** - `gs_layout-` 뒤에서 누르면 구조체 필드 목록 자동 표시
</br>

- `LVC_S_LAYO` : ALV 화면의 모양(레이아웃) 을 제어하는 구조체
```abap
DATA <ls_layout> TYPE lvc_s_layo.

<ls_layout>-grid_title = 'Booking List'.
<ls_layout>-zebra      = 'X'.
<ls_layout>-cwidth_opt = 'X'.
<ls_layout>-totals_bef = 'X'.
<ls_layout>-sel_mode   = 'A'.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
    is_layout        = <ls_layout>
  CHANGING
    it_outtab        = <itab>.
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM set_layout.
  gs_layout-grid_title = 'Booking List'.   " ALV 제목
  gs_layout-zebra      = 'X'.               " 행 줄무늬
  gs_layout-cwidth_opt = 'X'.               " 컬럼 너비 자동
  gs_layout-totals_bef = 'X'.               " 합계 상단
  gs_layout-sel_mode   = 'A'.               " 선택 모드 (_,A,B,C,D)
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
    is_layout        = gs_layout
  CHANGING
    it_outtab        = gt_data.
```
</br>
</br>

**< LVC_S_LAYO (ALV Layout Structure 주요 필드 정리) >**
  
| Field Name     | Description |
|---------------|-------------|
| `grid_title`  | ALV 상단에 표시되는 제목 텍스트 (최대 70자) |
| `zebra`       | `'X'` 설정 시 행을 교차 색상(Zebra 패턴)으로 표시 |
| `no_headers`  | `'X'` 설정 시 컬럼 헤더 숨김 |
| `sel_mode`    | 행 / 셀 / 컬럼 선택 모드 설정 |
| `cwidth_opt`  | `'X'` 설정 시 컬럼 폭 자동 최적화 |
| `no_toolbar`  | `'X'` 설정 시 ALV 툴바 숨김 |
| `totals_bef`  | `'X'` 설정 시 합계 행을 데이터 맨 위에 표시 |
| `no_hgridln`  | `'X'` 설정 시 가로(Grid) 라인 숨김 |
| `no_vgridln`  | `'X'` 설정 시 세로(Grid) 라인 숨김 |
| `info_fname`  | **행(Row) 색상 정보**를 담고 있는 내부 테이블 필드명 |
| `ctab_fname`  | **셀(Cell) 색상 정보**를 담고 있는 내부 테이블 필드명 |

</br>

**< ALV Selection Mode (lvc_s_layo-sel_mode) >**

| 값(Value) | 선택 가능 범위 (Possible Selections) | 선택 컬럼 표시 (Selection Column) |
|-----------|--------------------------------------|-----------------------------------|
| space     | B와 동일                              | No                                |
| A         | 다중 행 선택<br>다중 컬럼 선택        | Yes                               |
| B         | 단일 행 선택<br>다중 컬럼 선택        | No                                |
| C         | 다중 행 선택<br>다중 컬럼 선택        | No                                |
| D         | 다중 행 선택<br>다중 컬럼 선택<br>셀 단위 선택 가능 | Yes |

</br>
</br>
</br>

### 3) ALV 임계값(Threshold) 아이콘 표시 – Exception Display
- 특정 조건에 따라 빨강/노랑/초록 아이콘 자동 표시
- `excp_fname` : 예외(아이콘) 값을 담고 있는 컬럼 이름을 ALV에 알려줌
- `excp_led` : 해당 컬럼 값을 신호등(LED) 아이콘으로 표시하도록 설정
- ALV Exception 컬럼은 `1 = 빨강` , `2 = 노랑` , `3 = 초록`
```abap
LOOP AT <itab> INTO <wa>.
  IF <조건1>.
    <wa>-<excp_field> = '1'.
  ELSEIF <조건2>.
    <wa>-<excp_field> = '2'.
  ELSE.
    <wa>-<excp_field> = '3'.
  ENDIF.

  MODIFY <itab> FROM <wa> TRANSPORTING <excp_field>.
ENDLOOP.

*&---------------------------------------------------------------------*

gs_layout-excp_fname = '<예외컬럼명>'.
gs_layout-excp_led   = 'X'.

*&---------------------------------------------------------------------*

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = <itab>.
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: excp_fld        TYPE char1,          " 예외(신호등) 값
      phone           TYPE scustom-telephone,
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.

********* F01 *********
LOOP AT gt_data INTO gs_data.

  IF gs_data-loccuram < 500.
    gs_data-excp_fld = '1'.   " 🔴 빨강
  ELSEIF gs_data-loccuram <= 800.
    gs_data-excp_fld = '2'.   " 🟡 노랑
  ELSE.
    gs_data-excp_fld = '3'.   " 🟢 초록
  ENDIF.

  MODIFY gt_data FROM gs_data
    TRANSPORTING excp_fld.    " 예외 컬럼만 반영

  CLEAR gs_data.
ENDLOOP.

********* F01 (LAYOUT) *********
gs_layout-excp_fname = 'EXCP_FLD'.   " 예외 아이콘 컬럼명
gs_layout-excp_led   = 'X'.          " LED(신호등) 아이콘

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = gt_data.
```
</br>
</br>
</br>

### 4) ALV 초기 정렬 기준 설정 (lvc_s_sort)
- ALV 최초 출력 시 정렬 기준을 지정
- **`set_sorter_criteria` 는 이미 생성된 ALV Grid에 정렬 기준만 새로 적용하는 메서드**
</br>

- `lvc_s_sort` : 정렬 기준 1개, `lvc_t_sort` : 정렬 기준 여러 개를 담는 테이블
- `spos` : 정렬 우선순위 (1, 2, 3 …)
- `up = 'X'` : 오름차순, `down = 'X'` : 내림차순
- `SUBTOT = 'X'` : 해당 정렬 필드 기준으로 소계 라인 출력하며, DO_SUM = 'X' 로 지정된 합계 필드가 있어야 소계 값이 계산·표시됨
```abap
DATA: gt_sort TYPE lvc_t_sort,
      gs_sort TYPE lvc_s_sort.

gs_sort-fieldname = '<FIELD_NAME>'.
gs_sort-spos      = <순번>.
gs_sort-up        = 'X'.   " 또는 gs_sort-down = 'X'
APPEND gs_sort TO gt_sort.
CLEAR gs_sort.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab = <itab>
    it_sort   = gt_sort.
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA: gt_sorter TYPE lvc_t_sort,    " 정렬 기준 테이블
      gs_sorter TYPE lvc_s_sort.    " 정렬 기준 구조

********* F01 *********
FORM set_sorter.
  IF gt_sorter IS INITIAL.
    gs_sorter-fieldname = 'ORDER_DATE'. " 1순위 정렬 필드
    gs_sorter-spos      = 1.            " 정렬 우선순위
    gs_sorter-up        = 'X'.          " 오름차순
    gs_sorter-subtot    = 'X'.          " 소계 표시
    APPEND gs_sorter TO gt_sorter.
    CLEAR gs_sorter.

    gs_sorter-fieldname = 'LOCCURAM'.   " 2순위 정렬 필드
    gs_sorter-spos      = 2.            " 정렬 우선순위
    gs_sorter-down      = 'X'.          " 내림차순
    APPEND gs_sorter TO gt_sorter.
  ENDIF.
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab        = gt_data
    it_sort          = gt_sorter.
```
</br>
</br>
</br>

### 5) ALV 초기 필터 조건 설정 (LVC_S_FILT)
- ALV 최초 출력 시 특정 조건으로 데이터 필터링
- **`set_filter_criteria` 는 이미 생성된 ALV Grid에 필터 조건만 새로 적용하는 메서드**
- `SY-REPID` 시스템 변수 → 현재 실행 중인 프로그램의 리포트(프로그램) ID를 담고 있는 시스템 필드
</br>

- `lvc_s_filt` : 필터 조건 1개
- `lvc_t_filt` : 필터 조건 여러 개를 담는 테이블
- `fieldname` 는 ALV에서 필터를 적용할 컬럼명을 의미
- `sign` 은 조건을 포함(I)할지 제외(E)할지를 결정
- `option` 은 비교 방식으로 EQ(같음), BT(범위), GE/LE(이상·이하) 등을 지정
- `low` 는 비교에 사용되는 기준 값이며, BT 사용 시 시작값. `high` 는 BT 옵션 사용 시 범위의 종료값.
```abap
DATA: gt_filter TYPE lvc_t_filt,
      gs_filter TYPE lvc_s_filt.

gs_filter-fieldname = '<FIELD_NAME>'.
gs_filter-sign      = 'I'.
gs_filter-option    = 'EQ'.
gs_filter-low       = <VALUE>.
APPEND gs_filter TO gt_filter.
CLEAR gs_filter.

CALL METHOD <alv_ref>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab = <itab>
    it_filter = gt_filter.
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA: gt_filter TYPE lvc_t_filt,    " ALV 필터 조건 테이블
      gs_filter TYPE lvc_s_filt.    " ALV 필터 조건 구조

SELECTION-SCREEN BEGIN OF SCREEN 1100 AS SUBSCREEN.
  SELECT-OPTIONS: so_car2 FOR SDYN_CONN-CARRID DEFAULT 'DL', " 항공사 필터
                  so_con2 FOR SDYN_CONN-CONNID.              " 노선 필터
SELECTION-SCREEN END OF SCREEN 1100.

********* START-OF-SELECTION *********
START-OF-SELECTION.
  PERFORM get_data.                 " 최초 데이터 조회
  CALL SCREEN 100.                  " 메인 화면 호출

********* (SCREEN 100) PROCESS BEFORE OUTPUT *********
PROCESS BEFORE OUTPUT.
  MODULE status_0100.                            " 상태바
  CALL SUBSCREEN SUB01 INCLUDING sy-repid '1100'. " 필터 입력
  MODULE init_alv.                               " ALV 출력

********* (SCREEN 100) PROCESS AFTER INPUT *********
PROCESS AFTER INPUT.
  MODULE exit AT EXIT-COMMAND.                   " 종료 처리
  CALL SUBSCREEN SUB01.                          " 필터 입력 반영
  MODULE user_command_0100.                      " SEARCH 처리

********* PAI *********
MODULE user_command_0100 INPUT.
  CASE ok_code.
    WHEN 'SEARCH'.
      PERFORM set_filter.           " 필터 재적용
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
  ENDCASE.
ENDMODULE.

********* F01 *********
FORM set_filter.
  CLEAR: gt_filter, gs_filter.

  IF so_car2 IS NOT INITIAL.
    LOOP AT so_car2.
      gs_filter-fieldname = 'CARRID'.        " 필터 컬럼
      gs_filter-sign      = so_car2-sign.    " 포함/제외
      gs_filter-option    = so_car2-option.  " EQ / BT
      gs_filter-low       = so_car2-low.     " 기준값
      gs_filter-high      = so_car2-high.    " 종료값
      APPEND gs_filter TO gt_filter.
    ENDLOOP.
  ENDIF.

  IF so_con2 IS NOT INITIAL.
    CLEAR gs_filter.
    LOOP AT so_con2.
      gs_filter-fieldname = 'CONNID'.        " 필터 컬럼
      gs_filter-sign      = so_con2-sign.
      gs_filter-option    = so_con2-option.
      gs_filter-low       = so_con2-low.
      gs_filter-high      = so_con2-high.
      APPEND gs_filter TO gt_filter.
    ENDLOOP.
  ENDIF.

  CALL METHOD go_alv->set_filter_criteria
    EXPORTING
      it_filter = gt_filter.

ENDFORM.

********* PBO (INIT_ALV 내부) *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab = gt_data
    it_filter = gt_filter.
```
</br>
</br>
</br>

### 6) ALV Line Color (행 색상 제어)
- ALV 행(Line)에 색상을 적용하는 기능
- `INFO_FNAME` : ALV에게 “행 색상(Line Color) 정보가 들어있는 필드명”을 알려주는 설정
- `TYPE-POOLS COL.` 로 색상 코드 확인 가능 (필수)
```abap
TYPE-POOLS COL.
'C' && <color_constant> && <intensified> && <inverse>
```
- `C` : 색상 사용 표시
- `<color_constant>` : 색상 번호 (0~7)
- `<intensified>` : 진하게 표시 (1) / 기본 (0)
- `<inverse>` : 글자색 ↔ 배경색 반전 (1) / 기본 (0)
```abap
REPORT ZPROGB03_0029.

********* TOP *********
TYPE-POOLS col.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: col_fld TYPE char4, "예시)'C310'
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM get_data.
    LOOP AT gt_data INTO gs_data.
      IF gs_data-smoker = 'X'.
        gs_data-col_fld = 'C' && col_negative && '10'.
      ENDIF.
      MODIFY gt_data FROM gs_data.
      CLEAR: gs_data.
    ENDLOOP.
  ENDIF.
ENDFORM.

********* F01 *********
FORM set_layout.
  gs_layout-info_fname = 'COL_FLD'. 
ENDFORM.

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout                     = gs_layout
  CHANGING
    it_outtab                     = gt_data
```
</br>
</br>
</br>

### 7) ALV Cell Color (셀 단위 색상 제어)
- ALV에서 특정 셀만 색상 변경하는 기능
- 행 색상과 달리 셀 색상용 내부테이블 사용
- 각 행마다 `lvc_t_scol` 타입 내부테이블을 가짐
- 색상 적용 후 `CTAB_FNAME` 으로 ALV에 전달해야 표시됨
- `CTAB_FNAME` : ALV에게 “셀 색상(Cell Color) 정보가 들어있는 필드명”을 알려주는 설정
</br>

- `lt_scol` : 이 행에서 색칠할 셀 목록 (`lvc_t_scol`)
- `ls_scol` : 셀 하나 색상 정보 (`lvc_s_scol`)
- `<ls_scol>-fname` : 색상을 적용할 컬럼(필드) 이름을 지정함.
- `<ls_scol>-color-col` : 해당 셀에 적용할 색상을 지정함.
- `<ls_scol>-color-int` : 색상을 진하게 표시할지 여부를 지정함.
- `<ls_scol>-color-inv` : 글자색과 배경색을 서로 바꿀지 여부를 지정함.
```abap
LVC_S_SCOL
 ├─ FNAME        "색칠할 컬럼명
 └─ COLOR        "색상 정보 구조체
     ├─ COL
     ├─ INT
     └─ INV
```
```abap
" 1. 출력 구조
DATA: BEGIN OF <gs_data>.
        INCLUDE TYPE <structure>.
DATA: <excp_fld> TYPE char1,
      <col_fld>  TYPE char4,        " Line Color
      <lt_scol>  TYPE lvc_t_scol,   " Cell Color
     END OF <gs_data>.
DATA <gt_data> LIKE TABLE OF <gs_data>.
DATA <ls_scol> TYPE lvc_s_scol.

" 2. 셀 색상 처리
LOOP AT <gt_data> INTO <gs_data>.
  IF <조건>.
    <ls_scol>-fname      = '<FIELDNAME>'.
    <ls_scol>-color-col  = <COLOR>.
    <ls_scol>-color-int  = '1'.
    <ls_scol>-color-inv  = '0'.
    APPEND <ls_scol> TO <gs_data>-<lt_scol>.
  ENDIF.
  MODIFY <gt_data> FROM <gs_data>
    TRANSPORTING <lt_scol>.
ENDLOOP.

" 3. Layout 연결
gs_layout-ctab_fname = '<lt_scol 필드명>'.

" 4. ALV 출력
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = <gt_data>.
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
TYPE-POOLS col.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: lt_scol TYPE lvc_t_scol,   " 셀 색상 정보 (행 단위)
     END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.
DATA ls_scol TYPE lvc_s_scol.    " 셀 1개 색상 구조
DATA gs_layout TYPE lvc_s_layo.

********* F01 *********
FORM get_data.
  LOOP AT gt_data INTO gs_data.
    IF gs_data-smoker = 'X'.
      CLEAR ls_scol.
      ls_scol-fname = 'SMOKER'.      " 색칠할 컬럼
      ls_scol-color-col = '3'.       " 색상 번호
      ls_scol-color-int = '1'.       " 진하게
      ls_scol-color-inv = '0'.       " 반전 없음
      APPEND ls_scol TO gs_data-lt_scol.
    ENDIF.
    MODIFY gt_data FROM gs_data.
    CLEAR gs_data.
  ENDLOOP.
ENDFORM.

FORM set_layout.
  gs_layout-ctab_fname = 'LT_SCOL'.  
ENDFORM.

********* PBO *********
PERFORM set_layout.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout = gs_layout
  CHANGING
    it_outtab = gt_data.
```
</br>
</br>
</br>

### 8) 결과 확인

<img width="536" height="370" alt="image" src="https://github.com/user-attachments/assets/e036f5f4-8676-4972-9727-f6f3954d72af" />

</br>
</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
- layout 실습
</br>

### 실습 1
</br>

- 문제

<img width="497" height="276" alt="image" src="https://github.com/user-attachments/assets/3d4d776c-d9ab-4b3a-ae11-d5e9062edb65" />
</br>
</br>
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.
DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.

START-OF-SELECTION.
  PERFORM get_data.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc < 50.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc >= 50 AND gs_flight-seatsocc <= 200.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout.
  gs_layout-grid_title = 'Flight List'.
  gs_layout-zebra = 'X'.
  gs_layout-sel_mode = 'C'.
  gs_layout-excp_fname = 'EXCP'.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
  CALL METHOD go_alv_grid->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'sflight'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = gt_flight
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      others                        = 4.
ENDMODULE.
```
</br>
</br>

**4) 결과 확인**

<img width="499" height="384" alt="image" src="https://github.com/user-attachments/assets/ac3e549d-a76c-48cd-b8b3-da963ca38b9a" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
</br>

- 문제

<img width="474" height="283" alt="image" src="https://github.com/user-attachments/assets/c6b45c6d-2851-41f2-97a9-1a145c147234" />

</br>
</br>
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_TOP
*&---------------------------------------------------------------------*

DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_col TYPE LVC_T_SCOL,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
DATA: ls_col TYPE LVC_S_SCOL.
```
</br>
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_F01
*&---------------------------------------------------------------------*

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc < 50.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc >= 50 AND gs_flight-seatsocc <= 200.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+0(4) = sy-datum+0(4).
        gs_flight-color = 'C' && col_positive && 10.
      ENDIF.

      IF gs_flight-seatsmax - gs_flight-seatsocc  < 10.
        ls_col-fname = 'SEATSMAX'.
        ls_col-color-col = '6'.
        ls_col-color-int = '0'.
        ls_col-color-inv = '1'.
        APPEND ls_col TO gs_flight-lt_col.
        CLEAR ls_col.

        ls_col-fname = 'SEATSOCC'.
        ls_col-color-col = '6'.
        ls_col-color-int = '0'.
        ls_col-color-inv = '1'.

        APPEND ls_col TO gs_flight-lt_col.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp color lt_col.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-grid_title = 'Flight List'.
  gs_layout-zebra = 'X'.
  gs_layout-sel_mode = 'C'.
  gs_layout-excp_fname = 'EXCP'.
  gs_layout-info_fname = 'COLOR'.
  gs_layout-ctab_fname = 'LT_COL'.
ENDFORM.
```
</br>
</br>

**3) `PBO` 모듈**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'sflight'
        is_variant                    = gs_variant
        i_save                        = 'A'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_flight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE A012(BC405_408).
    ENDIF.
```
</br>
</br>

**4) 결과 확인**

<img width="399" height="308" alt="image" src="https://github.com/user-attachments/assets/fd86219f-6de5-48d8-8e4b-006378b0b7a9" />

</br>
</br>

# 12/30
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV 표준 툴바 버튼 제어**
- **2) ALV Field Catalog**
</br>

- **SELECT-OPTIONS는 `Selection Screen` 에서만 가능**

</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>


### 1) ALV 표준 툴바 버튼 제어
- ALV Grid에서 기본 제공되는 표준 버튼(필터, 합계 등)을 화면에서 숨김 → 기능은 유지되고 화면에서만 제거
</br>

- `CL_GUI_ALV_GRID` → ALV Grid 화면을 생성·제어하는 표준 클래스
- `UI_FUNCTIONS` → 숨길 Function Code들을 담는 내부테이블 타입
- `IT_TOOLBAR_EXCLUDING` → ALV에서 제외할 툴바 기능 전달 파라미터
- `GS_LAYOUT-NO_TOOLBAR` → ALV 툴바 영역 자체를 통째로 숨김 (툴바 자체 제거)
</br>

- `MC_FC_*` → ALV 개별 버튼 1개를 의미하는 Function Code
- `MC_MB_*` → 여러 기능이 묶인 메뉴 버튼 Function Code
- `MC_FC_EXCL_ALL` → ALV 툴바 전체 버튼을 숨기는 Function Code (버튼만 숨김)
```abap
********* TOP *********
DATA: <gt_excluded> TYPE ui_functions.

********* F01 *********
APPEND cl_gui_alv_grid=><function_code> TO <gt_excluded>.

********* PBO *********
go_alv_grid->set_table_for_first_display(
  EXPORTING
    i_structure_name     = '<DDIC 구조>'
    it_toolbar_excluding = <gt_excluded>
  CHANGING
    it_outtab            = <itab>
).
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
DATA gt_uifunc TYPE ui_functions.      " ALV 툴바 제외 기능 목록

********* F01 *********
APPEND cl_gui_alv_grid=>mc_fc_filter TO gt_uifunc.    " 필터(Filter) 버튼 숨김
APPEND cl_gui_alv_grid=>mc_mb_sum TO gt_uifunc.       " 합계/소계 메뉴 전체 숨김
APPEND cl_gui_alv_grid=>mc_fc_excl_all TO gt_uifunc.  " 모든 표준 툴바 버튼 숨김

********* PBO *********
CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name     = 'SBOOK'
    it_toolbar_excluding = gt_uifunc
  CHANGING
    it_outtab            = gt_data.
```
</br>
</br>

### 2) ALV Field Catalog
- ALV 컬럼의 제목, 순서, 숨김, 아이콘·단위 등 화면 표시 방식을 제어하는 설정
- 구조에 없는 컬럼을 추가하거나, 기존 컬럼의 출력 형태를 변경하기 위해 사용
- `LVC_T_FCAT` → Field Catalog 전체를 담는 내부테이블
- `LVC_S_FCAT` → 컬럼 1개에 대한 속성 구조
</br>
</br>

**1) 기본 컬럼 정의 / DDIC 참조**
- `FIELDNAME` → 내부테이블의 컬럼명
- `REF_FIELD` → DDIC에서 참조할 필드명
- `REF_TABLE` → DDIC에서 참조할 테이블/구조명

**2) 컬럼 표시 방식 제어**
- `ICON` → `X` 면 아이콘으로 표시, 공백이면 일반 값
- `CHECKBOX` → `X` 면 값을 읽기 전용 체크박스로 표시하고, 공백이면 일반 값으로 표시
- `NO_OUT` → `X` 면 컬럼을 ALV 화면에서 숨김, 공백이면 정상 표시
- `TECH` → `X` 면 기술용 컬럼으로 처리하고, 공백이면 일반 사용자 컬럼으로 표시

**3) 컬럼 제목 / 위치 / 크기**
- `COLTEXT` → 컬럼 헤더에 표시될 제목 문자열, 미지정 시 DDIC 텍스트 사용
- `TOOLTIP` → 컬럼 헤더에 마우스를 올렸을 때 표시되는 설명 문구
- `COL_POS` → 컬럼의 출력 위치 번호, 숫자가 작을수록 왼쪽에 표시
- `OUTPUTLEN` → 컬럼 출력 폭(너비)

**4) 금액(Currency) / 수량(Unit) 포맷 설정**
- `CFIELDNAME` → 금액 컬럼에 대해 행별 통화 필드 지정
- `CURRENCY` → 금액 컬럼 전체에 고정 통화 코드 적용
- `QFIELDNAME` → 수량 컬럼에 대해 행별 단위 필드 지정
- `QUANTITY` → 수량 컬럼 전체에 고정 단위 적용

**5) 집계 / 키 / 인터랙션**
- `DO_SUM` → `X` 면 합계 라인을 생성하고, 공백이면 합계 계산 안 함
- `NO_SUM` → `X` 면 합계 기능을 비활성화하고, 공백이면 합계 가능
- `KEY` → `X` 면 키 컬럼으로 고정 표시되고, 공백이면 일반 컬럼
- `HOTSPOT` → `X` 면 클릭 이벤트가 발생하고, 공백이면 일반 텍스트 컬럼

**6) 강조 / 값 표시 포맷**
- `EMPHASIZE` → 컬럼 값을 색상·강조·반전 효과로 표시
- `DECIMALS_O` → 표시할 소수점 자릿수 지정
- `DECMLFIELD` → 소수점 자릿수를 행별 필드로 제어
- `ROUND` → `X` 면 반올림 처리하고, 공백이면 반올림 안 함
- `ROUNDFIELD` → 반올림 자릿수를 행별 필드로 제어
- `EDIT_MASK` → 출력 시 편집 마스크 적용
- `JUST` → 값 정렬 방식(L/R/C) 지정
- `LZERO` → `X` 면 앞의 0을 표시하고, 공백이면 0 제거
- `NO_ZERO` → `X` 면 값이 0일 때 표시하지 않고, 공백이면 0 표시
- `NO_SIGN` → `X` 면 부호(+/–)를 숨기고, 공백이면 부호 표시
  
</br>
</br>
</br>

**1) 전역 구조(DDIC) 필드 그대로 사용하는 경우**
- `FIELDNAME` 만 지정
- `REF_FIELD` / `REF_TABLE` 자동
  
**2) 전역 구조 필드 + 속성 조정하는 경우**
- `FIELDNAME` + `REF_FIELD` + `REF_TABLE` 지정
- 길이·텍스트 등만 추가 설정
  
**3) 로컬 타입(내부 정의 타입) 사용하는 경우**
- `FIELDNAME` 만 사용
- `REF_FIELD` / `REF_TABLE` 사용 ❌
- 컬럼 속성 전부 직접 정의
</br>
</br>

```abap
********* TOP *********
TYPE-POOLS: icon.

TYPES: BEGIN OF <gs_type>.
  INCLUDE TYPE <DDIC_STRUCTURE>.
  <add_field_1> TYPE <ddic_type_1>.
  <add_field_2> TYPE <ddic_type_2>.
END OF <gs_type>.
DATA: <gt_outtab> TYPE TABLE OF <gs_type>.

" Field Catalog 선언
DATA: <gt_fcat> TYPE lvc_t_fcat,
      <gs_fcat> TYPE lvc_s_fcat.

********* F01 *********
<gs_fcat>-fieldname = '<ADD_FIELD_1>'.      " 추가 컬럼 1
<gs_fcat>-ref_field = '<REF_FIELD>'.
<gs_fcat>-ref_table = '<REF_TABLE>'.
<gs_fcat>-col_pos   = <N>.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

<gs_fcat>-fieldname = '<ADD_FIELD_2>'.      " 추가 컬럼 2 (아이콘 등 표현 변경)
<gs_fcat>-icon      = 'X'.
<gs_fcat>-coltext   = '<TEXT>'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

<gs_fcat>-fieldname = '<ORIGINAL_FIELD>'.   " 기존 컬럼 숨김
<gs_fcat>-no_out    = 'X'.
APPEND <gs_fcat> TO <gt_fcat>.
CLEAR <gs_fcat>.

********* PBO *********
<go_alv>->set_table_for_first_display(
  EXPORTING
    i_structure_name = '<DDIC_STRUCTURE>'
  CHANGING
    it_outtab        = <gt_outtab>
    it_fieldcatalog  = <gt_fcat>
).
```
```abap
REPORT ZPROGB03_0029.

********* TOP *********
TYPE-POOLS: icon.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA:   cancelled_icon TYPE icon-id,           " 취소 아이콘
        phone          TYPE scustom-telephone,  " 전화번호
      END OF gs_data.
DATA gt_data LIKE TABLE OF gs_data.

DATA: BEGIN OF ls_custom.
        id        TYPE s_customer,
        telephone TYPE s_phoneno,
      END OF ls_custom.
DATA lt_custom LIKE TABLE OF ls_custom.

DATA: gt_fcat TYPE lvc_t_fcat,                 " Field Catalog itab
      gs_fcat TYPE lvc_s_fcat.                 " Field Catalog str

********* F01 (데이터 조회) *********
SELECT *
  FROM sbook
  INTO CORRESPONDING FIELDS OF TABLE gt_data.

SELECT id telephone
  FROM scustom
  INTO TABLE lt_custom
  FOR ALL ENTRIES IN gt_data
  WHERE id = gt_data-customid
    AND telephone <> space.

********* F01 (데이터 가공) *********
LOOP AT gt_data INTO gs_data.

  IF gs_data-cancelled = 'X'.                  " 취소 여부 아이콘
    gs_data-cancelled_icon = icon_cancel.
  ENDIF.

  READ TABLE lt_custom INTO ls_custom           " 전화번호 매핑
       WITH KEY id = gs_data-customid.
  IF sy-subrc = 0.
    gs_data-phone = ls_custom-telephone.
  ENDIF.

  MODIFY gt_data FROM gs_data
    TRANSPORTING cancelled_icon phone.

  CLEAR gs_data.
ENDLOOP.

********* F01 (Field Catalog 설정) *********
CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED_ICON'.   " 취소 아이콘 표시
gs_fcat-icon      = 'X'.
gs_fcat-coltext   = 'Cancelled'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CANCELLED'.        " 기존 취소 컬럼 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSFORM'.         " 여권 형식 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PASSBIRTH'.        " 여권 생년월일 숨김
gs_fcat-no_out    = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'PHONE'.            " 전화번호 표시
gs_fcat-ref_field = 'TELEPHONE'.
gs_fcat-ref_table = 'SCUSTOM'.
gs_fcat-col_pos   = 4.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'SMOKER'.           " 체크박스
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'INVOICE'.          " 체크박스
gs_fcat-checkbox = 'X'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'CLASS'.            " 컬럼 강조 표시
gs_fcat-emphasize = 'C510'.
APPEND gs_fcat TO gt_fcat.

CLEAR gs_fcat.
gs_fcat-fieldname = 'LOCCURAM'.          " 합계 적용
gs_fcat-do_sum    = 'X'.
APPEND gs_fcat TO gt_fcat.

********* PBO *********
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name = 'SBOOK'
  CHANGING
    it_outtab        = gt_data
    it_fieldcatalog  = gt_fcat.
```

- 결과출력
  
<img width="626" height="343" alt="image" src="https://github.com/user-attachments/assets/3918147c-8030-496b-8685-9a8cb5eca11b" />

</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- layout 실습
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid TYPE REF TO cl_gui_alv_grid.
DATA: gs_layout TYPE LVC_S_LAYO.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
DATA: ls_field_colors TYPE LVC_S_SCOL.

START-OF-SELECTION.
  PERFORM get_data.
  CALL SCREEN 100.
```
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_F01
*&---------------------------------------------------------------------*

TYPE-POOLS COL.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight TRANSPORTING excp color lt_field_colors.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-info_fname = 'COLOR'.
  gs_layout-ctab_fname = 'LT_FIELD_COLORS'.
  gs_layout-no_hgridln = 'X'.
  gs_layout-no_vgridln = 'X'.
ENDFORM.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
  CALL METHOD go_alv_grid->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'sflight'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = gt_flight
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      others                        = 4.
ENDMODULE.
```
</br>

**4) `PAI` 모듈**
```abap
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
      PERFORM free_alv.
    WHEN 'CANCEL'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```

</br>

**5) 결과 출력**

<img width="433" height="332" alt="image" src="https://github.com/user-attachments/assets/738970ec-5c0c-419b-955d-116a19b81e09" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
- Field Catalog 실습
- 위 코드 이어서 작성
</br>
</br>

**1) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_TOP
*&---------------------------------------------------------------------*

DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

DATA: BEGIN OF gs_flight.
        INCLUDE TYPE sflight.
DATA: excp TYPE CHAR1,
      color TYPE C LENGTH 4,
      lt_field_colors TYPE LVC_T_SCOL,
      changes_possible TYPE icon-id,
      END OF gs_flight.
DATA: gt_flight LIKE TABLE OF gs_flight.
```
</br>

**2) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_F01
*&---------------------------------------------------------------------*

TYPE-POOLS: COL, ICON.

FORM get_data.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_flight
    WHERE carrid IN so_car
      AND connid IN so_con.
  IF sy-subrc <> 0.
    MESSAGE '데이터가 존재하지 않습니다.' TYPE 'E'.
  ELSE.
    LOOP AT gt_flight INTO gs_flight.
      IF gs_flight-seatsocc = 0.
        gs_flight-excp = '1'.
      ELSEIF gs_flight-seatsocc < 50.
        gs_flight-excp = '2'.
      ELSE.
        gs_flight-excp = '3'.
      ENDIF.

      IF gs_flight-fldate+4(2) = sy-datum+4(2).
        gs_flight-color = 'C' && col_negative && 10.
      ENDIF.

      IF gs_flight-fldate < sy-datum.
        gs_flight-changes_possible = ICON_SPACE.
      ELSE.
        gs_flight-changes_possible = ICON_OKAY.
      ENDIF.

      IF gs_flight-PLANETYPE = '747-400'.
        ls_field_colors-fname = 'PLANETYPE'.
        ls_field_colors-color-col = col_positive.
        ls_field_colors-color-int = '1'.
        ls_field_colors-color-inv = '0'.
        APPEND ls_field_colors TO gs_flight-lt_field_colors.
      ENDIF.

      MODIFY gt_flight FROM gs_flight
        TRANSPORTING excp color lt_field_colors changes_possible.
      CLEAR gs_flight.
    ENDLOOP.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSOCC'.
  gs_fcat-do_sum = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'PAYMENTSUM'.
  gs_fcat-no_out = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CHANGES_POSSIBLE'.
  gs_fcat-icon = 'X'.
  gs_fcat-coltext = 'Changes Possible'.
  gs_fcat-tooltip = 'Are changes still possible?'.
  gs_fcat-col_pos = 5.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'EXCP'.
  gs_fcat-coltext = 'traffic light'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV2_O01
*&---------------------------------------------------------------------*

PERFORM set_layout.
PERFORM set_fcat.
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
    is_variant                    = gs_variant
    i_save                        = 'A'
    is_layout                     = gs_layout
  CHANGING
    it_outtab                     = gt_flight
    IT_FIELDCATALOG               = gt_fcat
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
IF sy-subrc <> 0.
  MESSAGE A012(BC405_408).
ENDIF.
```
</br>

**4) 결과 출력**

<img width="530" height="403" alt="image" src="https://github.com/user-attachments/assets/fee92021-0c8d-45a3-8159-d042cbf869df" />

</br>
</br>
</br>
</br>
</br>
</br>

### 실습 3
</br>

- 문제

<img width="485" height="288" alt="image" src="https://github.com/user-attachments/assets/b550caff-ae1b-44b1-b987-1829e83d7599" />
</br>
</br>
</br>

**1) `Program` 코드 생성**
```abap
INCLUDE ZBC400_B03_PRACTICE7_TOP.
INCLUDE ZBC400_B03_PRACTICE7_F01.
INCLUDE ZBC400_B03_PRACTICE7_O01.
INCLUDE ZBC400_B03_PRACTICE7_I01.

AT SELECTION-SCREEN ON BLOCK blk1.
  IF so_car IS INITIAL AND so_con IS INITIAL AND so_fdt IS INITIAL.
    MESSAGE '전부 입력하지 않았습니다.' TYPE 'E'.
  ENDIF.

START-OF-SELECTION.
  PERFORM set_data.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm.

DATA: go_cont TYPE REF TO cl_gui_custom_container,
      go_alv TYPE REF TO cl_gui_alv_grid.

DATA: BEGIN OF gs_data.
        INCLUDE TYPE ZSCARRFLIGHT_B03.
DATA: seatsrem TYPE INT4,
      class TYPE sbook-CLASS,
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data,
      gt_sflight TYPE TABLE OF sflight,
      gt_scarr TYPE TABLE OF scarr,
      gs_scarr LIKE LINE OF gt_scarr,
      gt_sbook TYPE TABLE OF sbook,
      gs_sbook LIKE LINE OF gt_sbook.

DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT,
      gt_sort TYPE LVC_T_SORT,
      gs_sort TYPE LVC_S_SORT.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECT-OPTIONS: so_car FOR gs_data-carrid,
                  so_con FOR gs_data-connid,
                  so_fdt FOR gs_data-fldate.
SELECTION-SCREEN END OF BLOCK blk1.
```
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_F01
*&---------------------------------------------------------------------*

FORM set_data.
  SELECT *
    FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid IN so_car
      AND connid IN so_con
      AND fldate IN so_fdt.

  SELECT *
    FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE gt_scarr
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid.

  SELECT *
    FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE gt_sbook
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid
      AND connid = gt_data-connid
      AND fldate = gt_data-fldate.
  SORT gt_sbook.  " 첫번째 인덱스의 class를 가져오므로

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid
      TRANSPORTING carrname currcode.
    READ TABLE gt_sbook INTO gs_sbook
      WITH KEY carrid = gs_data-carrid
               connid = gs_data-connid
               fldate = gs_data-fldate
      TRANSPORTING class.
    gs_data-class = gs_sbook-class.
    gs_data-carrname = gs_scarr-carrname.
    gs_data-currcode = gs_scarr-currcode.
    gs_data-seatsrem = gs_data-seatsmax - gs_data-seatsocc.
    MODIFY gt_data FROM gs_data TRANSPORTING carrname currcode seatsrem class.
    CLEAR gs_data.
  ENDLOOP.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSREM'.
  gs_fcat-coltext = 'Seats Remain'.
  gs_fcat-col_pos = 3.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CLASS'.
  gs_fcat-ref_field = 'CLASS'.
  gs_fcat-ref_table = 'SBOOK'.
  gs_fcat-col_pos = 2.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'PRICE'.
  gs_fcat-do_sum = 'X'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_sort.
  CLEAR gs_sort.
  gs_sort-fieldname = 'CLASS'.
  gs_sort-spos = 1.
  gs_sort-up = 'X'.
  gs_sort-subtot = 'X'.
  APPEND gs_sort TO gt_sort.
ENDFORM.
```
</br>

**4) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
* SET TITLEBAR 'xxx'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    PERFORM set_fcat.
    PERFORM set_sort.
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'ZSCARRFLIGHT_B03'
      CHANGING
        it_outtab                     = gt_data
        it_fieldcatalog               = gt_fcat
        it_sort                       = gt_sort.
    IF sy-subrc <> 0.
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>

**5) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_B03_PRACTICE7_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**6) 결과 확인**

<img width="500" height="146" alt="image" src="https://github.com/user-attachments/assets/326940de-3a7a-4697-9af2-cb232651886c" />
</br>

<img width="515" height="357" alt="image" src="https://github.com/user-attachments/assets/765b20d2-30a5-4a6a-a180-8d5741bd635e" />

</br>
</br>

# 12/31
</br>
</br>

## < 핵심 개념 >
</br>

- **1) ALV Grid 이벤트 처리 정리 (`CLASS-METHODS`)**
- **2) ALV Hotspot 이벤트 처리**
</br>

- **ALV 핫스팟 클릭은 OKCODE가 없어서 PAI를 안 탐**
- **`i_structure_name` 는 DDIC(Dictionary)에 정의된 구조만 가능**
</br>
</br>

---

</br>

## < ABAP 객체 생성 기본 문법 >
</br>

### 1) ALV Grid 이벤트 처리 정리
- ALV 화면에서 발생하는 사용자 동작을 처리
- `CL_GUI_ALV_GRID` : ALV Grid 이벤트를 제공하는 클래스
</br>

- `Instance Attribute` → 객체마다 메모리 가지는 속성 (DATA)
- `Instance Method` → 객체마다 호출되는 메서드 (METHODS)
- `Static Attribute` → 클래스마다 하나만 존재하는 속성 (CLASS-DATA)
- `Static Method` → 클래스마다 하나만 존재하는 메서드 (CLASS-METHODS)
- `Constants` → 값 변경 불가한 고정값 (CONSTANTS)
</br>
</br>

**< ALV Grid 이벤트 / 파라미터 >**
- 이벤트: 언제 발생했는가
- 파라미터: 발생했을 때 어떤 정보가 넘어오는가

| 이벤트 | 설명 |
|--------|------|
| `DOUBLE_CLICK` | 셀을 더블클릭했을 때 발생 |
| `PRINT_TOP_OF_PAGE` | 출력 시 페이지 상단 처리 |
| `USER_COMMAND` | ALV 버튼 클릭 시 발생 |
| `TOOLBAR` | ALV 툴바 생성 시 발생 |
  
| 파라미터 | 설명 |
|----------|------|
| `ES_ROW_NO` | 화면 기준 행 번호 (정렬/필터 적용 후 기준) |
| `E_ROW_ID` | 내부 테이블 기준 행 인덱스 |
| `E_COLUMN` | 더블클릭한 컬럼 정보 (`fieldname`) |
| `E_COLUMN_ID` | 핫스팟 클릭한 컬럼명 |
| `E_UCOMM` | 눌린 버튼의 Function Code |
| `E_OBJECT` | 툴바 버튼 추가/제어 객체 |
| `E_INTERACTIVE` | 인터랙티브 ALV 여부 |
| `SENDER` | 이벤트를 발생시킨 ALV 객체 |
| `ER_DATA_CHANGED` | 변경된 셀 정보 객체 |
| `E_MODIFIED` | 데이터 변경 발생 여부 |
| `E_FIELDNAME` | F4 도움말이 호출된 필드명 |
| `E_FIELDVALUE` | F4 호출 시 필드의 현재 값 |
| `ER_EVENT_DATA` | 이벤트 제어 객체 (F4 처리 등) |
| `E_ONF4` | F4 도움말 호출 여부 |
| `E_ONF4_BEFORE` | F4 호출 이전 시점 |
| `E_ONF4_AFTER` | F4 호출 이후 시점 |

</br>
</br>

**1) CLASS-METHODS (정적 메서드)**
- 객체 생성 없이 클래스명으로 바로 호출
- 공통 로직, 이벤트 핸들러에 자주 사용
- 인스턴스 속성 사용 불가

**2) METHODS (인스턴스 메서드)**
- 객체 생성(CREATE OBJECT) 후에만 호출
- 객체별 인스턴스 속성 사용 가능
- 객체 상태(속성)를 사용하는 로직에 사용
</br>
</br>

- `CLASS-METHODS` : 정적(static) 메서드,  `METHODS` : 인스턴스 메서드
- `<handler_class>` : 이벤트 핸들러 클래스명
- `<handler_method>` : 이벤트 처리 메소드명
- `<event_name>` : 발생 이벤트명
- `<event_class>` : 이벤트를 가진 클래스
- `<param_1>, <param_2>` : 이벤트 파라미터
```abap
*************** C01 ****************
CLASS <handler_class> DEFINITION.
  [PUBLIC | PROTECTED | PRIVATE] SECTION.
    [CLASS-]METHODS <handler_method>
      FOR EVENT <event_name> OF <event_class>
      IMPORTING
        <param_1>
        <param_2>
        sender.
ENDCLASS.

CLASS <handler_class> IMPLEMENTATION.
  METHOD <handler_method>.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER <handler_class>=><handler_method> FOR <event_object>.

CALL METHOD <event_object>->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC 구조>'
  CHANGING
    it_outtab        = <itab>
```
- `SENDER` → 이벤트를 발생시킨 객체(오브젝트) 참조
```abap
REPORT ZPROGB03_0029.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_DOUBLE_CLICK
    FOR EVENT DOUBLE_CLICK OF CL_GUI_ALV_GRID
    IMPORTING ES_ROW_NO E_COLUMN SENDER.
  " CL_GUI_ALV_GRID 클래스의 더블클릭 이벤트가 발생하면
  " ON_DOUBLE_CLICK 이라는 메서드를 실행하겠다
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  "더블클릭 이벤트의 메서드 구현
  METHOD ON_DOUBLE_CLICK.
    IF SENDER = GO_ALV.
      MESSAGE I004(ZMCB03) WITH ES_ROW_NO-row_id        " 더블클릭된 ALV 행 번호
                                E_COLUMN-fieldname.     " 더블클릭된 ALV 컬럼명
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_double_click FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name     = 'SBOOK'
  CHANGING
    it_outtab            = gt_data
```
</br>
</br>

### 2) ALV Hotspot 이벤트 처리
- ALV 컬럼을 클릭 가능한 링크(밑줄)로 표시하는 기능
- 필드카탈로그 생성 시(PBO) `gs_fcat-hotspot = 'X'` 로 적용
```abap
*************** F01 ****************
CLEAR gs_fcat.
gs_fcat-fieldname = '<HOTSPOT_FIELD>'.
gs_fcat-hotspot   = 'X'.      " 클릭 가능한 링크(밑줄) 처리
APPEND gs_fcat TO gt_fcat.

*************** C01 ****************
CLASS <handler_class> DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS <handler_method>
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING e_row_id e_column_id sender.
ENDCLASS.

CLASS <handler_class> IMPLEMENTATION.
  METHOD <handler_method>.
    " e_row_id-row_id        : 클릭된 행 번호
    " e_column_id-fieldname : 클릭된 컬럼명
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER <handler_class>=><handler_method> FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC>'
  CHANGING
    it_outtab        = <itab>
    it_fieldcatalog  = gt_fcat.
```
```abap
REPORT ZPROGB03_0029.

*************** F01 ****************
CLEAR gs_fcat.
gs_fcat-fieldname = 'PHONE'.   " 핫스팟 적용 컬럼
gs_fcat-hotspot   = 'X'.       " 핫스팟(링크) 활성화
APPEND gs_fcat TO gt_fcat.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_hotspot_click
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING
        es_row_no     " 클릭된 행 정보 (row_id)
        e_column_id  " 클릭된 컬럼 정보 (fieldname)
        sender.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_hotspot_click.
    IF sender = go_alv.
      MESSAGE I004(ZMCB03)
        WITH es_row_no-row_id        " 클릭된 행 번호
             e_column_id-fieldname. " 클릭된 컬럼명
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>
</br>

- 문제
- 전 exercise 코드에서 추가
- CLASS-METHODS, Hotspot 실습
<img width="374" height="208" alt="image" src="https://github.com/user-attachments/assets/90e9c079-c318-4291-9848-07923ecc223d" />
</br>
</br>

**1) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_F01
*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'SEATSOCC'.
  gs_fcat-do_sum = 'X'.
  gs_fcat-hotspot = 'X'.    " 핫스팟 추가
  APPEND gs_fcat TO gt_fcat.
ENDFORM.
```
</br>

**2) `C01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_C01_3
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_double_click
      FOR EVENT double_click OF cl_gui_alv_grid
      IMPORTING es_row_no sender.

    CLASS-METHODS: on_hotspot_click
      FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING es_row_no sender.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_double_click.
    IF sender = go_alv_grid.
      DATA: total_seat TYPE int4.

      READ TABLE gt_flight INTO gs_flight INDEX es_row_no-row_id.
      IF sy-subrc <> 0.
        MESSAGE i075(bc405_408).
        EXIT.
      ENDIF.

      total_seat = gs_flight-seatsocc + gs_flight-seatsocc_b + gs_flight-seatsocc_f.
      MESSAGE 'Total:' && total_seat TYPE 'I'.
    ENDIF.
  ENDMETHOD.

  METHOD on_hotspot_click.
    IF sender = go_alv_grid.
      READ TABLE gt_flight INTO gs_flight INDEX es_row_no-row_id.
      IF gs_flight-excp = '1'.
        MESSAGE '빨간색 입니다.' TYPE 'I'.
      ELSEIF gs_flight-excp = '2'.
        MESSAGE '노란색 입니다.' TYPE 'I'.
      ELSE.
        MESSAGE '초록색 입니다.' TYPE 'I'.
      ENDIF.
    ENDIF.
  ENDMETHOD.
endclass.
```
</br>

**3) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV3_O01
*&---------------------------------------------------------------------*

MODULE create_and_transfer OUTPUT.
    SET HANDLER lcl_event_handler=>on_double_click FOR go_alv_grid.
    SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_flight
        IT_FIELDCATALOG               = gt_fcat
ENDMODULE.
```
</br>

**4) 결과 확인**
- 더블클릭시 이벤트
<img width="513" height="394" alt="image" src="https://github.com/user-attachments/assets/5e605617-f7f0-43bf-b979-bc7aaa143583" />
</br>

- 핫스팟 설정
<img width="515" height="394" alt="image" src="https://github.com/user-attachments/assets/1e321642-c072-4eb0-a2e1-7458d965dd97" />

</br>
</br>

# 01/02
</br>
</br>

## <  핵심 개념 >
</br>

- **1) ALV 표준 툴바 확장 (TOOLBAR 이벤트)**
- **2) ALV 셀 버튼(Button) 동적 생성 및 클릭 이벤트 처리**
- **3) ALV USER_COMMAND 실행 전 제어 (before_user_command)**
- **4) ALV Control 주요 메소드 (get_current_cell, set_user_command, get_selected_rows)**

</br>
</br>

---

</br>

## < ALV 이벤트 정리 >
</br>

### 1) ALV 표준 툴바 확장 (TOOLBAR 이벤트)
- `TOOLBAR` : 툴바에 버튼을 추가/구성
- `USER_COMMAND` : 버튼 클릭 시 동작 처리
  
| 값 | 의미 |
|----|------|
| `0` | 일반 버튼 |
| `1` | 메뉴 + 표준 버튼 |
| `2` | 메뉴 |
| `3` | 구분선 (Separator) |
| `4` | 선택 버튼 |
| `5` | 체크박스 버튼 |
| `6` | 메뉴 엔트리 |

- `e_object` = ALV 툴바 자체를 담고 있는 객체 (여기에 버튼을 추가/삭제함)
- `e_interactive` = 툴바 이벤트가 사용자 조작으로 발생했는지 알려주는 플래그
- `e_object->mt_toolbar` = ALV 툴바에 표시될 버튼 목록 테이블
```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_toolbar
      FOR EVENT toolbar OF cl_gui_alv_grid
      IMPORTING e_object e_interactive sender.

    CLASS-METHODS on_user_command
      FOR EVENT user_command OF cl_gui_alv_grid
      IMPORTING e_ucomm sender.
ENDCLASS.


CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE stb_button.

    CLEAR ls_button.
    ls_button-function  = '<FUNC_CODE>'.
    ls_button-icon      = <ICON>.
    ls_button-text      = '<TEXT>'.
    ls_button-quickinfo = '<QUICKINFO>'.
    ls_button-butn_type = '0'.

    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.


  METHOD on_user_command.
    CASE e_ucomm.
      WHEN '<FUNC_CODE>'.
        " 버튼 클릭 시 처리 로직
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_toolbar      FOR go_alv.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name = '<DDIC 구조>'
  CHANGING
    it_outtab        = <itab>.

```
```abap
REPORT ZPROGB03_0029.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_TOOLBAR FOR EVENT TOOLBAR
    OF CL_GUI_ALV_GRID IMPORTING E_OBJECT,
      ON_USER_COMMAND FOR EVENT USER_COMMAND
    OF CL_GUI_ALV_GRID IMPORTING E_UCOMM.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD ON_TOOLBAR.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'CUSTOM_TB'.
    ls_button-icon = ICON_FLIGHT.
    ls_button-quickinfo = 'This is tooltip~~'.
    ls_button-butn_type = 0.
    ls_button-text = 'flight'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD ON_USER_COMMAND.
    CASE E_UCOMM.
      WHEN 'CUSTOM_TB'.
        MESSAGE 'flight' TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SBOOK'
  CHANGING
    it_outtab                     = gt_data
```
</br>
</br>
</br>

### 2) ALV 셀 버튼(Button) 동적 생성 및 클릭 이벤트 처리
- 조건에 따라 ALV 특정 셀을 버튼으로 표시하고, 버튼 클릭 시 해당 행 데이터를 기준으로 동작을 처리
- `LVC_S_STYL` → ALV 셀 단위 스타일(버튼/입력불가 등) 정의 구조
- `LVC_T_STYL` → 셀 스타일을 여러 개 담는 내부테이블(행·컬럼별 적용용)
</br>

- ALV에서 셀 단위로 버튼/비활성 등 UI 스타일을 다르게 적용하기 위해 사용하는 방식
- `cl_gui_alv_grid=>mc_style_button` → ALV 셀을 “클릭 가능한 버튼 형태” 로 표시하는 스타일 상수
- `gs_cell_style-fieldname` → 버튼/스타일을 적용할 ALV 컬럼(필드)명 지정
- `gs_cell_style-style` → 해당 컬럼 셀에 적용할 UI 스타일(버튼, 비활성 등) 지정
- `gs_layout-stylefname` → ALV 데이터 구조에서 셀 스타일 정보를 담고 있는 필드명 연결
</br>

<img width="464" height="222" alt="image" src="https://github.com/user-attachments/assets/ceb7e53e-2aa1-451a-a292-50d109b49cc8" />
</br>
</br>

```abap
*************** TOP ****************
DATA: BEGIN OF gs_data.
        INCLUDE TYPE <DB_STRUCTURE>.
DATA: <BUTTON_FIELD> TYPE c LENGTH 10,
      <STYLE_FIELD>  TYPE lvc_t_styl,
     END OF gs_data.

DATA: gt_data       TYPE STANDARD TABLE OF gs_data,
      gs_cell_style TYPE lvc_s_styl.


*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_button_click
      FOR EVENT button_click OF cl_gui_alv_grid
      IMPORTING es_row_no es_col_id.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_button_click.
    IF es_col_id-fieldname = '<BUTTON_FIELD>'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.


*************** F01 ****************
FORM set_button.
  LOOP AT gt_data INTO gs_data.
    gs_cell_style-fieldname = '<BUTTON_FIELD>'.
    gs_cell_style-style     = cl_gui_alv_grid=>mc_style_button.
    APPEND gs_cell_style TO gs_data-<STYLE_FIELD>.
    gs_data-<BUTTON_FIELD> = '<BUTTON_TEXT>'.
    MODIFY gt_data FROM gs_data.
  ENDLOOP.
ENDFORM.


*************** PBO ****************
SET HANDLER lcl_event_handler=>on_button_click FOR <ALV_OBJECT>.
gs_layout-stylefname = '<STYLE_FIELD>'.

CALL METHOD <ALV_OBJECT>->set_table_for_first_display
  CHANGING it_outtab = gt_data.
```
```abap
REPORT ZPROGB03_0029.

*************** TOP ****************
DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: display_bookings TYPE c LENGTH 10,   " 버튼 텍스트
      it_cell_style    TYPE LVC_T_STYL,     " 셀 스타일
     END OF gs_data.
DATA gs_cell_style TYPE LVC_S_STYL.

*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: on_button_click
    FOR EVENT button_click OF CL_GUI_ALV_GRID
    IMPORTING ES_ROW_NO ES_COL_ID.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_button_click.
    IF ES_COL_ID-fieldname = 'DISPLAY_BOOKINGS'.
      READ TABLE gt_data INTO gs_data INDEX es_row_no-row_id.
      MESSAGE gs_data-class TYPE 'I'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.

*************** F01 ****************
FORM get_data.
  SELECT * FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE gt_data.

  LOOP AT gt_data INTO gs_data.
    IF gs_data-loccuram >= 800.
      " excp_fld 초록일 때, 버튼도 표시하도록 함
      gs_cell_style-fieldname = 'DISPLAY_BOOKINGS'.
      gs_cell_Style-style = cl_gui_alv_grid=>MC_STYLE_BUTTON.
      APPEND gs_cell_style TO gs_data-it_cell_style.
      gs_data-display_bookings = 'Detail'. " 버튼Text
    ENDIF.

    MODIFY gt_data FROM gs_data.
    CLEAR: gs_data, gs_cell_style.
  ENDLOOP.
ENDFORM.


FORM set_layout.
  gs_layout-stylefname = 'IT_CELL_STYLE'.
ENDFORM.


FORM set_fieldcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'DISPLAY_BOOKINGS'.
  gs_fcat-col_pos = 3.
  gs_fcat-coltext = 'Display Detail'.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_button_click FOR go_alv.

CALL METHOD go_alv->set_table_for_first_display
  EXPORTING
    is_layout        = gs_layout
  CHANGING
    it_outtab        = gt_data
    it_fieldcatalog  = gt_fcat.
```
</br>
</br>
</br>

### 3) ALV USER_COMMAND 실행 전 제어 (before_user_command)
- UUSER_COMMAND보다 먼저 동일한 UCOMM을 받아 실행 여부를 제어하는 이벤트
- `before_user_command` → ALV에서 사용자 액션이 발생했을 때, USER_COMMAND가 처리되기 직전에 먼저 호출되는 이벤트
- `e_ucomm` → 클릭된 버튼의 Function Code
```abap
*************** C01 ****************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS on_before_user_command
      FOR EVENT before_user_command OF cl_gui_alv_grid
      IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_before_user_command.
    CASE e_ucomm.
      WHEN <FUNCTION_CODE>.
        MESSAGE '실행 전 제어' TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*************** PBO ****************
SET HANDLER lcl_event_handler=>on_before_user_command FOR go_alv.
```
</br>
</br>

---

</br>

## < ALV Control 주요 메소드 >
</br>

- 메서드 = 객체의 상태를 조회하거나 동작을 수행하는 클래스 내부 함수

| Method | 설명 |
|------|------|
| **get_current_cell** | 현재 커서가 위치한 셀의 좌표와 값 반환 |
| **get_selected_cells** | 사용자가 선택한 셀들의 좌표와 값 반환 |
| **get_selected_columns** | 선택된 컬럼 이름 목록 반환 |
| **get_selected_rows** | 선택된 행 번호(Row Index) 반환 |
| **get_subtotals** | ALV Grid에 설정된 현재 소계(Subtotal) 정보 반환 |
| **set_toolbar_interactive** | ALV 툴바 이벤트를 강제로 발생시킴 |
| **set_user_command** | 현재 USER-COMMAND 값을 강제로 덮어씀 |
</br>
</br>

### 1) get_current_cell
- 현재 커서가 위치한 ALV 셀의 행·컬럼 인덱스를 가져오는 메서드
```abap
*************** C01 ****************
CALL METHOD <alv_grid>->get_current_cell
  IMPORTING
    e_row = <row_index>
    e_col = <col_index>.
```
- 커스텀 툴바 버튼(`CUSTOM_TB`) 클릭 시 현재 커서가 있는 ALV 셀의 행·컬럼을 조회
```abap
REPORT ZPROGB03_0029.

*************** C01 ****************
CLASS-METHODS on_user_command
  FOR EVENT user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.

METHOD on_user_command.
  DATA: lv_row TYPE i,   " 현재 행 번호
        lv_col TYPE i.   " 현재 컬럼 번호

  CASE e_ucomm.
    WHEN 'CUSTOM_TB'.   " 툴바 버튼 클릭 시
      CALL METHOD go_alv->get_current_cell
        IMPORTING
          e_row = lv_row
          e_col = lv_col.
      MESSAGE 'Row: ' && lv_row && ', Col: ' && lv_col TYPE 'I'.
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
```
</br>
</br>
</br>

### 2) set_user_command
- USER_COMMAND 값을 강제로 세팅하여 해당 USER_COMMAND 이벤트를 실행시키는 메서드

<img width="34" height="29" alt="image" src="https://github.com/user-attachments/assets/232a14e6-7938-429f-9d88-57338d1bd4d0" />
</br>

```abap
*************** C01 ****************
CALL METHOD <alv_grid>->set_user_command
  EXPORTING
    i_ucomm = <FUNCTION_CODE>.
```
```abap
REPORT ZPROGB03_0029.

*************** C01 ****************
CLASS-METHODS on_before_user_command
  FOR EVENT before_user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.
CLASS-METHODS on_user_command
  FOR EVENT user_command OF cl_gui_alv_grid
  IMPORTING e_ucomm.

METHOD on_before_user_command.
  CASE e_ucomm.
    WHEN go_alv->mc_fc_detail.   " ALV 표준 Detail 버튼 클릭 시
      CALL METHOD go_alv->set_user_command
        EXPORTING
          i_ucomm = 'SHOW_BOOK'.  " USER_COMMAND 강제 세팅
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
METHOD on_user_command.
  CASE e_ucomm.
    WHEN 'SHOW_BOOK'.            " set_user_command로 유도된 명령
      MESSAGE 'Show-book 클릭되었습니다' TYPE 'I'.
    WHEN OTHERS.
  ENDCASE.
ENDMETHOD.
```
</br>
</br>
</br>

### 3) get_selected_rows
- ALV에서 사용자가 선택(체크/드래그)한 행들의 인덱스 목록을 가져오는 메서드
```abap
*************** C01 ****************
CALL METHOD <alv_grid>->get_selected_rows
  IMPORTING
    et_index_rows = <selected_rows>.
```
```abap
REPORT ZPROGB03_0029.

*************** PAI ****************
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'SELECT'.
      DATA: lt_select_data TYPE LVC_T_ROW,
            ls_select_data TYPE LVC_S_ROW.
      DATA: lt_data LIKE gt_data,
            ls_data LIKE gs_data.

      CALL METHOD go_alv->get_selected_rows
        IMPORTING
          et_index_rows = lt_select_data.

      IF lt_select_data IS NOT INITIAL.
        LOOP AT lt_select_data INTO ls_select_data.
          READ TABLE gt_data INTO ls_data
          INDEX ls_select_data-index.
          IF sy-subrc = 0.
            APPEND ls_data TO lt_data.
          ENDIF.
        ENDLOOP.
        cl_demo_output=>display( lt_data ).
      ELSE.
        MESSAGE 'Please select record' TYPE 'E'.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>
</br>

</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 새로 구성하는 실습
- alv 2개, hotspot 실습
  
<img width="384" height="223" alt="image" src="https://github.com/user-attachments/assets/e891f22d-5304-45f1-b709-97a0ac023e6f" />
</br>
</br>
</br>

**1) `Report program` 에서 `Include` 파일 생성**
```abap
*&---------------------------------------------------------------------*
*& Report ZALV_SPFLI_B03
*&---------------------------------------------------------------------*

INCLUDE ZALV_SPFLI_B03_TOP.
INCLUDE ZALV_SPFLI_B03_C01.
INCLUDE ZALV_SPFLI_B03_O01.
INCLUDE ZALV_SPFLI_B03_I01.
INCLUDE ZALV_SPFLI_B03_F01.

START-OF-SELECTION.
  PERFORM get_data.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm.
DATA: go_cont TYPE REF TO cl_gui_custom_container,
      go_alv TYPE REF TO cl_gui_alv_grid,
      go_cont2 TYPE REF TO cl_gui_custom_container,
      go_alv2 TYPE REF TO cl_gui_alv_grid,
      gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

DATA: gs_sflight TYPE sflight,
      gt_sflight TYPE TABLE OF sflight,
      gv_index TYPE I.

DATA: BEGIN OF gs_data,
        carrid TYPE spfli-carrid,
        carrname TYPE scarr-carrname,
        connid TYPE spfli-connid,
        cityfrom TYPE spfli-cityfrom,
        cityto TYPE spfli-cityto,
        airpfrom TYPE spfli-airpfrom,
        airpto TYPE spfli-airpto,
      END OF gs_data.
DATA: gt_data LIKE TABLE OF gs_data.

PARAMETERS: pa_carr TYPE spfli-carrid DEFAULT 'AZ'.
SELECT-OPTIONS: so_con FOR gs_data-connid.
```
</br>

**3) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_F01
*&---------------------------------------------------------------------*

FORM get_data.
  DATA: gt_scarr TYPE TABLE OF scarr,
        gs_scarr TYPE scarr.

  SELECT carrid connid cityfrom cityto airpfrom airpto FROM spfli
    INTO CORRESPONDING FIELDS OF TABLE gt_data
    WHERE carrid = pa_carr
      AND connid IN so_con.
  SELECT carrid carrname FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE gt_scarr
    FOR ALL ENTRIES IN gt_data
    WHERE carrid = gt_data-carrid.

  LOOP AT gt_data INTO gs_data.
    READ TABLE gt_scarr INTO gs_scarr WITH KEY carrid = gs_data-carrid
      TRANSPORTING carrname.
    gs_data-carrname = gs_scarr-carrname.
    MODIFY gt_data FROM gs_data TRANSPORTING carrname.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*

FORM get_sflight.
  READ TABLE gt_data INTO gs_data INDEX gv_index.
  SELECT * FROM sflight
    INTO CORRESPONDING FIELDS OF TABLE gt_sflight
    WHERE carrid = gs_data-carrid
      AND connid = gs_data-connid.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fcat.
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CARRID'.
  gs_fcat-ref_field = 'CARRID'.
  gs_fcat-ref_table = 'SPFLI'.
  gs_fcat-hotspot = 'X'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CARRNAME'.
  gs_fcat-ref_field = 'CARRNAME'.
  gs_fcat-ref_table = 'SCARR'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CONNID'.
  gs_fcat-ref_field = 'CONNID'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CITYFROM'.
  gs_fcat-ref_field = 'CITYFROM'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CITYTO'.
  gs_fcat-ref_field = 'CITYTO'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'AIRPFROM'.
  gs_fcat-ref_field = 'AIRPFROM'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

    CLEAR gs_fcat.
  gs_fcat-fieldname = 'AIRPTO'.
  gs_fcat-ref_field = 'AIRPTO'.
  gs_fcat-ref_table = 'SPFLI'.
  APPEND gs_fcat TO gt_fcat.

ENDFORM.

*&---------------------------------------------------------------------*

FORM free_alv.
  IF GO_ALV2 IS NOT INITIAL.
    GO_ALV2->FREE( ).
  ENDIF.
  IF GO_CONT2 IS NOT INITIAL.
    GO_CONT2->FREE( ).
  ENDIF.
  FREE: GO_ALV2, GO_CONT2.
ENDFORM.
```
</br>

**4) `C01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_C01
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_HOTSPOT_CLICK
    FOR EVENT HOTSPOT_CLICK OF CL_GUI_ALV_GRID
    IMPORTING E_ROW_ID SENDER.
ENDCLASS.


CLASS lcl_event_handler IMPLEMENTATION.
  METHOD ON_HOTSPOT_CLICK.
    IF SENDER = GO_ALV.
      gv_index = E_ROW_ID-index.
      PERFORM get_sflight.
      CALL SCREEN 110.
    ENDIF.
  ENDMETHOD.
ENDCLASS.
```
</br>

**5) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
  SET PF-STATUS 'S100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        OTHERS                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    PERFORM set_fcat.
    SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv.
    CALL METHOD go_alv->set_table_for_first_display
      CHANGING
        it_outtab                     = gt_data
        it_fieldcatalog               = gt_fcat
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
    ENDIF.
  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE status_0110 OUTPUT.
 SET PF-STATUS 'S110'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv2 OUTPUT.
  IF go_cont2 IS INITIAL.
    CREATE OBJECT go_cont2
      EXPORTING
        container_name              = 'AREA2'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        others                      = 6.
    IF sy-subrc <> 0.
    ENDIF.

    CREATE OBJECT go_alv2
      EXPORTING
        i_parent          = go_cont2
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        others            = 5.
    IF sy-subrc <> 0.
    ENDIF.

    CALL METHOD go_alv2->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_sflight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
    ENDIF.

  ENDIF.
ENDMODULE.
```
</br>

**6) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZALV_SPFLI_B03_I01
*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE user_command_0110 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      PERFORM free_alv.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**7) 결과 확인**

<img width="423" height="102" alt="image" src="https://github.com/user-attachments/assets/0336d2c5-97f0-46db-9709-17cb3433a6b6" />
</br>

<img width="470" height="164" alt="image" src="https://github.com/user-attachments/assets/4c4c5b8d-8711-428a-b928-30b01aa655cd" />
</br>

<img width="472" height="193" alt="image" src="https://github.com/user-attachments/assets/69b1f9a6-1e38-40b3-b325-dd25f24d2f93" />
</br>
</br>
</br>
</br>

### 실습 2
- 전 exercise 코드에서 이어서 진행
</br>
</br>

**1) `C01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV4_C01_3
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_toolbar FOR EVENT toolbar
      OF cl_gui_alv_grid IMPORTING e_object,
        on_user_command FOR EVENT user_command
      OF cl_gui_alv_grid IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'PERCENTAGE'.
    ls_button-text = '% total'.
    ls_button-quickinfo = 'Total economy utilization'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'PERCENTAGE_MARKED'.
    ls_button-text = '% marked'.
    ls_button-quickinfo = 'Economy utilization (marked flights)'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD on_user_command.
    DATA: gv_seatsocc TYPE I VALUE IS INITIAL,
          gv_seatsmax TYPE I VALUE IS INITIAL,
          gv_percentage TYPE P LENGTH 8 DECIMALS 2 VALUE IS INITIAL.
    DATA: lt_select_data TYPE LVC_T_ROW,
          ls_select_data TYPE LVC_S_ROW,
          gs_select_flight LIKE gs_flight.
    CASE E_UCOMM.
      WHEN 'PERCENTAGE'.
        LOOP AT gt_flight INTO gs_flight.
          gv_seatsocc = gv_seatsocc + gs_flight-seatsocc.
          gv_seatsmax = gv_seatsmax + gs_flight-seatsmax.
        ENDLOOP.
        gv_percentage = ( gv_seatsocc / gv_seatsmax ) * 100.
        MESSAGE '총 좌석: ' && gv_percentage TYPE 'I'.
      WHEN 'PERCENTAGE_MARKED'.
        CALL METHOD go_alv_grid->get_selected_rows
          IMPORTING et_index_rows = lt_select_data.
        IF lt_select_data IS NOT INITIAL.
          LOOP AT lt_select_data INTO ls_select_data.
            READ TABLE gt_flight INTO gs_select_flight
            INDEX ls_select_data-index.
            gv_seatsocc = gv_seatsocc + gs_select_flight-seatsocc.
            gv_seatsmax = gv_seatsmax + gs_select_flight-seatsmax.
          ENDLOOP.
          gv_percentage = ( gv_seatsocc / gv_seatsmax ) * 100.
          MESSAGE '총 좌석: ' && gv_percentage TYPE 'I'.
        ELSE.
          MESSAGE 'Please select record' TYPE 'E'.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.
```
</br>

**2) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC405_03_ALV4_O01
*&---------------------------------------------------------------------*

SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv_grid.
SET HANDLER lcl_event_handler=>on_user_command FOR go_alv_grid.
CALL METHOD go_alv_grid->set_table_for_first_display
  EXPORTING
    i_structure_name              = 'SFLIGHT'
  CHANGING
    it_outtab                     = gt_flight
  EXCEPTIONS
    invalid_parameter_combination = 1
    program_error                 = 2
    too_many_lines                = 3
    others                        = 4.
```
</br>

**3) 결과 확인**

<img width="510" height="349" alt="image" src="https://github.com/user-attachments/assets/2673cb36-cefa-4576-952e-98928d7e6b08" />
</br>

<img width="510" height="353" alt="image" src="https://github.com/user-attachments/assets/8ce97d02-ede4-4a4a-aa23-2f7286e3deb5" />

</br>
</br>

# 01/05
</br>
</br>

## <  핵심 개념 >
</br>

- **1) Docking Container 기반 ALV 화면 구성**
- **2) Splitter Container 기반 다중 ALV 구성**
- **3) SUBMIT 호출 방식**
- **4) 트랜잭션 호출 방식**
- **5) 프로그램 간 데이터 전달 (`SUBMIT ~ WITH`)**
- **6) EXPORT / IMPORT를 이용한 ABAP Memory 데이터 공유 (ABAP Memory)**
- **7) MEMORY ID를 이용한 조회조건 자동 유지 (SAP Memory)**
</br>

- **C01에서 더블클릭 이벤트 발생해도 자동으로 PAI/PBO 안 탐**
- **더블클릭 이후 로직은 기본적으로 C01에서만 처리**
</br>
</br>

---

</br>

## < ALV Docking Container >
</br>

### 1) Docking Container 기반 ALV 화면 구성

- `Docking Container` : 선택화면을 유지한 채 화면 가장자리에 ALV를 고정 표시하기 위한 컨테이너
- `side` : 도킹 위치 (TOP / BOTTOM / LEFT / RIGHT)
- `extension` : 도킹 영역 크기
- Docking Container 사용 시 → 별도의 AREA(스크린 영역) 필요 없음.

```abap
*************** TOP ****************
DATA: <DOCKING_CONTAINER> TYPE REF TO cl_gui_docking_container,
      <ALV_GRID>          TYPE REF TO cl_gui_alv_grid.

DATA: <ITAB> TYPE TABLE OF <DDIC_STRUCTURE>.

*************** Docking Container ****************
IF <DOCKING_CONTAINER> IS INITIAL.
  CREATE OBJECT <DOCKING_CONTAINER>
    EXPORTING
      side      = <DOCK_POSITION>
      extension = <SIZE>
      repid     = <PROGRAM_ID>
      dynnr     = <SCREEN_NO>.

  CREATE OBJECT <ALV_GRID>
    EXPORTING
      i_parent = <DOCKING_CONTAINER>.
ENDIF.

*************** Docking ALV 최초 출력 ****************
CALL METHOD <ALV_GRID>->set_table_for_first_display
  EXPORTING
    i_structure_name = <DDIC_STRUCTURE>
  CHANGING
    it_outtab        = <ITAB>.
```
- `UP TO <ROW_COUNT> ROWS` : 조회 결과 중 최대 N건까지만 내부테이블에 담음 (성능/미리보기용)
```abap
SELECT *
  FROM <DB_TABLE>
  INTO TABLE <ITAB>
  UP TO <ROW_COUNT> ROWS
  WHERE <CONDITION>.
```
```abap
REPORT zprogb03_0030.

*************** REPORT PROGRAM ****************
PARAMETERS: pa_car TYPE s_carr_id DEFAULT 'AA'.

DATA: go_docking TYPE REF TO cl_gui_docking_container,
      go_alv TYPE REF TO cl_gui_alv_grid.
DATA: gt_data TYPE TABLE OF sflight.

***********************************************
AT SELECTION-SCREEN OUTPUT.
  IF go_docking IS INITIAL.
    SELECT * FROM sflight INTO TABLE gt_data
      UP TO 20 ROWS
      WHERE carrid = pa_car.

    CHECK sy-subrc = 0. " 참이면 아래 로직 실행
    " Docking Container 생성
    CREATE OBJECT go_docking
      EXPORTING
        side      = cl_gui_docking_container=>dock_at_bottom
        extension = 300
        repid     = sy-repid
        dynnr     = sy-dynnr.
    " Docking Container에 ALV 생성
    CREATE OBJECT go_alv EXPORTING i_parent = go_docking.

    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ENDIF.

START-OF-SELECTION.
  SELECT * FROM sflight INTO TABLE gt_data
    UP TO 20 ROWS
    WHERE carrid = pa_car.

  IF sy-subrc = 0.
    CALL METHOD go_alv->refresh_table_display
      EXCEPTIONS
        finished       = 1
        others         = 2.
    IF sy-subrc <> 0.
      MESSAGE 'Refresh 작업 중 오류 발생' TYPE 'E'.
    ENDIF.
  ENDIF.
```
</br>
</br>
</br>

### 2) Splitter Container 기반 다중 ALV 구성
- 하나의 화면(또는 Docking 영역)을 여러 영역으로 나눠, 각 영역에 서로 다른 ALV를 동시에 보여주기 위해 사용
- `cl_gui_splitter_container` : 하나의 컨테이너를 행·열로 분할해 여러 영역을 동시에 사용할 수 있게 해주는 컨테이너 클래스
- `->get_container` : Splitter로 나뉜 특정 행·열 위치의 하위 컨테이너를 가져오는 메서드
```abap
*************** TOP ****************
DATA: <DOCKING_CONTAINER>  TYPE REF TO cl_gui_docking_container,
      <SPLITTER_CONTAINER> TYPE REF TO cl_gui_splitter_container,
      <LEFT_CONTAINER>     TYPE REF TO cl_gui_container,
      <RIGHT_CONTAINER>    TYPE REF TO cl_gui_container,
      <ALV_LEFT>           TYPE REF TO cl_gui_alv_grid,
      <ALV_RIGHT>          TYPE REF TO cl_gui_alv_grid.

*************** Docking Container ****************
CREATE OBJECT <DOCKING_CONTAINER>
    EXPORTING
      side      = <DOCK_POSITION>
      extension = <SIZE>
      repid     = <PROGRAM_ID>
      dynnr     = <SCREEN_NO>.

*************** Splitter Container ****************
CREATE OBJECT <SPLITTER_CONTAINER>
  EXPORTING
    parent  = <PARENT_CONTAINER>
    rows    = <ROW_COUNT>
    columns = <COLUMN_COUNT>.

*************** Split 영역 Container 획득 ****************
CALL METHOD <SPLITTER_CONTAINER>->get_container      " 왼쪽
  EXPORTING
    row    = <ROW_NO>
    column = <COL_NO_LEFT>
  RECEIVING
    container = <LEFT_CONTAINER>.
CALL METHOD <SPLITTER_CONTAINER>->get_container      " 오른쪽
  EXPORTING
    row    = <ROW_NO>
    column = <COL_NO_RIGHT>
  RECEIVING
    container = <RIGHT_CONTAINER>.

*************** Split 영역에 ALV 생성 ****************
CREATE OBJECT <ALV_LEFT>      " 왼쪽
  EXPORTING
    i_parent = <LEFT_CONTAINER>.
CREATE OBJECT <ALV_RIGHT>      " 오른쪾
  EXPORTING
    i_parent = <RIGHT_CONTAINER>.
```
```abap
REPORT zprogb03_0030.

*************** TOP ****************
DATA: go_docking TYPE REF TO cl_gui_docking_container,
      go_splitter TYPE REF TO cl_gui_splitter_container,
      go_cont_l   TYPE REF TO cl_gui_container,
      go_cont_r   TYPE REF TO cl_gui_container,
      go_alv_l    TYPE REF TO cl_gui_alv_grid,
      go_alv_r    TYPE REF TO cl_gui_alv_grid.

*************** Docking Container ****************
CREATE OBJECT go_dock
      EXPORTING
        repid                       = sy-repid
        dynnr                       = sy-dynnr
        side                        = cl_gui_docking_container=>dock_at_bottom
        extension                   = 300.

*************** Splitter Container ****************
CREATE OBJECT go_splitter            " Container 영역 분할 (1행 2열로)
EXPORTING parent  = go_docking
        rows    = 1
        columns = 2.

*************** Split 영역 Container 획득 ****************
CALL METHOD go_splitter->get_container      " 왼쪽 영역 컨테이너
EXPORTING row    = 1
          column = 1
RECEIVING container = go_cont_l.
CALL METHOD go_splitter->get_container      " 오른쪽 영역 컨테이너
EXPORTING row    = 1
          column = 2
RECEIVING container = go_cont_r.

*************** Split 영역에 ALV 생성 ****************
CREATE OBJECT go_alv_l
EXPORTING i_parent = go_cont_l.
CREATE OBJECT go_alv_r
EXPORTING i_parent = go_cont_r.
```
</br>
</br>
</br>

---

</br>

## < ABAP 프로그램·트랜잭션 호출 방식 정리 >
- **ABAP Memory** : 같은 사용자 세션(SAP GUI 창) 안에서만 공유되는 메모리
- **SAP Memory** : 세션을 넘어 다른 창·트랜잭션에서도 공유 가능한 메모리
- **`SUBMIT / CALL TRANSACTION`** → ABAP Memory 사용 가능
- **`LEAVE TO TRANSACTION`** → SAP Memory만 사용 가능
- 서로 다른 External Session(SAP GUI 창) 간에는 ABAP Memory 공유 불가 → 데이터 전달이 필요한 경우 SAP Memory 사용
- SAP Memory는 사용자 로그아웃 시 초기화됨
</br>

### 1) SUBMIT 호출 방식
- 다른 프로그램을 잠깐 실행했다가 돌아오는 방식 → 같은 메모리(ABAP Memory) 그대로 사용
</br>

- 다른 프로그램 실행만 하고 자동으로 돌아오지 않음
```abap
SUBMIT prog_name_2.
```
- 다른 프로그램 실행 후 끝나면 원래 프로그램으로 돌아옴
```abap
SUBMIT prog_name_2 AND RETURN.
```
- 실행 전 조회조건 화면을 보여주고, 끝나면 원래 프로그램으로 돌아옴
```abap
SUBMIT prog_name_2 VIA SELECTION-SCREEN AND RETURN.
```
</br>
</br>

**1) SUBMIT ZPROGB03_CALLED.**

- `ZPROGB03_CALLED` 만 실행되고 “CALL 프로그램으로 돌아왔어요!”는 안 찍힘
  
**2) SUBMIT ZPROGB03_CALLED AND RETURN.**

- `PROGB03_CALLED` 실행 후 돌아와서 “CALL 프로그램으로 돌아왔어요!”가 찍힘
  
**3) SUBMIT ZPROGB03_CALLED VIA SELECTION-SCREEN AND RETURN.**

- `ZPROGB03_CALLED` 의 조회조건 화면을 먼저 보여준 뒤 실행하고, 끝나면 돌아와서 “CALL 프로그램으로 돌아왔어요!”가 찍힘

```abap
*************** CALL ****************
REPORT ZPROGB03_CALL.

PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_sflight-connid.

START-OF-SELECTION.

SUBMIT ZPROGB03_CALLED
VIA SELECTION-SCREEN
AND RETURN
WITH pa_car2 = pa_car
WITH so_con2 IN so_con.
SUBMIT ZPROGB03_CALLED.
SUBMIT ZPROGB03_CALLED AND RETURN.
SUBMIT ZPROGB03_CALLED VIA SELECTION-SCREEN AND RETURN.

*SUBMIT ZPROGB03_CALLED.
*SUBMIT ZPROGB03_CALLED AND RETURN.
*SUBMIT ZPROGB03_CALLED VIA SELECTION-SCREEN AND RETURN.

WRITE: 'CALL 프로그램으로 돌아왔어요!'.

*************** CALLED ****************
REPORT ZPROGB03_CALLED.

PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_sflight-connid.

IF sy-subrc = 0.
  LOOP AT gt_sflight INTO gs_sflight.
    WRITE:/ gs_sflight-carrid,
            gs_sflight-connid,
            gs_sflight-fldate,
            gs_sflight-seatsmax,
            gs_sflight-seatsocc.
    CLEAR: gs_sflight.
  ENDLOOP.
ELSE.
  WRITE:/ 'No data'.
ENDIF.
```
</br>
</br>
</br>

### 2) 트랜잭션 호출 방식
- 현재 프로그램 종료 후 트랜잭션으로 이동 (복귀 ❌)
- 내부 세션이 종료되어 ABAP Memory는 리셋됨
- 새로운 세션으로 트랜잭션이 시작되며 SAP Memory만 유지됨
```abap
LEAVE TO TRANSACTION 'T_CODE'
```
- 트랜잭션의 첫 화면을 건너뛰고 바로 실행
```abap
LEAVE TO TRANSACTION 'T_CODE' AND SKIP FIRST SCREEN
```
- 트랜잭션 실행 후 종료 시 원래 프로그램으로 복귀 ⭕
- 트랜잭션 화면을 열었다가 끝나면 돌아옴 → 메모리 유지됨
```abap
CALL TRANSACTION 'T_CODE'
```
</br>
</br>

**1) LEAVE TO TRANSACTION 'ZTCODEB03_CALLED'**

- ZTCODEB03_CALLED로 넘어가서 실행되고 CALL 프로그램으로 돌아오지 않아 "CALL 프로그램으로 돌아왔어요!"는 안 찍힘

**2) LEAVE TO TRANSACTION 'ZTCODEB03_CALLED' AND SKIP FIRST SCREEN**

- ZTCODEB03_CALLED를 조회조건 화면 없이 바로 실행하고, CALL 프로그램으로 돌아오지 않음

**3) CALL TRANSACTION 'ZTCODEB03_CALLED'**

- ZTCODEB03_CALLED를 실행한 뒤 돌아와서 "CALL 프로그램으로 돌아왔어요!"가 찍힘

```abap
*************** CALL ****************
REPORT ZPROGB03_CALL.

PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_sflight-connid.

SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT pos_low(20) TEXT-001 FOR FIELD pa_rad1.
    PARAMETERS: pa_rad1 RADIOBUTTON GROUP RAD.
    SELECTION-SCREEN COMMENT pos_high(20) TEXT-002 FOR FIELD pa_rad2.
    PARAMETERS: pa_rad2 RADIOBUTTON GROUP RAD DEFAULT 'X'.
  SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK blk1.

START-OF-SELECTION.
CASE 'X'.
  WHEN pa_rad1.
*    LEAVE TO TRANSACTION 'ZTCODEB03_CALLED'
    LEAVE TO TRANSACTION 'ZTCODEB03_CALLED'
    AND SKIP FIRST SCREEN.
  WHEN pa_rad2.
    CALL TRANSACTION 'ZTCODEB03_CALLED'.
  WHEN OTHERS.
ENDCASE.

WRITE: 'CALL 프로그램으로 돌아왔어요!'.
```
</br>
</br>
</br>

### 3) 프로그램 간 데이터 전달
- 앞 프로그램에서 입력한 값을 다음 프로그램으로 그대로 넘길 때 사용
```abap
SUBMIT <CALLED_PROGRAM>
  VIA SELECTION-SCREEN AND RETURN
  WITH <CALLED_PARM> = <CALL_PARM>
  WITH <CALLED_SO>   IN <CALL_SO>.
```
```abap
*************** CALL ****************
PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_sflight-connid.

START-OF-SELECTION.

SUBMIT ZPROGB03_CALLED
VIA SELECTION-SCREEN
AND RETURN
WITH pa_car2 = pa_car
WITH so_con2 IN so_con.

*************** CALLED ****************
PARAMETERS: pa_car2 TYPE s_carr_id.
SELECT-OPTIONS: so_con2 FOR gs_sflight-connid.
```

</br>
</br>
</br>

### 4) EXPORT / IMPORT를 이용한 ABAP Memory 데이터 공유 (ABAP Memory)
- 프로그램을 이동해도 이전에 입력한 값을 다시 쓰기 위해 ABAP Memory를 사용함
- `LEAVE TO TRANSACTION` 은 세션을 종료하므로 ABAP Memory(EXPORT/IMPORT)는 사용 불가
- `CALL` 프로그램에서 저장한 값을 같은 창에서 `CALLED` 프로그램을 실행하면 정상적으로 가져옴
- 새 창(New Session)에서 `CALLED` 프로그램을 실행하면 ABAP Memory를 못 가져와 `No data` 발생
```abap
" ABAP Memory에 값 저장
EXPORT
  <MEM_PARM> FROM <CALL_PARM>
  <MEM_SO>   FROM <CALL_SO>
  TO MEMORY ID '<MEMORY_ID>'.

" ABAP Memory에서 값 불러오기
IMPORT
  <MEM_PARM> TO <CALLED_PARM>
  <MEM_SO>   TO <CALLED_SO>
  FROM MEMORY ID '<MEMORY_ID>'.

" ABAP Memory 초기화
FREE MEMORY ID '<MEMORY_ID>'.
```
```abap
*************** CALL ****************
PARAMETERS: pa_car TYPE s_carr_id.
SELECT-OPTIONS: so_con FOR gs_sflight-connid.

FREE MEMORY ID 'CARCON'.

EXPORT
  p1 FROM pa_car
  s1 FROM so_con
  TO MEMORY ID 'CARCON'.

*************** CALLED ****************
PARAMETERS: pa_car2 TYPE s_carr_id.
SELECT-OPTIONS: so_con2 FOR gs_sflight-connid.

IMPORT
  p1 TO pa_car2
  s1 TO so_con2
  FROM MEMORY ID 'CARCON'.

FREE MEMORY ID 'CARCON'.
```
</br>
</br>
</br>

### 5) MEMORY ID를 이용한 조회조건 자동 유지 (SAP Memory, SET / GET PARAMETER ID)
- 한 번 입력한 조회조건을 SAP이 기억해두었다가, 다음 실행 때 자동으로 다시 채워줌
```abap
PARAMETERS: <PARAM> TYPE <TYPE> MEMORY ID '<MEM_ID>'.
SELECT-OPTIONS: <SO> FOR <FIELD> MEMORY ID '<MEM_ID>'.

" SAP Memory 초기화
SET PARAMETER ID '<MEM_ID>' FIELD SPACE.
```
```abap
*************** CALL ****************
PARAMETERS: pa_car TYPE s_carr_id MEMORY ID CAR1.
SELECT-OPTIONS: so_con FOR gs_sflight-connid MEMORY ID CON1.

SET PARAMETER ID 'CAR1' FIELD SPACE. " SAP Memory 초기화

*************** CALLED ****************
PARAMETERS: pa_car2 TYPE s_carr_id MEMORY ID CAR1.
SELECT-OPTIONS: so_con2 FOR gs_sflight-connid MEMORY ID CON1.
```
- SAP Memory에 값을 저장하고 같은 세션의 다른 프로그램·화면에서 그 값을 자동으로 전달·조회하는 기능
```abap
*************** SET ****************
SET PARAMETER ID '<MEM_ID>' FIELD <FIELD>.

*************** GET ****************
GET PARAMETER ID '<MEM_ID>' FIELD <FIELD>.
```
```abap
*************** SET ****************
SET PARAMETER ID 'CAR1' FIELD pa_car.     
SET PARAMETER ID 'CON1' FIELD pa_conn.   

*************** GET ****************
GET PARAMETER ID 'CAR1' FIELD pa_car2.    
GET PARAMETER ID 'CON1' FIELD pa_conn2.   
```
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 문제
<img width="356" height="196" alt="image" src="https://github.com/user-attachments/assets/4888e9dc-6d7b-43c1-ac54-52f8f3c76a29" />
</br>
</br>
</br>
</br>

**1) Report 프로그램에서 모듈 생성**
```abap
REPORT ZPROGB03_0031.

INCLUDE ZPROGB03_0031_TOP.
INCLUDE ZPROGB03_0031_O01.
INCLUDE ZPROGB03_0031_I01.
INCLUDE ZPROGB03_0031_F01.

START-OF-SELECTION.
  PERFORM get_data.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_dock TYPE REF TO cl_gui_docking_container,
      go_alv TYPE REF TO cl_gui_alv_grid.
DATA: gt_sflight TYPE TABLE OF sflight.

PARAMETERS: pa_car TYPE sflight-carrid DEFAULT 'AZ'.
```
</br>

**3) `PBO` 및 `F01` 모듈 생성**
```abap
*************** F01 ****************
FORM get_data.
  SELECT * FROM SFLIGHT
    INTO TABLE gt_sflight
    UP TO 30 ROWS
    WHERE carrid = pa_car.
ENDFORM.

*************** PBO ****************
MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
ENDMODULE.

MODULE init_alv OUTPUT.
  IF go_dock IS INITIAL.
    CREATE OBJECT go_dock
      EXPORTING
        repid                       = sy-repid
        dynnr                       = sy-dynnr
        side                        = cl_gui_docking_container=>dock_at_bottom
        extension                   = 300.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_dock.

    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_sflight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ELSE.
    IF sy-subrc = 0.
      CALL METHOD go_alv->refresh_table_display
        EXCEPTIONS
          finished       = 1
          others         = 2.
      IF sy-subrc <> 0.
        MESSAGE 'Refresh 작업 중 오류 발생' TYPE 'E'.
      ENDIF.
    ENDIF.
  ENDIF.
ENDMODULE.
```
</br>

**4) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_I01
*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*----------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**5) 결과 확인**

<img width="284" height="96" alt="image" src="https://github.com/user-attachments/assets/386d3fff-0177-4a29-b0ea-08e209effe26" />
</br>

<img width="454" height="258" alt="image" src="https://github.com/user-attachments/assets/2aa8ec00-46bf-4df0-b667-80620cbd6164" />
</br>
</br>
</br>
</br>
</br>
</br>

### 실습 2
</br>

- 문제
<img width="330" height="183" alt="image" src="https://github.com/user-attachments/assets/254b894d-5b6d-443b-8bf2-5ab47bb16ce7" />

</br>
</br>
</br>

**1) Report 프로그램에서 모듈 수정**
```abap
REPORT ZPROGB03_0031.

INCLUDE ZPROGB03_0031_TOP.
INCLUDE ZPROGB03_0031_C01.
INCLUDE ZPROGB03_0031_O01.
INCLUDE ZPROGB03_0031_I01.
INCLUDE ZPROGB03_0031_F01.

START-OF-SELECTION.
  PERFORM get_data2.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm,
      go_dock TYPE REF TO cl_gui_docking_container,
      go_splitter TYPE REF TO cl_gui_splitter_container,
      go_cont_l TYPE REF TO cl_gui_container,
      go_cont_r TYPE REF TO cl_gui_container,
      go_alv_l TYPE REF TO cl_gui_alv_grid,
      go_alv_r TYPE REF TO cl_gui_alv_grid.
```
</br>

**3) `F01` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_F01
*&---------------------------------------------------------------------*

*************** SFLIGHT 데이터ㅗ ****************
FORM get_data.
  READ TABLE gt_spfli INTO gs_spfli INDEX gv_index.

  SELECT * FROM SFLIGHT
    INTO TABLE gt_sflight
    UP TO 30 ROWS
    WHERE carrid = gs_spfli-carrid
      AND connid = gs_spfli-connid.
ENDFORM.

*************** SPFLI 데이터 ****************
FORM get_data2.
  SELECT * FROM SPFLI
    INTO TABLE gt_spfli
    UP TO 30 ROWS
    WHERE carrid = pa_car.
ENDFORM.

*************** REFRESH ****************
FORM set_refresh.
  CALL METHOD go_alv_r->refresh_table_display
    EXCEPTIONS
      finished       = 1
      others         = 2.
  IF sy-subrc <> 0.
    MESSAGE 'Refresh 작업 중 오류 발생' TYPE 'E'.
  ENDIF.
ENDFORM.
```
</br>

**4) `C01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_C01
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: ON_DOUBLE_CLICK
    FOR EVENT DOUBLE_CLICK OF CL_GUI_ALV_GRID
    IMPORTING ES_ROW_NO SENDER.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD ON_DOUBLE_CLICK.
    IF SENDER = GO_ALV_L.
      gv_index = ES_ROW_NO-row_id.
      PERFORM get_data.
      PERFORM set_refresh.
    ENDIF.
  ENDMETHOD.
ENDCLASS.
```
</br>

**5) `PBO` 모듈 수정**
```abap
*&---------------------------------------------------------------------*
*& Include          ZPROGB03_0031_O01
*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_dock IS INITIAL.

*************** Docking Container 생성 ****************
    CREATE OBJECT go_dock
      EXPORTING
        repid                       = sy-repid
        dynnr                       = sy-dynnr
        side                        = cl_gui_docking_container=>dock_at_bottom
        extension                   = 300.

*************** Splitter 생성 (1행 2열) ****************
    CREATE OBJECT go_splitter
    EXPORTING parent = go_dock
              rows = 1
              columns = 2.

*************** Split 영역 Container 획득 ****************
    CALL METHOD go_splitter->get_container
    EXPORTING row = 1
              column = 1
    RECEIVING container = go_cont_l.
    CALL METHOD go_splitter->get_container
    EXPORTING row = 1
              column = 2
    RECEIVING container = go_cont_r.

*************** Split 영역에 ALV 생성 ****************
    CREATE OBJECT go_alv_l
    EXPORTING i_parent = go_cont_l.
    CREATE OBJECT go_alv_r
    EXPORTING i_parent = go_cont_r.

*************** 데이터 조회 ****************
    SET HANDLER lcl_event_handler=>on_double_click FOR go_alv_l.

    CALL METHOD go_alv_r->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = gt_sflight
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.

    PERFORM get_data.
    CALL METHOD go_alv_l->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SPFLI'
      CHANGING
        it_outtab                     = gt_spfli
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ENDIF.
ENDMODULE.
```
</br>

**6) 결과 확인**

- 더블클릭 시 데이터 출력
<img width="535" height="240" alt="image" src="https://github.com/user-attachments/assets/0faf79a3-c470-4a4f-9e7a-d27efd122f58" />

</br>
</br>
</br>
</br>
</br>
</br>

</br>
</br>

# 01/06
</br>
</br>

## <  핵심 개념 >
</br>

- **1) BDC (Batch Data Creation)**
- **2) SAP Job(백그라운드 잡) - SM36, SM37**
- **3) CLIENT(MANDT) 지정 방식 – 구문법 vs 신문법**
- **4)  Open SQL INSERT 구문**
- **5) Open SQL UPDATE 구문**
</br>

- **`UPDATE` 는 기존 레코드만 수정하고, `MODIFY` 는 있으면 수정·없으면 INSERT까지 수행**
</br>
</br>

---

</br>

## < SAP 자동 실행 처리 기법 >
</br>

### 1) BDC (Batch Data Creation)
- 실제 트랜잭션 화면 입력 과정을 BDCDATA에 기록해 프로그램으로 자동 실행하는 방식
</br>

**< CALL TRANSACTION - MODE 옵션 >**

| MODE | 설명 |
|------|------|
| A | 모든 화면 표시 / 진행 중 수정 가능 |
| E | 정상 흐름은 숨기고 오류 화면만 표시 |
| N | 화면 없이 백그라운드 처리 (사용자는 못 봄) |
| P | 오류 발생 시 화면 일시정지 |

**< CALL TRANSACTION - UPDATE 옵션 >**

| UPDATE | 설명 |
|--------|------|
| S | 동기 처리 (작업 완료 후 프로그램 복귀) |
| A | 비동기 처리 (즉시 복귀, 작업은 백그라운드 진행) |

</br>

```abap
*************** BDC Dynpro 시작 ****************
PERFORM set_bdcdata USING 'X' <PROGRAM> <SCREEN>.
* <PROGRAM> : Dynpro 프로그램명
* <SCREEN>  : Dynpro 번호


*************** BDC 화면 필드 입력 ****************
PERFORM set_bdcdata USING ' ' <FIELDNAME> <VALUE>.
* <FIELDNAME> : 화면 필드명 (예: BDC_OKCODE, PA_CAR2)
* <VALUE>     : 입력값


*************** BDC DATA 공통 FORM ****************
FORM set_bdcdata USING pv_dynbegin   " Dynpro 시작 여부 ('X' / ' ')
                       pv_fname      " 프로그램명 or 필드명
                       pv_fval.      " 화면번호 or 입력값

  CLEAR gs_bdcdata.
  IF pv_dynbegin EQ abap_true.        " Dynpro 시작
    gs_bdcdata-dynbegin = abap_true.
    gs_bdcdata-program  = pv_fname.
    gs_bdcdata-dynpro   = pv_fval.
  ELSE.                              " 화면 필드 입력
    gs_bdcdata-fnam = pv_fname.
    gs_bdcdata-fval = pv_fval.
  ENDIF.
  APPEND gs_bdcdata TO gt_bdcdata.    " BDCDATA 한 줄 적재
ENDFORM.
```
```abap
REPORT ZPROGB03_CALL.

*************** BDC DATA 선언부 ****************
DATA: gs_bdcdata      TYPE bdcdata,             " BDCDATA 단건 구조
      gt_bdcdata      TYPE STANDARD TABLE OF bdcdata,
      lt_bdc_message  TYPE STANDARD TABLE OF bdcmsgcoll.


*************** BDC DATA 적재 (CALL 쪽) ****************
PERFORM set_bdcdata USING:
* STARTID   PROGRAM / FIELDNAME    SCREEN / VALUE
  'X'       'ZPROGB03_CALLED'      '1000',      " Dynpro 시작
  ' '       'BDC_CURSOR'           'SO_CON2-HIGH',
  ' '       'BDC_OKCODE'           '=ONLI',
  ' '       'PA_CAR2'              'AA',
  ' '       'SO_CON2-LOW'          '17',
  ' '       'SO_CON2-HIGH'         '64',
  'X'       'SAPMSSY0'              '0120',     " 팝업 Dynpro
  ' '       'BDC_OKCODE'           '=BACK',
  'X'       'ZPROGB03_CALLED'      '1000',      " 원화면
  ' '       'BDC_OKCODE'           '/EE',
  ' '       'BDC_CURSOR'           'PA_CAR2'.

*************** BDC CALL TRANSACTION ****************
CALL TRANSACTION 'ZTCODEB03_CALLED'
  USING gt_bdcdata
  MODE 'A'
  UPDATE 'S'
  MESSAGES INTO lt_bdc_message.

*************** BDC DATA 함수 ****************
FORM set_bdcdata  USING    pv_dynbegin
                           pv_fname
                           pv_fval.
  CLEAR gs_bdcdata.
  IF pv_dynbegin EQ abap_true. " pv_dynbegin = 'X'
    gs_bdcdata-dynbegin = abap_true.
    gs_bdcdata-program = pv_fname.
    gs_bdcdata-dynpro = pv_fval.
  ELSE.
    gs_bdcdata-fnam = pv_fname.
    gs_bdcdata-fval = pv_fval.
  ENDIF.
  APPEND gs_bdcdata TO gt_bdcdata.
ENDFORM.
```
</br>

- system -> services -> batch input -> recorder 클릭
<img width="498" height="258" alt="image" src="https://github.com/user-attachments/assets/70fb2045-a16b-4189-a43b-6dc7289f1fb0" />
</br>
</br>

- new recording 누르고 start recording 클릭
<img width="441" height="413" alt="image" src="https://github.com/user-attachments/assets/2f12a82d-cd6e-4f78-804f-6c51306bbd57" />
</br>
</br>

- 녹화 후 Process 클릭하여 실행
<img width="471" height="379" alt="image" src="https://github.com/user-attachments/assets/25131805-f01f-4c9f-a0d4-f34736a3188c" />
</br>
</br>

- 만든 레코드 확인 가능
<img width="501" height="150" alt="image" src="https://github.com/user-attachments/assets/b56b127f-f634-4c8b-833d-1325e1f61d4d" />
</br>
</br>
</br>
</br>
</br>

### 2) SAP Job(백그라운드 잡) - SM36, SM37
- Job = 사람이 안 눌러도 정해진 조건/시간에 ABAP 프로그램을 자동 실행하는 작업
- **SM36** → 잡(Job) 생성
- **SM37** → 생성된 잡(Job) 조회 / 상태 확인
</br>

- 백그라운드 잡에서는 ALV 사용 불가 (화면 인스턴스 생성 불가)
- 시스템 변수 `sy-batch = 'X'` → 현재 백그라운드 실행 중
- 이 경우 ALV 대신 WRITE, 스풀, 파일 출력 등으로 처리
```abap
REPORT ZPROGB03_0032.

DATA: gt_data TYPE TABLE OF ZDBWORK_B03,
      gs_data LIKE LINE OF gt_data.

SELECT * FROM ZDBWORK_B03
  INTO TABLE gt_data
  ORDER BY WORKNO DESCENDING." 내림차순

CHECK sy-subrc = 0.

READ TABLE gt_DATA INTO GS_DATA INDEX 1.
ADD 1 TO gs_data-workno.

*********** 처음 넣은 데이터 ***********
gs_data-workno = 1.
gs_data-workdesc = 'hello work~~'.
gs_data-ernam = sy-uname. " 유저이름
gs_data-erdat = sy-datum. " 현재 일자
gs_data-erzet = sy-uzeit. " 현재 시간
***************************************

gs_data-workdesc = 'hello work~~'.
gs_data-ernam = sy-uname. " 유저이름
gs_data-erdat = sy-datum. " 현재 일자
gs_data-erzet = sy-uzeit. " 현재 시간

INSERT ZDBWORK_B02 FROM gs_data.
IF sy-subrc = 0.
  SELECT * FROM ZDBWORK_B03
    INTO TABLE gt_data
    ORDER BY workno DESCENDING.
  cl_demo_output=>display( gt_data ).
ENDIF.
```
</br>

- `SM36` 로 JOB 생성하기
- 프로그램 이름 지정
<img width="448" height="279" alt="image" src="https://github.com/user-attachments/assets/b2b681d2-7fc4-4bf3-bc4f-9779b6f4eea4" />
</br>
</br>

- JOB 시작 조건을 주기적 실행으로 설정해 20분마다 백그라운드 JOB을 자동 실행하도록 설정
- `No Start After` : 종료일이 아님. 시작일의 유효기간. 종료일은 빈칸으로 둬야 함.
<img width="459" height="369" alt="image" src="https://github.com/user-attachments/assets/da67cc6f-53a4-4032-9a56-7644a4695deb" />
</br>
</br>

- `SM37` 에서 생성된 JOB 확인 가능
<img width="458" height="328" alt="image" src="https://github.com/user-attachments/assets/81eb3a77-eb99-4185-aabb-f116a08e790b" />
</br>
</br>
</br>
</br>

---

</br>

## < ABAP Open SQL 핵심 문법 정리 >
</br>

### 1) CLIENT(MANDT) 지정 방식 – 구문법 vs 신문법
- Open SQL에서 특정 클라이언트(MANDT)의 데이터를 직접 지정해 조회
```abap
*************** Open SQL CLIENT 지정 – 구문법 (형식) ****************
SELECT <FIELDS>
  INTO TABLE <ITAB>
  FROM <DB_TABLE>
  CLIENT SPECIFIED
  WHERE mandt = <CLIENT>.


*************** Open SQL CLIENT 지정 – 신문법 (형식) ****************
SELECT <FIELDS>
  INTO TABLE @<ITAB>
  FROM <DB_TABLE>
  USING CLIENT <CLIENT>.
```
```abap
REPORT ZPROGB03_0033.

*************** Open SQL – CLIENT 지정 (구문법) ****************
SELECT * INTO TABLE gt_scarr
  FROM scarr
  CLIENT SPECIFIED
  WHERE mandt = '100'.   
* WHERE MANDT BETWEEN '000' AND '900'.

*************** Open SQL – CLIENT 지정 (신문법) ****************
SELECT * INTO TABLE @gt_scarr
  FROM scarr
  USING CLIENT '000'.    
```
</br>
</br>

### 2) Open SQL INSERT 구문
- 워크에어리어로 단건, 내부테이블로 여러 건 데이터를 DB에 저장
- `ACCEPTING DUPLICATE KEYS` 를 사용하면 중복 키는 건너뛰고 나머지 데이터만 INSERT됨
- 중복 키 상태에서 INSERT를 실행하면 → 덤프 발생
- **`sy-dbcnt`** 로 실제 INSERT된 결과를 확인
```abap
*************** 단건 INSERT ****************
INSERT INTO <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  VALUES <WORK_AREA>.        " 워크에어리어 1건 INSERT

INSERT <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM <WORK_AREA>.          " VALUES와 동일 의미

*************** 다중 INSERT ****************
INSERT <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM TABLE <ITAB>
  [ACCEPTING DUPLICATE KEYS]. " 내부테이블 여러 건 INSERT
```
```abap
REPORT ZPROGB03_0033.

DATA: gt_scustom TYPE TABLE OF ZTSCUSTOM_B03,
      gs_scustom TYPE ZTSCUSTOM_B03.
* DELETE FROM ZTSCUSTOM_B03. => ACCEPTING DUPLICATE KEYS 안하면 써야함

*************** 단건 INSERT (Work Area) ****************
SELECT SINGLE *
  FROM SCUSTOM
  INTO gs_scustom
  WHERE ID = 1.

INSERT INTO ZTSCUSTOM_B03 VALUES gs_scustom.
INSERT ZTSCUSTOM_B03 FROM gs_scustom.

*************** 다중 INSERT (Internal Table) ****************
SELECT * FROM scustom
  INTO TABLE gt_scustom
  WHERE ID BETWEEN 1 AND 50.

INSERT ZTSCUSTOM_B03 FROM TABLE gt_scustom
  ACCEPTING DUPLICATE KEYS.
```
</br>
</br>

### 3) Open SQL UPDATE 구문
- 기존 DB 레코드를 PK 기준으로 1건 수정하거나, 조건(WHERE)에 맞는 여러 건을 한 번에 수정할 수 있음
- 수정된 레코드 수는 **`sy-dbcnt`** 로 확인 가능 (0이면 대상 없음)
- UPDATE는 기존 레코드만 수정하고, MODIFY는 있으면 수정·없으면 INSERT까지 수행
```abap
*************** 단건 UPDATE (Work Area) ****************
UPDATE <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM <WORK_AREA>.          " PK 기준 1건 수정

*************** 단건 / 다중 UPDATE (SET / WHERE) ****************
UPDATE <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  SET <FIELD1> = <VALUE1>,
      <FIELD2> = <VALUE2>
  WHERE <FULL_QUALIFIED_KEY>. " 조건에 맞는 필드만 단건/다중 수정

*************** 다중 UPDATE (Internal Table 방식) ****************
UPDATE <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM TABLE <ITAB>.          " 내부테이블 기준 여러 건 수정
```
```abap
REPORT ZPROGB03_0033.

*************** 단건 UPDATE (Work Area) ****************
gs_scustom-id = 1.
gs_scustom-name = 'change name'.
UPDATE ZTSCUSTOM_B03 FROM gs_scustom.

*************** 단건 / 다중 UPDATE (SET / WHERE) ****************
gs_scustom-id = 2.
gs_scustom-name = 'second change'.      " 단건 변경
UPDATE ZTSCUSTOM_B03
   SET name = gs_scustom-name
       street = 'hihi'
   WHERE ID = gs_scustom-id.

UPDATE ZTSCUSTOM_B03
  SET form = 'My form'                  " 여러건 변경
      city = 'Walldorf'
      country = 'DE'
  WHERE id BETWEEN 10 AND 20.

*************** 다중 UPDATE (Internal Table 방식) ****************
SELECT * FROM scustom
  INTO TABLE gt_scustom
  WHERE id BETWEEN 10 AND 20.

LOOP AT gt_scustom INTO gs_scustom.
  gs_scustom-form = 'My form'.
  gs_scustom-city = 'Walldorf'.
  gs_scustom-country = 'DE'.
  MODIFY gt_scustom FROM gs_scustom.
ENDLOOP.
UPDATE ZTSCUSTOM_B03 FROM TABLE gt_scustom.
```
</br>
</br>

### 4) Open SQL MODIFY 구문
- PK 기준으로 데이터가 있으면 UPDATE, 없으면 INSERT 수행
- **`sy-dbcnt`** 로 실제 반영된 수정 결과를 확인
- `APPENDING TABLE` : 기존 내부테이블을 비우지 않고 SELECT 결과를 뒤에 계속 추가하는 방식
```abap
*************** 단건 MODIFY (Work Area) ****************
MODIFY <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM <WORK_AREA>.          " 있으면 UPDATE, 없으면 INSERT

*************** 다중 MODIFY (Internal Table 방식) ****************
MODIFY <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM TABLE <ITAB>.          " 여러 건 UPSERT 처리
```
```abap
REPORT ZPROGB03_0033.

*************** 단건 MODIFY (Work Area) ****************
gs_scustom-id = 500.
gs_scustom-form = '변경~~'.
gs_scustom-name = 'MODIFY 변경~~'.
MODIFY ZTSCUSTOM_B03 FROM gs_scustom.

*************** 다중 MODIFY (Internal Table 방식) ****************
gs_scustom-name = 'modify name'.
MODIFY gt_scustom FROM gs_scustom
TRANSPORTING name WHERE id <> space.

SELECT *
  APPENDING TABLE gt_scustom
  FROM scustom
  WHERE ID IN ( 193, 195, 197 ).
" 완성된 itab을 db tab에 modify
MODIFY ZTSCUSTOM_B03 FROM TABLE gt_scustom.
```
</br>
</br>

### 5) Open SQL DELETE 구문
- PK 기준으로 단건 삭제하거나 조건(WHERE)에 맞는 여러 건 삭제 가능
- 삭제된 레코드 수는 **`sy-dbcnt`** 로 확인
- 조건이 맞는 데이터가 없으면 아무 것도 삭제되지 않음
```abap
*************** 단건 DELETE (Work Area) ****************
DELETE <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM <WORK_AREA>.          " PK 기준 1건 삭제

*************** 단건 / 다중 DELETE (WHERE) ****************
DELETE FROM <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  WHERE <FULL_QUALIFIED_KEY>. " 조건에 맞는 단건/다중 삭제

*************** 다중 DELETE (Internal Table 방식) ****************
DELETE <DB_TABLE>
  [CLIENT SPECIFIED | USING CLIENT <CLIENT>]
  FROM TABLE <ITAB>.          " 내부테이블의 PK 기준으로 여러 건 삭제
```
```abap
REPORT ZPROGB03_0033.

*************** 단건 DELETE (Work Area) ****************
gs_scustom-id = 1.
DELETE ZTSCUSTOM_B03 FROM gs_scustom.

*************** 단건 / 다중 DELETE (WHERE) ****************
DELETE FROM ZTSCUSTOM_B03 WHERE ID = 2.    " 단건 삭제

DELETE FROM ZTSCUSTOM_B03
  WHERE ID BETWEEN 55 AND 60.              " 여러건 삭제

*************** 다중 DELETE (Internal Table 방식) ****************
" DELETE FROM ZTSCUSTOM_B03 " (주의) 전체 삭제

SELECT * FROM ZTSCUSTOM_B03
  INTO TABLE gt_scustom
  WHERE ID <= 50.
DELETE ZTSCUSTOM_B03 FROM TABLE gt_scustom.
```

</br>
</br>

# 01/07
</br>
</br>

## <  핵심 개념 >
</br>

- **서브스크린 생성시 LAYOUT에 서브스크린 지정 필수**
- **`MEMORY ID` 는 PARAMETERS, SELECT-OPTIONS에서만 선언 가능 (DATA는 불가능)**
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 문제
- ALV에 DB 테이블 데이터 삽입, 수정, 삭제 실습
<img width="357" height="426" alt="image" src="https://github.com/user-attachments/assets/74c4ba9f-a621-4675-9072-30952ea8c91b" />
</br>
</br>
</br>

**1) Report program 코드 생성**
```abap
REPORT ZBC400_PRACTICE1_B03.

INCLUDE ZBC400_PRACTICE1_B03_TOP.
INCLUDE ZBC400_PRACTICE1_B03_C01.
INCLUDE ZBC400_PRACTICE1_B03_O01.
INCLUDE ZBC400_PRACTICE1_B03_I01.
INCLUDE ZBC400_PRACTICE1_B03_F01.

START-OF-SELECTION.
PERFORM set_data.
CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
- 서브스크린 생성시 LAYOUT에 서브스크린 지정 필수
```abap
DATA: OK_CODE TYPE sy-ucomm.

DATA: go_docking TYPE REF TO cl_gui_docking_container,
      go_alv TYPE REF TO cl_gui_alv_grid.

DATA: gs_data TYPE ZTMYCARRIER_B03,
      gt_data TYPE TABLE OF ZTMYCARRIER_B03,
      gt_sflight TYPE TABLE OF ZREADSFLIGHT_B03.

DATA gs_layout TYPE lvc_s_layo.

SELECTION-SCREEN BEGIN OF SCREEN 110 AS SUBSCREEN.
  PARAMETERS: pa_car TYPE ZTMYCARRIER_B03-carrid,
              pa_con TYPE ZTMYCARRIER_B03-connid,
              pa_name TYPE ZTMYCARRIER_B03-carrname,
              pa_cityf TYPE ZTMYCARRIER_B03-cityfrom,
              pa_airpf TYPE ZTMYCARRIER_B03-airpfrom,
              pa_cityt TYPE ZTMYCARRIER_B03-cityto,
              pa_airpt TYPE ZTMYCARRIER_B03-airpto.
SELECTION-SCREEN END OF SCREEN 110.
```
</br>

**3) `C01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_B03_C01
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: on_toolbar FOR EVENT toolbar
    OF cl_gui_alv_grid IMPORTING e_object,
      on_user_command FOR EVENT user_command
    OF cl_gui_alv_grid IMPORTING e_ucomm.
  CLASS-METHODS: on_double_click FOR EVENT double_click
    OF cl_gui_alv_grid IMPORTING ES_ROW_NO.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'INSERT'.
    ls_button-icon = ICON_EXPAND.
    ls_button-butn_type = 0.
    ls_button-text = 'INSERT'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'UPDATE'.
    ls_button-icon = ICON_INTENSIFY.
    ls_button-butn_type = 0.
    ls_button-text = 'UPDATE'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'DELETE'.
    ls_button-icon = ICON_COLLAPSE.
    ls_button-butn_type = 0.
    ls_button-text = 'DELETE'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD ON_USER_COMMAND.
    CASE E_UCOMM.
      WHEN 'INSERT'.
        PERFORM insert_data.
      WHEN 'UPDATE'.
        PERFORM update_data.
      WHEN 'DELETE'.
        DATA: lt_select_data TYPE LVC_T_ROW,
              ls_select_data TYPE LVC_S_ROW.

        CALL METHOD go_alv->get_selected_rows
          IMPORTING
            et_index_rows = lt_select_data.

        PERFORM delete_data USING lt_select_data ls_select_data.
      WHEN OTHERS.
    ENDCASE.
    PERFORM refresh_data.
  ENDMETHOD.

  METHOD on_double_click.
    CLEAR: gs_data, gt_sflight.
    READ TABLE gt_data INTO gs_data INDEX es_row_no-row_id.
    SELECT * FROM sflight
      INTO TABLE gt_sflight
      WHERE carrid = gs_data-carrid
        AND connid = gs_data-connid.

    IF sy-subrc = 0.
      DATA: gv_car TYPE sflight-carrid,
      gv_con TYPE sflight-connid.
      gv_car = gs_data-carrid.
      gv_con = gs_data-connid.
      SET PARAMETER ID 'CARID' FIELD gv_car.
      SET PARAMETER ID 'CONID' FIELD gv_con.

      LEAVE TO TRANSACTION 'ZTPRACTICE1_CALLED'
      AND SKIP FIRST SCREEN.
    ELSE.
      MESSAGE '해당하는 sflight 정보가 없습니다.' TYPE 'E'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.
```
</br>

**4) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_B03_F01
*&---------------------------------------------------------------------*

FORM insert_data .
  CLEAR gs_data.
  gs_data-carrid = pa_car.
  gs_data-connid = pa_con.
  gs_data-carrname = pa_name.
  gs_data-cityfrom = pa_cityf.
  gs_data-airpfrom = pa_airpf.
  gs_data-cityto = pa_cityt.
  gs_data-airpto = pa_airpt.
  gs_data-ernam = sy-uname.
  gs_data-erdat = sy-datum.
  gs_data-erzet = sy-uzeit.

  INSERT ZTMYCARRIER_B03 FROM gs_data.
  IF sy-subrc = 0.
    PERFORM set_data.
  ELSE.
    MESSAGE 'INSERT 실패' TYPE 'E'.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM update_data .
  CLEAR gs_data.
  SELECT SINGLE * FROM ZTMYCARRIER_B03
    INTO gs_data WHERE carrid = pa_car.
  gs_data-carrid = pa_car.
  gs_data-connid = pa_con.
  gs_data-carrname = pa_name.
  gs_data-cityfrom = pa_cityf.
  gs_data-airpfrom = pa_airpf.
  gs_data-cityto = pa_cityt.
  gs_data-airpto = pa_airpt.
  gs_data-aenam = sy-uname.
  gs_data-aedat = sy-datum.
  gs_data-aezet = sy-uzeit.

  UPDATE ZTMYCARRIER_B03 FROM gs_data.
  IF sy-subrc = 0.
    PERFORM set_data.
  ELSE.
    MESSAGE 'UPDATE 실패' TYPE 'E'.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM delete_data USING lt_select_data TYPE LVC_T_ROW
                       ls_select_data TYPE LVC_S_ROW.
  CLEAR gs_data.
  LOOP AT lt_select_data INTO ls_select_data.
    READ TABLE gt_data INTO gs_data INDEX ls_select_data-index.
    DELETE ZTMYCARRIER_B03 FROM gs_data.
  ENDLOOP.
  IF sy-subrc = 0.
    PERFORM set_data.
  ELSE.
    MESSAGE 'DELETE 실패' TYPE 'E'.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_data .
  SELECT * FROM ZTMYCARRIER_B03
    INTO TABLE gt_data.
  SORT gt_data BY carrid.
ENDFORM.

*&---------------------------------------------------------------------*

FORM refresh_data .
  CALL METHOD go_alv->refresh_table_display
    EXCEPTIONS
      finished       = 1
      others         = 2.
ENDFORM.
```
</br>

**5) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_B03_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_docking IS INITIAL.
    CREATE OBJECT go_docking
      EXPORTING
        side      = cl_gui_docking_container=>dock_at_bottom
        extension = 200
        repid     = sy-repid
        dynnr     = sy-dynnr.

    CREATE OBJECT go_alv EXPORTING i_parent = go_docking.

    gs_layout-sel_mode = 'A'.
    SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv.
    SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.
    SET HANDLER lcl_event_handler=>on_double_click FOR go_alv.

    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'ZTMYCARRIER_B03'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ENDIF.

ENDMODULE.
```
</br>

**6) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_B03_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**7) 결과 확인**

<img width="690" height="272" alt="image" src="https://github.com/user-attachments/assets/4f210c04-e441-4318-aafc-3097d6a80df8" />

</br>
</br>
</br>
</br>
</br>

### 실습 2
</br>

- 문제
- 티코드로 프로그램 이동 실습
<img width="534" height="244" alt="image" src="https://github.com/user-attachments/assets/b3fe975d-1d3a-4a66-af5a-f1ff20ae632b" />
</br>
</br>
</br>

**1) Report program 코드 생성**
```abap
REPORT ZBC400_PRACTICE1_CALLED_B03.

INCLUDE ZBC400_PRACTICE1_CALLED_TOP.
INCLUDE ZBC400_PRACTICE1_CALLED_O01.
INCLUDE ZBC400_PRACTICE1_CALLED_I01.

START-OF-SELECTION.
  CALL SCREEN 100.
```
</br>

**2) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_CALLED_TOP
*&---------------------------------------------------------------------*

DATA: OK_CODE TYPE sy-ucomm.
DATA: ZREADSFLIGHT_B03 TYPE TABLE OF sflight.

PARAMETERS: pa_car TYPE sflight-carrid,
            pa_con TYPE sflight-connid.

GET PARAMETER ID 'CARID' FIELD pa_car.
GET PARAMETER ID 'CONID' FIELD pa_con.

DATA: go_docking TYPE REF TO cl_gui_docking_container,
      go_alv TYPE REF TO cl_gui_alv_grid.
```
</br>

**3) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_CALLED_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_docking IS INITIAL.
    SELECT * FROM sflight
      INTO TABLE ZREADSFLIGHT_B03
      WHERE carrid = pa_car
        AND connid = pa_con.

    CREATE OBJECT go_docking
      EXPORTING
        side      = cl_gui_docking_container=>dock_at_bottom
        extension = 300
        repid     = sy-repid
        dynnr     = sy-dynnr.
    CREATE OBJECT go_alv EXPORTING i_parent = go_docking.

    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        i_structure_name              = 'SFLIGHT'
      CHANGING
        it_outtab                     = ZREADSFLIGHT_B03
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.
    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ENDIF.
ENDMODULE.
```
</br>

**4) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZBC400_PRACTICE1_CALLED_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**5) 결과 확인**

<img width="690" height="281" alt="image" src="https://github.com/user-attachments/assets/9bde7d50-2da6-4698-b117-8cdd08c60e7c" />


</br>
</br>
</br>
</br>

# 01/08
</br>
</br>

## <  핵심 개념 >
- **1) 암묵적 / 명시적 COMMIT / ROLLBACK**
- **2) PERFORM ON COMMIT**
- **3) Update Module (업데이트 모듈)**
- **4) Lock (SE11, SM12)**
- **5) CALL TRANSACTION MODE·UPDATE 및 호출 방식 요약**
- **6) Number Range (TCODE: `SNRO`)**
</br>

- **반복문(LOOP) 안에서 DB 테이블 직접 UPDATE 하면 안 됨**
</br>
</br>

---

</br>

## < ABAP LUW 기반 DB 처리 흐름 정리 >
- V1 Update는 즉시 실행되는 필수 DB 변경이며, V2 Update는 지연 실행되는 부가 작업임
- Lock은 V1 Update가 끝나면 자동 해제되고, V2 Update는 그 이후에 실행되며 Lock과는 무관함
- PERFORM ON COMMIT은 V1 Update가 끝난 뒤 DB COMMIT 직전에 자동 실행됨
</br>

### 1) COMMIT / ROLLBACK 구분 정리
</br>

- `COMMIT WORK` : 현재 LUW에서 수행된 모든 DB 변경사항을 확정 저장
- `ROLLBACK WORK` : 현재 LUW에서 수행된 모든 DB 변경사항을 취소 (COMMIT 이전까지만 가능)
</br>

**1-1) 암묵적 COMMIT**
- 화면이 변경되거나 프로그램이 비정상 종료될 때 자동 COMMIT
- RFC(Remote Function Call) 호출 시 / RFC 종료 시 자동 COMMIT
  
**1-2) 명시적 COMMIT**
- ABAP 구문으로 개발자가 직접 COMMIT 실행 : `COMMIT WORK`
  
**2-1) 암묵적 ROLLBACK**
- ABAP 덤프 발생 시 자동 ROLLBACK
- `MESSAGE TYPE 'A'` 또는 `MESSAGE TYPE 'X'` 발생 시 자동 ROLLBACK
  
**2-2) 명시적 ROLLBACK**
- ABAP 구문으로 개발자가 직접 ROLLBACK 실행 : `ROLLBACK WORK`
</br>


```abap
*************** 암묵적 COMMIT ****************
INSERT ztable FROM gs_data.
CALL SCREEN 200.     " 화면 전환 발생

INSERT ztable FROM gs_data.
LEAVE PROGRAM.      " 프로그램 비정상 종료 

INSERT ztable FROM gs_data.
CALL FUNCTION 'Z_RFC_FUNC'    " RFC 호출 / 종료 
  DESTINATION 'RFC_DEST'.

*************** 명시적 COMMIT ****************
INSERT ztable FROM gs_data.
COMMIT WORK.

*************** 암묵적 ROLLBACK ****************
INSERT ztable FROM gs_data.    " ABAP Dump 발생 → RUNTIME ERROR 발생

INSERT ztable FROM gs_data.
MESSAGE 'Fatal Error' TYPE 'A'.   " MESSAGE TYPE A / X 

*************** 명시적 ROLLBACK ****************
INSERT ztable FROM gs_data.
IF sy-subrc <> 0.
  ROLLBACK WORK.
ENDIF.
```
```abap
REPORT ZPROGB03_0034.

DATA: gt_scustom TYPE TABLE OF ZTSCUSTOM_B03,
      gs_scustom TYPE ZTSCUSTOM_B03.

DELETE FROM ZTSCUSTOM_B03.

SELECT * FROM scustom
  INTO TABLE gt_scustom
  WHERE ID BETWEEN 1 AND 30.

INSERT ZTSCUSTOM_B03 FROM TABLE gt_scustom.

UPDATE ZTSCUSTOM_B03
   SET name = 'First update~~~~~~'
   WHERE ID = 3.

COMMIT WORK. " LUW 종료되면서, 이전에 있던 변경사항이 DB에 반영됨

UPDATE ZTSCUSTOM_B03  " 여기서부터 새로운 LUW 시작
   SET name = 'Second update~~~~~'
   WHERE ID = 2.

UPDATE ZTSCUSTOM_B03
   SET name = 'Third update~~~~~'
   WHERE ID = 1.

ROLLBACK WORK.  " 해당 LUW에 있던 변경사항들 다 취소됨

IF sy-subrc = 0.
  WRITE: '성공'.
  ELSE.
    WRITE: '실패'.
ENDIF.
```
</br>
</br>
</br>

### 2) PERFORM ON COMMIT
- COMMIT WORK가 실행된 뒤 등록된 FORM들을 시스템 테이블에 쌓아두었다가 순서대로 실행
- ON COMMIT으로 묶인 Update Task는 하나라도 실패하면 전부 롤백
```abap
PERFORM x ON COMMIT.
PERFORM y ON COMMIT.
COMMIT WORK.
→ x 실행
→ y 실행
→ DB Commit 완료 (되돌릴 수 없음)
```
```abap
REPORT ZPROGB03_0034.

DATA: gt_scustom TYPE TABLE OF ZTSCUSTOM_B03,
      gs_scustom TYPE ZTSCUSTOM_B03.

DELETE FROM ZTSCUSTOM_B03.
SELECT *
  FROM scustom
  INTO TABLE gt_scustom
  WHERE id BETWEEN 1 AND 30.
INSERT ZTSCUSTOM_B03 FROM TABLE gt_scustom.

PERFORM update_1 ON COMMIT. " 커밋 대기
PERFORM update_2 ON COMMIT.

COMMIT WORK.

*&---------------------------------------------------------------------*

FORM update_1 .
  LOOP AT gt_scustom INTO gs_scustom.
    gs_scustom-name = 'Hone GIldong~~~~22'.
    MODIFY gt_scustom FROM gs_scustom.
  ENDLOOP.

  MODIFY ZTSCUSTOM_B03 FROM TABLE gt_scustom.
  IF sy-subrc = 0.
    WRITE: 'update 1번 성공'.
  ELSE.
    MESSAGE 'update 1번 실패' TYPE 'A'. " 롤백
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM update_2 .
  READ TABLE gt_scustom INTO gs_scustom INDEX 10.
  gs_scustom-name = 'Hong Gildong~~~~~'.
  UPDATE ZTSCUSTOM_B03
     SET name = gs_scustom-name
   WHERE id = gs_scustom-id.

  IF sy-subrc = 0.
    WRITE: 'Update 2번 성공'.
  ELSE.
    MESSAGE 'Update 2번 실패' TYPE 'A'.
  ENDIF.
ENDFORM.
```
</br>
</br>
</br>

### 3) Update Module (업데이트 모듈)
- `CALL FUNCTION … IN UPDATE TASK` 로 등록된 후 COMMIT WORK 시점에 별도 Update Work Process에서 실행되는 DB 전용 Function Module
- IMPORTING 파라미터만 사용하며, DB 변경 중 오류(MESSAGE A/X) 발생 시 해당 Update LUW 전체가 롤백되고 즉시 종료됨
</br>

**1) 비동기 Update**
- `COMMIT WORK` 시 실제 DB 저장은 나중에 `Update Work Process` 가 따로 처리하고, 프로그램은 먼저 종료되는 방식
  
**2) 동기 Update**
- `COMMIT WORK AND WAIT` 를 사용해 Update 작업이 DB에 끝날 때까지 기다린 후 다음 로직(LUW 2)으로 넘어가는 방식
  
**3) Local Update**
- SET UPDATE TASK LOCAL로 Update Task를 같은 워크프로세스에서 즉시 실행해, 별도 Update Work Process 없이 현재 LUW 안에서 DB 반영되는 방식
  
</br>

```abap
******** Dialog Program ********
CALL FUNCTION 'F1' IN UPDATE TASK EXPORTING p1 = a.
CALL FUNCTION 'F2' IN UPDATE TASK EXPORTING q1 = b.
CALL FUNCTION 'F3' IN UPDATE TASK EXPORTING s1 = a.
COMMIT WORK.   " Update Task 실행 시작

******** Update Module ********
FUNCTION F1.
  UPDATE ztable.
     SET field = p1
  IF sy-subrc <> 0.
    MESSAGE 'Error' TYPE 'A'.   " Update LUW 롤백
  ENDIF.
ENDFUNCTION.
```
</br>
</br>

- `SE37` 로 확인 가능
<img width="489" height="270" alt="image" src="https://github.com/user-attachments/assets/cdc3eb43-91f7-4d3b-b09c-f00066d31b74" />
</br>
</br>

- Importing 파라미터만 받음
<img width="486" height="121" alt="image" src="https://github.com/user-attachments/assets/d0192e24-aa8a-40ee-988b-f09a9e39f165" />

</br>
</br>
</br>
</br>
</br>

### 4) Lock (SE11, SM12)
- 다른 사용자가 같은 데이터를 동시에 수정하지 못하게 막는 장치
- Lock Object 이름은 EY 또는 EZ로 시작해야 함
- `SM12` 에서는 현재 설정된 Lock 정보를 확인 가능
- Lock은 전달한 키 값은 정확히 그 값만 잠그고, 키를 안 준 필드는 와일드카드(*)처럼 전부 잠금
</br>

- 기존 Lock과 충돌: 다른 세션이 이미 Lock을 보유한 경우 → 예외 `FOREIGN_LOCK`
- Lock 관리 오류: 시스템 또는 Lock 서버 문제로 Lock 처리 실패 → 예외 `SYSTEM_FAILURE`
</br>


**1) Set lock**
- 특정 데이터(키)에 대해 새로운 Lock 설정

**2️) Promote optimistic lock**
- 기존 약한 Lock을 더 강한 Lock으로 승격

**3️) Check whether lock can be set or promoted**
- 기존 Lock 존재 여부 확인 후 Lock 설정/승격 가능 여부 판단
</br>

- 같은 프로그램에서는 S·O·E Lock은 누적 가능하지만, X Lock이 있으면 추가 Lock은 불가능
- Lock은 너무 일찍 잡거나 오래 유지하면 충돌과 성능 문제를 유발함
  
| Parameter / Mode        | Value        | Meaning |
|-------------------------|-------------|---------|
| mode_<table_name>       | 'S' / 'W'   | **Shared Lock** </br> → 모두 조회 가능, 누구도 수정 불가 </br> → 여러 프로그램에서 추가 Shared Lock 가능 |
|                         | 'O'         | **Optimistic Lock** </br> → 모두 조회/수정 가능 </br> → 실제 DB Update 시에만 나만 수정 가능하도록 E Lock으로 전환 |
|                         | 'E' / 'V'   | **Exclusive Lock (누적)** </br> → 나만 수정 가능, 다른 세션 조회/수정 불가 </br> → 동일 프로그램에서는 추가 Lock 설정 가능 |
|                         | 'X' / 'U'   | **Exclusive Lock (비누적)** </br> → 나만 수정 가능, 다른 세션 조회/수정 불가 </br> → 동일 프로그램에서도 추가 Lock 설정 불가 |
|                         | 'R' / 'C'   | Optimistic(O) Lock을 Exclusive(E) Lock으로 **승격** </br> 또는 승격 가능 여부 체크 |
| <lock_parameter>        | <val>       | Lock Object에 정의된 키 필드 값 </br> (Lock Argument 구성 요소) |
| x_<lock_parameter>      | space       | 해당 Lock Parameter 값 기준으로 Lock 설정 (기본값) |
|                         | 'X'         | 해당 필드 값이 Initial인 모든 레코드에 대해 Lock |
| _scope                  | '1'         | Lock이 현재 프로그램 내에서만 유지 |
|                         | '2'         | Lock을 V1 Update Task로 전달 (기본값) |
|                         | '3'         | 프로그램 + V1 Update Task 모두에서 Lock 유지 |
| _wait                   | space       | 외부 Lock 존재 시 대기 없이 즉시 실패 (기본값) |
|                         | 'X'         | 외부 Lock 존재 시 한 번 더 Lock 시도 |
| _collect                | space       | Lock Container 미사용 </br> 즉시 Lock table에 반영 (기본값) |
|                         | 'X'         | Lock Container 사용 </br> 여러 Lock을 모아두었다가 FLUSH_ENQUEUE로 한 번에 반영 |

</br>

```abap
*************** LOCK 설정 (ENQUEUE) ****************
CALL FUNCTION 'ENQUEUE_ESFLIGHT'
  EXPORTING
    carrid = lv_carrid
    connid = lv_connid
    fldate = lv_fldate
  EXCEPTIONS
    foreign_lock   = 1
    system_failure = 2.

CASE sy-subrc.
  WHEN 0.
    " Lock 설정 성공
  WHEN 1.
    " FOREIGN_LOCK : 다른 세션이 이미 Lock 보유
  WHEN 2.
    " SYSTEM_FAILURE : Lock 서버/시스템 오류
ENDCASE.

*************** LOCK 해제 (DEQUEUE) ****************
CALL FUNCTION 'DEQUEUE_ESFLIGHT'
  EXPORTING
    carrid = lv_carrid
    connid = lv_connid
    fldate = lv_fldate.
```
- 같은 창에서는 이미 Lock을 잡고 있어서 성공하고, 다른 창에서는 해당 Lock이 선점되어 있어 FOREIGN_LOCK으로 실패함
```abap
REPORT ZPROGB03_0034.

*************** LOCK 설정 (ENQUEUE) ****************
CALL FUNCTION 'ENQUEUE_EZ_WORKB03'
 EXPORTING
   MODE_ZDBWORK_B03       = 'E'
   MANDT                  = SY-MANDT
   WORKNO                 = 1
 EXCEPTIONS
   FOREIGN_LOCK           = 1
   SYSTEM_FAILURE         = 2
   OTHERS                 = 3.
IF sy-subrc <> 0.
  WRITE: '실패'.
  ELSE.
    WRITE: '성공'.
ENDIF.

*************** LOCK 해제 (DEQUEUE) ****************
CALL FUNCTION 'DEQUEUE_EZ_WORKB03'
 EXPORTING
   MODE_ZDBWORK_B03       = 'E'
   MANDT                  = SY-MANDT
   WORKNO                 = 1.
* CALL FUNCTION 'DEQUEUE_ALL'. " 모든 락 해제
```
</br>
</br>

- Lock을 걸 대상 테이블(SPFLI)과 어떤 방식으로 잠글지 정하는 화면
- `primary table` : 락을 설정할 테이블
<img width="503" height="220" alt="image" src="https://github.com/user-attachments/assets/d5d730e6-e767-445c-bbaf-8b2248ca9fc7" />
</br>

- 드롭다운 리스트에서 키 값(S, O, E, X 등) 을 함께 표시하도록 하는 옵션
<img width="501" height="261" alt="image" src="https://github.com/user-attachments/assets/056eaeb6-c3e6-4e45-b6ad-f4b03b9a7925" />
</br>
</br>

<img width="484" height="376" alt="image" src="https://github.com/user-attachments/assets/e735aae2-0c76-4f13-875d-69fbc9f1d63a" />
</br>
</br>

- 해당 Lock Object로 자동 생성된 ENQUEUE / DEQUEUE 함수 목록을 확인하는 버튼
<img width="401" height="82" alt="image" src="https://github.com/user-attachments/assets/08595954-e580-4572-8ceb-9b6f861d139d" />
</br>

<img width="375" height="107" alt="image" src="https://github.com/user-attachments/assets/80c173bf-0a89-41ce-bbb6-4760e0c5bdf1" />
</br>
</br>

- `SM12` : 현재 시스템에 설정된 Lock 정보 조회
<img width="467" height="236" alt="image" src="https://github.com/user-attachments/assets/e55af051-0840-4e6f-8561-cabdee10e5b3" />
</br>
</br>
</br>
</br>
</br>

### 5) CALL TRANSACTION MODE·UPDATE 및 호출 방식 요약
- `MODE 'A'` : 모든 화면 표시 (디버깅용)
- `MODE 'N'` : 화면 없이 백그라운드 실행
- `MODE 'E'` : 오류 발생 시에만 화면 표시
- `UPDATE 'S'` : 동기 Update, COMMIT 끝날 때까지 대기
- `UPDATE 'A'` : 비동기 Update, COMMIT 후 바로 진행

| 호출 방식 | Data 공유 | SAP LUW | 언제 사용? |
|----------|-----------|---------|------------|
| CALL FUNCTION<br>CALL METHOD | 분리됨 | 동일 | 같은 LUW 안에서 조회 / 변경 |
| SUBMIT AND RETURN<br>CALL TRANSACTION | 분리됨 | 분리됨 | 다른 LUW에서 조회 / 변경 |
| CALL FUNCTION<br>STARTING NEW TASK | 분리됨 | 분리됨 | 병렬 처리 (Parallel Task) |

```abap
CALL TRANSACTION '<T_CODE>'
  USING <BDC_ITAB>
  MODE   '<A/N/E>'
  UPDATE '<S/A>'.
```
```abap
" 화면 모두 표시
CALL TRANSACTION 'T1'
  USING bdc_itab
  MODE 'A'      " 모든 화면 표시
  UPDATE 'S'.   " 동기 Update (완료까지 대기)

" 화면 없이 실행
CALL TRANSACTION 'T2'
  USING bdc_itab
  MODE 'N'      " 백그라운드 실행
  UPDATE 'A'.   " 비동기 Update (대기 없음)

" 오류 시에만 화면 표시
CALL TRANSACTION 'T3'
  USING bdc_itab
  MODE 'E'      " 오류 발생 시 화면 표시
  UPDATE 'S'.   " 동기 Update
```
</br>
</br>
</br>

---

</br>

## < Number Range (TCODE: `SNRO`) >
- 번호 범위는 `From ~ To` 로 정의되며, 서로 겹칠 수 없음
- Ext 체크 여부로 내부 번호 (시스템 자동 증가) 부여 / 외부 번호 (사용자 입력) 부여를 결정함
- 같은 번호라도 Subobject가 다르면 중복 사용 가능
- 그룹 테이블로 분류값을 지정해, 특정 종류별로 다른 번호 범위를 사용하도록 구성 가능
</br>

- `Number Range Object` 는 이름을 `Z` 또는 `Y` 로 시작해야 함
- `Number Length Domain` 은 필수값이며, 도메인에서 `NUMC` 또는 `CHAR` 타입으로 정의하고 최대 20자리까지 가능
- `% Warning` 은 번호 범위 사용률이 `0.1 ~ 99.9%` 에 도달하면 경고를 주는 기준이며, 보통 10.0으로 설정함
- `No rolling` 은 번호 범위를 다 써도 다시 처음으로 돌아가지 않고 더 이상 번호를 부여하지 않는 설정
- `To business year flag` 는 데이터를 사업연도(연도별)로 구분해서 관리 가능하게 해주는 표시
- `NR Status` 는 현재까지 채번된 번호

<img width="508" height="316" alt="image" src="https://github.com/user-attachments/assets/dbcfea6c-aab7-424f-968d-1c70d07578c4" />
</br>
</br>

```abap
*************** Number Range 정보 조회 ****************
CALL FUNCTION 'NUMBER_GET_INFO'
  EXPORTING
    nr_range_nr = <number_range_number>      " Number Range Interval
    object      = <number_range_object_name> " Number Range Object
    subobject   = <number_range_sub_object>  " Subobject (옵션)
    toyear      = <to_fiscal_year>           " 회계연도 (옵션)
  IMPORTING
    interval    = <interval_info>             " Interval 정보
    NRIV        = <internal_info_table>       " 내부 관리 정보 테이블
  EXCEPTIONS
    interval_not_found = 1
    object_not_found  = 2.

*************** Number Range 다음 번호 채번 ****************
CALL FUNCTION 'NUMBER_GET_NEXT'
  EXPORTING
    nr_range_nr   = <number_range_number>       " Number Range Interval 번호
    object        = <number_range_object_name>  " Number Range Object
    quantity      = <quantity_of_numbers>       " 요청할 번호 개수 (보통 1)
    subobject     = <number_range_sub_object>   " Subobject (옵션)
    toyear        = <to_fiscal_year>             " 회계연도 기준 (옵션)
    ignore_buffer = <ignore_buffer_flag>        " 버퍼 무시 여부
  IMPORTING
    number        = <next_free_number>           " 할당된 다음 번호
    quantity      = <quantity_of_numbers_read>  " 실제 할당된 개수
    returncode    = <return_code>                " 처리 결과 코드
  EXCEPTIONS
    interval_not_found       = 1
    number_range_not_intern  = 2
    object_not_found         = 3
    quantity_is_0            = 4
    quantity_is_not_1        = 5
    interval_overflow        = 6
    buffer_overflow          = 7.

*************** External Number Range 값 검증 ****************
CALL FUNCTION 'NUMBER_CHECK'
  EXPORTING
    nr_range_nr   = <number_range_number>        " Number Range Interval 번호
    number        = <number_to_be_checked>       " 사용자가 입력한 번호
    object        = <number_range_object_name>   " Number Range Object
    subobject     = <number_range_sub_object>    " Subobject (옵션)
    toyear        = <to_fiscal_year>              " 회계연도 기준 (옵션)
    numeric_check = <numeric_check_flag>         " 숫자 여부 체크
  IMPORTING
    returncode    = <return_code>                 " space: 사용 가능 / 'X': 사용 불가
  EXCEPTIONS
    interval_not_found      = 1
    number_range_not_extern = 2
    object_not_found        = 3.
```
</br>
</br>

- 번호 범위를 정해두고 순서대로 번호를 발급하는 과정
<img width="492" height="282" alt="image" src="https://github.com/user-attachments/assets/83103505-1afd-4666-9bf2-7d1bde67045e" />
</br>

<img width="364" height="99" alt="image" src="https://github.com/user-attachments/assets/c187b9e3-1763-4e75-a57e-b5fe3cb53210" />
</br>

<img width="526" height="86" alt="image" src="https://github.com/user-attachments/assets/bcd30086-2172-4876-a8c3-4262f4f100e7" />
</br>
</br>

```abap
REPORT ZPROGB03_0035.

DATA: lv_number TYPE NUMC10.

*************** Number Range 다음 번호 채번 ****************
CALL FUNCTION 'NUMBER_GET_NEXT'
  EXPORTING
    nr_range_nr = 'A1'
    object      = 'ZNUM_B03'
  IMPORTING
    NUMBER      = lv_number
  EXCEPTIONS
    INTERVAL_NOT_FOUND            = 1
    NUMBER_RANGE_NOT_INTERN       = 2
    OBJECT_NOT_FOUND              = 3
    QUANTITY_IS_0                 = 4
    QUANTITY_IS_NOT_1             = 5
    INTERVAL_OVERFLOW             = 6
    BUFFER_OVERFLOW               = 7
    OTHERS      = 8.

IF sy-subrc = 0.
  WRITE: lv_number.
ELSE.
  EXIT.
ENDIF.
```
</br>

- 현재까지 사용된 마지막 번호 확인
<img width="528" height="87" alt="image" src="https://github.com/user-attachments/assets/ee38de26-11bd-4415-9da0-856c7684d883" />
</br>
</br>

- NR Status에서 값을 직접 바꿔서 마지막 사용 번호를 수정
<img width="380" height="111" alt="image" src="https://github.com/user-attachments/assets/c2e42be0-a083-4666-9d3b-050bd211b684" />
</br>

<img width="523" height="90" alt="image" src="https://github.com/user-attachments/assets/c813b8e0-5523-4bf6-9346-99aad5b773d4" />
</br>
</br>

- 수정된 번호 결과 확인
<img width="528" height="88" alt="image" src="https://github.com/user-attachments/assets/0910bf1b-eecd-49b6-b57e-b85958e6d321" />
</br>
</br>
</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 문제
- Number Range 실습
</br>

<img width="426" height="235" alt="image" src="https://github.com/user-attachments/assets/a055f4dd-2cc4-4f10-82aa-5d200ce1446f" />
</br>
</br>
</br>

**1) Number Range Object 생성**

<img width="446" height="271" alt="image" src="https://github.com/user-attachments/assets/75d8940f-3d14-4b1d-b942-88a5a586445f" />
</br>
</br>

- NR Status를 29로 설정 -> 30부터 시작이므로
<img width="538" height="87" alt="image" src="https://github.com/user-attachments/assets/9c8e782c-7492-4843-a054-9fb29bce36b5" />
</br>
</br>
</br>

**2) Program 코드 생성**
```abap
REPORT ZBC400_B03_PRACTICE9.

DATA: gv_num TYPE S_BUSPANUM,
      gv_cnt TYPE I VALUE 31,
      gt_data TYPE TABLE OF ZTSCUSTOM_B03.
PARAMETERS: pa_num TYPE I.

DO pa_num TIMES.
  PERFORM number_get_next CHANGING gv_num gv_cnt gt_data.
ENDDO.

INSERT ZTSCUSTOM_B03 FROM TABLE gt_data.

*&---------------------------------------------------------------------*

FORM number_get_next CHANGING gv_num TYPE S_BUSPANUM
                              gv_cnt TYPE I
                              gt_data LIKE gt_data.
  DATA: gs_data TYPE ZTSCUSTOM_B03.

  CALL FUNCTION 'NUMBER_GET_NEXT'
    EXPORTING
      nr_range_nr = '01'
      object      = 'ZNO_B03'
    IMPORTING
      NUMBER      = gv_num
    EXCEPTIONS
      INTERVAL_NOT_FOUND            = 1
      NUMBER_RANGE_NOT_INTERN       = 2
      OBJECT_NOT_FOUND              = 3
      QUANTITY_IS_0                 = 4
      QUANTITY_IS_NOT_1             = 5
      INTERVAL_OVERFLOW             = 6
      BUFFER_OVERFLOW               = 7
      OTHERS      = 8.
  gs_data-id = gv_cnt.
  gs_data-NUM_RANGE = gv_num.
  APPEND gs_data TO gt_data.
  gv_cnt = gv_cnt + 1.

  IF sy-subrc = 0.
    WRITE:/ gv_num.
  ELSE.
    EXIT.
  ENDIF.

ENDFORM.
```
</br>

**3) 결과 확인**
- 20 입력
  
<img width="236" height="273" alt="image" src="https://github.com/user-attachments/assets/51995506-1e91-4bb1-a640-b0126e842e69" />

</br>

<img width="413" height="280" alt="image" src="https://github.com/user-attachments/assets/318d16a4-d30f-4044-a04a-cb31591d0e4e" />
</br>

<img width="486" height="82" alt="image" src="https://github.com/user-attachments/assets/83b8badc-30ce-4078-b88c-9fec0c1c4d63" />

</br>
</br>
</br>
</br>
</br>
</br>

# 01/09
</br>
</br>

## <  핵심 개념 >
- **1) `CALL METHOD go_alv->check_changed_data.` : ALV에 바인딩된 it_outtab 내부테이블의 변경사항을 반영함**
</br>
</br>

---

</br>

## < ALV 셀 단위 편집(Edit) 제어 구현 >
</br>

### 1) ALV 셀 단위 Edit 제어
- 필드카탈로그를 이용해 해당 컬럼을 ALV에서 수정 가능하도록 설정하는 UI 플래그
```abap
gs_fcat-edit = 'X'.
```
- 선택한 ALV 행의 INVOICE 컬럼만 수정 가능하도록, 버튼(EDIT) 클릭 시 셀 단위로 Edit 모드를 켜는 로직
- `cl_gui_alv_grid=>mc_style_enabled` : ALV에서 특정 셀만 수정 가능하게 만드는 셀 스타일 플래그
- `set_ready_for_input` : ALV 그리드를 수정 모드로 켜고/끄는 스위치
```abap
REPORT ZPROGB03_0029.

*************** TOP ****************
DATA: gs_layout TYPE LVC_S_LAYO.
DATA: BEGIN OF gs_data.
        INCLUDE TYPE sbook.
DATA: it_cell_style TYPE LVC_T_STYL,
     END OF gs_data.
DATA gs_cell_style TYPE LVC_S_STYL.

*************** PAI ****************
MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'EDIT'.
      PERFORM set_edit_rows.
  ENDCASE.
ENDMODULE.

*************** F01 ****************
FORM set_edit_rows.
  DATA: lt_index_rows TYPE lvc_t_row,
        ls_index_row TYPE lvc_s_row.

  CALL METHOD go_alv->get_selected_rows
    IMPORTING
      et_index_rows = lt_index_rows.

  IF lt_index_rows IS INITIAL.
    MESSAGE '변경할 데이터를 선택하세요' TYPE 'I'.
    EXIT.
  ENDIF.

  " 기존에 변경모드로 설정되어있던 셀들 초기화
  LOOP AT gt_data INTO gs_data WHERE it_cell_style IS NOT INITIAL.
    DELETE gs_data-it_cell_style WHERE fieldname = 'INVOICE'.
    MODIFY gt_data FROM gs_data TRANSPORTING it_cell_style.
  ENDLOOP.

  " 선택한 Row 기준으로 변경모드 세팅
  LOOP AT lt_index_rows INTO ls_index_row.
    CLEAR: gs_data, gs_cell_style.
    READ TABLE gt_data INTO gs_data INDEX ls_index_row-index.
    IF sy-subrc = 0.
      gs_cell_style-fieldname = 'INVOICE'.
      gs_cell_style-style = cl_gui_alv_grid=>mc_style_enabled.
      INSERT gs_cell_style INTO TABLE gs_data-it_cell_style.
      MODIFY gt_data FROM gs_data INDEX ls_index_row-index
      TRANSPORTING it_cell_style.
    ENDIF.
  ENDLOOP.

  " Edit 활성화
  CALL METHOD go_alv->set_ready_for_input
    EXPORTING i_ready_for_input = 1.

  " ALV Refresh
  CALL METHOD go_alv->refresh_table_display.
ENDFORM.

FORM set_layout.
  gs_layout-stylefname = 'IT_CELL_STYLE'.
ENDFORM.

*************** PBO ****************
CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        is_layout = gs_layout
      CHANGING
        it_outtab = gt_data.
```
</br>

- 결과: 특정 열 수정 가능
  
<img width="502" height="297" alt="image" src="https://github.com/user-attachments/assets/2a4f9024-0f91-4619-9935-11df9ed4d17b" />
</br>
</br>
</br>
</br>

---

</br>

## < 팀과제 - 실습 코드 >
- 프로그램 코드 : `ZDSBOOK_CL222_01`
- `CALL METHOD go_alv->check_changed_data.` : ALV에 바인딩된 it_outtab 내부테이블의 변경사항을 반영함
</br>
</br>

**1) Report 프로그램 생성**
```abap
REPORT ZDSBOOK_CL222.

INCLUDE ZDSBOOK_CL222_TOP.
INCLUDE ZDSBOOK_CL222_C01.
INCLUDE ZDSBOOK_CL222_F01.
INCLUDE ZDSBOOK_CL222_O01.
INCLUDE ZDSBOOK_CL222_I01.

START-OF-SELECTION.
  PERFORM set_data.
  CALL SCREEN 100.
```
</br>

**2) Screen 100 코드에 서브스크린 생성**
```abap
PROCESS BEFORE OUTPUT.
 MODULE STATUS_0100.
 CALL SUBSCREEN SUB INCLUDING sy-repid '110'.
 MODULE init_alv.
 MODULE CLEAR_OK_CODE.

PROCESS AFTER INPUT.
 MODULE exit AT EXIT-COMMAND.
 CALL SUBSCREEN SUB.
 MODULE USER_COMMAND_0100.
```
</br>

**3) `TOP` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZDSBOOK_CL222_01_TOP
*&---------------------------------------------------------------------*

DATA: go_alv TYPE REF TO cl_gui_alv_grid,
      go_dock TYPE REF TO cl_gui_docking_container.

DATA: BEGIN OF gs_sbook.
        INCLUDE TYPE ZTBOOK_CL222.
DATA: it_cell_style TYPE LVC_T_STYL,
      update_cell TYPE icon-id,
     END OF gs_sbook.
DATA: gt_sbook LIKE TABLE OF gs_sbook.
DATA: gt_data TYPE TABLE OF ZTBOOK_CL222,
      gs_data LIKE LINE OF gt_data.

DATA gs_cell_style TYPE LVC_S_STYL.
DATA save_flag TYPE I.

DATA: OK_CODE TYPE sy-ucomm.
DATA: gs_layout TYPE LVC_S_LAYO.
DATA: gt_fcat TYPE LVC_T_FCAT,
      gs_fcat TYPE LVC_S_FCAT.

SELECTION-SCREEN BEGIN OF SCREEN 110 AS SUBSCREEN.
  SELECT-OPTIONS: pa_car FOR gs_sbook-carrid DEFAULT 'AA',
                  pa_con FOR gs_sbook-connid DEFAULT '17',
                  pa_fld FOR gs_sbook-fldate DEFAULT '20250508'.
SELECTION-SCREEN END OF SCREEN 110.
```
</br>

**3) `C01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZDSBOOK_CL222_01_C01
*&---------------------------------------------------------------------*

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
  CLASS-METHODS: on_toolbar FOR EVENT toolbar
    OF cl_gui_alv_grid IMPORTING e_object,
      on_user_command FOR EVENT user_command
    OF cl_gui_alv_grid IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'UPDATE'.
    ls_button-icon = ICON_EXPAND.
    ls_button-butn_type = 0.
    ls_button-text = 'UPDATE'.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD on_user_command.
    CASE e_ucomm.
      WHEN 'UPDATE'.
        DATA: lt_select_data TYPE LVC_T_ROW,
              ls_select_data TYPE LVC_S_ROW.

        CALL METHOD go_alv->get_selected_rows
          IMPORTING
            et_index_rows = lt_select_data.

        READ TABLE gt_sbook TRANSPORTING NO FIELDS
          WITH KEY update_cell = ICON_CHANGE.
        IF sy-subrc = 0.
          MESSAGE '이미 수정중인 셀이 있습니다.' TYPE 'I'.
        ELSE.
          IF lt_select_data IS INITIAL.
            MESSAGE '변경할 데이터를 선택하세요' TYPE 'I'.
          ELSE.
            PERFORM update_data USING lt_select_data ls_select_data.
          ENDIF.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.
```
</br>

**4) `F01` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZDSBOOK_CL222_01_F01
*&---------------------------------------------------------------------*

FORM set_data .
* 초기 설정
*  SELECT * from sbook
*    INTO CORRESPONDING FIELDS OF TABLE gt_data
*    WHERE cancelled = 'X'.
*
*  SORT gt_data BY carrid connid fldate.
*  INSERT ztbook_cl222 FROM TABLE gt_data
*    ACCEPTING DUPLICATE KEYS.

  SELECT * from ztbook_cl222
    INTO CORRESPONDING FIELDS OF TABLE gt_sbook
    WHERE carrid IN pa_car
      AND connid IN pa_con
      AND fldate IN pa_fld.
ENDFORM.

*&---------------------------------------------------------------------*

FORM update_data USING lt_select_data TYPE LVC_T_ROW
                       ls_select_data TYPE LVC_S_ROW.
  READ TABLE gt_sbook TRANSPORTING NO FIELDS
       WITH KEY update_cell = ICON_CHANGE.
  IF sy-subrc = 0.
    MESSAGE '이미 수정중인 셀이 있습니다.' TYPE 'I'.
  ELSE.
    save_flag = 1.
    LOOP AT gt_sbook INTO gs_sbook WHERE it_cell_style IS NOT INITIAL.
      DELETE gs_sbook-it_cell_style WHERE fieldname = 'REFUND'.
      MODIFY gt_sbook FROM gs_sbook TRANSPORTING it_cell_style.
    ENDLOOP.

    LOOP AT lt_select_data INTO ls_select_data.
      CLEAR: gs_sbook-it_cell_style, gs_cell_style.
      READ TABLE gt_sbook INTO gs_sbook INDEX ls_select_data-index.
      IF sy-subrc = 0.
        gs_cell_style-fieldname = 'REFUND'.
        gs_cell_style-style = cl_gui_alv_grid=>mc_style_enabled.
        INSERT gs_cell_style INTO TABLE gs_sbook-it_cell_style.

        gs_sbook-update_cell = ICON_CHANGE.

        MODIFY gt_sbook FROM gs_sbook INDEX ls_select_data-index
        TRANSPORTING it_cell_style update_cell.
      ENDIF.
    ENDLOOP.

    CALL METHOD go_alv->set_ready_for_input
      EXPORTING i_ready_for_input = 1.
    CALL METHOD go_alv->refresh_table_display.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_layout .
  gs_layout-sel_mode = 'A'.
  gs_layout-stylefname = 'IT_CELL_STYLE'.
ENDFORM.

*&---------------------------------------------------------------------*

FORM set_fieldcat .
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'REFUND'.
  gs_fcat-coltext = '환불여부'.
  gs_fcat-checkbox = 'X'.
  gs_fcat-col_pos = 2.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'UPDATE_CELL'.
  gs_fcat-coltext = '수정중'.
  gs_fcat-icon = 'X'.
  gs_fcat-col_pos = 1.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

*&---------------------------------------------------------------------*

FORM select_data .
  SELECT * from ztbook_cl222
    INTO CORRESPONDING FIELDS OF TABLE gt_sbook
    WHERE carrid IN pa_car
      AND connid IN pa_con
      AND fldate IN pa_fld.
  SORT gt_sbook BY carrid connid fldate.
ENDFORM.

*&---------------------------------------------------------------------*

FORM save_data .
  CALL METHOD go_alv->check_changed_data.

  CLEAR gt_data.
  LOOP AT gt_sbook INTO gs_sbook.
    CLEAR gs_data.
    MOVE-CORRESPONDING gs_sbook TO gs_data.
    APPEND gs_data TO gt_data.
  ENDLOOP.

  UPDATE ztbook_cl222 FROM TABLE gt_data.

  LOOP AT gt_sbook INTO gs_sbook WHERE update_cell = ICON_CHANGE.
    CLEAR gs_sbook-update_cell.
    DELETE gs_sbook-it_cell_style WHERE fieldname = 'REFUND'.

    MODIFY gt_sbook FROM gs_sbook INDEX sy-tabix.
  ENDLOOP.

  CALL METHOD go_alv->refresh_table_display.
  IF sy-subrc = 0.
    MESSAGE '저장되었습니다.' TYPE 'I'.
  ENDIF.
ENDFORM.
```
</br>

**5) `PBO` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZDSBOOK_CL222_01_O01
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
 SET PF-STATUS 'S100'.
 SET TITLEBAR 'T100'.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE init_alv OUTPUT.
  IF go_dock IS INITIAL.
    CREATE OBJECT go_dock
      EXPORTING
        side = cl_gui_docking_container=>dock_at_bottom
        extension = 400
        repid = sy-repid
        dynnr = sy-dynnr.
    CREATE OBJECT go_alv
      EXPORTING
        i_parent = go_dock.

    PERFORM set_layout.
    PERFORM set_fieldcat.
    SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv.
    SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        is_layout                     = gs_layout
        i_structure_name              = 'ZTBOOK_CL222'
      CHANGING
        it_outtab                     = gt_sbook
        it_fieldcatalog               = gt_fcat
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        others                        = 4.

    IF sy-subrc <> 0.
      MESSAGE '데이터 조회 중 오류 발생' TYPE 'E'.
    ENDIF.
  ELSE.
    CALL METHOD go_alv->refresh_table_display
      EXCEPTIONS
        finished       = 1
        others         = 2.
  ENDIF.
ENDMODULE.

*&---------------------------------------------------------------------*

MODULE clear_ok_code OUTPUT.
  CLEAR OK_CODE.
ENDMODULE.
```
</br>

**6) `PAI` 모듈 생성**
```abap
*&---------------------------------------------------------------------*
*& Include          ZDSBOOK_CL222_01_I01
*&---------------------------------------------------------------------*

MODULE user_command_0100 INPUT.
  CASE OK_CODE.
    WHEN 'ENTER' OR ''.
      PERFORM select_data.
    WHEN 'SAVE'.
      PERFORM save_data.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

*----------------------------------------------------------------------*

MODULE exit INPUT.
  CASE OK_CODE.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**7) 결과 확인**

<img width="512" height="309" alt="image" src="https://github.com/user-attachments/assets/6797cf68-25f3-4baf-a039-5f92b72884aa" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>
</br>

# 01/13
</br>
</br>

## <  핵심 개념 >
- **1) `PUBLIC SECTION`**
- **2) `PRIVATE SECTION`**
</br>

- **정적(Class) 메서드는 `클래스=>` , 인스턴스(Object) 메서드는 `객체->` 로 접근함**
- **Static Method(CLASS-METHODS) 안에서는 Static 컴포넌트(CLASS-DATA)만 직접 접근 가능**
- **캡슐화된 인스턴스 어트리뷰트 = `PRIVATE SECTION` 에 선언해 외부 직접 접근을 막은 객체 전용 변수**
- **CTS 생성 - `SE09`**
</br>
</br>

---

</br>

## < ABAP 클래스 정의 기본 구조 >
</br>

**1) PUBLIC SECTION (외부 접근 가능)**
- `DATA` : 인스턴스 속성 (객체마다 생성)
- `CLASS-DATA` : 정적 속성 (클래스 1개 공유)
- `METHODS` : 인스턴스 메서드
- `CLASS-METHODS` : 정적 메서드

**2) PRIVATE SECTION (외부 접근 불가)**
- `DATA / CLASS-DATA` : 내부 전용 속성
- `METHODS / CLASS-METHODS` : 내부 전용 메서드
</br>
</br>

### 1) ABAP 클래스 구조와 호출 방식 정리
- 정적 메서드는 클래스 기준, 인스턴스 메서드는 객체 기준으로 호출
```abap
클래스명=>CLASS-METHOD( ).     " 가능 (정적 메서드)
클래스명=>METHOD( ).           " ❌ 오류 (인스턴스 메서드)

참조변수->METHOD( ).           " 가능 (인스턴스 메서드)
참조변수->CLASS-METHOD( ).     " 가능 (정적 메서드, 문법상 허용)
```
- `DATA` 는 객체마다 따로 생기는 변수, `CLASS-DATA` 는 클래스 전체가 하나를 공유하는 변수
- `METHODS` 는 객체(인스턴스) 생성 후 호출, `CLASS-METHODS` 는 객체 없이 클래스명으로 바로 호출
- Static Method(`CLASS-METHODS`) 안에서는 Static 컴포넌트(`CLASS-DATA`)만 직접 접근 가능
- Public 속성은 외부 접근 가능, Private 속성은 내부 전용이며 READ-ONLY는 외부 수정만 제한
```abap
*************** 클래스 정의 (CLASS DEFINITION) ****************
CLASS class_name DEFINITION.
  PUBLIC SECTION.
    " 정적 속성 (Static Attribute)
    " 클래스당 1개만 존재
    CLASS-DATA: gv_n_o_vehicles TYPE i.       " 전체 객체가 공유
  PRIVATE SECTION.
    " 인스턴스 속성 (Instance Attribute)
    " 객체 생성 시마다 개별로 생성
    DATA: variable5 TYPE string READ-ONLY.    " 읽기 전용 - 외부: 조회 가능, 수정 불가
                                                          " 클래스 내부: 둘 다 가능
ENDCLASS.
```
```abap
*************** 메서드 정의 (METHODS Syntax) ****************
CLASS class_name DEFINITION.
  PUBLIC SECTION.
    METHODS method_name
      IMPORTING         iv_par TYPE type_name    " 입력 파라미터
      EXPORTING         ev_par TYPE type_name    " 출력 파라미터
      CHANGING          cv_par TYPE type_name    " 입·출력 파라미터
      RETURNING VALUE(rv_par)  TYPE type_name    " 반환값 (메서드 실행 결과를 단일 값으로 반환)
      EXCEPTIONS  exception.                  " Classic Exception
      RAISING   cx_exception_class.           " Class-based Exception
    CLASS-METHODS static_method.               " 정적 메서드
ENDCLASS.

*************** 메서드 구현 (METHOD IMPLEMENTATION) ****************
CLASS class_name IMPLEMENTATION.
  METHOD method_name.
    " 인스턴스 메서드 로직
  ENDMETHOD.
  METHOD static_method.
    " 정적 메서드 로직
  ENDMETHOD.
ENDCLASS.
```
- 정적 메서드는 객체 생성 없이 호출 가능
```abap
*************** 클래스 정의 ****************
CLASS <class> DEFINITION.
  PUBLIC SECTION.
    METHODS <inst_meth>.
    CLASS-METHODS <static_meth>.
  PRIVATE SECTION.
    DATA mv_attr TYPE <type>.
    CLASS-DATA gv_attr TYPE i.
ENDCLASS.

*************** 클래스 구현 ****************
CLASS <class> IMPLEMENTATION.
  METHOD <inst_meth>. ENDMETHOD.
  METHOD <static_meth>. ENDMETHOD.
ENDCLASS.

*************** 객체 생성 및 호출 ****************
DATA go_obj TYPE REF TO <class>.
CREATE OBJECT go_obj.
CALL METHOD go_obj-><inst_meth>.
CALL METHOD <class>=><static_meth>.
```
```abap
REPORT ZPROGB03_0036.

*************** 클래스 정의 ****************
CLASS lcl_vehicle DEFINITION.
  PUBLIC SECTION.  " 외부에서 접근이 가능
   METHODS: set_attribute IMPORTING iv_make TYPE s_make
                                    iv_mode TYPE s_model,
            display_attributes.
   CLASS-METHODS: display_global_vehicles.
  PRIVATE SECTION. " 외부에서 접근이 불가능
   DATA: mv_make TYPE s_make,
         mv_model TYPE s_model.
   CLASS-DATA: gv_vehicles TYPE I.
ENDCLASS.

*************** 클래스 구현 ****************
CLASS lcl_vehicle IMPLEMENTATION.
  METHOD set_attribute.  " instance Method
    mv_make = iv_make.
    mv_model = iv_mode.
    gv_vehicles = gv_vehicles + 1.
  ENDMETHOD.

  METHOD display_attributes. " instance Method
    WRITE:/ 'Make: ', mv_make,
          / 'Model: ', mv_model.
  ENDMETHOD.

  METHOD display_global_vehicles.  " Static Method
    WRITE:/ 'Number of vehicle: ', gv_vehicles.
  ENDMETHOD.
ENDCLASS.
```
</br>
</br>

### 2) ABAP 객체 참조 정리
- 참조 대입(=)은 객체를 복사하지 않고 같은 객체를 함께 가리킴
- `go_vehicle1` , `go_vehicle2` → 동일 객체, `go_vehicle3` → 별도 객체
- 같은 객체를 참조하므로 `go_vehicle1` 으로 변경한 내용은 `go_vehicle2` 에서도 그대로 보임
```abap
*************** 객체 참조 (Reference) ****************
DATA: go_vehicle1 TYPE REF TO lcl_vehicle,
      go_vehicle2 LIKE go_vehicle1,
      go_vehicle3 LIKE go_vehicle1.

CREATE OBJECT go_vehicle1.
go_vehicle2 = go_vehicle1.    " 같은 객체를 참조 (참조 복사)
CREATE OBJECT go_vehicle3.    " 새로운 객체 생성

" go_vehicle1, go_vehicle2 → 동일 객체
" go_vehicle3             → 별도 객체
```
</br>
</br>

### 3) ABAP 객체 참조 테이블 (TABLE OF REF) 사용 패턴
- `TABLE OF REF TO` 는 객체 자체를 테이블에 저장하는 것이 아니라, 객체를 가리키는 참조(주소) 를 저장함
- 실제 데이터는 객체 안에 있고, 테이블에는 그 객체로 가는 포인터만 존재
```abap
*************** 객체 참조 테이블 (Multiple Instantiation) ****************
DATA:
  go_obj   TYPE REF TO <class>,                 " 단일 객체 참조
  gt_objs  TYPE TABLE OF REF TO <class>.        " 객체 참조를 담는 내부테이블

CREATE OBJECT go_obj.
APPEND go_obj TO gt_objs.    " 첫 번째 객체 참조 저장

CREATE OBJECT go_obj.
APPEND go_obj TO gt_objs.    " 두 번째 객체 참조 저장

*************** 객체 참조 테이블 접근 ****************
gt_objs[ 1 ]-><inst_meth>( ).    " 1번째 객체의 인스턴스 메서드 호출
gt_objs[ 2 ]-><static_meth>( ).  " 정적 메서드 호출 (클래스 공용)
```
```abap
REPORT ZPROGB03_0036.
START-OF-SELECTION.

*************** 객체 참조 변수 선언 ****************
 DATA: go_vehicle TYPE REF TO lcl_vehicle.
* DATA: go_vehicle2 TYPE REF TO lcl_vehicle.
 DATA: gt_vehicle TYPE TABLE OF REF TO lcl_vehicle.

*************** 객체 생성 및 설정 ****************
 CREATE OBJECT go_vehicle.
 CALL METHOD go_vehicle->set_attribute
  EXPORTING iv_make = 'MK1'
            iv_mode = 'MODEL1'.
 APPEND go_vehicle TO gt_vehicle.

 CREATE OBJECT go_vehicle.
 CALL METHOD go_vehicle->set_attribute
  EXPORTING iv_make = 'MK2'
            iv_mode = 'MODEL2'.
 APPEND go_vehicle TO gt_vehicle.

*************** 객체 참조 테이블을 통한 메서드 호출 ****************
* CALL METHOD go_vehicle1->display_attributes.
* CALL METHOD go_vehicle2->display_attributes.
* CALL METHOD go_vehicle1->display_global_vehicles.
 gt_vehicle[ 1 ]->display_attributes( ).
 gt_vehicle[ 2 ]->display_attributes( ).
 gt_vehicle[ 2 ]->display_global_vehicles( ).
```
</br>
</br>

### 4) ABAP 최신 메서드 호출 및 Static/Instance 메서드 호출 정리
- 최신 메서드 호출은 `객체->메서드( )` 형식을 사용
```abap
*************** 메서드 호출 (Method Call) ****************
DATA go_obj TYPE REF TO <class>.

CREATE OBJECT go_obj.
<ref_obj>-><method_name>(
  EXPORTING
    <iv_par1> = <value1>
    <iv_par2> = <value2>
).

*************** 정적 메서드 호출 (Static Method Call) ****************
<class_name>=><static_method>( ).
```
- 정적(Class) 메서드는 `클래스=>` , 인스턴스(Object) 메서드는 `객체->` 로 접근함
```abap
REPORT ZPROGB03_0036.

*************** 클래스 정의 ****************
CLASS lcl_vehicle DEFINITION.
  PUBLIC SECTION.                 " 외부 접근 가능
    DATA: gv_data(2) TYPE c VALUE 'OK'.        " 인스턴스 속성
    CLASS-DATA: gv_cdata(2) TYPE c VALUE 'HI'. " 정적 속성
ENDCLASS.

START-OF-SELECTION.

*************** 객체 참조 변수 선언 ****************
DATA: go_vehicle TYPE REF TO lcl_vehicle.
DATA: gt_vehicle TYPE TABLE OF REF TO lcl_vehicle.

*************** 객체 생성 및 설정 ****************
CREATE OBJECT go_vehicle.
go_vehicle->set_attribute(
  EXPORTING
    iv_make = 'MK1'
    iv_mode = 'MODEL1'
).
APPEND go_vehicle TO gt_vehicle.

CREATE OBJECT go_vehicle.
go_vehicle->set_attribute(
  EXPORTING
    iv_make = 'MK2'
    iv_mode = 'MODEL2'
).
APPEND go_vehicle TO gt_vehicle.

*************** 정적 메서드 호출 ****************
lcl_vehicle=>display_global_vehicles( ).   " 정석 호출
* gt_vehicle[ 1 ]->display_global_vehicles( ). " 문법상 가능 (비권장)

*************** 데이터 접근 방식 비교 ****************
WRITE: / gt_vehicle[ 1 ]->gv_cdata,     " 객체 통해 정적 데이터 접근
       / lcl_vehicle=>gv_cdata,         " 클래스 통해 정적 데이터 접근 (정석)
       / go_vehicle->gv_data,           " 인스턴스 데이터
       / go_vehicle->gv_cdata.          " 정적 데이터 (객체 통해 접근 가능)
```
</br>
</br>

### 5) Functional Method (함수형 메서드)
- RETURNING 파라미터를 정확히 1개만 가지는 메서드 (함수처럼 동작)
- 대입문, 조건문, 계산식 안에서 바로 호출 가능
```abap
*************** Functional Method 정의 ****************
CLASS <class_name> DEFINITION.
  PUBLIC SECTION.
    METHODS <func_method>
      IMPORTING
        iv_par TYPE <type>
      RETURNING
        VALUE(rv_result) TYPE <return_type>.
ENDCLASS.

*************** 인스턴스 함수형 메서드 ****************
result = <ref_obj>-><func_method>(
  iv_par_1 = val_1
  iv_par_n = val_n
).

<ref_obj>-><func_method>(
  EXPORTING
    iv_par_1 = <value1>
    iv_par_n = <valueN>
  RECEIVING
    rv_result = <result>
).

*************** 정적 함수형 메서드 ****************
result = <class_name>=><func_method>(
  iv_par_1 = val_1
  iv_par_n = val_n
).

<class_name>=><func_method>(
  EXPORTING
    iv_par_1 = <value1>
    iv_par_n = <valueN>
  RECEIVING
    rv_result = <result>
).
```
```abap
REPORT ZPROGB03_0036.

*************** Static Functional Method 정의 ****************
CLASS lcl_vehicle DEFINITION.
  PUBLIC SECTION.  " 외부에서 접근이 가능
   CLASS-METHODS: add_vehicles IMPORTING lv_num TYPE I
                               RETURNING VALUE(rv_veh) TYPE I.
ENDCLASS.

*************** 정적 함수형 메서드 ****************
START-OF-SELECTION.
 DATA: gv_vehicle TYPE i. " 리턴값 받을 변수

 gv_vehicle = lcl_vehicle=>add_vehicles( EXPORTING lv_num = 100 ).
* lcl_vehicle=>add_vehicles( EXPORTING lv_num = 100            위랑 같은 코드
*                            RECEIVING rv_veh = gv_vehicle ).
 WRITE:/ gv_vehicle.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- local class 만들기 실습
```abap
REPORT ZBC401_03_MAIN.
TYPE-POOLS icon.

CLASS lcl_airplane DEFINITION.
  PUBLIC SECTION.
    METHODS: set_attributes IMPORTING iv_name TYPE string
                                      iv_planetype TYPE saplane-planetype,
             display_attributes.
    CLASS-METHODS: display_gv_n_o_airplanes.
  PRIVATE SECTION.
    CONSTANTS: c_pos_l TYPE I VALUE 30.
    DATA: mv_name TYPE string,
          mv_planetype TYPE saplane-planetype.
    CLASS-DATA: gv_n_o_airplanes TYPE I.
ENDCLASS.

CLASS lcl_airplane IMPLEMENTATION.
  METHOD set_attributes.
    mv_name = iv_name.
    mv_planetype = iv_planetype.
    gv_n_o_airplanes = gv_n_o_airplanes + 1.
  ENDMETHOD.

  METHOD display_attributes.
    WRITE:/ icon_ws_plane AS ICON,
          / 'Name: ', AT c_pos_l mv_name,
          / 'Planetype: ', AT c_pos_l mv_planetype.
  ENDMETHOD.

  METHOD display_gv_n_o_airplanes.
    WRITE:/ 'Number of airplane: ', gv_n_o_airplanes.
  ENDMETHOD.
ENDCLASS.

START-OF-SELECTION.
  DATA: go_airplane TYPE REF TO lcl_airplane,
        gt_airplane TYPE TABLE OF REF TO lcl_airplane.

  CREATE OBJECT go_airplane.
  APPEND go_airplane TO gt_airplane.
```
</br>
</br>

### 실습 2
</br>

- 문제
- 캡슐화된 인스턴스 어트리뷰트 = `PRIVATE SECTION` 에 선언해 외부 직접 접근을 막은 객체 전용 변수
</br>

<img width="379" height="196" alt="image" src="https://github.com/user-attachments/assets/82d06abd-b1ed-47cf-8641-827addf4b214" />

</br>
</br>

```abap
REPORT ZBC401_03_MAIN.

TYPE-POOLS icon.

CLASS lcl_airplane DEFINITION.
  PUBLIC SECTION.
    METHODS: set_attributes IMPORTING iv_name TYPE string
                                      iv_planetype TYPE saplane-planetype,
             display_attributes.
    CLASS-METHODS: display_gv_n_o_airplanes,
                   get_n_o_airpalnes RETURNING VALUE(rv_result) TYPE I.
  PRIVATE SECTION.
    CONSTANTS: c_pos_l TYPE I VALUE 30.
    DATA: mv_name TYPE string,
          mv_planetype TYPE saplane-planetype.
    CLASS-DATA: gv_n_o_airplanes TYPE I.
ENDCLASS.

CLASS lcl_airplane IMPLEMENTATION.
  METHOD set_attributes.
    mv_name = iv_name.
    mv_planetype = iv_planetype.
    gv_n_o_airplanes = gv_n_o_airplanes + 1.
  ENDMETHOD.

  METHOD display_attributes.
    WRITE:/ icon_ws_plane AS ICON,
          / 'Name: ', AT c_pos_l mv_name,
          / 'Planetype: ', AT c_pos_l mv_planetype.
  ENDMETHOD.

  METHOD display_gv_n_o_airplanes.
    WRITE:/ 'Number of airplane: ', gv_n_o_airplanes.
  ENDMETHOD.

  METHOD get_n_o_airpalnes.
    rv_result = gv_n_o_airplanes.
  ENDMETHOD.
ENDCLASS.

START-OF-SELECTION.
  DATA: go_airplane TYPE REF TO lcl_airplane,
        gt_airplane TYPE TABLE OF REF TO lcl_airplane,
        gv_cnt TYPE I.

  lcl_airplane=>display_gv_n_o_airplanes( ).
  CREATE OBJECT go_airplane.
  APPEND go_airplane TO gt_airplane.
  go_airplane->set_attributes(
    EXPORTING iv_name = '이름1'
              iv_planetype = '747-400'
  ).

  CREATE OBJECT go_airplane.
  APPEND go_airplane TO gt_airplane.
  go_airplane->set_attributes(
    EXPORTING iv_name = '이름2'
              iv_planetype = '146-200'
  ).

  CREATE OBJECT go_airplane.
  APPEND go_airplane TO gt_airplane.
  go_airplane->set_attributes(
    EXPORTING iv_name = '이름3'
              iv_planetype = '777-300'
  ).
  gt_airplane[ 1 ]->display_gv_n_o_airplanes( ).

  LOOP AT gt_airplane INTO go_airplane.
    go_airplane->display_attributes( ).
  ENDLOOP.
  lcl_airplane=>get_n_o_airpalnes( RECEIVING rv_result = gv_cnt ).
  WRITE:/ 'Number of airplane: ', gv_cnt.
```
</br>

- 결과 확인
<img width="288" height="256" alt="image" src="https://github.com/user-attachments/assets/6bb72263-072a-45b6-ac5d-a586f8f9d355" />
