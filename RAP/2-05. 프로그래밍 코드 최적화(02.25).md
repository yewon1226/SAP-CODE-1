## <  핵심 개념 >
- **1) CDS에서 산술 연산(계산 필드) 가능**
- **2) CDS에서 Literal(고정값) 사용 가능**
- **3) CDS에서 CAST로 타입 변환 가능**
- **4) 나누기 연산 시 CAST가 필요한 이유**
- **5) CDS CASE 구문 정리 (Simple vs Complex 분기 구조)**
- **6) CDS → OData → Gateway Client 흐름**
</br>
</br>

---

</br>

## < CDS Association 복습 정리 >
- <data source>는 다른 CDS View 또는 DB 테이블 모두 가능함
- `{ }` 안에 `<alias>` 를 노출하면 → OData에서 EntitySet의 확장 대상으로 사용 가능
```abap
define view ZCDS_EXAMPLE as select from <source>
  association [*] to <data source> as <alias>
    on <조건>
{
    <alias>
}
```
- Association이 있으면 필드 충돌 방지를 위해 데이터소스.필드명으로 명확히 적는 게 안전함
```abap
define view entity ZI_SPFLI as select from SPFLI
  association [*] to SCARR as _Scarr
    on SPFLI.carrid = _Scarr.carrid
{
  key SPFLI.carrid as Carr_Id,
      connid
}
```
</br>
</br>

---

</br>

## < ABAP SQL 내장 함수 정리 >
- 내장함수의 파라미터가 궁금하면 해당 함수 키워드에서 F1 Help로 확인
</br>

#### < Numeric Functions (숫자 계산 관련 함수) >
- DIVISION은 나눗셈 후 반올림, ROUND는 계산 없이 기존 값만 반올림
- CDS에서는 계산이나 함수 결과값을 별도 변수 없이 바로 필드로 정의할 수 있음
  
| 함수 | 의미 | 사용 예시 | 예시 의미 |
|------|------|------------|------------|
| `DIV()` | 정수 나눗셈 (몫 반환) | `DIV( field1, field2 )` | field1 ÷ field2의 정수 몫 (소수점 버림) |
| `MOD()` | 나머지 반환 | `MOD( field1, field2 )` | field1 ÷ field2의 나머지 |
| `DIVISION()` | 나눗셈 후 반올림하여 소수점까지 표시 | `DIVISION( field1, field2, 2 )` | field1 ÷ field2 결과를 소수점 2자리까지 반올림 |
| `ROUND()` | 지정한 소수점 위치에서 반올림 | `ROUND( field, 2 )` | field 값을 소수점 2자리에서 반올림 |

</br>

#### < String Processing (문자열 처리 함수) >

| 함수 | 의미 | 사용 예시 | 예시 의미 |
|------|------|------------|------------|
| `CONCAT()` | 문자열 연결 | `CONCAT( field1, field2 )` | field1과 field2를 이어붙임 |
| `UPPER()` | 대문자 변환 | `UPPER( field )` | field 값을 모두 대문자로 변환 |
| `LOWER()` | 소문자 변환 | `LOWER( field )` | field 값을 모두 소문자로 변환 |
| `SUBSTRING()` | 문자열 일부 추출 | `SUBSTRING( field, 1, 3 )` | field의 1번째 위치부터 3글자 추출 |

</br>

#### < Date and Time Processing (날짜·시간 처리 함수) >

| 함수 | 의미 | 사용 예시 | 예시 의미 |
|------|------|------------|------------|
| `IS_VALID()` | 날짜 형식 유효성 확인 | `IS_VALID( field )` | field가 올바른 날짜 형식인지 확인 |
| `ADD_DAYS()` | 날짜에 일수 추가 | `ADD_DAYS( field, 10 )` | field 날짜에 10일 추가 |
| `DAYS_BETWEEN()` | 두 날짜 사이 일수 계산 | `DAYS_BETWEEN( date1, date2 )` | date1과 date2 사이 일수 계산 |
| `WEEKDAY()` | 요일 반환 | `WEEKDAY( field )` | field 날짜의 요일 반환 |
| `EXTRACT_MONTH()` | 월 추출 | `EXTRACT_MONTH( field )` | field 날짜에서 월만 추출 |

</br>
</br>
</br>

### 1) Numeric Functions Example (숫자 함수 사용 예제)
#### 1) ABAP SQL 코드
```abap
SELECT FROM /dmo/flight
  FIELDS
    div( seats_occupied * 100, seats_max ) AS percentage_int,    " 정수 나눗셈 (몫만 반환)
    division( seats_occupied * 100, seats_max, 2 ) AS percentage_dec.    " 나눗셈 후 소수점 2자리까지 반올림
```
#### 2) CDS 코드
```abap
define view entity ZI_FLIGHT as select from /dmo/flight
{
  div( seats_occupied * 100, seats_max )        as percentage_int,
  division( seats_occupied * 100, seats_max, 2 ) as percentage_dec
}
```
</br>
</br>
</br>

### 2) CDS 문자열 연결 예제 (CONCAT / CONCAT_WITH_SPACE)
- concat_with_space는 공백 개수를 지정해 문자열을 연결하며, 함수 안에 다시 넣어 중첩 구성 가능
  
#### 1) ABAP SQL 코드
```abap
SELECT FROM /dmo/customer
  FIELDS
    " && 연산자를 사용한 문자열 연결 (CDS에서는 사용 불가)
    street && ', ' && postal_code && ' ' && city AS address_expr,

    " 함수 기반 문자열 연결
    concat(
      street,                                   " street 먼저 연결
      concat_with_space(
        ',',                                    " 쉼표 추가
        concat_with_space(
          postal_code,                          " 우편번호
          upper( city ),                        " 도시를 대문자로 변환
          1                                     " 우편번호와 도시 사이 공백 1개
        ),
        1                                       " 쉼표와 우편번호 사이 공백 1개
      )
    ) AS address_func.
```

#### 2) CDS 코드
```abap
define view entity ZI_CUSTOMER as select from /dmo/customer
{
  concat(
    street,                                   // street 먼저 연결
    concat_with_space(
      ',',                                    // 쉼표 추가
      concat_with_space(
        postal_code,                          // 우편번호
        upper( city ),                        // 도시를 대문자로 변환
        1                                     // 우편번호와 도시 사이 공백 1개
      ),
      1                                       // 쉼표와 우편번호 사이 공백 1개
    )
  ) as address                                // 최종 주소 필드
}
```
</br>
</br>
</br>

### 3) Date Processing Example (날짜 처리 함수 예제)
#### 1) ABAP SQL 코드
```abap
SELECT FROM /dmo/travel
  FIELDS
    is_valid( begin_date )                     AS valid,        " 날짜 형식 유효성 확인
    add_days( begin_date, 7 )                  AS add_7_days,   " 시작일 + 7일
    days_between( begin_date, end_date )       AS duration,     " 두 날짜 사이 일수 계산
    extract_month( begin_date )                AS month,        " 월 추출
    weekday( begin_date )                      AS weekday.      " 요일 반환
```
#### 2) CDS 코드
- `dats_` 계열 함수는 DATS 타입 전용이므로, 문자열 날짜는 먼저 CAST가 필요함
- `dats_add_days` 의 세 번째 파라미터는 오류 처리 옵션이며, 'FAIL'은 덤프 발생 가능, 'NULL'은 NULL 반환, 'INITIAL'은 초기값 반환
```abap
define view entity ZI_TRAVEL as select from /dmo/travel
{
  dats_is_valid( begin_date )                         as valid,       // CDS에서는 dats_ prefix 사용
  dats_add_days( begin_date, 7, 'FAIL' )             as add_7_days,  // 7일 추가, 오류 시 FAIL 처리
  dats_days_between( begin_date, end_date )          as duration,    // 날짜 차이 계산
  substring( begin_date, 5, 2 )                      as month        // 5번째 위치부터 2자리 → 월 추출
}
```
</br>
</br>
</br>

### 4) CURRENCY CONVERTION
- 금액을 다른 통화로 환율 적용해서 변환하는 함수
- `exchange_rate_date` 는 어떤 날짜의 환율을 적용할지 지정하는 필드
  
#### 1) ABAP SQL 코드
```abap
SELECT FROM /dmo/travel
  FIELDS
    currency_conversion(
      amount             = total_price,     " 변환할 금액
      source_currency    = currency_code,   " 기존 통화
      target_currency    = 'EUR',           " 목표 통화
      exchange_rate_date = begin_date       " 환율 기준 날짜
    ) AS total_price_EUR.
```

#### 2) CDS 코드
- CDS에서는 파라미터 전달 시 `=` 가 아니라 `=>` 를 사용해야 함
- CDS는 타입에 엄격하므로 통화코드(CUKY)는 `CAST()` 로 맞춰줘야 함
```abap
define view entity ZI_TRAVEL as select from /dmo/travel
{
  currency_conversion(
    amount             => total_price,                      // 변환할 금액
    source_currency    => currency_code,                    // 기존 통화
    target_currency    => cast( 'EUR' as abap.cuky ),       // CDS는 타입 엄격 → CAST 필요
    exchange_rate_date => begin_date                        // 환율 적용 기준 날짜
  ) as total_price_EUR
}
```
</br>
</br>
</br>

### 5) Timestamp Conversion (타임스탬프 날짜 변환)
- 타임존을 고려해 Timestamp(날짜+시간)를 날짜(DATS)만으로 변환하는 함수
- ABAP은 필수값만 넘기고 client, on_error는 생략 가능하지만, CDS는 모두 작성해야 함
- 'FAIL', 'NULL', 'INITIAL'은 오류 발생 시 각각 덤프 발생, NULL 반환, 초기값 반환으로 처리 방식을 지정하는 옵션
- `tstmp_to_dats()` 의 타임존 파라미터는 CHAR(6) 타입

#### 1) ABAP SQL 코드
```abap
tstmp_to_dats(
  tstmp     = <timestamp>,     " 날짜+시간이 들어있는 Timestamp 값 (DEC 15,0)
  tzone     = <timezone>,      " 변환 기준 타임존 (예: 'UTC', 'EST')
  client    = <client>,        " 클라이언트 번호 (시스템 구분값, 선택)
  on_error  = <error_option>   " 오류 처리 방식 ('FAIL' / 'NULL' / 'INITIAL', 선택)
)
```
```abap
SELECT FROM /dmo/travel
  FIELDS
    tstmp_to_dats(
      tstmp = cast( lastchangedat as dec(15,0) ),   " 날짜+시간(Timestamp) 값
      tzone = cast( 'EST' as char(6) )              " 변환 기준 타임존 (미국 동부시간)
      " client, on_error 는 선택 파라미터
    ) AS date_est.                                  " 날짜(DATS)만 반환
```

#### 2) CDS 코드
```abap
tstmp_to_dats(
  <timestamp>,      // Timestamp 값 (날짜+시간, DEC 15,0)
  <timezone>,       // 타임존 (CHAR 6, 예: 'UTC')
  <client>,         // 클라이언트 번호 (CLNT 타입)
  <error_option>    // 오류 처리 옵션 ('FAIL' / 'NULL' / 'INITIAL')
)
```
```abap
define view entity ZI_TRAVEL as select from /dmo/travel
{
  tstmp_to_dats(
    cast( lastchangedat as abap.dec(15,0) ),   // Timestamp 값 (날짜+시간)
    cast( 'EST' as abap.char(6) ),             // 타임존 지정
    cast( '001' as abap.clnt ),                // 클라이언트 (CDS는 필수)
    'FAIL'                                     // 오류 처리 옵션
  ) as date_est                                // 변환된 날짜
}
```
</br>
</br>
</br>

### 6) CDS Session Variable
- `$session` 은 ABAP의 `sy-` 구조처럼 현재 로그인 세션 정보를 가져오는 변수
- CDS에서도 SELECT 영역과 WHERE 절에서 모두 사용 가능
```abap
define view entity ZS4D430_Session_Variables as select from /DMO/I_Flight
{
  _Currency._Text[ 1: Language = $session.system_language ].CurrencyName as CurrencyName    // 현재 로그인 사용자의 언어 기준으로 통화 텍스트 조회
}
where FlightDate > $session.system_date    // 오늘 날짜 이후의 항공편만 조회
```
