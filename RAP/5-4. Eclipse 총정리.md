# 02/19
</br>

## <  핵심 개념 >
- **1) ABAP Dictionary 및 CDS 객체 구조**
- **2) SAP UI5 테마 커스터마이징 (TCODE - FIORI LAUNCHPAD : `/n/ui2/flp`), TCODE - 테마 : `/n/ui5/theme_designer`)**
- **3) Eclipse에서 ABAP 패키지 생성 절차**
- **4) Eclipse에서 Domain 생성 절차**
- **5) Eclipse에서 Data Element 생성 절차**
- **6) Eclipse에서 Database Table 생성 절차**
- **7) Eclipse에서 Structure 생성 절차**
- **8) Eclipse에서 ABAP 클래스 복사 절차**
- **9) 테이블 구조 변경 오류**
- **10) `Ctrl + Shift + F` : Pretty Print**
</br>
</br>

---

</br>

## < ABAP Dictionary 및 CDS 객체 구조 >
</br>

### < ABAP Dictionary 객체 유형 정리표 >

| Dictionary Object Type        | DB Object | Data Type | Remark                                  |
|------------------------------|-----------|-----------|------------------------------------------|
| Domain                       | no        | no        | 데이터 요소에서만 사용됨                |
| Data Element                 | no        | yes       |                                          |
| Structure                    | no        | yes       |                                          |
| Table Type                   | no        | yes       |                                          |
| Database Table Definition    | yes       | yes       | 데이터 타입으로 사용하는 것은 권장되지 않음 |
| Database View                | yes       | yes       | 구식 방식, 대신 CDS View 사용 권장      |

</br>

### < ABAP CDS 주요 개발 객체 정리 >

| 개발 객체 (Development Object) | 목적 | 설명 (이해 쉽게 정리) |
|---------------------------------|------|------------------------|
| CDS 데이터 정의 (CDS Data Definition - DDL) | CDS 엔티티 정의 | CDS View Entity 등 데이터 모델을 정의하는 객체<br>→ DB 테이블을 직접 사용하는 것이 아니라, CDS 기반의 논리적 뷰를 정의함<br><br>CDS 엔티티 종류:<br>- CDS View Entity (최신 표준)<br>- CDS DDIC 기반 View (구방식, obsolete)<br>- CDS Projection View<br>- CDS Abstract Entity<br>- CDS Table Function<br>- CDS Custom Entity<br>- CDS Hierarchy |
| CDS 접근 제어 (CDS Access Control) | CDS 역할(Role) 정의 | 사용자 또는 역할별 데이터 조회 권한을 정의함<br>→ 특정 조건에 따라 데이터 읽기(Read) 접근을 제어 |
| CDS 메타데이터 확장 (CDS Metadata Extension) | CDS 엔티티에 메타데이터 추가 | CDS 정의와 분리하여 UI 관련 정보(애노테이션 등)를 저장<br>→ 화면 설정을 CDS 본문과 분리 가능 |
| CDS 행위 정의 (CDS Behavior Definition) | 비즈니스 객체의 동작 정의 | 생성(Create), 수정(Update), 삭제(Delete) 등 트랜잭션 동작 정의<br>→ RAP 모델에서 사용되는 핵심 구성 요소 |

</br>
</br>
</br>

---

</br>

## < Eclipse(ADT) 기반 ABAP Cloud 데이터 구조 설계 >
</br>

### < ABAP 사전(Dictionary) 미리 정의된 타입 >

| 구분 | 용도 | 사전 타입 (Predefined Dictionary Type) | 대응 ABAP 기본 타입 |
|------|------|------------------------------------------|-----------------------|
| 일반 | 문자형 데이터 | CHAR(n) | C LENGTH n |
| 일반 | 4바이트 정수 | INT4 | I |
| 일반 | 고정 소수점 숫자 (Binary Coded Decimals) | DEC(n,d) | P LENGTH n DECIMALS d |
| 문자형 (특수 의미) | 숫자 나열 (앞자리 0 유지) | NUMC(n) | N LENGTH n |
| 통화/수량 | 금액 필드 (통화 키 필요) | CURR(n,d) | P LENGTH n DECIMALS d |
| 통화/수량 | 수량 필드 (단위 필요) | QUAN(n,d) | P LENGTH n DECIMALS d |
| 통화/수량 | 통화 코드 필드 | CUKY | C LENGTH 5 |
| 통화/수량 | 단위 필드 | UNIT(n) | C LENGTH n (n=2 또는 3) |
| 날짜/시간 | 날짜 (YYYYMMDD 형식) | DATS | D |
| 날짜/시간 | 시간 (HHMMSS 형식) | TIMS | T |
</br>

### < DEC(P 타입) 길이 설정 >
- P 타입 = 2자리당 1바이트 + 마지막 1자리는 부호
- 계산식: (자릿수 + 1) ÷ 2 = 바이트 수
  
  - 길이 15 : (15+1)/2 = 8바이트 → 정확히 15자리 저장
  - 길이 16 : (16+1)/2 = 8.5 → 9바이트 할당 → 17자리 저장 가능
  - 짝수는 메모리 낭비 발생 → 홀수 권장


</br>
</br>


### < ABAP Cloud 테이블 정의 문법 및 주요 개념 >
- `@` 로 시작하는 Annotation으로 테이블 속성을 지정하고, `define table` 안에서 기본키와 필드 타입을 설계함.
- `Quick Fix` : 정의되지 않은 타입으로 오류가 발생했을 때 `Ctrl + 1` 또는 전구 아이콘을 통해 Data Element를 자동 생성 가능
- Primary Key는 key 키워드로 지정하며, 최소 1개 이상 최대 16개까지 설정 가능. 모든 키 필드는 테이블 맨 앞에 연속으로 위치해야 함.
- `NOT NULL` 은 해당 필드를 필수값으로 지정하는 옵션이며, 기본키에는 반드시 적용되고 일반 필드에는 선택적으로 설정 가능
```abap
@EndUserText.label : 'Connection'                 " @Annotation - 테이블 설명
@AbapCatalog.tableCategory : #TRANSPARENT         " 투명 테이블 설정
@AbapCatalog.deliveryClass : #A                   " 어플리케이션 데이터
@AbapCatalog.dataMaintenance : #RESTRICTED        " 유지보수 제한
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE   " 확장 불가 설정
@AbapCatalog.compiler.compareFilter : true            " 필터 비교 최적화
@AccessControl.authorizationCheck : #NOT_REQUIRED     " CDS 권한 체크 미적용

define table /dmo/connection {                    " 실제 DB 테이블 생성

  key client        : abap.clnt not null;         " 기본키 (표준 타입 사용)
  key carrier_id    : /dmo/carrier_id not null;   " 기본키 (Data Element 참조)
  key connection_id : /dmo/connection_id not null;" 기본키 (Data Element 참조)

  airport_from_id   : /dmo/airport_from_id;       " 출발 공항
  airport_to_id     : /dmo/airport_to_id;         " 도착 공항
  departure_time    : /dmo/flight_departure_time; " 출발 시간
  arrival_time      : /dmo/flight_arrival_time;   " 도착 시간
  distance          : /dmo/flight_distance;       " 거리
  distance_unit     : msehi;                      " 단위 (표준 Data Element)

}
```
</br>

<img width="521" height="238" alt="image" src="https://github.com/user-attachments/assets/6bba0ae2-4ebe-4979-92f6-1924300f74d4" />
</br>
</br>
</br>
</br>

### < 통화/수량 관련 타입 & Annotation 정리 >
- CURR/QUAN 필드에 통화·단위가 없으면 오류가 발생하며, Ctrl+1 Quick Fix로 자동 생성 및 Annotation 연결 가능

- **CURR / CUKY**

  - 금액 필드(CURR)는 반드시 통화 코드 필드(CUKY)와 함께 사용
  - CDS에서 `@Semantics.amount.currencyCode` 로 통화 필드를 연결
- **QUAN / UNIT**

  - 수량 필드(QUAN)는 반드시 단위 필드(UNIT)와 함께 사용
  - CDS에서 `@Semantics.quantity.unitOfMeasure` 로 단위 필드를 연결
  
</br>
</br>
</br>
</br>

---

</br>

## < Eclipse에서 ABAP 패키지 생성 절차 >
</br>

#### 1) New → ABAP Package 선택하여 새로운 ABAP 패키지 생성

<img width="592" height="295" alt="image" src="https://github.com/user-attachments/assets/5f1e1d7b-4301-49a3-9afe-797bcd997faf" />
</br>
</br>

#### 2) Z 패키지명(ZS4D430B03)으로 신규 ABAP 패키지를 생성

<img width="592" height="402" alt="image" src="https://github.com/user-attachments/assets/505a7350-99c1-4c85-a6a4-dc4ce482b1f5" />
</br>
</br>

#### 3) 패키지에 속할 Software Component(HOME) 선택

<img width="591" height="202" alt="image" src="https://github.com/user-attachments/assets/7b9316e3-25b9-49c6-a997-438a8123c44b" />
</br>
</br>

#### 4) Development 패키지로 설정 후 CA / ZEFX 지정

<img width="594" height="222" alt="image" src="https://github.com/user-attachments/assets/33fa1796-11f4-454a-bf62-85b2691f617d" />
</br>
</br>

#### 5) 신규 CTS 생성 후 패키지 생성
- CTS TCODE: `SE09`

<img width="570" height="512" alt="image" src="https://github.com/user-attachments/assets/f77cedaf-0ae1-4642-93d1-52e4c566a622" />
</br>
</br>

#### 6) Favorite Packages에 패키지 추가 가능

<img width="571" height="256" alt="image" src="https://github.com/user-attachments/assets/41adbab5-b80f-4fe0-a821-8f9d1145e163" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 Domain 생성 절차 >
</br>

#### 1) New → Other ABAP Repository Object 선택하여 새로운 ABAP 개발 객체를 생성

<img width="764" height="145" alt="image" src="https://github.com/user-attachments/assets/d6c67084-85d0-44e7-b213-62e06f149da4" />
</br>
</br>

#### 2) Dictionary → Domain 선택하여 도메인 객체를 생성
<img width="523" height="234" alt="image" src="https://github.com/user-attachments/assets/25343d54-8d37-4425-8489-4e498f424024" />
</br>
</br>

#### 3) 신규 Domain(ZDOPRICE_B03)을 생성하기 위해 프로젝트와 패키지를 지정
<img width="582" height="350" alt="image" src="https://github.com/user-attachments/assets/672f08c0-2538-466f-a146-50c8320d87c4" />
</br>
</br>

#### 4) Domain 생성 시 사용할 Transport Request(CTS) 선택
<img width="580" height="345" alt="image" src="https://github.com/user-attachments/assets/1910b78f-0094-47c3-b962-7be40b8f4470" />
</br>
</br>

#### 5) Domain(ZDOPRICE_B03)에서 데이터 타입(CURR), 길이(15), 소수점(2)을 설정
- `Case Sensitive` : 입력한 대소문자를 그대로 유지하여 화면에 출력
- `Sign` : 음수 값 입력이 가능하도록 허용

<img width="605" height="283" alt="image" src="https://github.com/user-attachments/assets/5b303fec-8d02-4e1c-b302-d71eb75afe2a" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 Data Element 생성 절차 >
</br>

#### 1) Dictionary → Data Element 객체 선택

<img width="509" height="263" alt="image" src="https://github.com/user-attachments/assets/ec904b30-caf2-4e08-b56d-10a4397b234c" />
</br>
</br>

#### 2) Data Element 기본 정보 입력
<img width="536" height="325" alt="image" src="https://github.com/user-attachments/assets/6c819ebc-80f0-4fb2-9509-d3bd73ed1fca" />
</br>
</br>

#### 3) Data Element에 Domain 연결 설정
<img width="746" height="274" alt="image" src="https://github.com/user-attachments/assets/6f0ce5bb-304c-4375-8c48-3efbc2c79c8c" />
</br>
</br>

#### 4) Domain 데이터 타입 및 길이 설정
<img width="799" height="127" alt="image" src="https://github.com/user-attachments/assets/013a14bd-741c-43a8-870d-f7753761da8d" />
</br>
</br>

#### 5) Data Element 타입 및 필드 라벨 설정

<img width="814" height="358" alt="image" src="https://github.com/user-attachments/assets/7e1e0dd6-b930-4b14-bbe2-fb87bca8e4f1" />
</br>
</br>

#### 6) Data Element 속성(Properties) 확인

<img width="922" height="195" alt="image" src="https://github.com/user-attachments/assets/9f1bb11d-d71a-4f9d-847b-c3be9b5391c8" />
</br>
</br>

#### 7) ABAP 개발 툴바 아이콘 기능
- 문법 검사(Syntax Check) – 코드 오류 여부 확인
- 실행(Run) – SAP GUI의 F8과 동일한 실행 기능 (`Ctrl + shift + A`)
- 활성화(Activate) – 수정한 객체를 Active 상태로 반영
<img width="513" height="68" alt="image" src="https://github.com/user-attachments/assets/4130ad23-fee6-4e92-8fb6-9fc710f0cf40" />
</br>
</br>

- Open With → SAP GUI를 통해 테이블을 SAP GUI 화면으로 열기 (`Ctrl + 6`)
  
<img width="698" height="237" alt="image" src="https://github.com/user-attachments/assets/0ec2bd6e-74fc-4d86-894a-90529ac7c8f6" />
</br>
</br>

- SQL Console을 통해 해당 테이블에 직접 SQL문을 실행 가능

<img width="606" height="221" alt="image" src="https://github.com/user-attachments/assets/f30961a2-a091-4f1c-98a8-38743091b9c6" />

</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 Database Table 생성 절차 >
</br>

#### 1) clipse(ADT)에서 새로운 ABAP Repository Object 생성

<img width="633" height="159" alt="image" src="https://github.com/user-attachments/assets/bba4b9ec-e94d-4bdf-8fec-3ad075b384ff" />

</br>
</br>


#### 2) ABAP Repository Object에서 Database Table 생성 선택

<img width="471" height="366" alt="image" src="https://github.com/user-attachments/assets/54f88c10-da8d-455b-8b68-b8ac9c8cd42e" />
</br>
</br>

#### 3) Eclipse(ADT)에서 신규 Database Table 생성 기본 정보 입력

<img width="583" height="330" alt="image" src="https://github.com/user-attachments/assets/dd414fad-f671-45df-adf6-884395e8b803" />
</br>
</br>

#### 4) ABAP DDL 문법으로 Eclipse(ADT)에서 DB 테이블을 정의
```abap
" ZTSCARR_C203

@EndUserText.label : 'Scarr'                     " 테이블 설명(라벨)
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE " 확장 불가 설정
@AbapCatalog.tableCategory : #TRANSPARENT        " 투명(Transparent) 테이블
@AbapCatalog.deliveryClass : #A                  " 어플리케이션 데이터
@AbapCatalog.dataMaintenance : #ALLOWED          " 데이터 유지보수 허용

define table ztscarr_c203 {                      " DB 테이블 생성

  key client : abap.clnt not null;               
  key carrid : s_carr_id not null;              
  carrname   : s_carrname;                       
  timestamp  : include zc203_timestamp;

}
```
</br>

- 테이블 위에 커서 올리고 `F2` 누르면 안의 필드들 확인 가능
<img width="459" height="149" alt="image" src="https://github.com/user-attachments/assets/7db9570e-9e8c-464b-959e-c4744a3c1ba4" />

</br>
</br>

#### 5) Eclipse(ADT)에서 테이블 Data Preview 실행
- 마우스 우클릭
  
<img width="569" height="218" alt="image" src="https://github.com/user-attachments/assets/27577e56-619f-4b7b-99ea-e95a2ac3b0d9" />
</br>
</br>

#### 6) Eclipse(ADT)에서 테이블 Technical Settings 접근
- 마우스 우클릭 또는 F8
  
<img width="595" height="205" alt="image" src="https://github.com/user-attachments/assets/8621b7c5-c2e3-4450-a68e-acc45f5ead70" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 Structure 생성 절차 >
</br>

#### 1) Eclipse(ADT)에서 신규 Structure 생성 기본 정보 입력

<img width="566" height="334" alt="image" src="https://github.com/user-attachments/assets/244e4490-ce66-4cdc-a20b-8c40f67072a8" />
</br>
</br>

#### 2) zc203_timestamp 스트럭처 구성
```abap
" zc203_timestamp

@EndUserText.label : 'TIMESTAMP'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure zc203_timestamp {

  created_by       : abp_creation_user;
  created_at       : abp_creation_tstmpl;
  local_changed_by : abp_locinst_lastchange_user;
  local_changed_at : abp_locinst_lastchange_tstmpl;
  last_changed_at  : abp_lastchange_tstmpl;

}
```
</br>
</br>

#### 3) zsinfoflight_c203 스트럭처 구성
```abap
" zsinfoflight_c203

@EndUserText.label : 'flight info'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure zsinfoflight_c203 {

  fldate    : s_date;
  @Semantics.amount.currencyCode : 'zsinfoflight_c203.currency'
  price     : s_price;
  currency  : s_currcode;
  planetype : s_planetye;
  seatsmax  : s_seatsmax;
  seatsocc  : s_seatsocc;

}
```
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 ABAP 클래스 복사 절차 >
</br>

#### 1) Show In → Project Explorer를 통해 객체 위치 확인

<img width="609" height="289" alt="image" src="https://github.com/user-attachments/assets/6e049df5-a959-43fa-87a9-aa04e06666ff" />

</br>
</br>

#### 2) Duplicate 메뉴를 이용해 객체 복사

<img width="441" height="551" alt="image" src="https://github.com/user-attachments/assets/e90df841-c86c-45d5-92cf-bd3193665d68" />
</br>
</br>

#### 3) 패키지와 신규 클래스명을 지정하여 복사 클래스 생성

<img width="592" height="330" alt="image" src="https://github.com/user-attachments/assets/ec28bec3-326c-43b9-99cc-b560c1b8dcd7" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < 테이블 구조 변경 오류 >

</br>

#### 1) 필드 구조를 변경해서 DB 테이블을 다시 변환(Conversion)해야 하는 상태

<img width="495" height="246" alt="image" src="https://github.com/user-attachments/assets/27057c4b-ab3f-4c20-a003-4da6b2e6b3af" />
</br>
</br>

#### 2) Adjust and activate database table, convert data → 테이블 구조를 변경하고 기존 데이터는 유지한 채 DB 테이블을 변환·재활성화하는 옵션

<img width="664" height="171" alt="image" src="https://github.com/user-attachments/assets/3beac783-4582-4b7b-80f5-3d5bd3f6cebf" />
</br>
</br>

#### 3) Quick Fix 화면에서 해당 도메인을 사용 중인 테이블/객체들을 확인하고 함께 활성화할 수 있음
<img width="752" height="478" alt="image" src="https://github.com/user-attachments/assets/1f80ea35-117b-4754-8c72-60e5a3501dc1" />

</br>
</br>
</br>
</br>
</br>
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
- 타일에 SAP GUI 프로그램 추가
- 다른 절차 다 동일하고, Tcode 생성 후 transaction 선택하는 부분만 다름

<img width="796" height="246" alt="image" src="https://github.com/user-attachments/assets/bc14454c-f350-4fcf-86f0-fdc96606530b" />

<img width="790" height="204" alt="image" src="https://github.com/user-attachments/assets/649d407d-9f5f-4942-a753-364f154ea43a" />

<img width="785" height="383" alt="image" src="https://github.com/user-attachments/assets/15e5d686-ad80-4140-b075-e7a2c6a5643f" />
</br>
</br>
</br>
</br>
</br>

### 실습 2
- domain, element 생성 실습
  
<img width="614" height="283" alt="image" src="https://github.com/user-attachments/assets/f00295a0-c18f-4e16-bfc6-385ec41d765b" />
</br>
</br>

<img width="614" height="317" alt="image" src="https://github.com/user-attachments/assets/19b91819-8dfa-4529-be71-c941354ac4ad" />
</br>
</br>

<img width="603" height="366" alt="image" src="https://github.com/user-attachments/assets/d144b4c0-01cb-4eab-8c43-f18f75da9ae4" />
</br>
</br>

<img width="621" height="328" alt="image" src="https://github.com/user-attachments/assets/376b8524-23bb-4cb0-b165-c41e47d99816" />
</br>
</br>
</br>
</br>
</br>

### 실습 3
- 디비테이블 생성 실습 ( 테이블 안에 스트럭처 구조 넣기 )
```abap
" Z03EMPLOY

@EndUserText.label : 'Z03EMPLOY'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
@AbapCatalog.tableCategory : #TRANSPARENT
@AbapCatalog.deliveryClass : #A
@AbapCatalog.dataMaintenance : #RESTRICTED
define table z03employ {

  key client      : abap.clnt not null;
  key employee_id : z03_employee_id not null;
  first_name      : /dmo/first_name;
  last_name       : /dmo/last_name;
  birth_date      : z03_birth_date;
  entry_date      : z03_entry_date;
  @Semantics.amount.currencyCode : 'z03employ.currency_code'
  annual_salary   : z03_annual_salary;
  currency_code   : waers;
  include /lrn/s_admin;

}
```
- Size Category로 테이블의 예상 데이터 양(레코드 수)을 기준으로 저장 공간 크기를 지정
<img width="395" height="331" alt="image" src="https://github.com/user-attachments/assets/0b422c89-8b75-4b09-a40e-6d5cd2d4612e" />
</br>
</br>
</br>
</br>
</br>

# 02/20

</br>

## <  핵심 개념 >
- **1) ABAP 타입 정의 위치별 차이와 Nested/Flat 구조 비교**
- **2) Eclipse에서 Type 생성 절차**
- **3) Eclipse에서 CDS View / View Entity 생성 절차**
- **4) CDS View / View Entity 어노테이션 설정**
- **5) CDS SAP GUI OData 서비스 생성 및 활성화 절차**
</br>
</br>

---

</br>

## < ABAP 타입 정의 위치별 차이와 Nested/Flat 구조 비교 >

</br>

#### 1) Global Class에서 정의한 타입
- 클래스 내부 TYPES는 사용할 때 `=>` 로 접근
```abap
CLASS cl_class DEFINITION.
  PUBLIC SECTION.
    TYPES the_type TYPE c LENGTH 10.
ENDCLASS.

DATA var1 TYPE cl_class=>the_type.
```

#### 2) Global Interface에서 정의한 타입
- 인터페이스 내부 TYPES도 `=>` 로 접근
```abap
INTERFACE if_intf.
  TYPES the_type TYPE c LENGTH 10.
ENDINTERFACE.

DATA var2 TYPE if_intf=>the_type.
```

#### 3) ABAP Dictionary (Data Element) 타입
- 딕셔너리 타입은 `=>` 없이 바로 사용
```abap
DATA var3 TYPE zmy_de.
```

#### 4) Data Element (Reference to Class/Interface) 타입
- 딕셔너리에서 참조타입으로 만들면 ABAP에서는 TYPE REF TO와 동일 의미
```abap
TYPES ref_type TYPE REF TO zcl_s4d400_class.
```

#### 5) Data Element (Predefined Type)
- 딕셔너리에서 기본타입(DATS)으로 만들면 ABAP에서는 TYPE d와 동일 의미
```abap
TYPES predef_type TYPE d.
```

#### 6-1) ABAP Dictionary Structure 타입
- 딕셔너리에서 여러 필드를 묶어 하나의 구조 타입 생성
```abap
define structure zs4d430_address {
  street       : /dmo/street;
  postal_code  : /dmo/postal_code;
  city         : /dmo/city;
  country_code : land1;
  status       : abap.char(1);
}
```

#### 6-2) ABAP 코드에서 Structure 타입 정의
- ABAP 소스코드에서는 `BEGIN OF` ~ `END OF` 로 구조 정의
```abap
TYPES:
  BEGIN OF st_address,
    street       TYPE /dmo/street,
    postal_code  TYPE /dmo/postal_code,
    city         TYPE /dmo/city,
    country_code TYPE land1,
    status       TYPE c LENGTH 1,
  END OF st_address.
```

#### 7-1) ABAP Dictionary Nested Structure 타입
- `ls_person-address-city` 처럼 상위-하위-필드로 접근
```abap
define structure zs4d430_person_nested {
  first_name : /dmo/first_name;
  last_name  : /dmo/last_name;
  address    : zs4d430_address;   " 다른 구조 포함
}
```

#### 7-2) ABAP 코드에서 Nested Structure 정의
- `ls_person-address-city` 처럼 구조-구조-필드로 접근
```abap
TYPES:
  BEGIN OF st_person_nested,
    first_name TYPE /dmo/first_name,
    last_name  TYPE /dmo/last_name,
    address    TYPE st_address,   " 구조 안에 구조 포함
  END OF st_person_nested.
```

#### 8-1) ABAP Dictionary Flat Structure 타입
- `ls_person-city` 처럼 바로 필드 접근 가능
```abap
define structure zs4d430_person_flat {
  first_name : /dmo/first_name;
  last_name  : /dmo/last_name;
  address    : include zs4d430_address;      " include z03s_name as name; 도 가능
}
```
#### 8-2) ABAP 코드에서 Flat Structure 정의
- `ls_person-city` 처럼 중간 구조 없이 바로 접근 가능
```abap
TYPES:
  BEGIN OF st_person_flat,
    first_name TYPE /dmo/first_name,
    last_name  TYPE /dmo/last_name,
    INCLUDE TYPE st_address AS address,
  END OF st_person_flat.
```

#### 9-1) ABAP Dictionary Deep Structure 타입 (Table-like Component)
- `ls_person-addresses[ 1 ]-city` 처럼 테이블 인덱스 거쳐 접근
```abap
define structure zs4d430_person_deep {
  first_name : /dmo/first_name;
  last_name  : /dmo/last_name;
  addresses  : zs4d430_t_addresses;   " Table Type
}
```

#### 9-2) ABAP 코드에서 Deep Structure 정의
- `ls_person-addresses[ 1 ]-city` 처럼 내부테이블[ ]-필드 접근
```abap
TYPES:
  BEGIN OF st_person_deep,
    first_name TYPE /dmo/first_name,
    last_name  TYPE /dmo/last_name,
    addresses  TYPE st_addresses,   " 내부 테이블 타입
  END OF st_person_deep.
```
</br>
</br>
</br>

---

</br>

## < Eclipse에서 Type 생성 절차 >
</br>

#### 1) ABAP Dictionary Table Type 생성

<img width="509" height="279" alt="image" src="https://github.com/user-attachments/assets/0da4c6fd-720e-43ef-b33b-2e863c2e49a2" />
</br>
</br>

#### 2) Table Type 이름 및 패키지 설정

<img width="509" height="284" alt="image" src="https://github.com/user-attachments/assets/b92aa9bd-9ddc-4bfe-a16c-48f8bf3d4eeb" />
</br>
</br>

<img width="509" height="293" alt="image" src="https://github.com/user-attachments/assets/d40527cb-ba36-4e21-8026-fa3c7b4b7734" />
</br>
</br>

<img width="502" height="292" alt="image" src="https://github.com/user-attachments/assets/b2e9e88e-f47d-4d7a-8af2-c08d922e6f89" />
</br>
</br>

#### 3) Table Type 세부 설정 (Row Type, Access, Key 설정 화면)

<img width="511" height="371" alt="image" src="https://github.com/user-attachments/assets/d0b2cd77-8af5-4e5c-95bf-4de31939803d" />
</br>
</br>

<img width="505" height="349" alt="image" src="https://github.com/user-attachments/assets/c1f9623f-059e-4d31-bc4c-a90544c9920a" />
</br>
</br>
</br>
</br>
</br>
</br>
</br>

## < Eclipse에서 CDS View Entity 생성 절차 >
- 테이블에 저장된 데이터를 조회(읽기)하기 위한 가상 뷰 구조
```abap
define view entity <CDS_엔티티이름>
  as select from <데이터소스(테이블/다른CDS)>
{
    <필드1>,
    <필드2>,
    <필드3>
}
```
</br>

#### 1) CDS View(Entity)를 생성

<img width="493" height="412" alt="image" src="https://github.com/user-attachments/assets/54dcfcdf-e410-4567-9d91-edddb2128694" />
</br>
</br>

#### 2) 구문법 defineView(AS ABAP 7.57부터 obsolete)를 선택

<img width="524" height="323" alt="image" src="https://github.com/user-attachments/assets/03ca9f3b-fe73-401a-b122-775e8618ec72" />
</br>

<img width="522" height="415" alt="image" src="https://github.com/user-attachments/assets/35238a04-6caa-4cc5-96cf-e8835e4ec5a5" />
</br>
</br>

#### 3) 구문법 define view를 사용해 SCARR 테이블을 기반으로 SQL View(ZCDSSCARR_B03_S)를 생성
```abap
@AbapCatalog.sqlViewName: 'ZCDSSCARR_B03_S'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Scarr SCDS View'
@Metadata.ignorePropagatedAnnotations: true
define view ZCDSSCARR_B03 as select from scarr
{
    key carrid as Carr_Id,
    carrname as Carr_Name,
    currcode,
    url 
}
```
</br>

#### 4) 신문법 defineViewEntity 템플릿을 선택하여 CDS View Entity 생성

<img width="524" height="333" alt="image" src="https://github.com/user-attachments/assets/4e5ff12e-4296-4b55-a88a-1c4ab4a2db70" />
</br>

<img width="522" height="398" alt="image" src="https://github.com/user-attachments/assets/bc023980-cd5c-4033-81f5-8e9afe6a0a32" />
</br>
</br>

#### 5) 신문법 define view entity를 사용해 SCARR 테이블을 기반으로 CDS View Entity(ZCDSSCARR2_B03)를 생성
```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Scarr CDS View 2'
@Metadata.ignorePropagatedAnnotations: true
define view entity ZCDSSCARR2_B03
  as select from scarr
{
  key carrid   as Carrid,   // 주석은 이렇게
      carrname as Carrname  // 주석 !!
      /*
        다중 주석 처리
        Ctrl + Shift + < : 주석 적용
        Ctrl + Shift + > : 주석 해제
      */
}
```
</br>
</br>
</br>
</br>

## < CDS View Entity 어노테이션 설정 >
</br>

#### 1) 구문법 CDS View(DDIC 기반) 어노테이션 설정 구조
- 구문법 define view에서는 SQL View 생성을 위해 @AbapCatalog.sqlViewName을 반드시 설정해야 함
```abap
@AbapCatalog.sqlViewName: 'S4D430_ANNO1'        // 생성될 DDIC SQL View 이름 지정
@ClientHandling.type: #INHERITED               // 클라이언트 처리 방식 상속
@ClientHandling.algorithm: #AUTOMATED          // Open SQL에서 클라이언트 자동 처리
@AbapCatalog.compiler.compareFilter: true      // 필터 비교 최적화
@AbapCatalog.Buffering.type: #GENERIC          // 버퍼링 유형 설정
@AbapCatalog.Buffering.numberOfKeyFields: 2    // 제네릭 버퍼 키 필드 개수
@AbapCatalog.Buffering.status: #ACTIVE         // SQL View 버퍼링 활성화

define view S4D430_Annotations1                // 구문법 CDS View 정의
  as select from <테이블>
{
    key <필드1>,
        <필드2>
}
```

</br>

#### 2) 신문법 CDS View Entity 어노테이션 설정 구조
```abap
@AbapCatalog.dataMaintenance: #RESTRICTED          // CDS 데이터 프리뷰 제한 설정
@AbapCatalog.viewEnhancementCategory: [#NONE]     // CDS 확장(Extension) 허용 안 함
@AccessControl.authorizationCheck: #NOT_REQUIRED  // Open SQL 권한 체크 수행 안 함
@ObjectModel.usageType.serviceQuality: #X         // 서비스 품질 특성 지정
@ObjectModel.usageType.sizeCategory: #S           // 데이터 규모(사이즈) 분류
@ObjectModel.usageType.dataClass: #MIXED          // 데이터 성격(트랜잭션/마스터 혼합)
@AbapCatalog.entityBuffer.definitionAllowed: false // CDS View Entity 버퍼링 허용 안 함

define view entity ZS4D430_R_Employee              // CDS View Entity 정의 시작
```
</br>

#### 3) CDS 금액·수량 Semantics 어노테이션 설정
- 구문법은 DDIC 참조 상속이 약해 `currencyCode: true` 를 명시하고, 신문법은 참조를 자동 상속하므로 보통 생략
```abap
@Semantics.amount.currencyCode: 'Currency'   // Price 필드의 통화코드는 Currency 별칭 필드에 있음
price      as Price,                        // 금액 필드
@Semantics.currencyCode: true               // 이 필드는 통화코드(CUKY) 필드임을 표시
currency   as Currency,                     // 통화코드 필드 (별칭 사용)

@Semantics.quantity.unitOfMeasure: 'DistanceID'  // Distance 필드의 단위는 DistanceID 별칭 필드에 있음
distance   as Distance,                          // 수량 필드
@Semantics.unitOfMeasure: true                   // 이 필드는 단위(Unit) 필드임을 표시
distid     as DistanceID                         // 단위 필드 (별칭 사용)
```
</br>

#### 4) CDS 어노테이션 개별 작성 vs 그룹 작성 구조
- 개별 작성 방식
```abap
@ObjectModel.usageType.serviceQuality: #X   // 서비스 품질 특성
@ObjectModel.usageType.sizeCategory:   #S   // 데이터 규모 분류
@ObjectModel.usageType.dataClass:      #MIXED // 데이터 성격(마스터/트랜잭션 혼합)
```
- 그룹(Sub-Annotation) 작성 방식
```abap
@ObjectModel: {                          // ObjectModel 어노테이션 그룹 시작
  usageType: {                           // usageType 하위 그룹
    serviceQuality: #X,                  // 서비스 품질
    sizeCategory:   #S,                  // 데이터 규모
    dataClass:      #MIXED               // 데이터 성격
  }
}
```
</br>

#### 5) CDS 번역 가능 텍스트(Translatable Text) 어노테이션
- 화면에 표시되는 텍스트를 정의하는 어노테이션
- View 레벨 텍스트
```abap
@EndUserText.label: 'Employee'   // CDS View 제목 (최대 60자)

define view entity ZI_Employee
  as select from <table>
{
    ...
}
```
- Element(필드) 레벨 텍스트
```abap
@EndUserText.label: 'Date of Birth'                  // 필드 표시명 (최대 60자)
@EndUserText.quickInfo: 'Date of birth of this employee'  // 필드 툴팁 설명 (가변 길이)
birth_date as BirthDate,
```
</br>
</br>
</br>
</br>

## < CDS View Entity 어노테이션 생성 절차 >
</br>

#### 1) 신문법

<img width="522" height="345" alt="image" src="https://github.com/user-attachments/assets/929ae0a9-8f71-4ccb-910e-2a655c56c504" />
</br>

<img width="527" height="343" alt="image" src="https://github.com/user-attachments/assets/63031994-f5eb-4373-a7d5-07a8c919deca" />
</br>

```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]          " View 확장(Enhancement) 허용 안 함
@AccessControl.authorizationCheck: #NOT_REQUIRED       " 권한 체크 없이 조회 가능
@EndUserText.label: 'Flight CDS View'                  " 화면에 표시될 설명 이름
@Metadata.ignorePropagatedAnnotations: true            " 하위 객체 Annotation 전파 무시

define view entity ZCDSFLIGHT_B03 as select from sflight  " View Entity(신문법) 방식으로 sflight 기반 CDS 생성
{
    key carrid as Carrid,                              " 항공사 ID 기본키
    key connid as Connid,                              " 연결 ID 기본키
    key fldate as Fldate,                              " 비행 날짜 기본키
    
    @Semantics.amount.currencyCode: 'Currency'         " 금액 필드이며 통화는 Currency 컬럼 기준
    price as Price,                                    " 가격
    currency as Currency,                              " 통화 코드
    planetype as Planetype,                            " 비행기 기종
    seatsmax as Seatsmax,                              " 최대 좌석 수
    seatsocc as Seatsocc,                              " 예약 좌석 수
    @Semantics.amount.currencyCode: 'Currency'         " 금액 필드이며 통화는 Currency 기준
    paymentsum as Paymentsum                           " 총 결제 금액
}
```
</br>

#### 2) 구문법

<img width="522" height="310" alt="image" src="https://github.com/user-attachments/assets/9064e92e-2e8e-49eb-81fe-44f75cf12550" />


```abap
@AbapCatalog.sqlViewName: 'ZCDSFLT2_B03'          " DB에 생성될 SQL View 이름 (구문법에서 필수)
@AbapCatalog.compiler.compareFilter: true         " WHERE 조건 비교를 최적화
@AbapCatalog.preserveKey: true                    " 원본 테이블의 Key 유지
@AccessControl.authorizationCheck: #NOT_REQUIRED  " 권한 체크 없이 조회 가능
@EndUserText.label: 'Flight CDS View 2'           " 화면에 표시될 설명 이름
@Metadata.ignorePropagatedAnnotations: true       " 하위 객체 Annotation 전파 무시

define view ZCDSFLIGHT2_B03 as select from sflight  " 구문법(Classic CDS) 방식으로 sflight 기반 View 생성
{
    key carrid as Carrid,                         " 항공사 ID 기본키
    key connid as Connid,                         " 연결 ID 기본키
    key fldate as Fldate,                         " 비행 날짜 기본키
    
    @Semantics.amount.currencyCode: 'Currency'    " 금액 필드이며 통화는 Currency 컬럼 기준
    price as Price,                               " 가격

    @Semantics.currencyCode: true                 " 이 필드는 통화 코드임을 명시
    currency as Currency,                         " 통화 코드

    planetype as Planetype,                       " 비행기 기종
    seatsmax as Seatsmax,                         " 최대 좌석 수
    seatsocc as Seatsocc,                         " 예약 좌석 수

    @Semantics.amount.currencyCode: 'Currency'    " 금액 필드이며 통화는 Currency 기준
    paymentsum as Paymentsum                      " 총 결제 금액
}
```

</br>

#### 3) 신문법

<img width="524" height="384" alt="image" src="https://github.com/user-attachments/assets/167c1128-2498-4045-a9bc-a114a0b7c593" />

<img width="522" height="352" alt="image" src="https://github.com/user-attachments/assets/d47cdd7e-579d-4383-bdc7-2c07325b0942" />

```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Cds View spfli'
@Metadata.ignorePropagatedAnnotations: true
define view entity ZCDSSPFLI_B03 as select from spfli
{
    key carrid as Carrid,
    key connid as Connid,
    countryfr as Countryfr,
    cityfrom as Cityfrom,
    cityto as Cityto,
    @Semantics.quantity.unitOfMeasure: 'Distid'
    distance as Distance,
    distid as Distid
    
}
```
</br>
</br>
</br>
</br>

## < CDS OData 서비스 생성 및 활성화 절차 >
</br>

#### 1) CDS OData 자동 Publish 설정
- `@OData.publish: true` → CDS를 자동으로 OData 서비스로 생성
- `@OData.entitySet.name: 'Flights'` → OData에서 사용할 엔티티셋 이름을 지정하는 설정
```abap
@AbapCatalog.sqlViewName: 'ZCDSFLT2_B03'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Flight CDS View 2'
@Metadata.ignorePropagatedAnnotations: true

@OData.publish: true
@OData.entitySet.name: 'Flights'
define view ZCDSFLIGHT2_B03 as select from sflight
{
    key carrid as Carrid,
    key connid as Connid,
    key fldate as Fldate,
    
    @Semantics.amount.currencyCode: 'Currency'
    @EndUserText.label: 'myPrice'
    price as Price,
    @Semantics.currencyCode: true
    currency as Currency,
    planetype as Planetype,
    seatsmax as Seatsmax,
    seatsocc as Seatsocc,
    @Semantics.amount.currencyCode: 'Currency'
    paymentsum as Paymentsum
}
```
</br>

#### 2) 자동 생성된 CDS OData 서비스를 Gateway에 등록
- TCODE : `/n/iwfnd/maint_service`

<img width="726" height="233" alt="image" src="https://github.com/user-attachments/assets/b69dbab0-fa69-49b9-a102-c6c61a8b4a94" />
</br>
</br>

#### 3) OData 서비스를 로컬 패키지($TMP)로 등록하여 Gateway에 최종 활성화

<img width="724" height="497" alt="image" src="https://github.com/user-attachments/assets/fb44f563-98a5-4e40-909a-82616640e726" />
</br>
</br>

#### 4) Service Catalog에서 기술 서비스명으로 조회(Filter)하여 등록된 OData 서비스를 확인
<img width="629" height="300" alt="image" src="https://github.com/user-attachments/assets/fd64480f-703f-44d3-94de-1251aaa14b54" />
</br>
</br>

#### 5) 등록된 OData 서비스를 선택한 후 SAP Gateway Client에서 테스트

<img width="730" height="362" alt="image" src="https://github.com/user-attachments/assets/78873ce1-ffab-411f-a77c-726db8f9c0ea" />
</br>
</br>

#### 6) Gateway Client에서 EntitySet(Flights)을 선택하고 `$metadata` 를 호출하여 OData 서비스 메타데이터를 확인

<img width="695" height="237" alt="image" src="https://github.com/user-attachments/assets/77a8ded1-7abc-4291-b22c-b7076b203ffc" />

<img width="697" height="229" alt="image" src="https://github.com/user-attachments/assets/e1cc289c-f963-469a-b8e3-63d477cc9c4e" />
</br>
</br>

#### 7) Gateway Client의 `$metadata` 에서 `@EndUserText.label: 'myPrice'` 가 OData 메타데이터에 정상 반영되었는지 확인

<img width="940" height="435" alt="image" src="https://github.com/user-attachments/assets/88d80c2f-1242-431e-96d1-10e80d613fb8" />

</br>
</br>
</br>
</br>
</br>

---


</br>


## < 실습 코드 >
</br>

### 실습 1
- nested, flat 구조 실습
- 클래스 카피해서 코드 수정
</br>

<img width="285" height="154" alt="image" src="https://github.com/user-attachments/assets/fe126750-2d78-4629-9b19-9c13a8720060" />

</br>
</br>

#### 1) DDIC Structure 생성
- z03s_address structure
```abap
@EndUserText.label : 'address'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure z03s_address {

  street      : /dmo/street;
  postal_code : /dmo/postal_code;
  city        : /dmo/city;
  country     : land1;

}
```
- z03s_name structure
```abap
@EndUserText.label : 'Name'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure z03s_name {

  first_name : /dmo/first_name;
  last_name  : /dmo/last_name;

}
```
- z03s_person structure
```abap
@EndUserText.label : 'Person'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure z03s_person {

  name    : z03s_name;
  address : z03s_address;

}
```
- z03s_person_inc structure
```abap
@EndUserText.label : 'PERSON_INC'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure z03s_person_inc {

  name    : include z03s_name;
  address : include z03s_address;

}
```
</br>

#### 2) 클래스 코드에 적용
```abap
CLASS zc203s_structure DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun .

* Task 1: Simple Structure
**********************************************************************
    TYPES:
      BEGIN OF st_address,
        street      TYPE /dmo/street,
        postal_code TYPE /dmo/postal_code,
        city        TYPE /dmo/city,
        country     TYPE land1,
      END OF st_address.

* Task 2: Nested Structure
**********************************************************************
    TYPES:
      BEGIN OF st_name,
        first_name TYPE /dmo/first_name,
        last_name  TYPE /dmo/last_name,
      END OF st_name.

    TYPES: BEGIN OF st_person,
             name    TYPE st_name,
             address TYPE st_address,
           END OF st_person.


*Task 3: Named Includes
**********************************************************************
    TYPES BEGIN OF st_person_inc.
    INCLUDE TYPE st_name    AS name.
    INCLUDE TYPE st_address AS address.
    TYPES END OF st_person_inc.

protected section.
private section.
ENDCLASS.



CLASS ZC203S_STRUCTURE IMPLEMENTATION.


  METHOD if_oo_adt_classrun~main.

* Task 1
**********************************************************************
    DATA address TYPE z03s_address.

    address-street      = 'Dietmar-Hopp-Allee 16'.
    address-postal_code = '69190'.
    address-city        = 'Walldorf'.
    address-country     = 'DE'.

* Task 2
**********************************************************************
    DATA person TYPE z03s_person.

    person-name-first_name     = 'Dictionary'.
    person-name-last_name      = 'ABAP'.
    person-address-street      = 'Dietmar-Hopp-Allee 16'.
    person-address-postal_code = '69190'.
    person-address-city        = 'Walldorf'.
    person-address-country     = 'DE'.

* Task 2
**********************************************************************
    DATA person2 TYPE z03s_person_inc.

    person2-name-first_name     = 'Dictionary'.
    person2-name-last_name      = 'ABAP'.
    person2-address-street      = 'Dietmar-Hopp-Allee 16'.
    person2-address-postal_code = '69190'.
    person2-address-city        = 'Walldorf'.
    person2-address-country     = 'DE'.
* or -------------------------------------------------------
    person2-first_name  = 'Dictionary'.
    person2-last_name   = 'ABAP'.
    person2-street      = 'Dietmar-Hopp-Allee 16'.
    person2-postal_code = '69190'.
    person2-city        = 'Walldorf'.
    person2-country     = 'DE'.

  ENDMETHOD.
ENDCLASS.
```
</br>
</br>
</br>

### 실습 2
- 테이블 타입 생성 실습
</br>

#### 1) DDIC 타입으로 지정할 요소들 생성
- Table Type 생성

<img width="690" height="319" alt="image" src="https://github.com/user-attachments/assets/fb45fbe2-028a-4068-9175-c93054303465" />
</br>

<img width="670" height="310" alt="image" src="https://github.com/user-attachments/assets/3bd095e4-e67b-45fe-8bc7-b18b9ea61c3c" />
</br>

- `Z03S_PERSON_DEEP` Structure 생성
```abap
@EndUserText.label : 'Z03S_PERSON_DEEP'
@AbapCatalog.enhancement.category : #NOT_EXTENSIBLE
define structure z03s_person_deep {

  first_name : /dmo/first_name;
  last_name  : /dmo/last_name;
  addresses  : z03t_address;

}
```
</br>

#### 2) 클래스 코드에 적용
- 신문법 `VALUE #( )` 를 사용하면 `APPEND` 없이 내부 테이블 데이터를 한 번에 생성할 수 있음
```abap
DATA <itab> TYPE <table_type>.

<itab> = VALUE <table_type>(
  ( <field1> = <value1>
    <field2> = <value2>
    <sub_table> = VALUE <sub_table_type>(
                     ( <sub_field1> = <value> )
                     ( <sub_field1> = <value> )
                   )
  )
).
```
```abap
CLASS ZCL_03_TABLE_TYPE IMPLEMENTATION.


  METHOD if_oo_adt_classrun~main.

* Task 1
**********************************************************************
    DATA addresses TYPE Z03T_ADDRESS.

    addresses =
      VALUE #(
              ( street      = 'Dietmar-Hopp-Allee 16'
                postal_code = '69190'
                city        = 'Walldorf'
                country     = 'DE'
              )
              ( street      = '3999 West Chester Pike'
                postal_code = '19073'
                city        = 'Newtown Square, PA'
                country     = 'US'
              )
             ).

* Task 2
**********************************************************************
    DATA person TYPE z03s_person_deep.

    person-first_name = 'Dictionary'.
    person-last_name  = 'ABAP'.
    person-addresses  = addresses.

* Task 3
**********************************************************************
    DATA persons TYPE Z03T_PERSONS.

    persons =
       VALUE #(
          ( person )
          (
            first_name = 'CDS'
            last_name  = 'ABAP'
            addresses =
              VALUE #(
                      ( street      = 'SAP-Allee 29'
                        postal_code = '68789'
                        city        = 'St.Leon-Rot'
                        country     = 'DE'
                      )
                      ( street      = '35 rue d''Alsace'
                        postal_code = '92300'
                        city        = 'Levallois-Perret'
                        country     = 'FR'
                      )
                      ( street      = 'Bedfont Road'
                        postal_code = 'TW14 8HD'
                        city        = 'Feltham'
                        country     = 'GB'
                      )
                     )
           )
         ).

  ENDMETHOD.
ENDCLASS.
```
</br>
</br>
</br>

### 실습 3
- CDS View 생성 실습

```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Z03_R_Employee'
@Metadata.ignorePropagatedAnnotations: true
define view entity Z03_R_Employee as select from z03employ
{
    key employee_id as EmployeeId,
    first_name as FirstName,
    last_name as LastName,
    birth_date as BirthDate,
    entry_date as EntryDate,
    // annual_salary as AnnualSalary,
    // currency_code as CurrencyCode,
    created_by as CreatedBy,
    created_at as CreatedAt,
    local_last_changed_by as LocalLastChangedBy,
    local_last_changed_at as LocalLastChangedAt,
    last_changed_at as LastChangedAt
}
```
</br>
</br>
</br>

### 실습 4
- 어노테이션 설정 실습

<img width="840" height="441" alt="image" src="https://github.com/user-attachments/assets/382b4a54-706e-45bd-8447-78e627b2e6be" />

```abap
@AbapCatalog: {
    dataMaintenance: #RESTRICTED,
    viewEnhancementCategory: [#NONE]
}
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Employee (Entity)'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #D,
    sizeCategory:   #M,
    dataClass:      #MASTER
}
define view entity Z03_EMPLOYEE
  as select from /lrn/employ_dep
  {
    key employee_id           as EmployeeId,
        first_name            as FirstName,
        last_name             as LastName,
        birth_date            as BirthDate,
        entry_date            as EntryDate,
        department_id         as DepartmentId,
        @Semantics.amount.currencyCode: 'CurrencyCode'
        annual_salary as AnnualSalary,
        @EndUserText.label: 'Currency Key'
        currency_code as CurrencyCode,
        created_by            as CreatedBy,
        created_at            as CreatedAt,
        local_last_changed_by as LocalLastChangedBy,
        local_last_changed_at as LocalLastChangedAt,
        last_changed_at       as LastChangedAt
  }
```
</br>
</br>
</br>

### 실습 5
- CDS OData 서비스 생성 및 테스트 실습
</br>

#### 1) SCARR 테이블 기반 CDS View OData 자동 Publish 설정
```abap
@AbapCatalog.sqlViewName: 'ZCDSSCARR_B03_S'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Scarr SCDS View'
@Metadata.ignorePropagatedAnnotations: true

@OData.publish: true
@OData.entitySet.name: 'Scarr'
define view ZCDSSCARR_B03 as select from scarr
{
    key carrid as Carr_Id,
    carrname as Carr_Name,
    currcode,
    url 
}
```
</br>

#### 2) 자동 생성된 SCARR CDS OData 서비스를 Gateway에 추가

<img width="790" height="222" alt="image" src="https://github.com/user-attachments/assets/ad62269a-7c01-449f-9ff4-55001cafdf10" />
</br>

#### 3) CDS OData 서비스를 로컬 패키지($TMP)로 지정하여 Gateway에 등록

<img width="729" height="508" alt="image" src="https://github.com/user-attachments/assets/8dac0b44-3342-4018-948f-16c5ce82c71f" />
</br>

#### 4) Gateway Client에서 SCARR CDS OData 서비스의 `$metadata` 를 호출하여 메타데이터 응답 확인
<img width="853" height="454" alt="image" src="https://github.com/user-attachments/assets/26e7005b-c82b-4262-afaf-b5b076258271" />
