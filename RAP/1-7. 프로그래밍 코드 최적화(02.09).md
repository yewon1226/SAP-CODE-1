## <  핵심 개념 >
- **1) 조회(GET)는 HTTP 가능, 데이터 변경(Create/Update/Delete)은 HTTPS 필수**

</br>
</br>

---

</br>

## < 추가적인 내용 >
</br>

- 외부(OData) 호출로 실행되는 DPC_EXT 메소드 디버깅을 위해 External Breakpoint 설정
- 오른쪽 마우스 클릭으로 설정 가능
<img width="523" height="323" alt="image" src="https://github.com/user-attachments/assets/26704ad0-f422-4243-8533-f5264f80e8d7" />
</br>
</br>
</br>

- `GET`: FilterOperator.EQ 조건으로 특정 필드 값이 정확히 일치하는 데이터만 조회하는 OData 필터 조건
<img width="752" height="404" alt="image" src="https://github.com/user-attachments/assets/7fe1ff26-aaf6-4bc6-a118-8e7a29b51ed8" />
</br>
</br>
</br>

- `sap.ui.model.odata.v2.ODataModel` 로 검색하면 OData CRUD(Create/Read/Update/Delete) 메소드들을 한 번에 확인할 수 있음
<img width="614" height="347" alt="image" src="https://github.com/user-attachments/assets/ce95e16e-be58-4fc0-b8e5-290b2d333fbe" />
</br>
</br>
</br>

---

</br>

## < sap.ui.model.odata.v2.ODataModel CRUD 메서드 정리 >
</br>

### 1) Create
- `ZCL_ZCODE_PERSON2_MPC=>TS_PERSON` : 화면에서 보내는 Person 데이터 구조
- `io_data_provider->read_entry_data` : 화면 입력값을 ABAP 변수로 읽어옴
- `er_entity` : 저장 결과를 화면으로 다시 전달
```abap
" ZCL_ZGWEMP_B03_DPC_EXT : EMPLOYEESET_CREATE_ENTITY

  method EMPLOYEESET_CREATE_ENTITY.
* POST 요청을 통해 실제 DB TABLE에 데이터를 추가하는 과정
* BODY 에 담긴 생성 데이터를 io_data_provider를 통해 가져옴
* DB Table에 Insert 후, 처리 결과를 ER_ENTITY로 전달

    " Entity 구조(UI5 ↔ OData 통신용)와 DB 테이블 구조 분리
    DATA: ls_entity TYPE ZCL_ZGWEMP_B03_MPC=>ts_employee,
          ls_data TYPE ztempinfo_b03.

    " es_data에 Request Body(JSON) 정보가 들어옴
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).
    CHECK sy-subrc = 0.
    MOVE-CORRESPONDING ls_entity TO ls_data.

* 실제 DB에 추가하기 전, 데이터 작업 가능
    ls_data-memo = '이건 메모야~~'.
    INSERT ztempinfo_b03 FROM ls_data.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING ls_entity TO er_entity.
    ENDIF.
  endmethod.
```
- SAP Gateway Client에서 POST(Create) 요청으로 JSON 데이터를 보내고, DB에 저장된 결과가 OData 응답으로 그대로 반환되는 화면
- 조회(GET)는 HTTP 가능, 데이터 변경(Create/Update/Delete)은 HTTPS 필수
<img width="632" height="366" alt="image" src="https://github.com/user-attachments/assets/cf01e657-90d9-49c0-a80f-11526fff16d4" />
</br>
</br>
</br>
</br>

### 2) Update
- `ZCL_ZCODE_PERSON2_MPC=>TS_PERSON` : 화면에서 보내는 Person 데이터 구조
- `UPDATE_ENTITY` 는 응답으로 데이터를 돌려줄 필요가 없어서 `er_entity` 가 없어도 정상임
```abap
" ZCL_ZGWEMP_B03_DPC_EXT : EMPLOYEESET_UPDATE_ENTITY

  METHOD employeeset_update_entity.
* PUT 요청을 통해서 실제 테이블에 데이터를 변경하는 과정
* BODY에 담긴 변경 대상 데이터를 io_data_provider로 얻어옴
* DB Update를 진행

    DATA: ls_entity TYPE zcl_zgwemp_b03_mpc=>ts_employee,
          ls_data   TYPE ztempinfo_b03.
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).

    SELECT SINGLE * FROM ztempinfo_b03
      INTO ls_data
      WHERE empno = ls_entity-empno
        AND projod = ls_entity-projod.
    IF sy-subrc = 0.
      " 만약, 별도의 DATA 세팅이 필요한 경우 여기서 ㅈ가성
      " LS_DATA-MEMO = '이건 변경이야'.
      ls_data-memo = ls_entity-memo.
      ls_data-assigndate = ls_entity-assigndate.
      ls_data-role = ls_entity-role.
      UPDATE ztempinfo_b03 FROM ls_data.
    ENDIF.
  ENDMETHOD.
```
- SAP Gateway Client에서 PUT(UPDATE) 요청으로 특정 Entity를 수정하고, UPDATE_ENTITY가 실행되어 DB 데이터가 변경되는 테스트 화면
<img width="623" height="347" alt="image" src="https://github.com/user-attachments/assets/337c1a2b-df31-458c-bd93-936a54d8ad50" />
</br>
</br>
</br>
</br>

### 3) Delete
- DELETE 요청 시 URL로 전달된 키 값(`it_key_tab`)을 DB 구조에 매핑하여 해당 레코드를 테이블에서 삭제
```abap
" ZCL_ZGWEMP_B03_DPC_EXT : EMPLOYEESET_DELETE_ENTITY

  METHOD employeeset_delete_entity.
    " Delete 요청을 통해서 실제 테이블 데이터를 삭제
    " IT_KEY_TAB 파라미터에 들어온 값을 갖고
    " 해당되는 데이터를 DB Table에서 삭제

    " 1. DB 테이블과 동일한 구조의 변수 선언
    " 2. IT_KEY_TAB 파라미터에 들어온 값으로 변수에 키 값 세팅
    " 3. 변수에 해당되는 레코드를 DB Table에서 삭제
    "    delete <db tab> FROM <structure>

    " 실제 테스트.Body 없이 HTTP Method, URL만 구성
    " => /EntitySet(key1='값',key2='값')

    DATA: ls_data TYPE ztempinfo_b03.

    LOOP AT it_key_tab INTO DATA(ls_key).
      ls_data-(ls_key-name) = ls_key-value.
*      CASE ls_key-name.
*        WHEN 'Empno'.
*          ls_data-empno = ls_key-value.
*        WHEN 'Projod'.
*          ls_data-projod = ls_key-value.
*        WHEN OTHERS.
*      ENDCASE.
    ENDLOOP.

    DELETE ztempinfo_b03 FROM ls_data.
  ENDMETHOD.
```
