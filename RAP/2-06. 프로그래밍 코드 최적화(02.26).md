## <  핵심 개념 >
- **1) Class 데이터 테스트는 `F9` 로**
- **2) ABAP SQL 내장 함수 정리(Numeric Functions)**

  - Date Processing Example (날짜 처리 함수 예제)
  - CURRENCY CONVERSION
  - Timestamp Conversion (타임스탬프 날짜 변환)
  - CDS Session Variable
- **3) CDS View에서 파라미터 설정하기**
- **4) DISTINCT 위치 차이**
- **5) JOIN / UNION / UNION ALL**

</br>
</br>

---

</br>

## < ABAP SQL 신문법 >
</br>

- CDS View : `ZCDSCUSTOM_B03`
```abap
REPORT z_new_sql_all_in_one_b03.

SELECT FROM ZCDSMEALRESERVE_MB03
  FIELDS
    *,         " 1. 현재 뷰의 모든 필드 가져오기 (*)
    \_Spfli._Scarr.Carrname as Long_Airline_Name,      " 2. 추가로 Association 필드 하나 더 얹기 (\)
    count( * )            as Booking_Count,        " 3. 집계 함수 (Aggregate)
    avg( Luggweight )     as Avg_Weight

  WHERE cancelled <> 'X'              " 취소되지 않은 건만
    AND carrid = 'AA'                 " 특정 항공사 조건

  /* [중요] *를 썼을 때의 GROUP BY 규칙 */
  " 집계 함수를 썼다면, *에 포함된 모든 일반 필드와 \ 경로 필드를 그룹바이에 적어야 함
  " (실무에서는 *와 GROUP BY를 함께 쓰기보다 필요한 필드만 적는 것을 권장)
  GROUP BY 
    Carrid, Connid, Fldate, Bookid,   " * 안에 포함된 키 필드들
    AirlineName, AirlineCurrency, 
    DepartureCity, ArrivalCity, FlightTime,
    Cust_Rank, Meal_Name, Custtype, Smoker,
    Luggweight, Wunit, Class,
    \_Spfli._Scarr.Carrname           " 경로 표현식 필드

  ORDER BY     " [정렬] 별칭(Alias)으로 정렬
    Avg_Weight DESC,                  " 평균 무게가 무거운 순서
    Booking_Count ASC                 " 예약 건수는 적은 순서
  INTO TABLE @DATA(gt_final_data).    " 인라인 선언으로 한 방에 수신

" 결과 출력
IF sy-subrc = 0.
  cl_demo_output=>display( gt_final_data ).
  MESSAGE |총 { lines( gt_final_data ) }개 그룹의 데이터가 분석되었습니다.| TYPE 'S'.  " 선택된 행 개수 체크 (LINES 함수)
ENDIF.
```
</br>
</br>

---

</br>

## < 실습 코드 >
</br>

### 실습 1
</br>

- 도메인 설정 실습
- CDS 실습
<img width="707" height="323" alt="image" src="https://github.com/user-attachments/assets/7c087899-d7de-4691-8f47-042a143f6b56" />
</br>
</br>

- CDS View : `ZCDSCUSTOM_B03`
```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'ZCDSCUSTOM_B03'
@Metadata.ignorePropagatedAnnotations: true
define view entity ZCDSCUSTOM_B03 as select from sbuspart as _Buspart
    inner join scustom as _Custom on _Buspart.buspartnum = _Custom.id
    association to I_DomainFixedValueText as _DomainFixed
        on 'S_CUSTTYPE' = _DomainFixed.SAPDataDictionaryDomain
//        and _Custom.custtype = _DomainFixed.DomainValue
//        and 'E' = _DomainFixed.Language
{
    key _Buspart.buspartnum as Buspartnum,       // 고객 ID
    _Custom.name as Name,   // 고객명
    _Custom.custtype as Custtype,   // 고객타입
//    _DomainFixed.DomainText as CusttypeName,    // 고객타입명
    _DomainFixed[1: Language = $session.system_language and DomainValue = $projection.Custtype ].DomainText as CusttypeName,
    
    case        // 고객등급
    when _Custom.custtype = 'B' and cast(_Custom.discount as abap.fltp) >= 20 then 'VB'
    when _Custom.custtype = 'B' and cast(_Custom.discount as abap.fltp) < 20 then 'B'
    when _Custom.custtype = 'P' and cast(_Custom.discount as abap.fltp) >= 20 then 'VP'
    when _Custom.custtype = 'P' and cast(_Custom.discount as abap.fltp) < 20 then 'P'
    else ' '
    end   as Custrate,
    
    case        // 고객등급명
    when _Custom.custtype = 'B' and cast(_Custom.discount as abap.fltp) >= 20 then 'VIP 고객(기업)'
    when _Custom.custtype = 'B' and cast(_Custom.discount as abap.fltp) < 20 then '고객(기업)'
    when _Custom.custtype = 'P' and cast(_Custom.discount as abap.fltp) >= 20 then 'VIP 고객(개인)'
    when _Custom.custtype = 'P' and cast(_Custom.discount as abap.fltp) < 20 then '고객(개인)'
    else ' '
    end as CustrateText,
    
    _Buspart.buspatyp as Buspatyp,  // 고객구분
    _Custom.discount as Discount,   // 할인율
    
    _DomainFixed
}
```
</br>
</br>
</br>

### 실습 2
- 실습 1에 이어서 진행
- SAP GUI에서 CDS View를 이용해 ALV 출력
</br>

<img width="382" height="92" alt="image" src="https://github.com/user-attachments/assets/1be4ab5b-4ff8-47bf-8bb4-c1dedb662356" />
</br>
</br>
</br>

**1) Report 프로그램**
```abap
REPORT zcustom_b03_0226.

INCLUDE zcustom_b03_0226_TOP.
INCLUDE zcustom_b03_0226_C01.
INCLUDE zcustom_b03_0226_F01.
INCLUDE zcustom_b03_0226_O01.
INCLUDE zcustom_b03_0226_I01.

START-OF-SELECTION.
  PERFORM set_data.
  CALL SCREEN 100.
```
</br>

**2) C01 프로그램**
```abap
REPORT zcustom_b03_0226.

CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_toolbar FOR EVENT toolbar
      OF cl_gui_alv_grid IMPORTING e_object,
        on_user_command FOR EVENT user_command
      OF cl_gui_alv_grid IMPORTING e_ucomm.
ENDCLASS.

CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_toolbar.
    DATA ls_button TYPE STB_BUTTON.
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    CLEAR ls_button.
    ls_button-function = 'DETAIL'.
    ls_button-text = 'Detail'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.
  ENDMETHOD.

  METHOD on_user_command.
    DATA: lt_select_data TYPE LVC_T_ROW,
          ls_select_data TYPE LVC_S_ROW,
          gs_data LIKE LINE OF gt_data.
    CASE E_UCOMM.
      WHEN 'DETAIL'.
        CALL METHOD go_alv->get_selected_rows
          IMPORTING et_index_rows = lt_select_data.
        IF LINES( lt_select_data ) NE 1.
          MESSAGE '1행만 선택해주세요.' TYPE 'I'.
        ELSE.
          READ TABLE gt_data INTO gs_data INDEX lt_select_data[ 1 ]-index.
          MOVE-CORRESPONDING gs_data TO ZSCUSTOM_B03_0226.
          CALL SCREEN 110
            STARTING AT 5 5
            ENDING AT 58 17.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.
```
</br>

**3) F01 프로그램**
```abap
REPORT zcustom_b03_0226.

******************** SET_DATA ********************
FORM set_data .
  " 이것도 가능
* SELECT * FROM zcdscustom_b03
* WHERE buspartnum < 30
* INTO CORRESPONDING FIELDS OF TABLE @gt_data.
  SELECT FROM zcdscustom_b03
    FIELDS buspartnum, Name, Custtype, CusttypeName, Custrate, CustrateText, Buspatyp, Discount
    WHERE buspartnum < 30
    INTO TABLE @gt_data.
ENDFORM.

******************** SET_FCAT ********************
FORM set_fcat .
  CLEAR gs_fcat.
  gs_fcat-fieldname = 'BUSPARTNUM'.
  gs_fcat-coltext   = 'Buspartnum'.
  gs_fcat-col_pos = 1.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'NAME'.
  gs_fcat-coltext   = 'Name'.
  gs_fcat-col_pos = 2.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CUSTTYPE'.
  gs_fcat-coltext   = 'Custtype'.
  gs_fcat-col_pos = 3.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CUSTTYPENAME'.
  gs_fcat-coltext   = 'Custtype Name'.
  gs_fcat-col_pos = 4.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CUSTRATE'.
  gs_fcat-coltext   = 'Cust rate'.
  gs_fcat-col_pos = 5.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CUSTRATETEXT'.
  gs_fcat-coltext   = 'Cust Rate Text'.
  gs_fcat-col_pos = 6.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'BUSPATYP'.
  gs_fcat-coltext   = 'Buspatyp'.
  gs_fcat-col_pos = 7.
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'DISCOUNT'.
  gs_fcat-coltext   = 'Discount'.
  gs_fcat-col_pos = 8.
  APPEND gs_fcat TO gt_fcat.
ENDFORM.

******************** SET_LAYOUT ********************
FORM set_layout .
  gs_layout-grid_title = 'CDS View에서 가져옴'.
  gs_layout-zebra      = 'X'.
  gs_layout-sel_mode   = 'A'.
ENDFORM.
```
</br>

**4) O01 프로그램**
```abap
REPORT zcustom_b03_0226.

******************** STATUS_0100 ********************
MODULE status_0100 OUTPUT.
  SET PF-STATUS 'S100'.
  SET TITLEBAR 'T100'.
ENDMODULE.

******************** INIT_ALV ********************
MODULE init_alv OUTPUT.
  IF go_cont IS INITIAL.
    CREATE OBJECT go_cont
      EXPORTING
        container_name              = 'AREA'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        OTHERS                      = 6.

    CREATE OBJECT go_alv
      EXPORTING
        i_parent          = go_cont
      EXCEPTIONS
        error_cntl_create = 1
        error_cntl_init   = 2
        error_cntl_link   = 3
        error_dp_create   = 4
        OTHERS            = 5.

    PERFORM set_layout.
    PERFORM set_fcat.
    SET HANDLER lcl_event_handler=>on_toolbar FOR go_alv.
    SET HANDLER lcl_event_handler=>on_user_command FOR go_alv.
    
    CALL METHOD go_alv->set_table_for_first_display
      EXPORTING
        is_layout                     = gs_layout
* i_structure_name              = 'ZSCUSTOM_B03_0226'
      CHANGING
        it_fieldcatalog               = gt_fcat
        it_outtab                     = gt_data
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4.
  ELSE.
    CALL METHOD go_alv->refresh_table_display
      EXCEPTIONS
        finished = 1
        OTHERS   = 2.
  ENDIF.
ENDMODULE.

******************** STATUS_0110 ********************
MODULE status_0110 OUTPUT.
 SET PF-STATUS 'S_110'.
 SET TITLEBAR 'T110'.
ENDMODULE.
```
</br>

**5) I01 프로그램**
```abap
REPORT zcustom_b03_0226.

******************** USER_COMMAND_0100 ********************
MODULE user_command_0100 INPUT.
  CASE ok_code.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

******************** EXIT ********************
MODULE exit INPUT.
  CASE ok_code.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.

******************** USER_COMMAND_0110 ********************
MODULE user_command_0110 INPUT.
  CASE ok_code.
    WHEN 'CLOSE'.
      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.
```
</br>

**6) 결과 확인**

<img width="472" height="410" alt="image" src="https://github.com/user-attachments/assets/d193dcc0-4b3a-4c84-9543-685317254a99" />

<img width="456" height="410" alt="image" src="https://github.com/user-attachments/assets/6c9853b3-7744-430c-8c45-10a04dd8f603" />

<img width="472" height="441" alt="image" src="https://github.com/user-attachments/assets/49aaf859-dbbe-4d22-b7b0-f98bafde186d" />
