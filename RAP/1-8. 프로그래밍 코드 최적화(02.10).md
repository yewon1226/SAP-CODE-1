## <  핵심 개념 >
- **1) 조회(GET)는 HTTP 가능, 데이터 변경(Create/Update/Delete)은 HTTPS 필수**
</br>
</br>

---

</br>

## < Header–Item 구조 OData 서비스 구현 >
</br>

### 1) Read (GET)
</br>

**1) HEADERSET_GET_ENTITYSET**
- Header 엔티티 전체 목록을 조회하는 기본 EntitySet 조회 메서드
```abap
" ZCL_ZGWHEADITEM_B03_DPC_EXT : HEADERSET_GET_ENTITYSET

  method HEADERSET_GET_ENTITYSET.
    SELECT * FROM zscarrcl2_b03
      INTO TABLE et_entityset.
  endmethod.
```
</br>

**2) ITEMSET_GET_ENTITYSET**
- 특정 Header(Carrid)에 속한 Item 목록을 조회하는 Item EntitySet 메서드
```abap
" ZCL_ZGWHEADITEM_B03_DPC_EXT : ITEMSET_GET_ENTITYSET

  method ITEMSET_GET_ENTITYSET.
    DATA lv_carrid TYPE s_carr_id.
    DATA lt_data TYPE TABLE OF zspflicl2_b03.

    LOOP AT it_key_tab INTO DATA(ls_key).
      IF ls_key-name EQ 'Carrid'.
        MOVE ls_key-value TO lv_carrid.
      ENDIF.
    ENDLOOP.

    SELECT * FROM ZSPFLICL2_B03
      INTO TABLE lt_data
      WHERE carrid = lv_carrid.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING lt_data TO et_entityset.
    ENDIF.
  endmethod.
```
</br>

**3) /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITY**
- `$expand` 요청 시 Header 1건과 연관된 ItemSet을 함께 조회해 한 번에 반환하는 메서드
</br>

- `copy_data_to_ref` → ABAP 내부 구조(또는 테이블) 데이터를 OData 응답(`er_entity` / `er_entityset`)으로 변환·출력하는 메서드
- `lv_nav = 'ITEMSET'` → `$expand` 로 처리한 Navigation Property(ItemSet) 이름을 Gateway에 알려주는 값
- `et_expanded_tech_clauses` → `$expand` 요청 중 실제로 처리한 Navigation Property들을 Gateway에 전달하는 파라미터
- `er_entity` → 조회·가공된 단일 Entity 결과를 OData 응답으로 내려보내는 출력 파라미터

<img width="518" height="323" alt="image" src="https://github.com/user-attachments/assets/21524dd0-fb91-4aac-b01a-26cedd4070d5" />

```abap
" ZCL_ZGWHEADITEM_B03_DPC_EXT : /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITY

  method /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITY.
    TYPES BEGIN OF ty_headeritem.
      INCLUDE TYPE ZCL_ZGWHEADITEM_B03_MPC=>ts_header.
      TYPES ItemSet TYPE TABLE OF ZCL_ZGWHEADITEM_B03_MPC=>ts_item WITH DEFAULT KEY.
    TYPES END OF ty_headeritem.
    DATA: lv_carrid TYPE s_carr_id,
          ls_header_item TYPE ty_headeritem,
          lv_nav TYPE string VALUE 'ITEMSET'.

    LOOP AT it_key_tab INTO DATA(ls_key).
      IF ls_key-name EQ 'Carrid'.
        MOVE ls_key-value TO lv_carrid.
      ENDIF.
    ENDLOOP.

    " Header 조회
    SELECT SINGLE * FROM zscarrcl2_b03
      INTO CORRESPONDING FIELDS OF ls_header_item
      WHERE carrid = lv_carrid.
    if sy-subrc = 0.
      " Item 조회 (Navigation Property 이름에 맞춰 데이터 채움)
      SELECT * FROM zspflicl2_b03
        INTO CORRESPONDING FIELDS OF TABLE ls_header_item-itemset
        WHERE carrid = lv_carrid.
    ENDIF.

    " 결과 출력(ER_ENTITY)
    copy_data_to_ref(
      EXPORTING is_data = ls_header_item
      CHANGING cr_data = er_entity ).

    " Navigation Property 데이터 처리 완료되었다고 알려주는 파라미터
    INSERT lv_nav INTO TABLE et_expanded_tech_clauses.
  endmethod.
```
</br>

**4) /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITYSET**
- `$expand` 요청으로 Header 여러 건과 각 Header의 ItemSet을 함께 조회할 때 사용하는 메서드
</br>

- `copy_data_to_ref` → ABAP 내부 구조(또는 테이블) 데이터를 OData 응답(`er_entity` / `er_entityset`)으로 변환·출력하는 메서드
- `lv_nav = 'ITEMSET'` → `$expand` 로 처리한 Navigation Property(ItemSet) 이름을 Gateway에 알려주는 값
- `et_expanded_tech_clauses` → `$expand` 요청 중 실제로 처리한 Navigation Property들을 Gateway에 전달하는 파라미터
- `er_entityset` → 조회·가공된 결과(Entity 여러 건)를 OData 응답으로 내려보내는 출력 파라미터
```abap
" ZCL_ZGWHEADITEM_B03_DPC_EXT : /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITYSET

  method /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_EXPANDED_ENTITYSET.
    TYPES BEGIN OF ts_headeritem.
      INCLUDE TYPE ZCL_ZGWHEADITEM_B03_MPC=>ts_header.
      TYPES ItemSet TYPE TABLE OF ZCL_ZGWHEADITEM_B03_MPC=>ts_item WITH DEFAULT KEY.
    TYPES END OF ts_headeritem.

    TYPES tt_headeritem TYPE STANDARD TABLE OF
      ts_headeritem WITH DEFAULT KEY.

    DATA: lt_header_item_set TYPE tt_headeritem,
          lt_header_db TYPE TABLE OF ZSCARRCL2_B03,
          lt_item_db TYPE TABLE OF ZSPFLICL2_B03,
          lv_nav TYPE string VALUE 'ITEMSET'.
    FIELD-SYMBOLS <fs> TYPE ts_headeritem.

    " 1. Header 조회하고, Headeritem 구조체에 헤더 적용
    SELECT * FROM zscarrcl2_b03
      INTO TABLE lt_header_db.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING lt_header_db TO lt_header_item_set.

      " 2-1. Header에 맞는 Item 조회
      SELECT * FROM zspflicl2_b03
        FOR ALL ENTRIES IN @lt_header_db
        WHERE carrid = @lt_header_db-carrid
        INTO TABLE @lt_item_db.

      " 2-2. Item 루프 돌면서, HeaderItem 구조체의 ItemSet테이블에 적용
      LOOP AT lt_item_db ASSIGNING FIELD-SYMBOL(<fs_item>).
        UNASSIGN <fs>.
        ASSIGN lt_header_item_set[ carrid = <fs_item>-carrid ] TO <fs>.
        APPEND <fs_item> TO <fs>-itemset.
      ENDLOOP.

      " 3. 결과 출력 (ER_ENTITYSET)
      copy_data_to_ref(
        EXPORTING is_data = lt_header_item_set
        CHANGING cr_data = er_entityset
      ).
      INSERT lv_nav INTO TABLE et_expanded_tech_clauses.
    ENDIF.
  endmethod.
```
</br>

**5) Get Gateway로 확인 가능**
```html
/HeaderSet?$format=json&$expand=ItemSet
```
<img width="644" height="167" alt="image" src="https://github.com/user-attachments/assets/4f537639-2bc0-48de-8220-705ab26ad9b2" />
</br>
</br>
</br>
</br>

### 2) Create (POST)
</br>
- `io_data_provider->read_entry_data` → 요청 payload(body)에 담긴 입력 데이터를 ABAP 구조로 읽어오는 메서드
- `er_deep_entity` → Deep Create 수행 후 생성된 Header–Item 구조를 OData 응답으로 반환하는 출력 파라미터
```abap
" ZCL_ZGWHEADITEM_B03_DPC_EXT : /IWBEP/IF_MGW_APPL_SRV_RUNTIME~CREATE_DEEP_ENTITY

  method /IWBEP/IF_MGW_APPL_SRV_RUNTIME~CREATE_DEEP_ENTITY.
    TYPES BEGIN OF ts_headeritem.
      INCLUDE TYPE ZCL_ZGWHEADITEM_B03_MPC=>ts_header.
      TYPES ItemSet TYPE TABLE OF ZCL_ZGWHEADITEM_B03_MPC=>ts_item WITH DEFAULT KEY.
    TYPES END OF ts_headeritem.

    DATA: ls_create_data TYPE ts_headeritem,
          ls_header TYPE ZCL_ZGWHEADITEM_B03_mpc=>ts_header,
          lt_item TYPE ZCL_ZGWHEADITEM_B03_mpc=>tt_item.
    CASE iv_entity_name.
      WHEN 'Header'.
        io_data_provider->read_entry_data(
          IMPORTING es_data = ls_create_data
        ).

        " 받아온 Header-Item 데이터 분리
        MOVE-CORRESPONDING ls_create_data TO ls_header.
        MOVE-CORRESPONDING ls_create_data-itemset TO lt_item.

        " 각 DB Table에 Create
        INSERT zscarrcl2_b03 FROM ls_header.
        IF sy-subrc = 0.
          INSERT zspflicl2_b03 FROM TABLE lt_item.
          IF sy-subrc <> 0.
            ROLLBACK WORK.  " item insert 실패 시 전체 롤백
          ENDIF.
        ENDIF.

        " 결과 출력 (ER_DEEP_ENTITY)
        copy_data_to_ref(
          EXPORTING is_data = ls_create_data
          CHANGING cr_data = er_deep_entity
        ).
    ENDCASE.
  endmethod.
```

<img width="940" height="682" alt="image" src="https://github.com/user-attachments/assets/3ac5c726-7f61-48a0-acf2-67f377e82d8b" />
