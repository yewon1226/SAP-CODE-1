## <  핵심 개념 >
- **1) **

</br>
</br>

---

</br>

## < RAP 개념 정리 >
</br>

- RAP란 ABAP으로 Fiori 앱을 만들고, RESTful OData 서비스를 표준 방식으로 제공하는 개발 모델
</br>

#### < REST 핵심 정리 >
- REST → 자원(Resource) 중심 설계 방식
- 자원 → 웹 상의 데이터/객체, 고유한 URI로 구분
- HTTP Method로 자원에 작업 수행
  - GET(조회) / POST(생성) / PUT(수정) / DELETE(삭제)
- RESTful API → HTTP 기반으로 자원에 접근하는 구조

</br>

#### < RESTful API 특징 >
- 1. 단순성 : HTTP 기반 구조라 구현이 간단하고 이해하기 쉬움.
- 2. 확장성 : 서버와 클라이언트가 분리되어 서로 독립적으로 개발 가능함.
- 3. 유지보수성 : URI와 HTTP Method만으로 일관되게 자원에 접근해 관리가 용이함.
</br>

#### < RAP 모델의 구성 요소 >
1) `CDS View (Core Data Service)` → 데이터 구조를 정의하는 기본 설계도
2) `Metadata Extension` → 화면(UI) 관련 속성을 추가로 정의
3) `Behavior Definition` → 생성·수정·삭제 등 비즈니스 동작 규칙 정의
4) `Service Definition` → 외부에 노출할 데이터 범위 지정
5) `Service Binding` → OData 서비스로 실제 연결 및 실행 설정
6) `기타 비즈니스 로직 구현` → 필요한 추가 로직(검증, 계산 등) 구현

</br>
</br>

---

</br>

# < RAP 기반 CDS → OData → Fiori Elements 전체 구현 흐름 정리 >
</br>

## < RAP 개발 흐름 정리 (CDS 기반) >
1) `Interface View` → 실제 테이블 기반의 핵심 데이터 모델 정의
2) `Projection View (Consumption View)` → 외부에 노출할 필드만 선택해 정리
3) `Metadata Extension` → 화면(UI) 관련 설정 추가
4) `Service Definition` → 어떤 View를 서비스로 공개할지 정의
5) `Service Binding → OData Service 생성` → OData로 연결해 실제 서비스 활성화
6) `Fiori App 개발` → 생성된 OData를 사용해 UI 구현
7) `Fiori Launchpad 배포` → 앱을 런치패드에 등록해 사용자에게 제공
</br>
</br>
</br>

## < RAP 기반 CDS → OData 서비스 → Fiori UI 구현 전체 개발 절차 >
- `_R_` , `_IR_` 는 Interface(데이터 모델) View를 의미하고, `_C_` , `_CR_` 는 Projection(Consumption) View를 의미
</br>

### 1) Interface Root CDS View 생성 화면 (Name 및 Description 설정)
- Interface View는 보통 이름에 IR (Interface Root) 패턴을 포함해 생성함 (예: `ZSFLIGHT_IR_B##`)

<img width="536" height="345" alt="image" src="https://github.com/user-attachments/assets/7a7b2a29-eeec-4b17-a13d-731fb4e556d3" />
</br>
</br>

- Define Root View Entity 템플릿 선택
<img width="536" height="376" alt="image" src="https://github.com/user-attachments/assets/f2173dd2-5413-4dc4-a693-2827df7e37e1" />
</br>
</br>

- sflight을 기반으로 항공편 데이터와 항공사 정보를 Association으로 연결
- _Scarr Association 자체를 외부에 노출하려는 경우에만 필요하고, 단순 필드 조회만 할 거면 없어도 됨
```abap
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Interface view (root)'
@Metadata.ignorePropagatedAnnotations: true
define root view entity ZDSFLIGHT_IR_B03 as select from sflight
association to scarr as _Scarr
    on $projection.Carrid  = _Scarr.carrid
{
    
   key carrid as Carrid,
   key connid as Connid,
   key fldate as Fldate,
   _Scarr.carrname as Carrname,
   @Semantics.amount.currencyCode: 'Currency'
   price as Price,
   currency as Currency,
   planetype as Planetype,
   seatsmax as Seatsmax,
   seatsocc as Seatsocc,
   @Semantics.amount.currencyCode: 'Currency'
   paymentsum as Paymentsum,
   seatsmax_b as SeatsmaxB,
   seatsocc_b as SeatsoccB,
   seatsmax_f as SeatsmaxF,
   seatsocc_f as SeatsoccF,
   _Scarr
}
```
</br>
</br>
</br>

### 2) Consumption(Projection) View 생성
- 보통 이름에 CR 패턴을 사용함 (예: `ZDSFLIGHT_CR_B03`)

<img width="536" height="343" alt="image" src="https://github.com/user-attachments/assets/318e86d6-4848-4f3a-a47f-466765f7df70" />
</br>
</br>

- Define Root View Entity 템플릿 선택
<img width="536" height="413" alt="image" src="https://github.com/user-attachments/assets/da47c335-2f04-45b9-92ee-51bc2a71b83a" />
</br>
</br>

- Interface View를 기반으로 외부에 노출할 필드를 정의하는 Projection(Consumption) CDS 코드를 생성
- 서비스에 직접 노출될 Consumption View라면 `root` 선언이 필수
```abap
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Consumption View (root, Projection View)'
@Metadata.ignorePropagatedAnnotations: false
define root view entity ZDSFLIGHT_CR_B03
    as projection on ZDSFLIGHT_IR_B03
{
    @UI.lineItem: [{ position: 10, label: 'Carrier' }]
    @UI.selectionField: [{ position: 10 }]
    // @EndUserText.label: '항공사2' // 이름 (기본: Data elemen) 덮어쓰기
    key Carrid,
    @UI.lineItem: [{ position: 30, label: '항공평' }]
    @UI.selectionField: [{ position: 20 }]
    key Connid,
    @UI.lineItem: [{ position: 40, label: '비행일' }]
    key Fldate,
    @UI.lineItem: [{ position: 20, label: '항공편이름' }]
    Carrname,
    @UI.lineItem: [{ position: 50, label: '가격' }]
    Price,
    @UI.lineItem: [{ position: 60, label: '통화키' }]
    Currency,
    @UI.lineItem: [{ position: 70, label: '항공기타입' }]
    @UI.selectionField: [{ position: 30 }]
    Planetype,
    @UI.lineItem: [{ position: 80, label: '최대좌석수' }]
    Seatsmax,
    @UI.lineItem: [{ position: 90, label: '예약좌석수' }]
    Seatsocc
}
```
</br>
</br>
</br>

### 3) Projection View에서 Service Definition 생성
- Projection(CR) View를 선택한 후 New Service Definition을 생성해 해당 View를 OData 서비스로 노출
<img width="538" height="288" alt="image" src="https://github.com/user-attachments/assets/19ffbe5e-eded-4f5d-bae1-a37ced557abe" />
</br>
</br>

- Projection View를 참조해 외부에 노출할 서비스를 정의
<img width="536" height="381" alt="image" src="https://github.com/user-attachments/assets/0431f414-4bb6-47da-9b89-cfb2337ed132" />
</br>
</br>

- `as Flight` 는 `ZDSFLIGHT_CR_B03` 를 OData 서비스에서 Entity Set 이름을 Flight로 노출하겠다는 의미
- `as Flight` 를 안 쓰면 CDS View 이름(`ZDSFLIGHT_CR_B03`)이 그대로 OData Entity 이름으로 노출됨
```abap
@EndUserText.label: 'Service Definition'
define service ZSFLIGHT_SRV_B03 {
  expose ZDSFLIGHT_CR_B03 as Flight;
}
```
</br>
</br>
</br>

### 4) Service Binding 생성
- Service Definition을 선택한 후 New Service Binding을 생성해 OData 서비스로 연결
<img width="534" height="284" alt="image" src="https://github.com/user-attachments/assets/1c8a1a27-a19b-4712-b726-b8326d6b6b6c" />
</br>
</br>

- Service Binding은 보통 Service Definition과 동일한 이름으로 생성함
- Service Definition을 OData V2(UI) 방식으로 바인딩해 실제 실행 가능한 서비스로 연결
<img width="536" height="366" alt="image" src="https://github.com/user-attachments/assets/c96ea090-38df-4efa-b4e8-80e3b3bae343" />
</br>
</br>

- Service Binding에서 버전을 선택한 후 Publish 버튼을 눌러 OData 서비스를 활성화

<img width="786" height="258" alt="image" src="https://github.com/user-attachments/assets/dc308ffe-632a-4b1c-aacd-4031d376f96d" />
</br>
</br>

- 서비스가 Published 상태인지 확인한 후, Entity Set을 선택해 Preview로 OData 서비스 정상 동작 여부를 확인
<img width="754" height="264" alt="image" src="https://github.com/user-attachments/assets/a49a383b-c89b-4a26-b6a6-e4470bc6d87e" />
</br>
</br>

- OData 서비스 기반으로 생성된 Fiori 앱이 실행되었으며, 아직 표시할 컬럼이 선택되지 않은 초기 화면임
<img width="817" height="177" alt="image" src="https://github.com/user-attachments/assets/c19c5bd6-545c-401d-a1c9-d63e5e3c8b81" />
</br>
</br>
</br>
</br>
</br>

### 5) Web API용 Service Binding 생성
- Service Definition을 OData V2 - Web API 타입으로 바인딩하여 외부 시스템에서 사용할 API 서비스를 생성
- `UI(Service Binding - OData V2 UI)` 는 Metadata + Fiori UI 미리보기 기능까지 제공
- `Web API(Service Binding - OData V2 Web API)` 는 Metadata만 제공하며 UI Preview는 제공하지 않음
<img width="538" height="421" alt="image" src="https://github.com/user-attachments/assets/032ac8a7-3c54-4afc-a428-1681ab1750c2" />
</br>
</br>

- `OData V2 - Web API` 타입으로 바인딩한 경우, UI용이 아니므로 Preview 버튼이 제공되지 않음
- Web API는 Fiori 화면 미리보기용이 아니라 외부 시스템 연동용 API 서비스이기 때문
<img width="710" height="245" alt="image" src="https://github.com/user-attachments/assets/70ab4e57-f7ab-443c-af64-f609286a3fd3" />
</br>
</br>

- Web API 타입으로 바인딩된 서비스의 Service URL을 확인해 외부 시스템에서 호출할 엔드포인트를 확인
<img width="733" height="263" alt="image" src="https://github.com/user-attachments/assets/bb2f2d87-b95f-4e6b-88b8-710680137110" />
</br>
</br>

- Service URL 뒤에 `/$metadata` 를 붙여 접속해서 해당 OData 서비스의 메타데이터(XML 구조 정보)를 확인
<img width="859" height="278" alt="image" src="https://github.com/user-attachments/assets/fc4657d6-46f8-42a9-83b9-6049e6eccaa4" />
</br>
</br>
</br>
</br>
</br>

### 6) Projection View 기반 Service Definition 생성 및 Service Version 추가
- Projection(CR) View에서 Service Definition 생성
<img width="511" height="428" alt="image" src="https://github.com/user-attachments/assets/cf894564-c90c-4700-865f-74ececb94af3" />
</br>
</br>

<img width="536" height="401" alt="image" src="https://github.com/user-attachments/assets/1565f60b-0a4d-47dc-bee8-b5a4b026a151" />
</br>
</br>

- Service Binding 화면에서 Add 버튼을 눌러 Service Version을 추가
<img width="708" height="296" alt="image" src="https://github.com/user-attachments/assets/1767a77a-33e9-4dbc-af9e-6a8759586171" />
</br>
</br>

<img width="361" height="196" alt="image" src="https://github.com/user-attachments/assets/b6b9231c-0985-4fad-b2ea-00e96aef239b" />

<img width="336" height="71" alt="image" src="https://github.com/user-attachments/assets/a85046fa-bc6a-4539-aa32-78a4ef4cef5d" />

</br>
</br>
</br>
</br>
</br>


### 8) Projection(CR) View 코드 생성
- Interface View를 기반으로 외부에 노출할 필드를 정의하는 Projection(Consumption) CDS 코드를 생성
```abap
@UI.lineItem: [{ position: <순서>, label: '<컬럼명>' }]    // <순서> → 화면에 표시될 위치
@UI.selectionField: [{ position: <순서> }]                // <컬럼명> → 테이블에 보일 컬럼 제목
```
</br>
</br>
</br>

### 9) Fiori Elements 항공편 목록 조회 화면
- Service Binding을 통해 생성된 OData 데이터를 기반으로 Fiori Elements에서 항공편 목록이 조회되어 테이블에 표시
- 컬럼을 설정한 후 Go 버튼을 누르면 OData 데이터가 조회되어 테이블에 표시

<img width="956" height="413" alt="image" src="https://github.com/user-attachments/assets/e6225a2e-ef41-4060-935c-7b9f434ad93c" />
</br>
</br>
</br>
</br>
</br>
</br>

## < RAP Metadata Extension 적용 후 Fiori Elements 앱 생성 및 서비스 연동 절차 >
</br>

### 1) Projection View에서 Metadata Extension 생성

<img width="534" height="370" alt="image" src="https://github.com/user-attachments/assets/9d58cb0c-6fc9-4056-8078-5c197290cf7b" />
</br>
</br>

- 보통 Projection View와 동일한 이름으로 Metadata Extension을 생성

<img width="572" height="386" alt="image" src="https://github.com/user-attachments/assets/a14b1414-c7ff-4434-bfdd-83c4eb247e7b" />
</br>
</br>

- Annotate View 템플릿을 선택해 기존 Projection View에 UI 어노테이션을 추가
- Projection View에는 데이터 구조만 유지하고, Metadata Extension에서 UI 어노테이션만 따로 관리하기 위함임
<img width="550" height="422" alt="image" src="https://github.com/user-attachments/assets/f2e7b690-4ce1-4a39-9af5-851e4df8835d" />
</br>
</br>
</br>
</br>
</br>

### 2) `ZDSFLIGHT_CR_B03` Projection View 어노테이션 제거
- UI 어노테이션을 Metadata Extension으로 분리했기 때문에, Projection View 안의 UI 어노테이션은 제거
- Projection View에서 Metadata Extension을 사용하려면 `@Metadata.allowExtensions: true` 를 반드시 설정해야 함
```abap
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Consumption View (root, Projection View)'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
define root view entity ZDSFLIGHT_CR_B03
    as projection on ZDSFLIGHT_IR_B03
{

    // @EndUserText.label: '항공사2' // 이름 (기본: Data elemen) 덮어쓰기
    key Carrid,
    key Connid,
    key Fldate,
    Carrname,
    Price,
    Currency,
    Planetype,
    Seatsmax,
    Seatsocc,
    Paymentsum,
    SeatsmaxB,
    SeatsoccB,
    SeatsmaxF,
    SeatsoccF

}
```
</br>
</br>
</br>


### 3) `ZDSFLIGHT_CR_B03` Metadata Extension (UI 어노테이션 정의)
- Projection View에 대해 annotate view를 사용해 리스트 컬럼과 필터 필드를 UI 어노테이션으로 별도 정의
- Metadata Extension에 UI 어노테이션을 작성하지 않고 CR(View)에 필드만 추가하면, Fiori에서 톱니바퀴(설정) 버튼 클릭 시 해당 필드가 기본 미선택 상태로 표시
```abap
@Metadata.layer: #CORE                         // 기본(Core) 메타데이터 레이어에서 정의
@UI: {
    headerInfo: {
        typeName: 'SFLIGHT Typename',      // 상세(Object Page) 화면 상단에 표시되는 오브젝트 이름
        typeNamePlural: 'Flight Plural',   // 메인(List Report) 화면 상단 제목
        title: { value: 'Carrid' },        // 상세 화면에서 가장 크게 표시되는 대표 값
        description: { value: 'Planetype' } // 대표 값 아래에 보이는 보조 정보
    }
}
// title과 description은 한 행 클릭 시 이동하는 Object Page 상단에 표시되는 제목과 설명 필드를 설정

annotate view ZDSFLIGHT_CR_B03 with
{
  @UI: { lineItem: [{ position: 10, label: '항공사' }], selectionField: [{ position: 10 }] }
  Carrid;
  @UI: {lineItem: [{ position: 30, label: '항공평' }], selectionField: [{ position: 20 }] }
  Connid;
  @UI.lineItem: [{ position: 40, label: '비행일' }]
  Fldate;
  @UI.lineItem: [{ position: 20, label: '항공편이름' }]
  Carrname;
  @UI.lineItem: [{ position: 50, label: '가격' }]
  Price;
  @UI.lineItem: [{ position: 60, label: '통화키' }]
  Currency;
  @UI: {lineItem: [{ position: 70, label: '항공기타입' }], selectionField: [{ position: 30 }] }
  Planetype;
  @UI.lineItem: [{ position: 80, label: '최대좌석수' }]
  Seatsmax;
  @UI.lineItem: [{ position: 90, label: '예약좌석수' }]
  Seatsocc;
  @UI.hidden: true
  Paymentsum;
  @UI.hidden: true
  SeatsmaxB;
  @UI.hidden: true
  SeatsoccB;
  @UI.hidden: true
  SeatsmaxF;
  @UI.hidden: true
  SeatsoccF;
}
```
</br>
</br>
</br>

### 4) SAP Fiori Generator – List Report Page 선택
- Fiori 애플리케이션 생성 시 List Report Page 템플릿을 선택해 리스트 화면과 오브젝트 페이지가 함께 생성되도록 설정

<img width="645" height="274" alt="image" src="https://github.com/user-attachments/assets/40dc36af-39b5-4035-b49c-e8eae372ff32" />
</br>
</br>
</br>
</br>
</br>

### 5) Service Binding에서 Service URL 확인
- Service Binding이 Published 상태일 때, 표시된 Service URL을 통해 OData 서비스 엔드포인트를 확인
<img width="654" height="226" alt="image" src="https://github.com/user-attachments/assets/986e7c57-90e9-49a3-b430-a623745fbb2a" />
</br>
</br>
</br>
</br>
</br>

### 6) Fiori Generator – OData 서비스 선택
- Fiori 앱 생성 시 연결할 시스템을 선택하고, 사용할 OData 서비스(`ZSFLIGHT_SRV_B03`)를 지정
<img width="519" height="267" alt="image" src="https://github.com/user-attachments/assets/30697adb-ebd8-4512-a2ee-2ec421589a5b" />
</br>
</br>
</br>
</br>
</br>

### 7) Fiori Generator – Entity 선택
- 선택한 OData 서비스에서 사용할 Main Entity(Flight)를 지정하고, 테이블 타입(Responsive)을 설정

<img width="531" height="267" alt="image" src="https://github.com/user-attachments/assets/9b1ee642-934f-420e-9d4d-37f51fc87eff" />
</br>
</br>
</br>
</br>
</br>

### 8) Fiori List Report 화면으로 결과 확인
- BAS에서 OData 서비스에 연결해 실행하여 결과 확인
  
<img width="536" height="231" alt="image" src="https://github.com/user-attachments/assets/81633dd3-e54d-4d8b-93d9-84ec5454474c" />

</br>
</br>
</br>
</br>
</br>

---

</br>
</br>

## < 추가 기능들 >
</br>

### < RAP 어노테이션 정리 (Projection View vs Metadata Extension) >
- `Projection View` : 데이터를 어떻게 쓰게 할지(동작·의미)를 구상하는 곳
- `Metadata Extension` : 화면에 어떻게 보이게 할지(배치·표시)를 구상하는 곳
</br>

#### 1) Projection View에 작성된 어노테이션 (구조 · 검색 · 값도움 · 모델 성격)

| 어노테이션 | 적용 위치 | 설명 |
|------------|------------|------|
| `@AccessControl.authorizationCheck: #NOT_REQUIRED` | View 상단 | 권한 체크 비활성화 |
| `@Metadata.ignorePropagatedAnnotations: false` | View 상단 | 상위 View 어노테이션 전파 허용 |
| `@Metadata.allowExtensions: true` | View 상단 | Metadata Extension 확장 허용 |
| `@Search.searchable: true` | View 상단 | 전체 View를 검색 가능하게 설정 |
| `@Search.defaultSearchElement: true` | Carrid / Currency / Carrname | 기본 검색 필드 지정 |
| `@Search.fuzzinessThreshold: 0.8` | Carrname | 유사 검색 허용 (80% 일치) |
| `@Consumption.valueHelpDefinition` | Carrid | F4 Value Help 연결 |
| `additionalBinding` | Carrid | 값 도움 시 추가 필드 자동 바인딩 |
| `@ObjectModel.text.element: ['Carrname']` | Carrid | 코드-텍스트 연결 |
| `@UI.textArrangement: #TEXT_FIRST` | Carrid | 텍스트 먼저 표시 (텍스트-코드 순서) |

</br>

#### 2) Metadata Extension에 작성된 어노테이션 (UI · 화면 구성)

| 구분 | 어노테이션 | 설명 |
|------|------------|------|
| 레이어 | `@Metadata.layer: #CORE` | 메타데이터 적용 레이어 지정 |
| 헤더 | `@UI.headerInfo` | Object Page 상단 제목 영역 전체 설정 |
| 헤더 | `typeName` | 오브젝트 페이지 상단에 표시되는 종류 이름(타이틀 라벨) |
| 헤더 | `typeNamePlural` | 메인(List Report) 화면 상단 제목 |
| 헤더 | `title` | Object Page에서 크게 보이는 대표 값 |
| 헤더 | `description` | 대표 값 아래 보이는 보조 설명 |
| 리스트 | `@UI.lineItem` | List Report 테이블 컬럼 표시 |
| 필터 | `@UI.selectionField` | 상단 필터 영역 표시 |
| 숨김 | `@UI.hidden` | 화면에서 해당 필드 숨김 |
| 그룹 | `@UI: { ... }` | 여러 UI 설정을 하나로 묶는 그룹핑 방식 |
| 필터동작 | `@Consumption.filter` | 필터 입력 방식 제어 (단일 선택, 기본값, 다중 선택 여부 등) |

</br>
</br>
</br>
</br>

### 1) 

- 어노테이션 그룹핑 Grouping : 같은 필드에 적용되는 UI 설정을` @UI: { }` 안에 공통으로 묶어서 정의하는 방식
```abap
@UI: {
  lineItem:       [{ position: <순서>, label: '<컬럼명>' }],
  selectionField: [{ position: <순서> }]
}
```
- `@UI.hidden: true` → 해당 필드를 화면(UI)에 표시하지 않도록 숨기는 설정

</br>
</br>

---

</br> 

## < 실습 코드 >
</br>

### 실습 1

